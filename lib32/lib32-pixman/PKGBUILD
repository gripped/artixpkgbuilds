# Maintainer : Ionut Biru <ibiru@archlinux.org>

_pkgbasename=pixman
pkgname=lib32-$_pkgbasename
pkgver=0.43.2
pkgrel=1
pkgdesc="Pixman library (32-bit)"
arch=('x86_64')
url="https://xorg.freedesktop.org"
license=('MIT')
depends=('lib32-gcc-libs' 'lib32-glibc' $_pkgbasename)
makedepends=('meson' 'gcc-multilib' 'lib32-libpng')
source=(https://xorg.freedesktop.org/releases/individual/lib/${_pkgbasename}-${pkgver}.tar.xz)
sha512sums=('1a1d21b86b3c6784c4c2606b7069723b4dbb747eac9fce95bca86516787840379ffd49abc42d11e7143e85c32c85496f33c2562c7a910ca6f963599affdc3224')
#validpgpkeys=('') # Maarten Lankhorst <maarten.lankhorst@linux.intel.com>

build() {
  local meson_options=(
    --cross-file lib32
    -D loongson-mmi=disabled
    -D vmx=disabled
    -D arm-simd=disabled
    -D neon=disabled
    -D a64-neon=disabled
    -D iwmmxt=disabled
    -D mips-dspr2=disabled
    -D gtk=disabled
  )

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  artix-meson "$_pkgbasename"-"$pkgver" build "${meson_options[@]}"
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  rm -rf "${pkgdir}"/usr/include
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
