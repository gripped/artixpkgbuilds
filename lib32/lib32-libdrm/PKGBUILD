# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libdrm
pkgname=lib32-$_pkgbasename
pkgver=2.4.131
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services (32-bit)"
url="https://dri.freedesktop.org/"
arch=(x86_64)
license=('MIT')
depends=('lib32-libpciaccess' 'lib32-glibc' $_pkgbasename)
makedepends=('meson')
checkdepends=('lib32-cairo')
source=(https://dri.freedesktop.org/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.xz)
sha512sums=('d75894215600b648e25bbe13422901b650fb94bba8276d77971a49db7dc001819c9fc4b7e90ed82275f170a331687960ca5752cbb12064b2eec9c5c8eb60bf3c')
#validpgpkeys=('10A6D91DA1B05BD29F6DEBAC0C74F35979C486BE') # David Airlie <airlied@redhat.com>
#validpgpkeys=('2617F6F8DEBB0A7A15C405C983FD600E340FB5FF') # Samuel Pitoiset <samuel.pitoiset@gmail.com>
validpgpkeys=('34FF9526CFEF0E97A340E2E40FDE7BE0E88F5E48') # emersion <contact@emersion.fr>
# validpgpkeys=('F3A52DB8ECE9EB9E33BC48C6917A3EE97A0FCD1A') # "Marek Olšák <maraeo@gmail.com>"
#validpgpkeys=('34FF9526CFEF0E97A340E2E40FDE7BE0E88F5E48'  # Simon Ser <contact@emersion.fr>
#              '3BB639E56F861FA2E86505690FDD682D974CA72A') # Matt Turner <mattst88@gmail.com>

prepare() {
  cd $_pkgbasename-$pkgver

}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  artix-meson $_pkgbasename-$pkgver build \
    --libdir=/usr/lib32 \
    -Dudev=false \
    -Dman-pages=disabled \
    -Detnaviv=disabled \
    -Dfreedreno=disabled \
    -Dvc4=disabled \
    -Dvalgrind=disabled
  meson compile -C build
}

check() {
  cd build
  meson test --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
