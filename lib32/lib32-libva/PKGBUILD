# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Llewelyn Trahaearn <WoefulDerelict at GMail dot com>
# Contributor: kokoko3k <kokoko3k at gmail dot com>
# Contributor: Ionut Biru <ibiru at archlinux dot org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski at archlinux dot org>

pkgname=lib32-libva
pkgver=2.24.1
pkgrel=1
pkgdesc='Video Acceleration (VA) API for Linux (32-bit)'
arch=(x86_64)
url=https://intel.github.io/libva/
license=(MIT)
depends=(
  lib32-glibc
  lib32-libdrm
  lib32-libgl
  lib32-libx11
  lib32-libxcb
  lib32-libxext
  lib32-libxfixes
  lib32-wayland
  libva
)
makedepends=(
  git
  lib32-libglvnd
  lib32-mesa
  meson
)
optdepends=(
  'lib32-libva-intel-driver: backend for Intel GPUs (<= Haswell)'
  'lib32-libva-mesa-driver: backend for AMD and NVIDIA GPUs'
)
provides=(
  libva-drm.so
  libva-glx.so
  libva-wayland.so
  libva-x11.so
  libva.so
)
source=(git+https://github.com/intel/libva.git#tag=${pkgver})
b2sums=('5e7b85e1c16a68432069cbad3e11096311b97e4532061e97f247c0abbe16c2e991771860b9976f459000247bbc280b4825241a5c0b09571cac6be9b432ab3eaa')

build() {
  CFLAGS+=" -DENABLE_VA_MESSAGING"  # Option missing
  artix-meson libva build --cross-file lib32
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
  rm -r "${pkgdir}/usr/include"
  install -Dm 644 libva/COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim:set sw=2 sts=-1 et:
