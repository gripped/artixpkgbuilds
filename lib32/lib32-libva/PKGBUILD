# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Llewelyn Trahaearn <WoefulDerelict at GMail dot com>
# Contributor: kokoko3k <kokoko3k at gmail dot com>
# Contributor: Ionut Biru <ibiru at archlinux dot org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski at archlinux dot org>

pkgname=lib32-libva
pkgver=2.21.0
pkgrel=1
pkgdesc='Video Acceleration (VA) API for Linux (32-bit)'
arch=(x86_64)
url=https://01.org/linuxmedia/vaapi
license=(MIT)
depends=(
  lib32-libdrm
  lib32-libgl
  lib32-libx11
  lib32-libxext
  lib32-libxfixes
  lib32-wayland
  libva
)
makedepends=(
  git
  lib32-libglvnd
  lib32-mesa
  meson
)
optdepends=(
  'lib32-libva-intel-driver: backend for Intel GPUs (<= Haswell)'
  'lib32-libva-mesa-driver: backend for AMD and NVIDIA GPUs'
)
provides=(
  libva-drm.so
  libva-glx.so
  libva-wayland.so
  libva-x11.so
  libva.so
)
_tag=0b01aed44ef1a6ad660261284ff266fa812829ef
source=(git+https://github.com/intel/libva.git#tag=${_tag})
b2sums=('91fab05ed62d1ada424612cef2bed65e45020bed00ed3bcedc6754e9abfbdbac265f4e06b68be45b14176caf5871ec38e3c24f997bde3ff2ebc74284f9f63cd9')

pkgver() {
  cd libva
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

build() {
  CFLAGS+=" -DENABLE_VA_MESSAGING"  # Option missing
  artix-meson libva build --cross-file lib32
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
  rm -r "${pkgdir}/usr/include"
  install -Dm 644 libva/COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim:set sw=2 sts=-1 et:
