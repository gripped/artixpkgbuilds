# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: jtts <jussaar@mbnet.fi>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=lib32-at-spi2-core
pkgname=(
  lib32-at-spi2-core
)
pkgver=2.54.0
pkgrel=2
pkgdesc="Protocol definitions and daemon for D-Bus at-spi (32-bit)"
url="https://gitlab.gnome.org/GNOME/at-spi2-core"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  at-spi2-core
  lib32-dbus
  lib32-glib2
  lib32-glibc
  lib32-libx11
  lib32-libxi
  lib32-libxtst
)
makedepends=(
  dbus-broker
  git
  glib2-devel
  lib32-libxml2
 
  meson
 
)
checkdepends=(
  at-spi2-core
)
source=("git+https://gitlab.gnome.org/GNOME/at-spi2-core.git#tag=AT_SPI2_CORE_${pkgver//./_}")
b2sums=('3b1b575634dfa5269b98469f761ea5ae9621302121fa243c532a537894fdc7ee9a16ce631093afbd9c3648272084b87a3dd5b4c2263c50fcd48791a16ab0de76')

prepare() {
  cd at-spi2-core
}

build() {
  local meson_options=(
    --cross-file lib32
    -D default_bus=dbus-broker
    -D docs=false
    -D introspection=disabled
  )

  artix-meson at-spi2-core build "${meson_options[@]}"
  meson compile -C build
}

check() {
  dbus-run-session meson test -C build --print-errorlogs
}

package_lib32-at-spi2-core() {
  provides=(
    "lib32-at-spi2-atk=$pkgver-$pkgrel"
    "lib32-atk=$pkgver-$pkgrel"
    libatk-1.0.so
    libatk-bridge-2.0.so
    libatspi.so
  )
  conflicts=(
    'lib32-at-spi2-atk<=2.38.0-2'
    'lib32-atk<=2.38.0-2'
  )
  replaces=(
    'lib32-at-spi2-atk<=2.38.0-2'
    'lib32-atk<=2.38.0-2'
  )

  meson install -C build --destdir "$pkgdir"

  rm -r "${pkgdir}"/{etc,usr/{include,lib,share}}
}

# vim:set sw=2 sts=-1 et:
