# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Sébastien Luttringer <sеblu@аrchlіnux․org>
# Contributor: Joel Teichroeb <joel@teichroeb.net>

pkgname=lib32-wayland
pkgver=1.23.1
pkgrel=1
pkgdesc='A computer display server protocol'
arch=(x86_64)
url=https://wayland.freedesktop.org
license=(MIT)
depends=(
  lib32-glibc
  lib32-libffi
  lib32-expat
  lib32-libxml2
  wayland
)
makedepends=(meson)
provides=(libwayland-{client,cursor,egl,server}.so)
validpgpkeys=('C7223EBE4EF66513B892598911A30156E0E67611'  # Bryce Harrington
              'C0066D7DB8E9AC6844D728715E54498E697F11D7'  # Derek Foreman
              '34FF9526CFEF0E97A340E2E40FDE7BE0E88F5E48') # Simon Ser <contact@emersion.fr>
source=("https://gitlab.freedesktop.org/wayland/wayland/-/releases/$pkgver/downloads/wayland-$pkgver.tar.xz"{,.sig})
b2sums=('74784b537a6e28610466ff1549286cea9ff8b9ca914afa26a07f177bbe0be56b80de037d5eb5d07a38bf02a9041c32e62c896facd5417c1435571640ad8f87b2'
        'SKIP')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  artix-meson build wayland-${pkgver} \
    --libdir=/usr/lib32 \
    -Ddocumentation=false
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"
  rm -rf "${pkgdir}"/usr/{bin,include,share}
  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s wayland "${pkgdir}"/usr/share/licenses/lib32-wayland
}

# vim: ts=2 sw=2 et:
