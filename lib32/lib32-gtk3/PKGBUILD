# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: GordonGR <ntheo1979@gmail.com>

pkgname=lib32-gtk3
pkgver=3.24.42
pkgrel=1
epoch=1
pkgdesc="GObject-based multi-platform GUI toolkit"
url="https://www.gtk.org/"
arch=(x86_64)
license=(LGPL-2.0-only)
depends=(
  gtk3
  lib32-at-spi2-atk
  lib32-colord
  lib32-dbus
  lib32-gdk-pixbuf2
  lib32-libcanberra
  lib32-libcups
  lib32-libepoxy
  lib32-librsvg
  lib32-libxcomposite
  lib32-libxcursor
  lib32-libxdamage
  lib32-libxinerama
  lib32-libxkbcommon
  lib32-libxrandr
  lib32-mesa
  lib32-pango
  lib32-wayland
)
makedepends=(
  git
  meson
  python-packaging
  sassc
  wayland-protocols
)
source=(
  "git+https://gitlab.gnome.org/GNOME/gtk.git#tag=$pkgver"
  gtk-query-immodules-3.0-32.hook
)
b2sums=('3f79244cfeac535186b84e6c9ad3dad7dbb5737e4ebd8762985fce95930a9bbb07fcc331fb915b7f6ab8496afd7995a5aa501377af01710ff2649e5adabcb249'
        'c7136723735c4cbfe654851d0ccc1698142fa8f90b724ed9a970ebd6a74b4f59ea558e134e600736a1041417fc7fd9b3baa8bc561ed9598f2fbf45392dcc9159')

pkgver() {
  cd gtk
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gtk
}

build() {
  local meson_options=(
    --cross-file lib32
    -D broadway_backend=true
    -D cloudproviders=false
    -D colord=yes
    -D gtk_doc=false
    -D introspection=false
    -D man=false
    -D tracker3=false
  )

  CFLAGS+=" -DG_DISABLE_CAST_CHECKS"
  artix-meson gtk build "${meson_options[@]}"
  meson compile -C build
}

package() {
  provides=(
    libgailutil-3.so
    libgdk-3.so
    libgtk-3.so
  )
  install=lib32-gtk3.install

  meson install -C build --destdir "$pkgdir"
  rm -r "${pkgdir}"/{etc,usr/{include,share}}
  find "${pkgdir}"/usr/bin -type f -not -name gtk-query-immodules-3.0 -delete
  mv "${pkgdir}"/usr/bin/gtk-query-immodules-3.0{,-32}

  install -Dm 644 gtk-query-immodules-3.0-32.hook -t "${pkgdir}"/usr/share/libalpm/hooks/
}

# vim:set sw=2 sts=-1 et:
