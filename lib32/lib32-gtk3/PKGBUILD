# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: GordonGR <ntheo1979@gmail.com>

pkgname=lib32-gtk3
pkgver=3.24.52
pkgrel=2
epoch=1
pkgdesc="GObject-based multi-platform GUI toolkit (32-bit)"
url="https://www.gtk.org/"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  gtk3
  lib32-at-spi2-core
  lib32-cairo
  lib32-colord
  lib32-fontconfig
  lib32-fribidi
  lib32-gdk-pixbuf2
  lib32-glib2
  lib32-glibc
  lib32-harfbuzz
  lib32-libcups
  lib32-libepoxy
  lib32-libgl
  lib32-librsvg
  lib32-libx11
  lib32-libxcomposite
  lib32-libxcursor
  lib32-libxdamage
  lib32-libxext
  lib32-libxfixes
  lib32-libxi
  lib32-libxinerama
  lib32-libxkbcommon
  lib32-libxrandr
  lib32-pango
  lib32-wayland
  lib32-zlib
)
makedepends=(
  git
  glib2-devel
  meson
  sassc
  wayland-protocols
)
source=(
  "git+https://gitlab.gnome.org/GNOME/gtk.git#tag=$pkgver"
  gtk-query-immodules-3.0-32.hook
  gtk-remove-immodules-cache-32.hook
  0001-Allow-disabling-legacy-Tracker-search.patch
)
b2sums=('b351d0e48b074ea0b6d75dd8b47bd5ff5897dce01b2c69894a5d20fff0205b8cfc4603ce556331fdf3afe4f0380c58212ebac7cd832c9b6d3fd05cbf822250d8'
        '707f28d0b2158c9f9b2d50eb5d245f580feef99852c55f996aaa9ccb0eda40c4f2958b3429ccc8ed442a27bb5be44fe4f8bd1284aa71afd4ee780b5a369ca4ed'
        '81564af5baa09f8cb8c19070b276e78c45d47635a615dd43256a2e9404f36edaf4c1b514d247f51ac16302721a7d2146bee19f2ee0656027bb8c02df0ce19955'
        '72075b7a95b61dacae009310f5884ea4474c2fd248f5cb65d6cc5fdfe2c0e55ea6f954f77d818ff27f531fba61baca13d178b93310a95459e629a9ec11b053c0')

prepare() {
  cd gtk

  # Don't try to use the old Tracker
  git apply -3 ../0001-Allow-disabling-legacy-Tracker-search.patch
}

build() {
  local meson_options=(
    --cross-file lib32
    -D broadway_backend=true
    -D cloudproviders=false
    -D colord=yes
    -D gtk_doc=false
    -D introspection=false
    -D man=false
    -D tracker=false
    -D tracker3=false
  )

  CFLAGS+=" -DG_DISABLE_CAST_CHECKS"
  artix-meson gtk build "${meson_options[@]}"
  meson compile -C build
}

package() {
  optdepends=(
    'evince: Default print preview command'
  )
  provides=(
    libgailutil-3.so
    libgdk-3.so
    libgtk-3.so
  )

  meson install -C build --destdir "$pkgdir"
  rm -r "$pkgdir"/{etc,usr/{include,share}}
  find "$pkgdir/usr/bin" -type f -not -name gtk-query-immodules-3.0 -delete
  mv "$pkgdir"/usr/bin/gtk-query-immodules-3.0{,-32}

  install -Dm644 gtk-*.hook -t "$pkgdir/usr/share/libalpm/hooks"
}

# vim:set sw=2 sts=-1 et:
