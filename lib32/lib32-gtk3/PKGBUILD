# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: GordonGR <ntheo1979@gmail.com>

pkgname=lib32-gtk3
pkgver=3.24.51
pkgrel=1
epoch=1
pkgdesc="GObject-based multi-platform GUI toolkit (32-bit)"
url="https://www.gtk.org/"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  gtk3
  lib32-at-spi2-core
  lib32-cairo
  lib32-colord
  lib32-fontconfig
  lib32-fribidi
  lib32-gdk-pixbuf2
  lib32-glib2
  lib32-glibc
  lib32-harfbuzz
  lib32-libcups
  lib32-libepoxy
  lib32-libgl
  lib32-librsvg
  lib32-libx11
  lib32-libxcomposite
  lib32-libxcursor
  lib32-libxdamage
  lib32-libxext
  lib32-libxfixes
  lib32-libxi
  lib32-libxinerama
  lib32-libxkbcommon
  lib32-libxrandr
  lib32-pango
  lib32-wayland
)
makedepends=(
  git
  glib2-devel
  meson
  sassc
  wayland-protocols
)
source=(
  "git+https://gitlab.gnome.org/GNOME/gtk.git#tag=$pkgver"
  gtk-query-immodules-3.0-32.hook
  0001-Allow-disabling-legacy-Tracker-search.patch
)
b2sums=('06f5dcfe6cf693d4e0da0964715e7ea07ab8940eea21b7fba28615c6d9ecdd8533e51e061b57e6b4f5b9401041adeb52fd81fc856fa79dc5bf8e5a4058d9fa92'
        'c7136723735c4cbfe654851d0ccc1698142fa8f90b724ed9a970ebd6a74b4f59ea558e134e600736a1041417fc7fd9b3baa8bc561ed9598f2fbf45392dcc9159'
        'f63a30a5823c564861145128a91b6d6ca4f13fd40cf09d1b8897bccaf8664af085b3cbe20ba7da6289439a820440a7af17b6281efea1f84a8c4decd62df00a54')

prepare() {
  cd gtk

  # Don't try to use the old Tracker
  git apply -3 ../0001-Allow-disabling-legacy-Tracker-search.patch
}

build() {
  local meson_options=(
    --cross-file lib32
    -D broadway_backend=true
    -D cloudproviders=false
    -D colord=yes
    -D gtk_doc=false
    -D introspection=false
    -D man=false
    -D tracker=false
    -D tracker3=false
  )

  CFLAGS+=" -DG_DISABLE_CAST_CHECKS"
  artix-meson gtk build "${meson_options[@]}"
  meson compile -C build
}

package() {
  optdepends=(
    'evince: Default print preview command'
  )
  provides=(
    libgailutil-3.so
    libgdk-3.so
    libgtk-3.so
  )
  install=lib32-gtk3.install

  meson install -C build --destdir "$pkgdir"
  rm -r "$pkgdir"/{etc,usr/{include,share}}
  find "$pkgdir/usr/bin" -type f -not -name gtk-query-immodules-3.0 -delete
  mv "$pkgdir"/usr/bin/gtk-query-immodules-3.0{,-32}

  install -Dm644 gtk-query-immodules-3.0-32.hook -t "$pkgdir/usr/share/libalpm/hooks"
}

# vim:set sw=2 sts=-1 et:
