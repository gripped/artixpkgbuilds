# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: GordonGR <ntheo1979@gmail.com>

pkgname=lib32-gtk3
pkgver=3.24.48
pkgrel=1
epoch=1
pkgdesc="GObject-based multi-platform GUI toolkit (32-bit)"
url="https://www.gtk.org/"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  gtk3
  lib32-at-spi2-core
  lib32-cairo
  lib32-colord
  lib32-fontconfig
  lib32-fribidi
  lib32-gdk-pixbuf2
  lib32-glib2
  lib32-glibc
  lib32-harfbuzz
  lib32-libcups
  lib32-libepoxy
  lib32-libgl
  lib32-librsvg
  lib32-libx11
  lib32-libxcomposite
  lib32-libxcursor
  lib32-libxdamage
  lib32-libxext
  lib32-libxfixes
  lib32-libxi
  lib32-libxinerama
  lib32-libxkbcommon
  lib32-libxrandr
  lib32-pango
  lib32-wayland
)
makedepends=(
  git
  glib2-devel
  meson
  sassc
  wayland-protocols
)
source=(
  "git+https://gitlab.gnome.org/GNOME/gtk.git#tag=$pkgver"
  gtk-query-immodules-3.0-32.hook
  0001-Allow-disabling-legacy-Tracker-search.patch
)
b2sums=('f91f3973b5e411ddbd045a6bc395c57d0299a41bdf19dfba48509112d6c10a3fbc53e98eb7cf74743af67af7c8ee67a6a111d07ba3131629e061b1d850711e60'
        'c7136723735c4cbfe654851d0ccc1698142fa8f90b724ed9a970ebd6a74b4f59ea558e134e600736a1041417fc7fd9b3baa8bc561ed9598f2fbf45392dcc9159'
        'ae371c52f24d00153037622de12a7e0026223c50fcb4b83e88138e2fd8b69a27422dfc91dae3a83c24705c57370bc9d75de4228210041c10c55482443478e240')

prepare() {
  cd gtk

  # Try to fix monitor geometry on sway
  # https://gitlab.archlinux.org/archlinux/packaging/packages/gtk3/-/issues/8
  # https://gitlab.gnome.org/GNOME/gtk/-/issues/6939
  git cherry-pick -n 1cf0520454f8f55256b7f6cdfeefa8360a5842c7

  # Don't try to use the old Tracker
  git apply -3 ../0001-Allow-disabling-legacy-Tracker-search.patch
}

build() {
  local meson_options=(
    --cross-file lib32
    -D broadway_backend=true
    -D cloudproviders=false
    -D colord=yes
    -D gtk_doc=false
    -D introspection=false
    -D man=false
    -D tracker=false
    -D tracker3=false
  )

  CFLAGS+=" -DG_DISABLE_CAST_CHECKS"
  artix-meson gtk build "${meson_options[@]}"
  meson compile -C build
}

package() {
  optdepends=(
    'evince: Default print preview command'
  )
  provides=(
    libgailutil-3.so
    libgdk-3.so
    libgtk-3.so
  )
  install=lib32-gtk3.install

  meson install -C build --destdir "$pkgdir"
  rm -r "$pkgdir"/{etc,usr/{include,share}}
  find "$pkgdir/usr/bin" -type f -not -name gtk-query-immodules-3.0 -delete
  mv "$pkgdir"/usr/bin/gtk-query-immodules-3.0{,-32}

  install -Dm644 gtk-query-immodules-3.0-32.hook -t "$pkgdir/usr/share/libalpm/hooks"
}

# vim:set sw=2 sts=-1 et:
