# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=lib32-dbus
pkgname=(
  lib32-dbus
)
pkgver=1.16.0
pkgrel=1.1
pkgdesc="Freedesktop.org message bus system - 32-bit"
url="https://www.freedesktop.org/wiki/Software/dbus/"
arch=(x86_64)
license=("AFL-2.1 OR GPL-2.0-or-later")
depends=(
  dbus
  lib32-glibc
  lib32-elogind
)
makedepends=(
  lib32-glib2
  meson
  python
  elogind
)
source=(
  https://dbus.freedesktop.org/releases/dbus/dbus-$pkgver.tar.xz{,.asc}
  0001-Arch-Linux-tweaks-lib32.patch
)
b2sums=('a5a3ebe777c1c0296ba7240f9ed29ad329a6578a05baf10a469ce8c7d243791d35aca42a70d04cdd88feea238d081c3c8b0db444df24abcf7ce5ffe9187a0440'
        'SKIP'
        'f0a08dee0ad5841b41ff5dd5d4c581113d6078739dd5442ec76f4e1e02882a5d95194adc21804495f8f129e69be911db3b56c6dfa6ff5fb66359f0483679b7c8')
validpgpkeys=(
  DA98F25C0871C49A59EAFF2C4DE8FF2A63C7CC90  # Simon McVittie <simon.mcvittie@collabora.co.uk>
)

prepare() {
  cd dbus-$pkgver
  patch -Np1 -i ../0001-Arch-Linux-tweaks-lib32.patch
}

build() {
  local meson_options=(
    --cross-file lib32
    -D apparmor=disabled
    -D dbus_user=dbus
    -D doxygen_docs=disabled
    -D ducktype_docs=disabled
    -D kqueue=disabled
    -D launchd=disabled
    -D libaudit=disabled
    -D message_bus=false
    -D qt_help=disabled
    -D relocation=disabled
    -D selinux=disabled
    -D tools=false
    -D x11_autolaunch=disabled
    -D xml_docs=disabled
    -D systemd=disabled
  )

  artix-meson dbus-$pkgver build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_lib32-dbus() {
  depends+=(
    libelogind.so
  )
  provides=(
    lib32-libdbus
    libdbus-1.so
  )
  conflicts=(lib32-libdbus)
  replaces=(lib32-libdbus)

  meson install -C build --destdir "$pkgdir"

  rm -r "$pkgdir/usr/include"

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s dbus "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 sts=-1 et:
