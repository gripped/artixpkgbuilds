# Maintainer: Levente Polyak <anthraxx@archlinux.org>
# Contributor: Evangelos Foutras <foutrelis@archlinux.org>
# Contributor: Daurnimator <quae@daurnimator.com>

pkgname=lib32-libidn2
pkgver=2.3.8
pkgrel=1
pkgdesc='Free software implementation of IDNA2008, Punycode and TR46'
url='https://www.gnu.org/software/libidn/#libidn2'
arch=('x86_64')
license=('GPL2' 'LGPL3')
depends=('libidn2' 'lib32-glibc' 'lib32-libunistring')
provides=('libidn2.so')
makedepends=('gtk-doc' 'ruby-ronn-ng')
source=(https://ftp.gnu.org/gnu/libidn/libidn2-${pkgver}.tar.gz{,.sig})
sha256sums=('f557911bf6171621e1f72ff35f5b1825bb35b52ed45325dcdee931e5d3c0787a'
            'SKIP')
validpgpkeys=('B1D2BD1375BECB784CF4F8C4D73CF638C53C06BE') # Simon Josefsson <simon@josefsson.org>

prepare() {
  cd libidn2-${pkgver}
}

build() {
  cd libidn2-${pkgver}
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib32
  make
}

check() {
  cd libidn2-${pkgver}
  make check
}
package() {
  cd libidn2-${pkgver}
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}

# vim: ts=2 sw=2 et:
