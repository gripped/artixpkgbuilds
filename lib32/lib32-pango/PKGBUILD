# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Biru Ionut <ionut@archlinux.ro>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>

pkgbase=lib32-pango
pkgname=(
  lib32-pango
)
pkgver=1.58.2
pkgrel=1
epoch=1
pkgdesc="A library for layout and rendering of text (32-bit)"
url="https://www.pango.org/"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  lib32-cairo
  lib32-fontconfig
  lib32-freetype2
  lib32-fribidi
  lib32-gcc-libs
  lib32-glib2
  lib32-glibc
  lib32-harfbuzz
  lib32-libthai
  lib32-libx11
  lib32-libxft
  lib32-libxrender
  pango
)
makedepends=(
  git
  glib2-devel
  meson
)
source=("git+https://gitlab.gnome.org/GNOME/pango.git#tag=$pkgver")
b2sums=('4f5c5f63dc741f9357028863052ee5065f6198fe8e0e9720d6ed0a8c04452c693defb13d688cc3151a9efc0c41392f3c224fac6b0b75f8519682236764152d7d')

prepare() {
  cd pango
}

build() {
  local meson_options=(
    --cross-file lib32
    -D introspection=disabled
  )

  artix-meson pango build "${meson_options[@]}"
  meson compile -C build
}

# not running checks: validation breaks when deps
# (especially harfbuzz) don't match upstream CI

package_lib32-pango() {
  provides=(libpango{,cairo,ft2,xft}-1.0.so)

  meson install -C build --destdir "$pkgdir"

  rm -r "$pkgdir"/usr/{bin,include}
}

# vim:set sw=2 sts=-1 et:
