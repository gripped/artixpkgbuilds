# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Biru Ionut <ionut@archlinux.ro>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>

pkgbase=lib32-pango
pkgname=(
  lib32-pango
)
pkgver=1.52.1
pkgrel=1
epoch=1
pkgdesc="A library for layout and rendering of text (32-bit)"
url="https://www.pango.org/"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  lib32-cairo
  lib32-fribidi
  lib32-harfbuzz
  lib32-libthai
  lib32-libxft
  pango
)
makedepends=(
  git
  meson
)
_commit=089aa34fbbc09cec556b818bd634260b4d2124c6  # tags/1.52.1^0
source=("git+https://gitlab.gnome.org/GNOME/pango.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd pango
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd pango
}

build() {
  local meson_options=(
    --cross-file lib32
    -D introspection=disabled
  )

  artix-meson pango build "${meson_options[@]}"
  meson compile -C build
}

# not running checks: validation breaks when deps
# (especially harfbuzz) don't match upstream CI

package_lib32-pango() {
  provides=(libpango{,cairo,ft2,xft}-1.0.so)

  meson install -C build --destdir "$pkgdir"

  rm -r "$pkgdir"/usr/{bin,include}
}

# vim:set sw=2 sts=-1 et:
