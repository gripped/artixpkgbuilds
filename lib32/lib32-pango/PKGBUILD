# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Biru Ionut <ionut@archlinux.ro>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>

pkgbase=lib32-pango
pkgname=(
  lib32-pango
)
pkgver=1.56.4
pkgrel=2
epoch=1
pkgdesc="A library for layout and rendering of text (32-bit)"
url="https://www.pango.org/"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  lib32-cairo
  lib32-fontconfig
  lib32-freetype2
  lib32-fribidi
  lib32-gcc-libs
  lib32-glib2
  lib32-glibc
  lib32-harfbuzz
  lib32-libthai
  lib32-libx11
  lib32-libxft
  lib32-libxrender
  pango
)
makedepends=(
  git
  glib2-devel
  meson
)
source=("git+https://gitlab.gnome.org/GNOME/pango.git#tag=$pkgver")
b2sums=('2396a29025fca9e843b4aad11369aa89141f65d55ee92bea633fcac714d881616b4d5405687375afddbcf3453b46fbf4ad6e41ba1d85d6d763e5c7493f305771')

prepare() {
  cd pango

  # Fix a crash
  # https://gitlab.archlinux.org/archlinux/packaging/packages/pango/-/issues/3
  git cherry-pick -n c7dcffdc86a2ac4fe8e176aac3a6b71cb79adbe4
}

build() {
  local meson_options=(
    --cross-file lib32
    -D introspection=disabled
  )

  artix-meson pango build "${meson_options[@]}"
  meson compile -C build
}

# not running checks: validation breaks when deps
# (especially harfbuzz) don't match upstream CI

package_lib32-pango() {
  provides=(libpango{,cairo,ft2,xft}-1.0.so)

  meson install -C build --destdir "$pkgdir"

  rm -r "$pkgdir"/usr/{bin,include}
}

# vim:set sw=2 sts=-1 et:
