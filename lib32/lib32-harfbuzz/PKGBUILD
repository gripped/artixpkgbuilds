# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Florian Pritz <bluewind@xinu.at>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=lib32-harfbuzz
pkgname=(
  lib32-harfbuzz
  lib32-harfbuzz-cairo
  lib32-harfbuzz-icu
)
pkgver=13.1.1
pkgrel=2
pkgdesc="OpenType text shaping engine - 32-bit"
url="https://harfbuzz.github.io/"
arch=(x86_64)
license=(MIT)
makedepends=(
  git
  glib2-devel
  harfbuzz
  lib32-cairo
  lib32-freetype2
  lib32-gcc-libs
  lib32-glib2
  lib32-glibc
  lib32-icu
  lib32-libpng
  lib32-zlib
  meson
  python
  ragel
)
checkdepends=(
  python-fonttools
  python-setuptools
)
source=(
  "git+https://github.com/harfbuzz/harfbuzz?signed#tag=$pkgver"
)
b2sums=('3626b19545a4ea4dd92cf5f38b8cdffd3a5d7e3eae7c49df23821e2c05bc299ec62373836454e5f92ab9e90c4b4d8c9a360f16eefa1c0690153d4be9403af266')
validpgpkeys=(
  053D20F17CCCA9651B2C6FCB9AB24930C0B997A2 # Khaled Hosny <khaled@aliftype.com> (@khaledhosny)
  9F377DDB6D3153A48EB3EB1E63CC496475267693 # Caleb Maclennan <caleb@alerque.com> (@alerque)
  2277650A4E8BDFE4B7F6BE419FEE04E5D3531115 # Ebrahim Byagowi <ebrahim@gnu.org> (@ebraminio)
  EACF64F53455E2771BA661A4803B21859F015E4E # Behdad Esfahbod <behdad@behdad.org> (@behdad)
)

prepare() {
  cd harfbuzz
}

build() {
  local meson_options=(
    --cross-file lib32
    -D chafa=disabled
    -D cpp_std=c++17
    -D docs=disabled
    -D introspection=disabled
  )

  # Harfbuzz wants no exceptions
  CFLAGS="${CFLAGS/-fexceptions/}"
  CXXFLAGS="${CXXFLAGS/-fexceptions/}"

  artix-meson harfbuzz build "${meson_options[@]}"
  meson compile -C build
}

check() {
  mkdir -p tmp
  TMPDIR="$PWD/tmp" meson test -C build --print-errorlogs --no-rebuild
  rm -r tmp
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_lib32-harfbuzz() {
  depends=(
    harfbuzz
    lib32-freetype2 libfreetype.so
    lib32-gcc-libs
    lib32-glib2 libg{lib,object}-2.0.so
    lib32-glibc
    lib32-libpng libpng16.so
    lib32-zlib libz.so
  )
  provides=(libharfbuzz{,-{subset,gobject,raster,vector}}.so)

  meson install -C build --destdir "$pkgdir" --no-rebuild

  ( cd "$pkgdir"
    rm -r usr/{bin,include}

    _pick hb-cairo usr/lib32/libharfbuzz-cairo*
    _pick hb-cairo usr/lib32/pkgconfig/harfbuzz-cairo.pc

    _pick hb-icu usr/lib32/libharfbuzz-icu*
    _pick hb-icu usr/lib32/pkgconfig/harfbuzz-icu.pc
  )

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 harfbuzz/COPYING
}

package_lib32-harfbuzz-cairo() {
  pkgdesc+=" - Cairo integration"
  depends=(
    harfbuzz-cairo
    lib32-cairo libcairo.so
    lib32-freetype2
    lib32-glib2
    lib32-glibc
    lib32-harfbuzz libharfbuzz.so
  )
  provides=(libharfbuzz-cairo.so)

  mv hb-cairo/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 harfbuzz/COPYING
}

package_lib32-harfbuzz-icu() {
  pkgdesc+=" - ICU integration"
  depends=(
    harfbuzz-icu
    lib32-glibc
    lib32-harfbuzz libharfbuzz.so
    lib32-icu libicuuc.so
  )
  provides=(libharfbuzz-icu.so)

  mv hb-icu/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 harfbuzz/COPYING
}

# vim:set sw=2 sts=-1 et:
