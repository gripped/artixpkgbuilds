# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Florian Pritz <bluewind@xinu.at>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=lib32-harfbuzz
pkgname=(
  lib32-harfbuzz
  lib32-harfbuzz-cairo
  lib32-harfbuzz-icu
)
pkgver=12.3.1
pkgrel=1
pkgdesc="OpenType text shaping engine - 32-bit"
url="https://harfbuzz.github.io/"
arch=(x86_64)
license=(MIT)
depends=(
  harfbuzz
  lib32-freetype2
  lib32-gcc-libs
  lib32-glib2
  lib32-glibc
)
makedepends=(
  git
  glib2-devel
  lib32-cairo
  lib32-icu
  meson
  python
  ragel
)
checkdepends=(
  python-fonttools
  python-setuptools
)
source=("git+https://github.com/harfbuzz/harfbuzz?signed#tag=$pkgver")
b2sums=('481fd13acdc85f4f97541aedec24222f87e218a429f7c82625b162069d8d881e12781aaab75a30ea8ce1b69eb74785a21215ccf0eb0f78cd3fa2ceb3b98bfb11')
validpgpkeys=(
  053D20F17CCCA9651B2C6FCB9AB24930C0B997A2 # Khaled Hosny <khaled@aliftype.com> (@khaledhosny)
  9F377DDB6D3153A48EB3EB1E63CC496475267693 # Caleb Maclennan <caleb@alerque.com> (@alerque)
  2277650A4E8BDFE4B7F6BE419FEE04E5D3531115 # Ebrahim Byagowi <ebrahim@gnu.org> (@ebraminio)
  EACF64F53455E2771BA661A4803B21859F015E4E # Behdad Esfahbod <behdad@behdad.org> (@behdad)
)

prepare() {
  cd harfbuzz
}

build() {
  local meson_options=(
    --cross-file lib32
    -D chafa=disabled
    -D cpp_std=c++17
    -D docs=disabled
    -D introspection=disabled
  )

  # Harfbuzz wants no exceptions
  CFLAGS="${CFLAGS/-fexceptions/}"
  CXXFLAGS="${CXXFLAGS/-fexceptions/}"

  artix-meson harfbuzz build "${meson_options[@]}"
  meson compile -C build
}

check() {
  mkdir -p tmp
  TMPDIR="$PWD/tmp" meson test -C build --print-errorlogs
  rm -r tmp
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_lib32-harfbuzz() {
  depends+=(
    libfreetype.so
    libg{lib,object}-2.0.so
  )
  provides=(libharfbuzz{,-subset,-gobject}.so)

  meson install -C build --destdir "$pkgdir"

  ( cd "$pkgdir"
    rm -r usr/{bin,include}

    _pick hb-cairo usr/lib32/libharfbuzz-cairo*
    _pick hb-cairo usr/lib32/pkgconfig/harfbuzz-cairo.pc

    _pick hb-icu usr/lib32/libharfbuzz-icu*
    _pick hb-icu usr/lib32/pkgconfig/harfbuzz-icu.pc
  )

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 harfbuzz/COPYING
}

package_lib32-harfbuzz-cairo() {
  pkgdesc+=" - Cairo integration"
  depends=(
    harfbuzz-cairo
    lib32-cairo
    lib32-freetype2
    lib32-glib2
    lib32-glibc
    lib32-harfbuzz
    libcairo.so
    libharfbuzz.so
  )
  provides=(libharfbuzz-cairo.so)

  mv hb-cairo/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 harfbuzz/COPYING
}

package_lib32-harfbuzz-icu() {
  pkgdesc+=" - ICU integration"
  depends=(
    harfbuzz-icu
    lib32-glibc
    lib32-harfbuzz
    lib32-icu
    libharfbuzz.so
    libicuuc.so
  )
  provides=(libharfbuzz-icu.so)

  mv hb-icu/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 harfbuzz/COPYING
}

# vim:set sw=2 sts=-1 et:
