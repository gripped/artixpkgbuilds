# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: 3V0LU710N <db_eee-at-hotmail-dot-com>

_name=libusb
pkgname=lib32-libusb
pkgver=1.0.30
pkgrel=1
pkgdesc="A cross-platform user library to access USB devices (32-bit)"
arch=(x86_64)
url="http://libusb.info/"
_url="https://github.com/libusb/libusb"
license=(LGPL-2.1-or-later)
depends=(
  lib32-glibc
  lib32-elogind
  libusb=$pkgver
)
makedepends=(
  git
)
provides=(libusb-1.0.so)
source=(git+$_url.git#tag=v$pkgver)
sha512sums=('d21fae358ebef28a733d69af9c361b853a437295230d57fe662b1a57a46480e459d49de4147d8d3297066dcf54f9e28fde28311a50eeec53a7453b71ae8077c6')
b2sums=('5e5f8ae594acf3808fba8b0fc01e94317d16fb3d3f25b9b8484d30ca922a75b2c00441a94f49cd2b03aff53f73f46ba88f1f31f065cf7041cf8f968d48ad8869')

prepare() {
  cd $_name
  autoreconf -fiv
}

build() {
  local configure_options=(
    --prefix=/usr
    --libdir=/usr/lib32
    --disable-static
  )

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd $_name
  ./configure "${configure_options[@]}"
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package () {
  make DESTDIR="$pkgdir" install -C $_name
  rm -rf "$pkgdir"/usr/include
  install -vDm 644 $_name/{AUTHORS,ChangeLog,NEWS,README,TODO} -t "$pkgdir/usr/share/doc/$pkgname/"
}
