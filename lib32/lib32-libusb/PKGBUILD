# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: 3V0LU710N <db_eee-at-hotmail-dot-com>

_name=libusb
pkgname=lib32-libusb
pkgver=1.0.29
pkgrel=1
pkgdesc="A cross-platform user library to access USB devices (32-bit)"
arch=(x86_64)
url="http://libusb.info/"
_url="https://github.com/libusb/libusb"
license=(LGPL-2.1-or-later)
depends=(
  lib32-glibc
  lib32-elogind
  libusb=$pkgver
)
makedepends=(
  git
)
provides=(libusb-1.0.so)
source=(git+$_url.git#tag=v$pkgver)
sha512sums=('1f30c7f4b57352db7c4e786043a431bb52a9faab3fc3fa17d7112d5671da8cc894ec1b8e117185081ea2faad28177d8db4b5fde053327bdadea327e099669cc5')
b2sums=('759be08ecf6d7e3c60427223dda4667538b7de32fe3fe6769b53c46859fec31f678967828fe278253a03677cf9c49a672b08ef39a525875c03ec04c0e1086015')

prepare() {
  cd $_name
  autoreconf -fiv
}

build() {
  local configure_options=(
    --prefix=/usr
    --libdir=/usr/lib32
    --disable-static
  )

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd $_name
  ./configure "${configure_options[@]}"
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package () {
  make DESTDIR="$pkgdir" install -C $_name
  rm -rf "$pkgdir"/usr/include
  install -vDm 644 $_name/{AUTHORS,ChangeLog,NEWS,README,TODO} -t "$pkgdir/usr/share/doc/$pkgname/"
}
