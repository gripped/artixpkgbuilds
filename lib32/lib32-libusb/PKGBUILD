# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: 3V0LU710N <db_eee-at-hotmail-dot-com>

_name=libusb
pkgname=lib32-libusb
pkgver=1.0.28
pkgrel=1
pkgdesc="A cross-platform user library to access USB devices (32-bit)"
arch=(x86_64)
url="http://libusb.info/"
_url="https://github.com/libusb/libusb"
license=(LGPL-2.1-or-later)
depends=(
  lib32-glibc
  lib32-elogind
  libusb=$pkgver
)
makedepends=(gcc-multilib)
provides=(libusb-1.0.so)
source=($_url/releases/download/v$pkgver/$_name-$pkgver.tar.bz2)
sha512sums=('0f4efa6b54e6195d2e5446652c2dc07358583e205d63bf438c4409511b8637d1700a71268c40499755747827d23cc730d9122267386f847bf781993c045c519f')
b2sums=('9bf506455fc1b981de155600936ab229f089ea28e58491076eab632455c4e814bf0336cd9326056a255508aa3956643c503af3ff2feda80bac036cf5cad86e80')

prepare() {
  cd $_name-$pkgver
  autoreconf -fiv
}

build() {
  local configure_options=(
    --prefix=/usr
    --libdir=/usr/lib32
    --disable-static
  )

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd $_name-$pkgver
  ./configure "${configure_options[@]}"
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package () {
  make DESTDIR="$pkgdir" install -C $_name-$pkgver
  rm -rf "$pkgdir"/usr/include
  install -vDm 644 $_name-$pkgver/{AUTHORS,ChangeLog,NEWS,README,TODO} -t "$pkgdir/usr/share/doc/$pkgname/"
}
