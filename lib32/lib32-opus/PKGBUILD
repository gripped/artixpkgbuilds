# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: farwayer <farwayer [at] gmail [dot] com>

pkgbase=lib32-opus
pkgname=(
  lib32-opus
)
pkgver=1.6.1
pkgrel=1
pkgdesc="Totally open, royalty-free, highly versatile audio codec (32-bit)"
url="https://www.opus-codec.org/"
arch=(x86_64)
license=(BSD-3-Clause)
depends=(
  lib32-gcc-libs
  lib32-glibc
  opus
)
makedepends=(
  meson
)
source=("https://downloads.xiph.org/releases/opus/opus-$pkgver.tar.gz")
b2sums=('dda8fb4e30e5f22d2b37616902892dcc715a2886150ab54f67d61a90c60a03afee05fb7b56a0d88355db007bb4d72c8bd6de3d874a49767318a5bba115b1a613')

# https://downloads.xiph.org/releases/opus/SHA256SUMS.txt
sha256sums=('6ffcb593207be92584df15b32466ed64bbec99109f007c82205f0194572411a1')

# Git doesn't contain model data
#source=("git+https://gitlab.xiph.org/xiph/opus.git?signed#tag=v$pkgver")
#validpgpkeys=(43E96DE704A2D88B339759C35E5DD9A36F9189C8) # Jean-Marc Valin <jmvalin@jmvalin.ca>

prepare() {
  ln -s opus-$pkgver opus
  cd opus
}

build() {
  local meson_options=(
    --cross-file lib32
    -D asm=disabled
    -D custom-modes=true
    -D deep-plc=enabled
    -D docs=disabled
    -D dred=enabled
    -D osce=enabled
  )

  artix-meson opus build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_lib32-opus() {
  provides=(libopus.so)

  meson install -C build --destdir "$pkgdir"

  rm -r "$pkgdir/usr/include"

  install -Dm644 opus/COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 sts=-1 et:
