# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Florian Pritz <bluewind@xinu.at>
# Contributor: Christoph Vigano <mail@cvigano.de>

pkgbase=lib32-p11-kit
pkgname=(
  lib32-p11-kit
)
pkgver=0.25.0
pkgrel=1
pkgdesc="Loads and enumerates PKCS#11 modules (32-bit library)"
url="https://p11-glue.freedesktop.org"
arch=(x86_64)
license=(BSD)
depends=(
  lib32-glibc
  lib32-libffi
  lib32-libtasn1
  libp11-kit
)
makedepends=(
  meson
)
source=(https://github.com/p11-glue/p11-kit/releases/download/$pkgver/p11-kit-$pkgver.tar.xz{,.sig})
b2sums=('6ffce977f86c516a327afe50f4cc5a36e86ba7f43c6cb555db419d9e4ba7543a9f1847ba83da348cd6d7bbebe55dfa26cfe3a3aaa3e1d5420a4b8dc6cbbff088'
        'SKIP')
validpgpkeys=(
  C0F67099B808FB063E2C81117BFB1108D92765AF  # Stef Walter <stef@thewalter.net>
  462225C3B46F34879FC8496CD605848ED7E69871  # Daiki Ueno <ueno@unixuser.org>
  5D46CB0F763405A7053556F47A75A648B3F9220C  # Zoltan Fridrich <zfridric@redhat.com>
)

prepare() {
  # Override p11tool so the tests don't try to use it (wrong arch)
  mkdir -p path
  ln -sf /usr/bin/false path/p11tool

  cd p11-kit-$pkgver
}

build() {
  local meson_options=(
    --libdir /usr/lib32
    -D bash_completion=disabled
    -D systemd=disabled
    -D trust_paths=/etc/ca-certificates/trust-source:/usr/share/ca-certificates/trust-source
  )

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  artix-meson p11-kit-$pkgver build "${meson_options[@]}"
  meson compile -C build
}

check() {
  PATH="$srcdir/path:$PATH" meson test -C build --print-errorlogs
}

package_lib32-p11-kit() {
  provides=(libp11-kit.so)

  meson install -C build --destdir "$pkgdir"

  rm -r "$pkgdir"/{etc,usr/{bin,include,lib,share}}

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 p11-kit-$pkgver/COPYING
}

# vim:set sw=2 sts=-1 et:
