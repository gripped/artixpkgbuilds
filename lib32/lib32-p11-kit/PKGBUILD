# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Florian Pritz <bluewind@xinu.at>
# Contributor: Christoph Vigano <mail@cvigano.de>

pkgbase=lib32-p11-kit
pkgname=(
  lib32-p11-kit
)
pkgver=0.25.10
pkgrel=2
pkgdesc="Loads and enumerates PKCS#11 modules (32-bit library)"
url="https://p11-glue.freedesktop.org"
arch=(x86_64)
license=(BSD-3-Clause)
depends=(
  lib32-glibc
  lib32-libffi
  lib32-libtasn1
  libp11-kit
)
makedepends=(
  git
  meson
)
checkdepends=(
  lib32-gnutls
)
source=(
  "git+https://github.com/p11-glue/p11-kit?signed#tag=$pkgver"
  git+https://github.com/p11-glue/pkcs11-json
)
b2sums=('47408bf6bded1f5de38d168a67fcfae460685fc6ee56627db99d9e2148c2f3dfeab0fce94f31026ca423b980b3df283a920cc5f87dc366699451f0e18d3b6497'
        'SKIP')
validpgpkeys=(
  C0F67099B808FB063E2C81117BFB1108D92765AF  # Stef Walter <stef@thewalter.net>
  462225C3B46F34879FC8496CD605848ED7E69871  # Daiki Ueno <ueno@unixuser.org>
  5D46CB0F763405A7053556F47A75A648B3F9220C  # Zoltan Fridrich <zfridric@redhat.com>
)

prepare() {
  cd p11-kit

  git submodule init
  git submodule set-url subprojects/pkcs11-json "$srcdir/pkcs11-json"
  git -c protocol.file.allow=always -c protocol.allow=never submodule update
}

build() {
  local meson_options=(
    --cross-file lib32
    -D bash_completion=disabled
    -D systemd=disabled
    -D trust_paths=/etc/ca-certificates/trust-source:/usr/share/ca-certificates/trust-source
  )

  artix-meson p11-kit build "${meson_options[@]}"
  meson compile -C build
}

check() {
  P11TOOL=p11tool-32 meson test -C build --print-errorlogs
}

package_lib32-p11-kit() {
  provides=(libp11-kit.so)

  meson install -C build --destdir "$pkgdir"

  rm -r "$pkgdir"/{etc,usr/{bin,include,lib,share}}

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 p11-kit/COPYING
}

# vim:set sw=2 sts=-1 et:
