# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=lib32-gdk-pixbuf2
pkgname=(
  lib32-gdk-pixbuf2
)
pkgver=2.44.6
pkgrel=2
pkgdesc="An image loading library (32-bit)"
url="https://gitlab.gnome.org/GNOME/gdk-pixbuf"
arch=(x86_64)
license=(LGPL-2.0-or-later)
depends=(
  gdk-pixbuf2
  lib32-glib2
  lib32-glibc
  lib32-libjpeg-turbo
  lib32-libpng
  lib32-libtiff
  shared-mime-info
)
makedepends=(
  git
  glib2-devel
  meson
)
source=(
  "git+https://gitlab.gnome.org/GNOME/gdk-pixbuf.git#tag=$pkgver"
  gdk-pixbuf-query-loaders-32.hook
)
b2sums=('e24fa06f587e81a4b5f5a265a1e449ab0c8d0ae75296cba1bfb8359ae72e3148b16c8b94767ae4ab20dd4017559e18133e43084a63557fa2af7f9deafec6cc28'
        '0b432bdeb31acdd66c8a861551cabf4f83efd3c441614dbb64b7ac11fdbb97c76412b5706bb18ff1ed890de0a4c51bf02bb531eb4693e6d68021b5372cb5897a')

prepare() {
  cd gdk-pixbuf

  # Unbreak gdk_pixbuf_new_from_xpm_data
  # https://gitlab.archlinux.org/archlinux/packaging/packages/gdk-pixbuf2/-/work_items/13
  git cherry-pick -n 62b8f9fd0bb3b862823cd34afce4b389fbd27569
}

build() {
  local meson_options=(
    --cross-file lib32
    -D android=disabled
    -D builtin_loaders=all
    -D documentation=false
    -D gif=enabled
    -D glycin=disabled
    -D gtk_doc=false
    -D installed_tests=false
    -D introspection=disabled
    -D jpeg=enabled
    -D legacy_xpm=enabled
    -D man=false
    -D others=enabled
    -D png=enabled
    -D thumbnailer=disabled
    -D tiff=enabled
  )

  artix-meson gdk-pixbuf build "${meson_options[@]}"
  meson compile -C build
}

package() {
  optdepends=(
    "lib32-librsvg: Load .svg, .svgz and .svg.gz"
  )
  provides=(libgdk_pixbuf-2.0.so)
  install=lib32-gdk-pixbuf2.install

  meson install -C build --destdir "$pkgdir"

  rm -rf "$pkgdir"/usr/{include,lib,share}
  find "$pkgdir/usr/bin" -type f -not -name gdk-pixbuf-query-loaders -delete
  mv "$pkgdir"/usr/bin/gdk-pixbuf-query-loaders{,-32}

  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 *.hook
}

# vim:set sw=2 sts=-1 et:
