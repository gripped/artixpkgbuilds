# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=lib32-gdk-pixbuf2
pkgname=(
  lib32-gdk-pixbuf2
)
pkgver=2.44.8
pkgrel=2
pkgdesc="An image loading library (32-bit)"
url="https://gitlab.gnome.org/GNOME/gdk-pixbuf"
arch=(x86_64)
license=(LGPL-2.0-or-later)
depends=(
  gdk-pixbuf2
  lib32-glib2
  lib32-glibc
  lib32-libjpeg-turbo
  lib32-libpng
  lib32-libtiff
  shared-mime-info
)
makedepends=(
  git
  glib2-devel
  meson
)
source=(
  "git+https://gitlab.gnome.org/GNOME/gdk-pixbuf.git#tag=$pkgver"
  gdk-pixbuf-query-loaders-32.hook
  gdk-pixbuf-remove-loaders-cache-32.hook
)
b2sums=('d553cb0654965cb153cd38385d39d6edafc5b9890661a7fce14713d40f53129057c4ba81970774bd449029a52aa3b06033a22dc9cfe5c263eb87f35095251340'
        '5d2073e3aae29c174634f610def710efe4fe54ba5ba75ecb76cbc3d1638d10fba41ff392655ff0ae5b2c2b55c362f5c4666011e3f1a0b9e0bb0501fdd78ca906'
        'defda0c7274d95fa4f847f12afb6ad76570de2adc2f17c2b5bc16398e6c146691bcbd32f15ae06c8204c8b3a7608e5448cb28400b481c0c5cb5440ea47549435')

prepare() {
  cd gdk-pixbuf
}

build() {
  local meson_options=(
    --cross-file lib32
    -D android=disabled
    -D builtin_loaders=all
    -D documentation=false
    -D gif=enabled
    -D glycin=disabled
    -D gtk_doc=false
    -D installed_tests=false
    -D introspection=disabled
    -D jpeg=enabled
    -D legacy_xpm=enabled
    -D man=false
    -D others=enabled
    -D png=enabled
    -D thumbnailer=disabled
    -D tiff=enabled
  )

  artix-meson gdk-pixbuf build "${meson_options[@]}"
  meson compile -C build
}

package() {
  optdepends=(
    "lib32-librsvg: Load .svg, .svgz and .svg.gz"
  )
  provides=(libgdk_pixbuf-2.0.so)

  meson install -C build --destdir "$pkgdir"

  rm -rf "$pkgdir"/usr/{include,lib,share}
  find "$pkgdir/usr/bin" -type f -not -name gdk-pixbuf-query-loaders -delete
  mv "$pkgdir"/usr/bin/gdk-pixbuf-query-loaders{,-32}

  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 *.hook
}

# vim:set sw=2 sts=-1 et:
