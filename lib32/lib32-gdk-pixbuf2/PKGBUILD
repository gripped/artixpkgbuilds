# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=lib32-gdk-pixbuf2
pkgname=(
  lib32-gdk-pixbuf2
)
pkgver=2.44.3
pkgrel=1
pkgdesc="An image loading library (32-bit)"
url="https://wiki.gnome.org/Projects/GdkPixbuf"
arch=(x86_64)
license=(LGPL-2.0-or-later)
depends=(
  gdk-pixbuf2
  lib32-glib2
  lib32-glibc
  lib32-libjpeg-turbo
  lib32-libpng
  lib32-libtiff
  shared-mime-info
)
makedepends=(
  git
  glib2-devel
  meson
)
source=(
  "git+https://gitlab.gnome.org/GNOME/gdk-pixbuf.git#tag=$pkgver"
  gdk-pixbuf-query-loaders-32.hook
)
b2sums=('2e56c33e61b6e2f2fafaa1f7997878b1de992e86b922e0da94f16b5112d72d95a88644b1f9d215f911fbb2a18e07bd67850af75b2afb0a61d5d575460fcb0945'
        '0b432bdeb31acdd66c8a861551cabf4f83efd3c441614dbb64b7ac11fdbb97c76412b5706bb18ff1ed890de0a4c51bf02bb531eb4693e6d68021b5372cb5897a')

prepare() {
  cd gdk-pixbuf
}

build() {
  local meson_options=(
    --cross-file lib32
    -D android=disabled
    -D builtin_loaders=all
    -D documentation=false
    -D gif=enabled
    -D glycin=disabled
    -D gtk_doc=false
    -D installed_tests=false
    -D introspection=disabled
    -D jpeg=enabled
    -D man=false
    -D others=enabled
    -D png=enabled
    -D thumbnailer=disabled
    -D tiff=enabled
  )

  artix-meson gdk-pixbuf build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  optdepends=(
    "lib32-librsvg: Load .svg, .svgz and .svg.gz"
  )
  provides=(libgdk_pixbuf-2.0.so)
  install=lib32-gdk-pixbuf2.install

  meson install -C build --destdir "$pkgdir"

  rm -rf "$pkgdir"/usr/{include,lib,share}
  find "$pkgdir/usr/bin" -type f -not -name gdk-pixbuf-query-loaders -delete
  mv "$pkgdir"/usr/bin/gdk-pixbuf-query-loaders{,-32}

  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 *.hook
}

# vim:set sw=2 sts=-1 et:
