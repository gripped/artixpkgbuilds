# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=lib32-libpulse
pkgname=(
  lib32-libpulse
)
pkgver=17.0+r88+geee0e8f22
pkgrel=1
pkgdesc="A featureful, general-purpose sound server (32-bit client libraries)"
url="https://www.freedesktop.org/wiki/Software/PulseAudio/"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  lib32-dbus
  lib32-gcc-libs
  lib32-glib2
  lib32-glibc
  lib32-libasyncns
  lib32-libsndfile
  lib32-libxcb
  libpulse
)
makedepends=(
  git
  lib32-libx11
  meson
  valgrind
)
optdepends=('lib32-alsa-plugins: ALSA support')
provides=("lib32-pulseaudio=$pkgver")
conflicts=(lib32-pulseaudio)
replaces=(lib32-pulseaudio)
_commit=eee0e8f22fdcb2fd4fbfa4311df7e5070239d435  # master
source=(
  "git+https://gitlab.freedesktop.org/pulseaudio/pulseaudio.git#commit=$_commit"
)
b2sums=('17981ec59730676b45fc1a0b36ee7d19fe099428e13a71ea904c4b3691a274d47846d71238bd29f8090e478ce59ef48b0b1ee2b1ab02ac67aa7b12a94b5c035d')

pkgver() {
  cd pulseaudio
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd pulseaudio

  # Freeze version before patching
  ./git-version-gen doesnt-exist >.tarball-version
}

build() {
  local meson_options=(
    --cross-file lib32
    -D avahi=disabled
    -D bashcompletiondir=no
    -D bluez5-gstreamer=disabled
    -D bluez5=disabled
    -D consolekit=disabled
    -D daemon=false
    -D doxygen=false
    -D elogind=disabled
    -D fftw=disabled
    -D gstreamer=disabled
    -D gtk=disabled
    -D jack=disabled
    -D lirc=disabled
    -D man=false
    -D orc=disabled
    -D pulsedsp-location='/usr/\$LIB/pulseaudio'
    -D soxr=disabled
    -D speex=disabled
    -D stream-restore-clear-old-devices=true
    -D tcpwrap=disabled
    -D tests=false
    -D udevrulesdir=/usr/lib/udev/rules.d
    -D webrtc-aec=disabled
    -D zshcompletiondir=no
-D systemduserunitdir=no
-D systemd=disabled
  )

  artix-meson pulseaudio build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_lib32-libpulse() {
  meson install -C build --destdir "$pkgdir"
  rm -r "$pkgdir/etc" "$pkgdir"/usr/{bin,include,share}
}

# vim:set sw=2 sts=-1 et:
