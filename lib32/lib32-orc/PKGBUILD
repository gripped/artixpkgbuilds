# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=lib32-orc
pkgver=0.4.41
pkgrel=1
pkgdesc="Optimized Inner Loop Runtime Compiler (32-bit)"
arch=(x86_64)
url=https://gstreamer.freedesktop.org/modules/orc.html
license=(BSD-3-Clause)
depends=(
  lib32-gcc-libs
  lib32-glibc
  orc
)
makedepends=(
  git
  gtk-doc
  meson
  valgrind
)
provides=(liborc{,-test}-${pkgver%.*}.so)
_tag=df0b576b8d1e4b27425d53e1f8638c8bf34e0d42
source=(git+https://gitlab.freedesktop.org/gstreamer/orc.git?signed#tag=${_tag})
b2sums=('eb29733e3033b272c718a622e774379d993aa50ca4f23be0ef1d7dc1e99c267b0c07173f2a79ce47cbf1cb1fecfe2742c48e43994b15308c703aecf8887526e3')
validpgpkeys=(
  D637032E45B8C6585B9456565D2EEE6F6F349D7C # Tim-Philipp MÃ¼ller <tim@centricular.com>
)

prepare() {
  cd orc
  git describe --tags
}

build() {
  artix-meson orc build --cross-file lib32
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  rm -r "$pkgdir"/usr/{bin,include,share}
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 orc/COPYING
}

# vim:set sw=2 sts=-1 et:
