# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=lib32-fontconfig
pkgver=2.17.0
pkgrel=2
epoch=2
pkgdesc="Library for configuring and customizing font access"
url=https://www.freedesktop.org/wiki/Software/fontconfig/
arch=(x86_64)
license=('HPND AND Unicode-DFS-2016')
depends=(
  fontconfig
  lib32-expat
  lib32-freetype2
  lib32-glibc
)
makedepends=(
  git
  gperf
  meson
)
install=fontconfig-32.install
source=(
  "git+https://gitlab.freedesktop.org/fontconfig/fontconfig.git#tag=$pkgver"
  fontconfig-32.hook
)
b2sums=('8ba4930ffcc3c6ad0af99e40bc6199dd674fccc2597746935f0e99fe23aae989e7f9194eb4804a704129e0280047abcad4e55b75daaa76eb0ba14d86738a1ad1'
        '1cba71810c9bde6ecb6fac124e458fb7260be3ea72ade82b836e0e8e1eaa7c7df31e6e92e405fa420325cec0ce14d0f19630e777308032b0c26ec96a3d668d93')
validpgpkeys=(
  F77A64C4B5B45FF8763A278F65755979B34E1294 # Akira TAGOH <akira@tagoh.org>
  ECFFBC3A6B365E721E5BD79830757AA21971A672 # Akira TAGOH <akira@tagoh.org>
)

prepare() {
  cd fontconfig

  # Fix a buffer overflow
  # https://gitlab.archlinux.org/archlinux/packaging/packages/fontconfig/-/issues/3
  git cherry-pick -n b9bec06d73340f1b5727302d13ac3df307b7febc
}

build() {
  local meson_options=(
    --cross-file lib32
    -D doc=disabled
  )

  artix-meson fontconfig build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(
    libexpat.so
    libfreetype.so
  )
  provides+=(libfontconfig.so)

  meson install -C build --destdir "$pkgdir"

  rm -r "$pkgdir"/{etc,usr/{include,share},var}

  mv "$pkgdir"/usr/bin/fc-cache{,-32}
  find "$pkgdir"/usr/bin -type f -not -name '*-32' -delete

  install -Dm644 *.hook -t "$pkgdir/usr/share/libalpm/hooks"
  install -Dm644 fontconfig/COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 sts=-1 et:
