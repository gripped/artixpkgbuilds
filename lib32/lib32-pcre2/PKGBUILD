# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=lib32-pcre2
_name="${pkgname#lib32-}"
pkgver=10.49
pkgrel=1
pkgdesc='A library that implements Perl 5-style regular expressions. 2nd version (32-bit)'
arch=(x86_64)
url='https://github.com/PCRE2Project/pcre2'
license=(
  BSD-2-Clause
  'BSD-3-Clause WITH PCRE2-exception'
)
depends=(
  lib32-glibc
  "$_name=$pkgver"
)
makedepends=(
  git
  lib32-bzip2
  lib32-readline
  lib32-zlib
)
provides=(libpcre2-{8,16,32,posix}.so)
source=(
  $_name::git+$url?signed#tag=$_name-$pkgver
  sljit::git+https://github.com/zherczeg/sljit.git
)
sha512sums=('3112ecb32bcbb8840c70e263915d0a690ae2cbc6b5160ff05d96419353290d5e2740a9dd4f7d59fff697bab58695751fedd3b665a7649e2b7c3af417d7f74698'
            'SKIP')
b2sums=('120754ca82467149bfb02c6b3f778a25c7a65b968ac358e01b1cdbb9114805a1e75585e049c8a25e0f6723a314798e39f6caa97a55aef3ce9d84e60566859592'
        'SKIP')
validpgpkeys=(
  45F68D54BBE23FB3039B46E59766E084FB0F43D8  # Philip Hazel <ph10@hermes.cam.ac.uk>
  A95536204A3BB489715231282A98E77EB6F24CA8  # Nicholas Wilson <nicholas@nicholaswilson.me.uk>
)

prepare() {
  cd $_name

  git submodule init
  git config submodule."deps/sljit".url ../sljit
  git -c protocol.file.allow=always submodule update

  ./autogen.sh

  # extract licenses
  cp -v deps/sljit/LICENSE ../BSD-2-Clause.txt
  sed -n '70,94p' LICENCE.md > ../BSD-3-Clause.txt
  sed -n '100,104p' LICENCE.md > ../PCRE2-exception.txt
}

build() {
  local configure_options=(
    --enable-jit
    --enable-pcre2-16
    --enable-pcre2-32
    --enable-pcre2grep-libbz2
    --enable-pcre2grep-libz
    --enable-pcre2test-libreadline
    --libdir=/usr/lib32
    --prefix=/usr
  )

  cd $_name

  export CFLAGS+=" -m32"
  export CXXFLAGS+=" -m32"
  export LDFLAGS+=" -m32"
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure "${configure_options[@]}"
  make
}

check() {
  make -j1 check -C $_name
}

package() {
  make DESTDIR="$pkgdir" install -C $_name

  rm -rv "$pkgdir"/usr/{bin,share,include}

  install -Dm644 ./*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}

# vim:set sw=2 sts=-1 et:
