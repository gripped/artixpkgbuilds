# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>

pkgbase=lib32-json-glib
pkgname=(
  lib32-json-glib
)
pkgver=1.8.0
pkgrel=2
pkgdesc="JSON library built on GLib (32-bit)"
url="https://gnome.pages.gitlab.gnome.org/json-glib/"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  json-glib
  lib32-glib2
  lib32-glibc
)
makedepends=(
  git
  glib2-devel
  meson
)
source=("git+https://gitlab.gnome.org/GNOME/json-glib.git?signed#tag=$pkgver-actual")
b2sums=('755b85f1deda24188d421debbf32563d47873228d2115cf86ffb46695d8dec8ed2e4f627489841d04a8625db73e40861dc29d18b1af2c5b5de5a4620d3a25ecc')
validpgpkeys=(
  53EF3DC3B63E2899271BD26322E8091EEA11BBB7 # Emmanuele Bassi <ebassi@gnome.org>
)

prepare() {
  cd json-glib
}

build() {
  local meson_options=(
    --cross-file lib32
    -D gtk_doc=disabled
    -D introspection=disabled
  )

  artix-meson json-glib build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_lib32-json-glib() {
  depends+=(libg{lib,object,io}-2.0.so)
  provides+=(libjson-glib-1.0.so)

  meson install -C build --destdir "$pkgdir"

  rm -r "$pkgdir"/usr/{bin,include,lib,share}
}

# vim:set sw=2 sts=-1 et:
