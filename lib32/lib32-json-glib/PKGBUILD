# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>

pkgbase=lib32-json-glib
pkgname=(
  lib32-json-glib
)
pkgver=1.8.0
pkgrel=1
pkgdesc="JSON library built on GLib (32-bit)"
url="https://wiki.gnome.org/Projects/JsonGlib"
arch=(x86_64)
license=(GPL)
depends=(
  json-glib
  lib32-glib2
)
makedepends=(
  git
  meson
)
_commit=66413437f80f6bd935827eef51dde47c0a114144  # tags/1.8.0-actual^0
source=("git+https://gitlab.gnome.org/GNOME/json-glib.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd json-glib
  git describe --tags | sed 's/-actual//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd json-glib
}

build() {
  local meson_options=(
    --cross-file lib32
    -D gtk_doc=disabled
    -D introspection=disabled
  )

  artix-meson json-glib build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_lib32-json-glib() {
  depends+=(libg{lib,object,io}-2.0.so)
  provides+=(libjson-glib-1.0.so)

  meson install -C build --destdir "$pkgdir"

  rm -r "$pkgdir"/usr/{bin,include,lib,share}
}

# vim:set sw=2 sts=-1 et:
