# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=lib32-pipewire
pkgname=(
  lib32-pipewire
  lib32-libpipewire
  lib32-pipewire-jack
  lib32-pipewire-v4l2
)
pkgver=1.2.1
pkgrel=1.1
epoch=1
pkgdesc="Low-latency audio/video router and processor - 32-bit"
url="https://pipewire.org"
arch=(x86_64)
license=(MIT)
makedepends=(
  git
  lib32-alsa-lib
  lib32-dbus
  lib32-glib2
  meson
)
source=(
  "git+https://gitlab.freedesktop.org/pipewire/pipewire.git#tag=$pkgver"
)
b2sums=('c956faaa5caeb95ea9c802744c0584f25b251f3f7d5460f938aafe75def298df136824a9aff5ce7ecaad6241db51e88a19f39a89aa9e30e2256bd8cee21dbcbb')

prepare() {
  cd pipewire
}

build() {
  local meson_options=(
    --cross-file lib32
    -D avahi=disabled
    -D avb=disabled
    -D bluez5-codec-lc3=disabled
    -D bluez5-codec-lc3plus=disabled
    -D bluez5-codec-opus=disabled
    -D bluez5=disabled
    -D compress-offload=disabled
    -D docs=disabled
    -D echo-cancel-webrtc=disabled
    -D examples=disabled
    -D gstreamer-device-provider=disabled
    -D gstreamer=disabled
    -D jack-devel=true
    -D jack=disabled
    -D libcamera=disabled
    -D libcanberra=disabled
    -D libffado=disabled
    -D libjack-path=/usr/lib32
    -D libmysofa=disabled
    -D libpulse=disabled
    -D libusb=disabled
    -D lv2=disabled
    -D man=disabled
    -D opus=disabled
    -D pw-cat=disabled
    -D raop=disabled
    -D readline=disabled
    -D rlimits-install=false
    -D roc=disabled
    -D sdl2=disabled
    -D selinux=disabled
    -D session-managers=[]
    -D snap=disabled
    -D sndfile=disabled
    -D tests=disabled
    -D udevrulesdir=/usr/lib/udev/rules.d
    -D systemd=disabled
    -D rlimits-install=false
    -D v4l2=disabled
    -D x11-xfixes=disabled
    -D x11=disabled
  )

  artix-meson pipewire build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

_pwname=pipewire-0.3
_spaname=spa-0.2

package_lib32-pipewire() {
  depends=(
    "lib32-libpipewire=$epoch:$pkgver-$pkgrel"
    lib$_pwname.so
    lib32-gcc-libs
    lib32-glibc
    libasound.so
    libdbus-1.so
    libglib-2.0.so
    pipewire
  )
  optdepends=(
    'lib32-pipewire-jack: JACK support'
    'lib32-pipewire-v4l2: V4L2 interceptor'
  )

  meson install -C build --destdir "$pkgdir"

  (
    cd "$pkgdir"

    _pick lib usr/lib32/lib$_pwname.so*
    _pick lib usr/lib32/pkgconfig/lib{$_pwname,$_spaname}.pc

    _pick jack usr/lib32/libjack*
    _pick jack usr/lib32/pkgconfig/jack.pc

    _pick v4l2 usr/lib32/$_pwname/v4l2

    rm -r usr/{bin,include,lib,share}
  )

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}

package_lib32-libpipewire() {
  pkgdesc+=" - client library"
  depends=(
    lib32-gcc-libs
    lib32-glibc
    libpipewire
  )
  provides=(lib$_pwname.so)

  mv lib/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}

package_lib32-pipewire-jack() {
  pkgdesc+=" - JACK support"
  license+=(
    # libjackserver
    GPL-2.0-only
    LGPL-2.1-or-later
  )
  depends=(
    lib$_pwname.so
    lib32-glibc
    lib32-pipewire
    pipewire-jack
  )
  conflicts=(
    lib32-jack
    lib32-jack2
  )
  provides=(
    lib32-jack
    libjack.so
    libjacknet.so
    libjackserver.so
  )

  mv jack/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}

package_lib32-pipewire-v4l2() {
  pkgdesc+=" - V4L2 interceptor"
  depends=(
    lib$_pwname.so
    lib32-glibc
    lib32-pipewire
    pipewire-v4l2
  )

  mv v4l2/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}

# vim:set sw=2 sts=-1 et:
