# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=lib32-pipewire
pkgname=(
  lib32-pipewire
  lib32-libpipewire
  lib32-pipewire-jack
  lib32-pipewire-v4l2
)
pkgver=1.4.8
pkgrel=2
epoch=1
pkgdesc="Low-latency audio/video router and processor - 32-bit"
url="https://pipewire.org"
arch=(x86_64)
license=(MIT)
makedepends=(
  git
  lib32-alsa-lib
  lib32-dbus
  lib32-glib2
  meson
)
source=(
  "git+https://gitlab.freedesktop.org/pipewire/pipewire.git#tag=$pkgver"
)
b2sums=('cf0125e7c4a7abbd760a8e389b61716107f11ef1cdb077eddbca1dc2b327bfe6d4476005e4fcf8b11312120bed0da0f1b9f2a23d568a56df515cbfb34c96e12f')

prepare() {
  cd pipewire

  # Fix xruns
  # https://gitlab.archlinux.org/archlinux/packaging/packages/pipewire/-/issues/21
  git cherry-pick -n 3e574b314a7d73801fe3aac012cf6671bb1f5575 \
                     24ab601201223e05950bb8133be5d77526de1997

  # Fix libcamera
  git cherry-pick -n 8f35e18d18b18b8db959793fe73fc594e5da8cd5
}

build() {
  local meson_options=(
    --cross-file lib32
    -D avahi=disabled
    -D avb=disabled
    -D bluez5-codec-lc3=disabled
    -D bluez5-codec-lc3plus=disabled
    -D bluez5-codec-opus=disabled
    -D bluez5=disabled
    -D compress-offload=disabled
    -D docs=disabled
    -D ebur128=disabled
    -D echo-cancel-webrtc=disabled
    -D examples=disabled
    -D gstreamer-device-provider=disabled
    -D gstreamer=disabled
    -D jack-devel=true
    -D jack=disabled
    -D libcamera=disabled
    -D libcanberra=disabled
    -D libffado=disabled
    -D libjack-path=/usr/lib32
    -D libmysofa=disabled
    -D libpulse=disabled
    -D libusb=disabled
    -D lv2=disabled
    -D man=disabled
    -D opus=disabled
    -D pw-cat=disabled
    -D raop=disabled
    -D readline=disabled
    -D rlimits-install=false
    -D roc=disabled
    -D sdl2=disabled
    -D selinux=disabled
    -D session-managers=[]
    -D snap=disabled
    -D sndfile=disabled
    -D tests=disabled
    -D udevrulesdir=/usr/lib/udev/rules.d
    -D systemd=disabled
    -D rlimits-install=false
    -D logind-provider=libelogind
    -D v4l2=disabled
    -D x11-xfixes=disabled
    -D x11=disabled
  )

  artix-meson pipewire build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

_pwname=pipewire-0.3
_spaname=spa-0.2

package_lib32-pipewire() {
  depends=(
    "lib32-libpipewire=$epoch:$pkgver-$pkgrel" lib$_pwname.so
    lib32-alsa-lib libasound.so
    lib32-dbus libdbus-1.so
    lib32-gcc-libs
    lib32-glib2 libglib-2.0.so
    lib32-glibc
    pipewire
  )
  optdepends=(
    'lib32-pipewire-jack: JACK support'
    'lib32-pipewire-v4l2: V4L2 interceptor'
  )

  meson install -C build --destdir "$pkgdir"

  (
    cd "$pkgdir"

    _pick lib usr/lib32/$_spaname/libspa.so*
    _pick lib usr/lib32/lib$_pwname.so*
    _pick lib usr/lib32/pkgconfig/lib{$_pwname,$_spaname}.pc

    _pick jack usr/lib32/libjack*
    _pick jack usr/lib32/pkgconfig/jack*.pc

    _pick v4l2 usr/lib32/$_pwname/v4l2

    rm -r usr/{bin,include,lib,share}
  )

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}

package_lib32-libpipewire() {
  pkgdesc+=" - client library"
  depends=(
    lib32-gcc-libs
    lib32-glibc
    libpipewire
  )
  provides=(lib$_pwname.so)

  mv lib/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}

package_lib32-pipewire-jack() {
  pkgdesc+=" - JACK support"
  license+=(
    # libjackserver
    GPL-2.0-only
    LGPL-2.1-or-later
  )
  depends=(
    "lib32-libpipewire=$epoch:$pkgver-$pkgrel" lib$_pwname.so
    "lib32-pipewire=$epoch:$pkgver-$pkgrel"
    lib32-glibc
    pipewire-jack
  )
  conflicts=(
    lib32-jack
    lib32-jack2
  )
  provides=(
    lib32-jack
    libjack.so
    libjacknet.so
    libjackserver.so
  )

  mv jack/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}

package_lib32-pipewire-v4l2() {
  pkgdesc+=" - V4L2 interceptor"
  depends=(
    "lib32-libpipewire=$epoch:$pkgver-$pkgrel" lib$_pwname.so
    "lib32-pipewire=$epoch:$pkgver-$pkgrel"
    lib$_pwname.so
    lib32-glibc
    pipewire-v4l2
  )

  mv v4l2/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}

# vim:set sw=2 sts=-1 et:
