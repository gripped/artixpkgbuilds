# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: FadeMind <fademind@gmail.com>
# Contributor: Evangelos Foutras <foutrelis@archlinux.org>

pkgname=lib32-libgudev
pkgver=238
pkgrel=3
pkgdesc="GObject bindings for libudev"
url="https://gitlab.gnome.org/GNOME/libgudev"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  lib32-gcc-libs
  lib32-glib2
  lib32-glibc
  lib32-elogind
  libgudev
)
makedepends=(
  git
  glib2-devel
  meson
  elogind
)
source=("git+$url.git#tag=$pkgver")
b2sums=('6da9512d87154fbd62aae044a39abd84bee451f5a21203180062c369ebedf2ee1bc7672408cf806918111397bc74be58314e6c3155e638137603d11012a4355f')

prepare() {
  cd libgudev
}

build() {
  local meson_options=(
    --libdir=/usr/lib32
    -D gtk_doc=false
    -D introspection=disabled
    -D tests=disabled # Needs lib32-umockdev
    -D vapi=disabled
  )

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  artix-meson libgudev build "${meson_options[@]}"
  meson compile -C build
}

package() {
  depends+=(
    libg{lib,object}-2.0.so
    libudev.so
  )
  provides+=(libgudev-1.0.so)

  meson install -C build --destdir "$pkgdir"
  rm -r "$pkgdir/usr/include"
}

# vim:set sw=2 sts=-1 et:
