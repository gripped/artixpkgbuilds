# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: FadeMind <fademind@gmail.com>
# Contributor: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=lib32-libgudev
pkgver=238
pkgrel=1.1
pkgdesc="GObject bindings for libudev"
url="https://wiki.gnome.org/Projects/libgudev"
arch=(x86_64)
license=(LGPL2.1)
depends=(
  lib32-glib2
  lib32-elogind
  libgudev
)
makedepends=(
  git
  meson
)
_commit=df7c9c9940160307aaeb31347f4776a46f8736a9  # tags/238^0
source=("git+https://gitlab.gnome.org/GNOME/libgudev.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd libgudev
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libgudev
}

build() {
  local meson_options=(
    --libdir=/usr/lib32
    -D gtk_doc=false
    -D introspection=disabled
    -D tests=disabled  # Need lib32-umockdev
    -D vapi=disabled
  )

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  artix-meson libgudev build "${meson_options[@]}"
  meson compile -C build
}

package() {
  depends+=(libg{lib,object}-2.0.so)
  provides+=(libgudev-1.0.so)

  meson install -C build --destdir "$pkgdir"
  rm -r "$pkgdir/usr/include"
}

# vim:set sw=2 sts=-1 et:
