# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Simone Sclavi 'Ito' <darkhado@gmail.com>

_name=libjpeg-turbo
pkgname=lib32-$_name
pkgver=3.0.0
pkgrel=1
pkgdesc="JPEG image codec with accelerated baseline compression and decompression"
url="https://libjpeg-turbo.org/"
arch=(x86_64)
license=(BSD)
depends=(
  lib32-glibc
  $_name=$pkgver
)
makedepends=(
  cmake
  ninja
  nasm)
provides=(
  lib32-libjpeg
  libjpeg.so
  libturbojpeg.so
)
source=(
  https://sourceforge.net/projects/$_name/files/$pkgver/$_name-$pkgver.tar.gz{,.sig}
  $pkgname-3.0.0-fix_test_concurrency.patch::https://github.com/libjpeg-turbo/libjpeg-turbo/commit/035ea386d1b6a99a8a1e2ab57cc1fc903569136c.patch
)
sha512sums=('2307a2e240faf5b3119cdc5cf6a0255813d74591d4a4201fd2374940683cb9de90cb5926367e633f3624b27edd292209c7cd3c38d64522716e83fc81361289b4'
            'SKIP'
            '2a9fc2b42d0e88c1d9daa80e0e3a4ee0aa669c273b58a26eac0f4e6e95b464e6d76b2b22b0de36014713d8cd03b0196b345d39b93608d77a3972286f0f93c0a2')
b2sums=('9a4b74aad81c2999ab7f3cd7d8695e1132edda141f4cc8a57e671f42cad4ac16b66b0f19cf4b874b61a82c008287ad88cd81d85547c195ed30456e29cb505285'
        'SKIP'
        '8e04da8af51c21cc461e8373dba6d48ae0c6a28f29f4de864afadf67224aa7d4ff794b2932d063b59b707430f20f3dd6578633f86ac97c3f383ff820763a7692')
validpgpkeys=('0338C8D8D9FDA62CF9C421BD7EC2DBB6F4DBF434') # The libjpeg-turbo Project (Signing key for official binaries) <information@libjpeg-turbo.org>

prepare() {
  patch -Np1 -d $_name-$pkgver -i ../$pkgname-3.0.0-fix_test_concurrency.patch
}

build() {
  local cmake_options=(
    -B build
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_INSTALL_LIBDIR=/usr/lib32
    -DCMAKE_BUILD_TYPE=None
    -DENABLE_STATIC=OFF
    -DWITH_JAVA=OFF
    -DWITH_JPEG8=ON
    -G Ninja
    -S $_name-$pkgver
    -W no-dev
  )

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  cmake "${cmake_options[@]}"
  cmake --build build -v
}

check() {
  ctest --test-dir build --output-on-failure --stop-on-failure -j$(nproc)
}

package() {
  DESTDIR="$pkgdir" cmake --install build -v

  # remove everything that is provided by libjpeg-turbo
  rm -r "$pkgdir"/usr/{include,share,bin}

  install -vDm 644 $_name-$pkgver/LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname"
}
