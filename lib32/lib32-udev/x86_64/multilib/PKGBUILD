# Maintainer: Christian Hesse <mail@eworm.de>
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Maintainer: Tom Gundersen <teg@jklm.no>

_pkgbase=systemd-stable

pkgname=lib32-udev
_tag='884b34747beaab5909cd65f36ff48afc86377d48' # git rev-parse v${_tag_name}
_tag_name=253.4
pkgver="${_tag_name/-/}"
pkgrel=1
pkgdesc='Userspace device file manager (32-bit)'
arch=('x86_64')
url='https://www.github.com/systemd/systemd'
license=('GPL2' 'LGPL2.1')
provides=("lib32-udev=${pkgver}" 'lib32-eudev')
replaces=('lib32-eudev')
depends=('lib32-gcc-libs' 'udev' 'lib32-libcap' 'lib32-glibc')
makedepends=('git' 'gperf' 'intltool' 'lib32-acl'
             'lib32-glib2' 'lib32-gnutls' 'lib32-libelf'
             'libxslt' 'meson' 'python-jinja' 'python-lxml')
options=('strip')
validpgpkeys=('63CDA1E5D3FC22B998D20DD6327F26951A015CC4'  # Lennart Poettering <lennart@poettering.net>
              'A9EA9081724FFAE0484C35A1A81CEA22BC8C7E2E'  # Luca Boccassi <luca.boccassi@gmail.com>
              '9A774DB5DB996C154EBBFBFDA0099A18E29326E1'  # Yu Watanabe <watanabe.yu+github@gmail.com>
              '5C251B5FC54EB2F80F407AAAC54CA336CFEB557E') # Zbigniew JÄ™drzejewski-Szmek <zbyszek@in.waw.pl>
source=("git+https://github.com/systemd/systemd-stable#tag=${_tag}?signed"
        "git+https://github.com/systemd/systemd#tag=v${_tag_name%.*}?signed"
        meson-install-tags.patch
        meson-artix.patch
        udev-log-msg.patch)
sha512sums=('SKIP'
            'SKIP'
            'e7412ffe57bcbdfdf6e63caa36726a735eaf99c533613218e11dfc3c63912bcb0d713327eb8492ae91e2635e8923e68e552a559d980bc29517f75c711f2b5d71'
            'a26003ddcac5a83ec1fec040b36ff46816869028dc79aebb2646f9f2ed090e6358601f911c9d6a04b426c7f3367595e12716e411fca34ffce59d718b067e4e5c'
            '268817ee63ca0b03d2a10bb952837052c1fa80a5d2f0fe933d2aa3d61a09d286f188a6358107114964930fcef7057392f9520f875a83f6e265d2f4bc2564c26e')

_backports=(
)

_reverts=(
)

prepare() {
    cd "$_pkgbase"

    # add upstream repository for cherry-picking
    git remote add -f upstream ../systemd

    local _c
    for _c in "${_backports[@]}"; do
        git cherry-pick -n "${_c}"
    done
    for _c in "${_reverts[@]}"; do
        git revert -n "${_c}"
    done
    patch -Np1 -i ../meson-install-tags.patch
    patch -Np1 -i ../meson-artix.patch
}

build() {
    export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

    local _meson_options=(
        --libexecdir	/usr/lib32
        --libdir		/usr/lib32

        -Dversion-tag="${_tag_name/-/\~}-${pkgrel}-artix"
        -Dshared-lib-tag="${pkgver}-${pkgrel}"
        -Dmode=release

        # features
        -Daudit=false
        -Dblkid=false
        -Dgnu-efi=false
        -Dima=false
        -Dkmod=false
        -Dlibcryptsetup=false
        -Dlibidn2=false
        -Dlibiptc=false
        -Dlz4=false
        -Dmicrohttpd=false
        -Dpam=false
        -Dseccomp=false

        -Dlink-udev-shared=false

        # components
        -Dutmp=false
        -Dhibernate=false
        -Dldconfig=false
        -Dresolve=false
        -Defi=false
        -Dtpm=false
        -Denvironment-d=false
        -Dbinfmt=false
        -Drepart=false
        -Dcoredump=false
        -Dpstore=false
        -Doomd=false
        -Dlogind=false
        -Dhostnamed=false
        -Dlocaled=false
        -Dmachined=false
        -Dportabled=false
        -Dsysext=false
        -Duserdb=false
        -Dhomed=false
        -Dnetworkd=false
        -Dtimedated=false
        -Dtimesyncd=false
        -Dremote=false
        -Dcreate-log-dirs=false
        -Dnss-myhostname=false
        -Dnss-mymachines=false
        -Dnss-resolve=false
        -Dnss-systemd=false
        -Dfirstboot=false
        -Drandomseed=false
        -Dbacklight=false
        -Dvconsole=false
        -Dquotacheck=false
        -Dsysusers=false
        -Dtmpfiles=false
        -Dimportd=false
        -Dhwdb=false
        -Drfkill=false
        -Dxdg-autostart=false
        -Dman=false
        -Dhtml=false
        -Dtranslations=false

        -Ddbuspolicydir=/usr/share/dbus-1/system.d
        -Ddefault-hierarchy=unified
        -Ddefault-kill-user-processes=false
        -Ddefault-locale=C
        -Dfallback-hostname='artixlinux'
        -Dnologin-path=/usr/bin/nologin
        -Dntp-servers=
        -Ddns-servers=
        -Drpmmacrosdir=no
        -Dsysvinit-path=
        -Dsysvrcnd-path=
    )

    artix-meson "$_pkgbase" build "${_meson_options[@]}"

    local _targets=()
    _targets+=(
        udev:shared_library
        src/libudev/libudev.pc
    )

    meson compile -C build "${_targets[@]}"
}

check() {
    local tests=()
    tests+=(
        test-libudev
        test-libudev-sym
    )
    meson test -C build --print-errorlogs "${tests[@]}"
}

package() {
    meson install -C build --destdir="$pkgdir" --no-rebuild --tags libudev,libudev-devel
}
