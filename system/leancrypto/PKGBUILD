# Maintainer: Andreas Radke <andyrtr@archlinux.org> 
# Contributor: Pan Lanlan <abbypan@gmail.com>

pkgname=leancrypto
pkgver=1.5.1
pkgrel=1
pkgdesc="Lean cryptographic library usable for bare-metal environments"
arch=('x86_64')
license=('GPL-2.0-or-later'
         'LicenseRef-BSDvariant'
         'LicenseRef-leancrypto')
url="https://leancrypto.org"
depends=('glibc')
makedepends=('meson')
source=("$pkgname-$pkgver.tar.gz::https://github.com/smuellerDD/leancrypto/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('9c14639ea047554598a99d6a8cf2598b3cd89be0608df8cc06d80dd66087e2da')

build() {
  artix-meson $pkgname-$pkgver build

  # Print config
  meson configure build

  meson compile -C build
}

check() {
  # may take pretty long
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" $pkgname-$pkgver/LICENSE{,.bsd,.gplv2}
}

