# Maintainer: Andreas Radke <andyrtr@archlinux.org> 
# Contributor: Pan Lanlan <abbypan@gmail.com>

pkgname=leancrypto
pkgver=1.7.2
pkgrel=1
pkgdesc="Lean cryptographic library usable for bare-metal environments"
arch=('x86_64')
license=('GPL-2.0-or-later'
         'LicenseRef-BSDvariant'
         'LicenseRef-leancrypto')
url="https://leancrypto.org"
depends=('glibc')
makedepends=('meson')
source=("$pkgname-$pkgver.tar.gz::https://github.com/smuellerDD/leancrypto/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('9aff52dfc5c4fdb0b7d4849ad0e002430e83d6880e4136b72a3098c801224fc3')

build() {
  artix-meson $pkgname-$pkgver build

  # Print config
  meson configure build

  meson compile -C build
}

check() {
  # may take pretty long
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" $pkgname-$pkgver/LICENSE{,.bsd,.gplv2}
}

