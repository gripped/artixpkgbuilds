# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=iputils
# Commit date + git rev-parse --short origin/master
_rev=23b0638
pkgver=20250602
pkgrel=1
pkgdesc="Network monitoring tools, including ping"
arch=('x86_64')
license=('BSD-3-Clause' 'GPL-2.0-or-later')
url="https://github.com/iputils/iputils"
depends=(
  libcap
  libidn2
)
makedepends=(
  docbook-xsl
  git
  iproute
  meson
  perl-sgmls
)
conflicts=(
  arping
  netkit-base
)
replaces=('netkit-base')
source=("git+https://github.com/iputils/iputils.git#tag=${pkgver}?signed")
validpgpkeys=('2016FEA4858B1C36B32E833AC0DEC2EE72F33A5F') # Petr Vorel
b2sums=('a75875b0fd30b00c45b1855eb8519409dc9a28bb3e1c365d5f5671837a2d33d3824f891fc18bb9ca43227944e3ef795f27b0706ad8d72ab0480ac4a527b5db24')

build() {
  mkdir -p build
  cd build
  artix-meson ../$pkgname 
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
  install -vDm 644 "../$pkgname/Documentation/LICENSE.BSD3" -t "$pkgdir/usr/share/licenses/$pkgname"
}
