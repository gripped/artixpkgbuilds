# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=jfsutils
pkgver=1.1.15
pkgrel=7.1
pkgdesc='JFS filesystem utilities'
url='http://jfs.sourceforge.net'
arch=('x86_64')
license=('GPL')
depends=('util-linux')
source=("http://jfs.sourceforge.net/project/pub/${pkgname}-${pkgver}.tar.gz"
        'inttypes.patch'
        'sysmacros.patch'
        format-security-errors.patch
        gcc10.patch
        linux-headers.patch)
sha256sums=('244a15f64015ce3ea17e49bdf6e1a0fb4f9af92b82fa9e05aa64cb30b5f07a4d'
            '15ea88350c240b23fe322d4adafbb79c8473b9850495d61efafc7bf68be391b3'
            'fd1a4e794a3bf2ebdb4b81f582e147655d70aadd59689aacea0ccc32c6381a28'
            '4e154ef13b0d0da15ffce57dbee794dd9004368f4676bf9a09d3510db3bbaabb'
            'decee9dac667f304d67f3f55a9e97aa884b1fe9cde1d3f50e16313882a8de7ba'
            '53df9e9c304bd0f13e12719aa69b4b825684aa0ebccc50aecceda37b282ad596')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i ../inttypes.patch
	patch -p1 -i ../sysmacros.patch

	patch -p1 -i ../format-security-errors.patch
	patch -p1 -i ../gcc10.patch
	patch -p1 -i ../linux-headers.patch
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --sbindir=/usr/bin
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
