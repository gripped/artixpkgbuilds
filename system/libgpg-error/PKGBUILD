# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=libgpg-error
pkgver=1.57
pkgrel=1
pkgdesc="Support library for libgcrypt"
arch=(x86_64)
url="https://www.gnupg.org"
license=(LGPL-2.1-or-later
         'BSD-3-Clause OR LGPL-2.1-or-later'
         FSFULLR
         GPL-2.0-or-later)
depends=('glibc' 'sh')
# https://git.gnupg.org/cgi-bin/gitweb.cgi?p=libgpg-error.git;a=summary
source=(https://www.gnupg.org/ftp/gcrypt/libgpg-error/${pkgname}-${pkgver}.tar.bz2{,.sig})
# https://www.gnupg.org/download/integrity_check.html
sha256sums=('ab807c81fbd2b8e1d6e3377383be802147c08818f87a82e87f85e5939c939def'
            'SKIP')
validpgpkeys=('6DAA6E64A76D2840571B4902528897B826403ADA') # Werner Koch (dist signing 2020)
validpgpkeys+=('AC8E115BF73E2D8D47FA9908E98E9B2D19C6C8BD') # Niibe Yutaka (GnuPG Release Key)

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -vfi
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}/" install
  install -vDm 644 COPYING{,.LIB} -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
