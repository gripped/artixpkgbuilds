# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Jason Hall <cakersq@gmail.com>

pkgname=thin-provisioning-tools
pkgver=1.2.0
pkgrel=1
pkgdesc='Suite of tools for manipulating the metadata of the dm-thin device-mapper target'
arch=('x86_64')
url='https://github.com/jthornber/thin-provisioning-tools'
license=('GPL3')
depends=('gcc-libs')
makedepends=('git' 'cargo' 'clang' 'device-mapper')
source=("git+https://github.com/jthornber/thin-provisioning-tools#tag=v${pkgver}")
sha256sums=('f57dc300f93a825f6359ffc6d11f8eff40c5c461946b221c00d4a805e620a66d')

build() {
  cd "${srcdir}/${pkgname}"

  cargo build --release
}

package() {
  cd "${srcdir}/${pkgname}"

  make \
    DESTDIR="${pkgdir}"/usr \
    BINDIR="${pkgdir}"/usr/bin \
    DATADIR="${pkgdir}"/usr/share \
    install
}

