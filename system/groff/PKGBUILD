# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=groff
pkgver=1.24.0
pkgrel=1
pkgdesc='GNU troff text-formatting system'
arch=('x86_64')
url='https://www.gnu.org/software/groff/groff.html'
license=('GPL-3.0-or-later')
depends=(
  glibc
  libgcc
  libstdc++
  perl
)
makedepends=(
  git
  netpbm
  psutils
  libxaw
  perl-file-homedir
)
optdepends=(
  'netpbm: for use together with man -H command interaction in browsers'
  'psutils: for use together with man -H command interaction in browsers'
  'libxaw: for gxditview'
  'perl-file-homedir: for use with glilypond'
)
options=(
  !docs
  !emptydirs
  !makeflags
)
validpgpkeys=(
  '2D0C08D2B0AD0D3D8626670272D23FBAC99D4E75' # G. Branden Robinson <g.branden.robinson@gmail.com
  '8773D61D68E30E072B10DC1AD19E9C7D71266DCE' # Bertrand Garrigues <bertrand.garrigues@laposte.net>
)
source=(
  git+https://git.savannah.gnu.org/git/groff.git?signed#tag=${pkgver}
  site.tmac
)
b2sums=('504e9c5bb49369f3cf3b7560798d9da2c9c3cc89a5e0e36714c3d291db76b77870768e809c696f47339efc88e6095bd40eb993e33fb4c8ca526015149386667c'
        '39087d04a2af011820e9428c3c01da4be63cd34125bb087097b4e56865806e33e3e20a666885110ab71d36eeafdbfb01202ca4ee45d6cdeed8ab71d83852341b')

prepare() {
  cd "${pkgname}"
  ./bootstrap
}  

build() {
  cd "${pkgname}"

  ./configure \
    --prefix=/usr \
    --with-x \
    --with-appresdir=/usr/share/X11/app-defaults
  make
}

check() {
  cd "${pkgname}"
  make check
}

package() {
  cd "${pkgname}"
  make DESTDIR="$pkgdir" install
  
  # add compatibility symlinks
  ln -s eqn "${pkgdir}"/usr/bin/geqn
  ln -s tbl "${pkgdir}"/usr/bin/gtbl
  ln -s soelim "${pkgdir}"/usr/bin/zsoelim

  cat "${srcdir}"/site.tmac >> \
  "${pkgdir}"/usr/share/groff/site-tmac/man.local
  cat "$srcdir"/site.tmac >> \
  "${pkgdir}"/usr/share/groff/site-tmac/mdoc.local
}
