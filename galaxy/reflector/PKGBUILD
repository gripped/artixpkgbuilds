# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Bert Peters <bertptrs@archlinux.org>
# Contributor: Xyne <gro xunilhcra enyx, backwards>
pkgname=reflector
pkgver=2023
pkgrel=5
pkgdesc='A Python 3 module and script to retrieve and filter the latest Pacman mirror list.'
arch=(any)
license=(GPL-2.0-only)
url="https://xyne.dev/projects/reflector"
depends=(python)
makedepends=(python-build python-installer python-setuptools python-wheel)
optdepends=('rsync: rate rsync mirrors')
backup=(etc/xdg/reflector/reflector.conf)
source=(https://xyne.dev/projects/reflector/src/reflector-2023.tar.xz{,.sig}
        mp-fork.patch)

sha512sums=('11aec550c15080695525409f11eae6d4b545df8b37a8e0727de939eefec2b2fa6aa95c5c3500a6c8a940b6060cdaf2526430ed47e01a3c6f098e1b77189eb479'
            'SKIP'
            '6bbb85a7e829b18ebc7451c38e7ef8ebb9c2d05dac7ab981fc8712b365867faa264eae177a0536a3ad72ffedbf5a6251fed4c6c834879b7e8ada9035ffbdb28e')
b2sums=('24ee3610eb94e4412bceba40f4763def380028683358cb1016546a4d0bb12eaa69aba4e83b7998ebb74ee6f025f9f342901d076e44302da47d0ed14b727fb9c1'
        'SKIP'
        '37d9980afa27eaf80796d090327c0ce0f78772d4ea8a651bd2fe37af29f002dcff434d9839f930317c4d441cf4236d60ad95676489ae0a22dbe0c37bbae765ef')
validpgpkeys=('D89FAAEB4CECAFD199A2F5E612C6F735F7A9A519')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 < "${srcdir}/mp-fork.patch"
}

build() {
  cd $pkgname-$pkgver
  sed -e "s|https://archlinux.org|https://status.artixlinux.org|g" \
      -e "s|Arch Linux|Artix Linux|g"  \
      -e "s|extra/os/x86_64/extra.db|system/os/x86_64/system.db|g" \
      -i Reflector.py
  python -m build --wheel --no-isolation
}

package ()
{
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 "man/${pkgname}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"
  install -Dm644 'reflector.conf' "$pkgdir/etc/xdg/reflector/reflector.conf"
}


# vim: set ts=2 sw=2 et:
