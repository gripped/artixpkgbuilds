# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Carl Smedstad <carsme@archlinux.org>

pkgname=cava
pkgver=0.10.4
pkgrel=3
pkgdesc='Console-based Audio Visualizer with support for multiple backends'
arch=('x86_64')
url='https://github.com/karlstav/cava'
license=('MIT')
# Compile with support for (and depend on) all input/output backends for
# simplicity's sake. One could possibly split this into one package per
# backend, but let's start with this.
depends=(
  'fftw'
  'glibc'
  'iniparser'
  # Input backends
  'alsa-lib'
  'jack'
  'libpipewire'
  'libpulse'
  'portaudio'
  'sndio'
  # Output backends
  'ncurses'
  'sdl2'
)
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('c0f1d40e6d3c7b06e2ebc9e4c80bd8b6b88b6fdcd083c30648fca34128e4349a46352a7526613ccbf6421eac29eb5778f396e47f74a03691224cf2d969872113')

prepare() {
  cd $pkgname-$pkgver
  echo "$pkgver" > version
  autoreconf -vif
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
