# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Robin Candau <antiz@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: TDY <tdy@gmx.com>

pkgname=parole
pkgver=4.18.1
pkgrel=3
pkgdesc="Modern media player based on the GStreamer framework"
arch=('x86_64')
url="https://docs.xfce.org/apps/parole/start"
license=('GPL-2.0-or-later')
groups=('xfce4-goodies')
depends=('dbus-glib' 'gst-plugins-base' 'gst-plugins-good' 'libnotify' 'libxfce4ui')
makedepends=('git' 'glib2-devel' 'intltool' 'python' 'xfce4-dev-tools')
optdepends=('gst-libav: Extra media codecs'
	    'gst-plugins-bad: Extra media codecs'
	    'gst-plugins-ugly: Extra media codecs')
source=("git+https://gitlab.xfce.org/apps/parole.git#tag=$pkgname-$pkgver")
sha256sums=('20806cc8fe54c3e626999958c19e7fdaf0158de64dbeea9771ff9882970ab8eb')

prepare() {
  cd $pkgname
  ./autogen.sh \
    --prefix=/usr
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
