# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Evangelos Foutras <foutrelis@archlinux.org>
# Contributor: Robin Candau <antiz@archlinux.org>
# Contributor: tobias <tobias funnychar archlinux.org>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>

pkgname=xfce4-settings
pkgver=4.18.6
pkgrel=2
pkgdesc="Xfce's Configuration System"
arch=('x86_64')
url="https://docs.xfce.org/xfce/xfce4-settings/start"
license=('GPL-2.0-or-later')
groups=('xfce4')
depends=('exo' 'garcon' 'libxfce4ui' 'xfconf' 'libnotify' 'libcanberra'
         'colord' 'libxklavier' 'elementary-icon-theme' 'gnome-themes-extra')
makedepends=('git' 'glib2-devel' 'intltool' 'xfce4-dev-tools' 'xf86-input-libinput')
optdepends=('python: xfce4-compose-mail -- "mailto:" URI handling'
            'xiccd: for displays support in xfce4-color-settings'
            'cupsd: for printers support in xfce4-color-settings'
            'saned: for scanners support in xfce4-color-settings')
source=("git+https://gitlab.xfce.org/xfce/xfce4-settings.git#tag=$pkgname-$pkgver"
        default-xsettings-xml.patch)
sha256sums=('f7908fa2dd5b8cde014f5c0f750d8c6e7edea8e5e7c6eac215a767314d66c7e0'
            'ca1c7562e209d9ab791b02f2a886c61a9ed705a912d9575f590734eff21d6e5c')
prepare() {
  cd $pkgname

  # Enable Adwaita theme, elementary icon theme and font hinting by default
  patch -Np1 -i "$srcdir/default-xsettings-xml.patch"

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-xrandr \
    --enable-xcursor \
    --enable-libnotify \
    --enable-libxklavier \
    --enable-pluggable-dialogs \
    --enable-sound-settings \
    --disable-debug
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
