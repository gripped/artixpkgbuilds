# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Evangelos Foutras <foutrelis@archlinux.org>
# Contributor: Robin Candau <antiz@archlinux.org>
# Contributor: tobias <tobias funnychar archlinux.org>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>

pkgname=xfce4-settings
pkgver=4.20.3
pkgrel=1
pkgdesc="Xfce's Configuration System"
arch=('x86_64')
url="https://docs.xfce.org/xfce/xfce4-settings/start"
license=('GPL-2.0-or-later')
groups=('xfce4')
depends=('exo' 'garcon' 'libxfce4ui' 'xfconf' 'libnotify' 'libcanberra'
         'colord' 'libxklavier' 'elementary-icon-theme' 'gnome-themes-extra')
makedepends=('git' 'glib2-devel' 'xfce4-dev-tools' 'xf86-input-libinput')
optdepends=('python: xfce4-compose-mail -- "mailto:" URI handling'
            'xiccd: for displays support in xfce4-color-settings'
            'cups: for printers support in xfce4-color-settings'
            'sane: for scanners support in xfce4-color-settings')
source=("git+https://gitlab.xfce.org/xfce/xfce4-settings.git#tag=$pkgname-$pkgver"
        git+https://gitlab.freedesktop.org/wlroots/wlr-protocols.git
        default-xsettings-xml.patch)
sha256sums=('d94645103a9f60093259be55ae1b7d21f6abb357e01d101b8680f84352c82662'
            'SKIP'
            '008da8cae0251864d1bf34e2649674e0566475a423becf81e8e3f5c6acb3877d')
prepare() {
  cd $pkgname

  # Enable Adwaita theme, elementary icon theme and font hinting by default
  patch -Np1 -i "$srcdir/default-xsettings-xml.patch"

  # Add option to disable XSettings and GTK settings helpers to avoid overwriting default GNOME settings
  sed -i 's/s_data->xsettings_helper = /if (g_getenv ("XFSETTINGSD_NO_XSETTINGS") == NULL) &/
          s/s_data->gtk_settings_helper = /if (g_getenv ("XFSETTINGSD_NO_GTK_SETTINGS") == NULL) &/' xfsettingsd/main.c

  git submodule init
  git config submodule.mate-submodules.url "$srcdir/protocols/wlr-protocols"
  git -c protocol.file.allow=always submodule update
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-x11 \
    --enable-wayland \
    --enable-xrandr \
    --enable-xcursor \
    --enable-libnotify \
    --enable-libxklavier \
    --enable-pluggable-dialogs \
    --enable-sound-settings \
    --disable-debug \
    --enable-maintainer-mode
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
