# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-session
pkgver=6.6.1
pkgrel=2
pkgdesc='The Cinnamon session handler'
arch=(x86_64)
url='https://github.com/linuxmint/cinnamon-session'
license=(GPL-2.0-or-later)
depends=(
  bash
  cinnamon-desktop
  dconf
  gcc-libs
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  libcanberra
  libglvnd
  libice
  libsm
  libx11
  libxau
  libxcomposite
  python
  python-gobject
  python-setproctitle
  systemd-libs
  xapp
)
optdepends=('cinnamon-translations: i18n')
makedepends=(
  git
  glib2-devel
  meson
  xtrans
)
source=("git+https://github.com/linuxmint/cinnamon-session.git#tag=$pkgver")
b2sums=(b8c352551d7381fe7dd718a760e77a1d6041065b26d8c88d34b76ea35d252fa25856117cc54038ee385787616828103e7f1b171efc379bc8738a5cb436390446)

build() {
  artix-meson $pkgname build \
    --libexecdir=lib/$pkgname
  meson compile -C build
}

package() {
  meson install -C build --destdir="$pkgdir"
}
