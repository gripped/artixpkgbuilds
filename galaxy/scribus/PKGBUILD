# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=scribus
pkgver=1.6.3
pkgrel=3
pkgdesc="Desktop publishing software"
arch=(x86_64)
url="https://www.scribus.net/"
license=(GPL-2.0-or-later)
depends=(cairo
         fontconfig
         freetype2
         harfbuzz-icu
         hunspell
         icu libicuuc.so
         lcms2
         libcdr
         libcups
         libfreehand
         libjpeg-turbo libjpeg.so
         libmspub
         libpagemaker
         libpng
         libqxp
         librevenge
         libtiff
         libvisio
         libxml2
         libzmf
         openscenegraph
         openssl
         podofo
         poppler
         python3
         qt5-base
         zlib) # graphicsmagick
makedepends=(cmake
             mesa
             qt5-tools)
optdepends=('gdal: enable gdal plugin'
            'hyphen-lang: hyphenation patterns for desired languages'
            'tk: scripts based on tkinter')
options=(!lto)
_archive="$pkgname-$pkgver"
source=("https://downloads.sourceforge.net/${pkgname}/$_archive.tar.xz"{,.asc}
        'fix_build_with_poppler_25.02.0.patch')
sha256sums=('0ae58ced410101e82655e3b4c20a070cf1767145ada233dcef7c20b8ba6bd487'
            'SKIP'
            'b812944dd1cd25b9f42132c5f107fa83412ad662c43ca4159ec29c357461ee5e')
validpgpkeys=(5086B8D68E70FDDF4C40045AEF7B95E7F60166DA  # Peter Linnell <plinnell@scribus.net>
              757F5E9B13DD648887AD50092D47C099E782504E  # The Scribus Team (www.scribus.net) <the_scribus_team@scribus.net>
              6558BE84D27273A438A151198BEA48118AEBEE64) # Craig Bradney <cbradney@zipworld.com.au>

prepare() {
    cd "$_archive"
    sed -e 's|WANT_CPP17|WANT_CPP20|g' -e 's|CMAKE_CXX_STANDARD 17|CMAKE_CXX_STANDARD 20|g' -i CMakeLists.txt

    # Temporary patch to fix build with poppler 25.02.0
    # See https://github.com/scribusproject/scribus/commit/188d030acd0cb71e89dbb57695fe52158b6a0959
    # and https://gitweb.gentoo.org/repo/gentoo.git/tree/app-office/scribus/files/scribus-1.7.0-poppler-25.02.0.patch
    patch -Np1 -i "$srcdir/fix_build_with_poppler_25.02.0.patch"
}

pkgver() {
        # People regularly flag this OOD pointing to development releases, avoid mistakenly packaging them
	if [[ $pkgver =~ ^[0-9]+\.[13579]+\.[0-9]+$ ]]; then
		echo 'Odd-numbered minor version segments indicate development versions.' >&2
		exit 1
	else
		echo "$pkgver"
	fi
}

build() {
	cmake -B build -S "$_archive" \
		-D CMAKE_INSTALL_PREFIX=/usr \
		-D CMAKE_BUILD_TYPE=None \
		-D CMAKE_SKIP_RPATH=On \
		-D WANT_CPP20=On \
		-D WANT_HUNSPELL=On \
		-D WITH_PODOFO=On \
		-D WANT_GRAPHICSMAGICK=On
	make -C build
}

package() {
	make -C build DESTDIR="${pkgdir}" install
	cd "$_archive"
	install -Dm0644 scribus.desktop -t "$pkgdir/usr/share/applications"
	pushd resources/iconsets/artwork
	for i in 16x16 32x32 128x128 256x256 512x512 1024x1024; do
		install -Dm0644 icon_$i.png "$pkgdir/usr/share/icons/hicolor/$i/apps/scribus.png"
	done
	# Use system hyphen
	rm -rf "$pkgdir/usr/share/scribus/dicts/hyph"
	ln -sf /usr/share/hyphen "$pkgdir/usr/share/scribus/dicts/hyph"
}
