# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Lex Black <autumn-wind@web.de>

pkgname=labwc
pkgver=0.20.1
pkgrel=1
pkgdesc='stacking wayland compositor with look and feel from openbox'
url="https://github.com/labwc/labwc"
arch=('x86_64')
license=('GPL-2.0-only')
depends=(
  cairo
  glib2
  glibc
  libinput
  libpng
  librsvg
  libsfdo
  libwlroots-0.20.so
  libxcb
  libxkbcommon
  libxml2
  pango
  pixman
  seatd
  ttf-font
  wayland
)
makedepends=(
  git
  meson
  scdoc
  wayland-protocols
  xorg-xwayland
)
optdepends=(
  "bemenu: default launcher via Alt+F3"
  "xorg-xwayland: X11 application support"
)
source=("git+https://github.com/labwc/labwc#tag=${pkgver}")
b2sums=('c925f1b07ff5fc3d2acc47c14d1725ea0c4675ac408357afb0bd28ca8d04742765269bc03720086d3c3147037fa1e6b7bc88e033035a7d795f3d8f94fe446d46')


build() {
  artix-meson -Dsystemd-session=disabled -Dman-pages=enabled "$pkgname" build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
