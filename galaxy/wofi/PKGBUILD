# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Ian Brunelli <ian@brunelli.me>

pkgname=wofi
pkgver=1.5.3
pkgrel=1
pkgdesc='launcher for wlroots-based wayland compositors'
arch=('x86_64')
url='https://hg.sr.ht/~scoopta/wofi'
license=('GPL-3.0-or-later')
depends=(
  'glibc'
)
makedepends=(
  'cairo'
  'gdk-pixbuf2'
  'glib2'
  'gtk3'
  'meson'
  'pango'
  'wayland'
)
source=("${pkgname}-v${pkgver}.tar.gz::$url/archive/v${pkgver}.tar.gz")
sha512sums=('59067d8ea080cde13ba4c278e5843e10300c9fd5e1a1445107f6045d01c23b2ceaf59cd3e0393c3d966553b04110c3ebb776b88df39885dd06620d3e49c8ffff')
b2sums=('9d0d389a7ce6935c0800388b5e099f8ef7dbf154f67d0c022735382307efeafdf87ef12f1af9dc9ad78e06df78072b544ec987450d341f37980a54ee6ef9b871')

build() {
  artix-meson $pkgname-v$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(
    cairo libcairo.so
    gdk-pixbuf2 libgdk_pixbuf-2.0.so
    glib2 libgio-2.0.so libglib-2.0.so libgobject-2.0.so
    gtk3 libgdk-3.so libgtk-3.so
    pango libpango-1.0.so
    wayland libwayland-client.so
  )

  meson install -C build --destdir "$pkgdir"
}
