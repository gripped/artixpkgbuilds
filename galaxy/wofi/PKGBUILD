# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Ian Brunelli <ian@brunelli.me>

pkgname=wofi
pkgver=1.5.1
pkgrel=1
pkgdesc='launcher for wlroots-based wayland compositors'
arch=('x86_64')
url='https://hg.sr.ht/~scoopta/wofi'
license=('GPL-3.0-or-later')
depends=(
  'glibc'
)
makedepends=(
  'cairo'
  'gdk-pixbuf2'
  'glib2'
  'gtk3'
  'meson'
  'pango'
  'wayland'
)
source=("${pkgname}-v${pkgver}.tar.gz::$url/archive/v${pkgver}.tar.gz")
sha512sums=('6c0a05e68022c64506b2a2d0b12c8f4a874070bfe598a371d6508c1faf62d967b76affcc4b41a1eaba294fbad3cae9f3ee84f6715df89bd353d44d41631e893c')
b2sums=('376416375e8339bd51eb6d3e1348bbca0e6f161e13ef2bde8ea93b15f41d3dd685f36df4fb86633b374b3a8583d0425a01b85328a6ec04d2e3ff3172d8dff48f')

build() {
  artix-meson $pkgname-v$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(
    cairo libcairo.so
    gdk-pixbuf2 libgdk_pixbuf-2.0.so
    glib2 libgio-2.0.so libglib-2.0.so libgobject-2.0.so
    gtk3 libgdk-3.so libgtk-3.so
    pango libpango-1.0.so
    wayland libwayland-client.so
  )

  meson install -C build --destdir "$pkgdir"
}
