# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Mark Wagie <mark dot wagie at proton dot me>
# Contributor: librewish <librewish at gmail dot com>
# Contributor: Chris Severance aur.severach aATt spamgourmet dott com
# Contributor: Jay Garcia <morbidj at gmail dot com>
# Contributor: Doug Newgard <scimmia22 at outlook dot com>
# Contributor: Robert Orzanna <orschiro at gmail dot com>

pkgname=timeshift
pkgver=25.12.1
pkgrel=1
pkgdesc="A system restore utility for Linux"
url="https://github.com/linuxmint/timeshift"
arch=('x86_64')
license=('GPL-2.0-or-later')
depends=('libgee' 'json-glib' 'gtk3' 'vte3' 'rsync' 'xapp' 'cronie')
makedepends=('help2man' 'meson' 'vala')
optdepends=('btrfs-progs: For BTRFS support'
            'grub-btrfs: For BTRFS snapshots in GRUB'
            'xorg-xhost: For authorization on Wayland')
install="${pkgname}.install"
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('d49f5cefbb12c214038d40c36f5a93f78fdea810dc9b57a7a08c3734346b0162')

build() {
	artix-meson "${pkgname}-${pkgver}" build
	meson compile -C build
}

check() {
	meson test -C build
}

package() {
	meson install -C build --destdir "${pkgdir}"
}
