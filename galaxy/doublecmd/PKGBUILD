# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# vim:set ft=sh:
# Contributor: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Contributor: (sirocco AT ngs.ru)

pkgbase=doublecmd
pkgname=('doublecmd-gtk2' 'doublecmd-qt5' 'doublecmd-qt6')
pkgver=1.1.20
pkgrel=1
url="http://doublecmd.sourceforge.net/"
arch=('x86_64')
license=('GPL')
provides=("$pkgbase")
makedepends=('lazarus' 'gtk2' 'qt5pas' 'qt6pas' 'imagemagick' 'ffmpegthumbnailer')
optdepends=(
    'lua: scripting'
    'unzip: support extracting zip archives'
    'zip: suport packing zip archives'
    'p7zip: support for 7zip archives'
    'libunrar: support for rar archives'
    'imagemagick: preview xcf files'
    'ffmpegthumbnailer: preview video files'
    'mplayer: to make use of the wlxmplayer plugin'
)
source=(
    "https://downloads.sourceforge.net/project/$pkgbase/Double%20Commander%20Source/$pkgbase-$pkgver-src.tar.gz"
)
sha512sums=('9e22fcda76f761f677942606d5165aaac770150d6935304fb6f200762615e844c5ffbcf40c5c265364de280bf0feb819db9235a47aa9b38b8003eb3efed8667f')

prepare() {
    cp -a /usr/lib/lazarus ./

    cd "$pkgbase-$pkgver"
    sed -e 's/LIB_SUFFIX=.*/LIB_SUFFIX=/g' -i install/linux/install.sh
    sed -e "s@=\$(which lazbuild)@=\"\$(which lazbuild) --lazarusdir=$srcdir/lazarus\"@" -i build.sh
    sed -e '/doublecmd.zdli/d' -i install/linux/install.sh
}

build() {
    cd "$srcdir/$pkgbase-$pkgver"
    cd "$srcdir"

    cp -a "$pkgbase-$pkgver" "$pkgbase-gtk"
    cp -a "$pkgbase-$pkgver" "$pkgbase-qt5"
    cp -a "$pkgbase-$pkgver" "$pkgbase-qt6"

    # build gtk
    cd "$srcdir/$pkgbase-gtk"
    ./build.sh components gtk2
    ./build.sh plugins gtk2
    ./build.sh doublecmd gtk2

    # build qt5
    cd "$srcdir/$pkgbase-qt5"
    ./build.sh components qt5
    ./build.sh plugins qt5
    ./build.sh doublecmd qt5

    # build qt6
    cd "$srcdir/$pkgbase-qt6"
    ./build.sh components qt6
    ./build.sh plugins qt6
    ./build.sh doublecmd qt6
}

package_doublecmd-gtk2() {
    pkgdesc="twin-panel (commander-style) file manager (GTK2)"
    depends=('gtk2' 'desktop-file-utils' 'hicolor-icon-theme' 'shared-mime-info')
    conflicts=('doublecmd-qt5' 'doublecmd-qt6')
    cd "$srcdir/$pkgbase-gtk"
    ./install/linux/install.sh --install-prefix="$pkgdir"
}

package_doublecmd-qt5() {
    pkgdesc="twin-panel (commander-style) file manager (Qt5)"
    depends=('qt5pas' 'desktop-file-utils' 'hicolor-icon-theme' 'shared-mime-info')
    conflicts=('doublecmd-gtk2' 'doublecmd-qt6')
    replaces=('doublecmd-qt' 'doublecmd-qt4')
    cd "$srcdir/$pkgbase-qt5"
    ./install/linux/install.sh --install-prefix="$pkgdir"
}

package_doublecmd-qt6() {
    pkgdesc="twin-panel (commander-style) file manager (Qt6)"
    depends=('qt6pas' 'desktop-file-utils' 'hicolor-icon-theme' 'shared-mime-info')
    conflicts=('doublecmd-gtk2' 'doublecmd-qt5')
    cd "$srcdir/$pkgbase-qt6"
    ./install/linux/install.sh --install-prefix="$pkgdir"
}
