# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Max Harmathy <harmathy@mailbox.org>
# Contributor: Dušan Simić <dusan.simic1810@gmail.com>

pkgname=adw-gtk-theme
pkgver=6.4
pkgrel=1.2
pkgdesc='Unofficial GTK 3 port of the libadwaita theme'
arch=(any)
url=https://github.com/lassekongo83/adw-gtk3
license=(LGPL-2.1-only)
makedepends=(
  dart-sass
  git
  meson
)
_tag=39ef8327790ca731ad0e87bbad7293ce0a83b26e
source=(git+https://github.com/lassekongo83/adw-gtk3.git#tag=${_tag})
b2sums=('c96090e9fe858876c7e683463c65b2b82bd5457fc7e0bf2a111ff2317ea149f1a47b6c40b18bdf5c3854a79a748eaa9db016f97f7a57effe7d3f94d2e0b6fb12')

pkgver() {
  cd adw-gtk3
  git describe --tags | sed 's/^v//'
}

build() {
  artix-meson adw-gtk3 build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
