# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-media
pkgver=1.28.1
pkgrel=3.1
pkgdesc="MATE Media Tools"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL-2.0-or-later')
depends=('libcanberra' 'libmatemixer' 'mate-desktop' 'mate-panel' 'gettext')
makedepends=('autoconf-archive' 'mate-common')
groups=('mate-extra')
conflicts=('mate-media-gtk3')
replaces=('mate-media-gtk3')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/mate-desktop/mate-media/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('57f60a4685fc2b35a9edb4cf3d967fe5be83df5b716feacabeb9f11b324f8a9b')

prepare() {
    cd "${pkgname}-${pkgver}"
    ./autogen.sh
}

build() {
    cd "${pkgname}-${pkgver}"
    ./configure \
                --prefix=/usr \
                --sysconfdir=/etc \
                --libexecdir="/usr/lib/${pkgname}" \
                --localstatedir=/var
    make
}

check() {
    cd "${pkgname}-${pkgver}"
    make check
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
