# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Danilo Bargen <aur at dbrgn dot ch>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>

pkgname=tealdeer
pkgver=1.8.0
pkgrel=1
pkgdesc='A fast tldr client in Rust'
arch=('x86_64')
url='https://github.com/tealdeer-rs/tealdeer'
license=('MIT' 'Apache-2.0')
depends=('gcc-libs')
makedepends=('git' 'rust')
provides=('tldr')
conflicts=('tldr')
options=('!lto')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('01e15ae452adb2b25b74c68de138de622f06d57e536b352bb00734e60518408e61f10acbe29b7a078cc1ba4d633c1a745df1b86017a34e8c92ff6fe42065ef80')
b2sums=('46971f990732664966037986f5d5878e2e9545f09a7b0997e2e9f732d51b8b24a0cb17a2890da5d7ae0b42024d3746bb8a56249557c58148141d142a88b116e0')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  cargo build --release --frozen
}

check() {
  cd "$pkgname"

  cargo test --frozen
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/tldr"

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*

  # completions
  cd completion
  install -vDm644 bash_tealdeer "$pkgdir/usr/share/bash-completion/completions/tldr"
  install -vDm644 fish_tealdeer "$pkgdir/usr/share/fish/vendor_completions.d/tldr.fish"
  install -vDm644 zsh_tealdeer "$pkgdir/usr/share/zsh/site-functions/_tldr"
}

# vim:set ts=2 sw=2 et:
