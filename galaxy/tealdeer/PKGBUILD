# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Danilo Bargen <aur at dbrgn dot ch>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>

pkgname=tealdeer
pkgver=1.9.0
pkgrel=1
pkgdesc='A fast tldr client in Rust'
arch=(x86_64)
url='https://github.com/tealdeer-rs/tealdeer'
license=(MIT Apache-2.0)
depends=(glibc libgcc)
makedepends=(git rust)
provides=(tldr)
conflicts=(tldr)
options=(!lto)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('643be84ae30c44d4bf33d0e3418c5947253f931027c4184da8580a427bcb879b9174d9002804aa3c7d771fbf78ce80299aaceca37fad50e8ae4cae988a29c8c5')
b2sums=('e7d7e8e7adf62d582958dd4d07e8b1dbdff152590fa233fe0a3a66281548500cf226d0d8c9079f9de72c4548e188fe63a46937ed2d7360615ace8dda3687e6e6')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname"

  cargo build --release --frozen
}

check() {
  cd "$pkgname"

  cargo test --frozen
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/tldr

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" ./LICENSE*

  # completions
  cd completion
  install -vDm644 bash_tealdeer "$pkgdir/usr/share/bash-completion/completions/tldr"
  install -vDm644 fish_tealdeer "$pkgdir/usr/share/fish/vendor_completions.d/tldr.fish"
  install -vDm644 zsh_tealdeer "$pkgdir/usr/share/zsh/site-functions/_tldr"
}

# vim:set ts=2 sw=2 et:
