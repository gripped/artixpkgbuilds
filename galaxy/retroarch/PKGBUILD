# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Themaister <maister@archlinux.us>
# Contributor: lifning <definelightning@gmail.com>

pkgname=retroarch
pkgver=1.21.0
pkgrel=1
pkgdesc='Reference frontend for the libretro API'
arch=(x86_64)
url=http://www.libretro.com/
license=(GPL)
groups=(libretro)
depends=(
  alsa-lib
  dbus
  ffmpeg
  flac
  fontconfig
  fribidi
  gcc-libs
  glibc
  libass.so
  libdrm
  libfreetype.so
  libgl
  libpulse
  libudev.so
  libusb
  libx11
  libxcb
  libxext
  libxinerama
  libxkbcommon
  libxrandr
  libxxf86vm
  libxv
  mesa
  openssl
  qt6-base
  sdl2
  v4l-utils
  wayland
  zlib
)
makedepends=(
  gamemode
  git
  libdecor
  vulkan-icd-loader
  wayland-protocols
)
optdepends=(
  'gamemode: GameMode support'
  'libdecor: Wayland decorations'
  'libretro-overlays: Collection of overlays'
  'libretro-shaders: Collection of shaders'
  'python: retroarch-cg2glsl'
  'retroarch-assets-ozone: Ozone menu assets'
  'retroarch-assets-xmb: XMB menu assets'
)
backup=(etc/retroarch.cfg)
_tag=baee906ef35b99283f9a1a060a2ce5ac86159b63
source=(
  git+https://github.com/libretro/RetroArch.git#tag=${_tag}
  retroarch-config.patch
)
b2sums=('0eb80d8ba9dabd3b1a40dd8beb3d2a83feb56b764085d827db6361509c3d57f871be3177ffb5b339727323f0492cd1f9d10f12361b318847605f4c992cebfbd7'
        '5ecbe57126c7f9b66bdf189f0af2f1cec031cb68ea85bb9609847ba9beb0e720c902cd3330d7c286901a73e6c2e272c3f04166cf9a47bb6f80097f19dde8ce3a')

pkgver() {
  cd RetroArch
  git describe --tags | sed 's/^v//'
}

prepare() {
  cd RetroArch
  patch -Np1 -i ../retroarch-config.patch
}

build() {
  cd RetroArch
  ./configure \
    --prefix=/usr \
    --disable-builtinflac \
    --enable-builtinmbedtls \
    --disable-builtinzlib \
    --disable-cg \
    --disable-jack \
    --disable-oss \
    --disable-sdl \
    --enable-dbus
  make
  make -C libretro-common/audio/dsp_filters
  make -C gfx/video_filters
}

package() {
  cd RetroArch
  make DESTDIR="${pkgdir}" install
  install -Dm 644 libretro-common/audio/dsp_filters/*.{dsp,so} -t "${pkgdir}"/usr/lib/retroarch/filters/audio/
  install -Dm 644 gfx/video_filters/*.{filt,so} -t "${pkgdir}"/usr/lib/retroarch/filters/video/
}

# vim: ts=2 sw=2 et:
