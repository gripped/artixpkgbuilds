# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=appstream-generator
pkgver=1.0.0
pkgrel=1
pkgdesc='A fast AppStream metadata generator'
arch=(x86_64)
url='https://github.com/ximion/appstream-generator'
license=(LGPL-3.0-only)
depends=(appstream
         curl
         glib2
         glibc
         libarchive
         libelf
         libfyaml
         libgcc
         libstdc++
         libxml2
         lmdb
         onetbb
         optipng)
makedepends=(backward-cpp
             catch2
             cmake # so that meson can find backward-cpp
             docbook-xsl
             inja
             meson
             quill)
source=(https://github.com/ximion/appstream-generator/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('ec5e45c28e4126809872d37ea41937908e3d1f237f6faac0086332454666b067')

build() {
  artix-meson $pkgname-$pkgver build
  meson compile -C build
}

package() {
  meson install -C build --destdir="$pkgdir"
}
