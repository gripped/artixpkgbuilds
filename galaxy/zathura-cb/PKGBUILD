# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Lex Black <autumn-wind at web dot de>

pkgname=zathura-cb
pkgver=2026.07.18
pkgrel=1
pkgdesc="Adds comic book support to zathura"
url="https://pwmt.org/projects/zathura-cb/"
arch=('x86_64')
license=('Zlib')
depends=('zathura' 'libarchive')
makedepends=('meson' 'ninja' 'appstream' 'desktop-file-utils')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pwmt/${pkgname}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('4b7eac4aec8be060526788b523fa190ece7f97ac187af5bec41752e11b99d023b5c5af5641f5316e0aa2866a299ca41681bc3008666435bde098340f30ac2aff')

build() {
	cd "${pkgname}-${pkgver}"
	artix-meson build
	ninja -C build
}

check() {
	cd "${pkgname}-${pkgver}"
	meson test -C build --print-errorlogs
}

package() {
	cd "${pkgname}-${pkgver}"
	DESTDIR="${pkgdir}" ninja -C build install
	install -Dm 664 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
