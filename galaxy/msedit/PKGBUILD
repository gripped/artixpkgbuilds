# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Bert Peters <bertptrs@archlinux.org>
pkgname=msedit
pkgver=1.2.1
pkgrel=1
pkgdesc="A simple editor for simple needs (Microsoft Edit)"
arch=(x86_64)
url="https://github.com/microsoft/edit"
license=('MIT')
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
)
optdepends=(
  'icu: for search and replace'
)
replaces=(ms-edit)
provides=(ms-edit)
makedepends=(
  rustup
  git
)
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha256sums=('272a51d6ddf0b8768b373ac3ed561af21ed266620e492a838df2226e82255c50')

prepare() {
  cd "$pkgname"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  # patch the desktop file so it matches the alternative binary name
  sed --in-place 's/=edit/=msedit/' assets/com.microsoft.edit.desktop
}

build() {
  cd "$pkgname"
  cargo build --locked --frozen --release
}

check() {
  cd "$pkgname"
  cargo test --frozen
}

package() {
  cd "$pkgname"
  install -Dm0755 "target/release/edit" "$pkgdir/usr/bin/$pkgname"
  install -Dm0644 "assets/edit.svg" "$pkgdir/usr/share/icons/hicolor/scalable/apps/$pkgname.svg"
  install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname" "LICENSE"
  install -Dm0644 -t "$pkgdir/usr/share/applications" "assets/com.microsoft.edit.desktop"
}
