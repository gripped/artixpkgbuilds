# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Bert Peters <bertptrs@archlinux.org>
pkgname=msedit
pkgver=1.2.0
pkgrel=2
pkgdesc="A simple editor for simple needs (Microsoft Edit)"
arch=(x86_64)
url="https://github.com/microsoft/edit"
license=('MIT')
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
)
replaces=(ms-edit)
provides=(ms-edit)
makedepends=(
  rustup
  git
)
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha256sums=('c9861d4e487b6fa1348457dc0590cf0ab199919d41285487e2131762dbd04423')

prepare() {
  cd "$pkgname"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  # patch the desktop file so it matches the alternative binary name
  sed --in-place 's/=edit/=msedit/' assets/com.microsoft.edit.desktop
}

build() {
  cd "$pkgname"
  cargo build --locked --frozen --release
}

check() {
  cd "$pkgname"
  cargo test --frozen
}

package() {
  cd "$pkgname"
  install -Dm0755 "target/release/edit" "$pkgdir/usr/bin/$pkgname"
  install -Dm0644 "assets/edit.svg" "$pkgdir/usr/share/icons/hicolor/scalable/apps/$pkgname.svg"
  install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname" "LICENSE"
  install -Dm0644 -t "$pkgdir/usr/share/applications" "assets/com.microsoft.edit.desktop"
}
