# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=lxhotkey
pkgver=0.1.2
pkgrel=3
pkgdesc='Keyboard shortcuts configurator (part of LXDE)'
arch=(x86_64)
license=(GPL-2.0-or-later)
url='https://github.com/lxde/lxhotkey'
depends=(
  glib2
  glibc
  gtk3
  libfm
  libfm-extra
  libunistring
  libx11
)
makedepends=(
  git
  intltool
)
conflicts=(lxhotkey-gtk3)
replaces=(lxhotkey-gtk3)
groups=(lxde)
source=(
  "git+https://github.com/lxde/lxhotkey.git#tag=$pkgver"
  lxhotkey-notshowin.patch
)
b2sums=(
  aea9a58096f46bd24eeae1e9cf592d37e929d36fac7b61a7aecaf47be3f7934e008d7b9ad467c5dc74c46593f64add84eb84774f8729f3a5e6252e849b1d303e
  a6fba9164d6fda8f53f0b5eca32bae87b83bf4d1c031f3a79992717b6e6d3292315c8806e98e49515aacd60b139efbb34b4ab71efa5f0c8f31825a0ac4335fd4
)

prepare() {
  cd $pkgname

  # https://github.com/lxde/lxhotkey/pull/16
  git apply -3 ../lxhotkey-notshowin.patch

  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --with-gtk=3
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package_lxhotkey() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
