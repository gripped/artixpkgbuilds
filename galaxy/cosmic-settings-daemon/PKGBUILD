# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-settings-daemon
pkgver=1.0.9
pkgrel=1
epoch=1
pkgdesc='Cosmic settings daemon'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-settings-daemon
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  acpid
  adw-gtk-theme
  alsa-utils
  breeze-icons
  libgcc
  geoclue
  glibc
  libinput
  libpulse
  playerctl
  pop-sound-theme
  qt6ct
  libelogind
  wireplumber
  xdg-utils
)
makedepends=(
  cargo
  git
  libxkbcommon
  lld
)
optdepends=(
  'pulseaudio-alsa: Media keys support'
  'qt5ct: Theming support for Qt5 apps'
)
source=(
  git+https://github.com/pop-os/cosmic-settings-daemon.git#tag=epoch-${pkgver}
  cosmic-settings-daemon-lto.patch
)
b2sums=('ebd4d02f3ade878e2884b73da7b5770fa0b2783c14a56cb342d61a6d1dde64d7d37b5976f640c9145e32fee98882ed822769d07031ae150bffcf878382a6ef36'
        '935604b934c6fb805fdef20a716838373c1c9463ddc215af9c9bd162ebacc422181f1c227f37b59bc617496a60b88f8b16132fac001f61a2c777179abd8c634a')

prepare() {
  cd cosmic-settings-daemon
  patch -Np1 -i ../cosmic-settings-daemon-lto.patch
  cargo fetch --locked
  sed 's/libexec/lib/g' -i Makefile src/main.rs
  sed 's/sudo/wheel/g' -i data/polkit-1/rules.d/cosmic-settings-daemon.rules
}

build() {
  cd cosmic-settings-daemon
  export GEOCLUE_AGENT="/usr/lib/geoclue-2.0/demos/agent"

  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  make ARGS+=" --frozen --release" geoclue_agent='/usr/lib/geoclue-2.0/demos/agent'
}

package() {
  cd cosmic-settings-daemon
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
