# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Carl Smedstad <carsme@archlinux.org>
# Contributor: Nikita Almakov <nikita.almakov@gmail.com>

pkgname=rate-mirrors
pkgver=0.31.0
pkgrel=1
pkgdesc="Everyday-use client-side map-aware mirror ranking tool"
arch=('x86_64')
url="https://github.com/westandskif/rate-mirrors"
license=('CC-BY-NC-SA-3.0')
depends=(
  'glibc'
  'libgcc'
  'openssl'
)
makedepends=('cargo')
options=('!lto')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('ab90534a251c7a88d3092f48d7b53f0a161aaf09950fc43252f2327ca8dd2a5e7fae07b790fa97092c3f137c8c587d5a0425bf06430f4703bacc4d1ee63d6c24')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd $pkgname-$pkgver
  OPENSSL_NO_VENDOR=true cargo build --frozen --release
}

check() {
  cd $pkgname-$pkgver
  OPENSSL_NO_VENDOR=true cargo test --frozen
}

package() {
  cd $pkgname-$pkgver
  install -vDm755 target/release/rate_mirrors "$pkgdir/usr/bin/rate-mirrors"
}
