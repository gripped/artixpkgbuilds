# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Evangelos Foutras <foutrelis@archlinux.org>
# Contributor: Robin Candau <antiz@archlinux.org>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=xfce4-verve-plugin
pkgver=2.0.3
pkgrel=4
pkgdesc="Command line plugin for the Xfce panel"
arch=('x86_64')
license=('GPL-2.0-or-later')
url="https://docs.xfce.org/panel-plugins/xfce4-verve-plugin/start"
groups=('xfce4-goodies')
depends=('xfce4-panel')
makedepends=('git' 'intltool' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/panel-plugins/xfce4-verve-plugin.git#tag=$pkgname-$pkgver")
sha256sums=('98344add0c918d406034aa4f5e70a9e9e32b36635195c2ca9d94d683b2948e11')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --disable-debug
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
