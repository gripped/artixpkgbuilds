# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=chess-tui
pkgver=2.3.0
pkgrel=1
pkgdesc="Play chess in your terminal"
arch=('x86_64')
url="https://github.com/thomas-mauran/chess-tui"
license=('MIT')
depends=('gcc-libs' 'alsa-lib')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('48277a5d25c17aa1843280e4ad2095724c4902ce3d532e968a9725d7afa3aeb4d3c83698f32384fc3c02897649b39b8554e138861ccebb8f7fa5eeb6578ca741')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
