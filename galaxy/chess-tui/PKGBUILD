# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Orhun Parmaksız <orhun@archlinux.org>

pkgname=chess-tui
pkgver=2.2.0
pkgrel=1
pkgdesc="Play chess in your terminal"
arch=('x86_64')
url="https://github.com/thomas-mauran/chess-tui"
license=('MIT')
depends=('gcc-libs' 'alsa-lib')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('0b7fb8342751ae618d38a138e864cbf7fed6033e781c9202b7f0b96ef78ec620a8403b0abe04d1bb6343a48e5a61d341f8c9b1ceb0d97d9548c42dac774f9a7e')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
