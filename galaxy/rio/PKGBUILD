# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Orhun Parmaksız <orhun@archlinux.org>
# Contributor: Caleb Maclennan <caleb@alerque.com>
# Contributor: bbx0 <39773919+bbx0@users.noreply.github.com>
# Contributor: Raphael Amorim <rapha850@gmail.com>

pkgname=rio
pkgver=0.4.12
pkgrel=1
pkgdesc="A hardware-accelerated GPU terminal emulator powered by WebGPU"
arch=('x86_64')
url="https://github.com/raphamorim/rio"
license=('MIT')
depends=(alsa-lib libasound.so
         fontconfig libfontconfig.so
         freetype2 libfreetype.so
         glibc # libc.so libm.so ld-linux-x86-64.so
         hicolor-icon-theme
         libgcc libgcc_s.so
         libstdc++ libstdc++.so
         oniguruma # libonig.so
         vulkan-driver)
makedepends=(cargo
             cmake
             glslang
             libxcb
             libxkbcommon
             python
             scdoc)
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('47c00bf33c82f9e489ab841f54d0723810cdfb664ec3b15fd71242b3fd71b60cea00358f41d6783ebf65149da05b716a7a526115621915834d846f8362dfa4da')

_srcenv() {
  cd "${pkgname}-${pkgver}"
  export CARGO_HOME="$srcdir"
  export CARGO_PROFILE_RELEASE_DEBUG=2
  export CARGO_PROFILE_RELEASE_STRIP=false
  export CARGO_PROFILE_RELEASE_LTO=true
  export CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1
  export CARGO_PROFILE_RELEASE_OPT_LEVEL=3
  CFLAGS+=' -ffat-lto-objects'
  CXXFLAGS+=' -ffat-lto-objects'
  export RUSTONIG_DYNAMIC_LIBONIG=1
}

prepare() {
  _srcenv
  cargo fetch --locked --target host-tuple

  # Fix launchable ID in metainfo file
  sed -i "s/com.rioterm.Rio.desktop/rio.desktop/" "misc/com.rioterm.Rio.metainfo.xml"
}

build() {
  _srcenv
  cargo build --frozen --release --all-features
  make -C extra/man
}

check() {
  _srcenv
  cargo test --frozen --release --all-features --workspace
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
  install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
  install -Dm0644 -t "$pkgdir/usr/share/applications/" "misc/$pkgname.desktop"
  install -Dm0644 "misc/logo.svg" "$pkgdir/usr/share/icons/hicolor/scalable/apps/$pkgname.svg"
  install -Dm0644 -t "$pkgdir/usr/share/metainfo/" misc/com.rioterm.Rio.metainfo.xml
  install -Dm0644 -t "$pkgdir/usr/share/man/man1/" extra/man/*.1
  install -Dm0644 -t "$pkgdir/usr/share/man/man5/" extra/man/*.5
}

# vim: ts=2 sw=2 et:
