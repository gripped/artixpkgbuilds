# Maintainer: artist for Artix Linux

pkgname=xlibre-video-qxl
_pkgname=xf86-video-qxl
pkgver=0.1.6.3
pkgrel=1
pkgdesc='XLibre fork of X.Org X11 qxl video driver'
arch=('x86_64')
url="https://github.com/X11Libre"
license=('MIT')
groups=('xlibre-drivers')
depends=('spice' 'libudev' 'libxfont2' 'glibc')
optdepends=('python: for Xspice')
makedepends=('xlibre-xserver-devel' 'X-ABI-VIDEODRV_VERSION=28.0' 'spice-protocol'
             'xorgproto' 'libcacard')
conflicts=("xlibre-${_pkgname}" 'X-ABI-VIDEODRV_VERSION<28' 'X-ABI-VIDEODRV_VERSION>=29')
provides=("xlibre-${_pkgname}" "${_pkgname}")
replaces=("xlibre-${_pkgname}")
source=("${url}/${_pkgname}/archive/refs/tags/xlibre-${_pkgname}-${pkgver}.tar.gz")

build() {
  cd ${_pkgname}-xlibre-${_pkgname}-${pkgver}
  export CFLAGS=${CFLAGS/-fno-plt}
  export CXXFLAGS=${CXXFLAGS/-fno-plt}
  export LDFLAGS=${LDFLAGS/-Wl,-z,now}

  autoreconf -fi
  ./configure --enable-xspice --prefix=/usr
  make
}

package() {
  cd ${_pkgname}-xlibre-${_pkgname}-${pkgver}
  make DESTDIR="${pkgdir}/" install

  install -v -Dm0755 scripts/Xspice "${pkgdir}"/usr/bin/Xspice
  install -Dm644 "${srcdir}"/${_pkgname}-xlibre-${_pkgname}-${pkgver}/COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

sha256sums=('3e02f822510f135048e14d424dc969c7245be381c39847d092aea2360348b4ea')
