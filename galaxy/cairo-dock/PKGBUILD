# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Hu <integral@archlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tofe <chris.chapuis@gmail.com>
# Contributor: erm67 <erm67@yahoo.it>

pkgname=cairo-dock
pkgver=3.6.2
pkgrel=1.1
pkgdesc="Light eye-candy fully themable animated dock"
arch=('x86_64')
url="https://github.com/Cairo-Dock/${pkgname}-core"
license=('GPL-3.0-or-later')
depends=(
	'gtk3'
	'gdk-pixbuf2'
	'glib2'
	'cairo'
	'pango'
	'librsvg'
	'dbus'
	'dbus-glib'
	'libxml2'
	'libxrender'
	'glu'
	'curl'
	'libx11'
	'libxtst'
	'libxcomposite'
	'libxrandr'
	'libxinerama'
	'wayland'
	'json-c'
	'gtk-layer-shell'
)
makedepends=('cmake' 'extra-cmake-modules' 'wayland-protocols')
optdepends=('cairo-dock-plug-ins: Plugins for Cairo-Dock')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('71e351d904d86f04d489e5b58c510fe2276ab129aee9ba98308508abf2dcfa87')

build() {
	cmake -B build \
		-S "${pkgname}-core-${pkgver}" \
		-D CMAKE_BUILD_TYPE=None \
		-D CMAKE_INSTALL_PREFIX=/usr \
		-D enable-desktop-manager=False \
		-D enable-systemd-service=False

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
}
