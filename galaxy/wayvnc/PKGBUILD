# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Andri Yngvason <andri@yngvason.is>

pkgname=wayvnc
pkgver=0.10.1
pkgrel=1
pkgdesc='VNC server for wlroots-based Wayland compositors'
arch=(x86_64)
url=https://github.com/any1/wayvnc
license=(ISC)
depends=(
  glibc
  jansson
  libaml.so
  libdrm
  libneatvnc.so
  libpixman-1.so
  libxkbcommon.so
  mesa
  wayland
)
makedepends=(
  git
  meson
  ninja
  pam
  scdoc
)
optdepends=('pam: PAM authentication')
source=(git+https://github.com/any1/wayvnc.git#tag=v${pkgver})
b2sums=('665d93ce3f075b2edd02aaa6bdc865107a853e2de005f8d977fd858810aabe1f987cc7140f4bd990f1b699793756f6528f78f007252656d8509c514afbaf389a')

build() {
  artix-meson wayvnc build \
    -Dman-pages=enabled \
    -Dpam=enabled
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
  install -Dm 644 wayvnc/wayvnc.pam "${pkgdir}"/etc/pam.d/wayvnc
  install -Dm 644 wayvnc/COPYING -t "${pkgdir}"/usr/share/licenses/wayvnc
}
