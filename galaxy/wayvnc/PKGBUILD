# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Andri Yngvason <andri@yngvason.is>

pkgname=wayvnc
pkgver=0.10.0
pkgrel=2
pkgdesc='VNC server for wlroots-based Wayland compositors'
arch=(x86_64)
url=https://github.com/any1/wayvnc
license=(ISC)
depends=(
  glibc
  jansson
  libaml.so
  libdrm
  libneatvnc.so
  libpixman-1.so
  libxkbcommon.so
  mesa
  wayland
)
makedepends=(
  git
  meson
  ninja
  pam
  scdoc
)
optdepends=('pam: PAM authentication')
source=(git+https://github.com/any1/wayvnc.git#tag=v${pkgver})
b2sums=('cf572306cc49e8c881c88ba4ff082189f4f99e163ff9db96257eebb37a5d529a55655251d1fb2388b6849916d0440f73e5c4db27c61cb2cd5adac236cff741d0')

build() {
  artix-meson wayvnc build \
    -Dman-pages=enabled \
    -Dpam=enabled
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
  install -Dm 644 wayvnc/wayvnc.pam "${pkgdir}"/etc/pam.d/wayvnc
  install -Dm 644 wayvnc/COPYING -t "${pkgdir}"/usr/share/licenses/wayvnc
}
