# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: Thayne McCombs <astrothayne at gmail dot com>

pkgname=wev
pkgver=1.1.0
pkgrel=1
pkgdesc="A tool for debugging wayland events on a Wayland window, analagous to the X11 tool xev"
url='https://git.sr.ht/~sircmpwn/wev'
license=('MIT')
arch=('x86_64')
depends=('wayland' 'libxkbcommon')
makedepends=('scdoc' 'wayland-protocols')
source=("$pkgname-$pkgver.tar.gz::https://git.sr.ht/~sircmpwn/wev/archive/$pkgver.tar.gz")
b2sums=('2e2a232d33ec274e5d9afa4ea3f11a13eb3f91e7ba1faddd6b324dc6ef93ecf71ffae81fd4f50ab9f48b4aaae68f498302b2799dd5aa688c73aef3cfa54e36c1')

build() {
    cd "$pkgname-$pkgver"
    # LDFLAGS are not respected
    make CFLAGS="$CFLAGS $LDFLAGS"
}

package() {
    cd "$pkgname-$pkgver"
    make DESTDIR="$pkgdir/" PREFIX=/usr MANDIR=/usr/share/man install
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
