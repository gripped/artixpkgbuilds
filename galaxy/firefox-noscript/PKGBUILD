# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Eli schwartz <eschwartz@archlinux.org>

pkgname=firefox-noscript
pkgver=13.6.31
pkgrel=1
pkgdesc="Extension for firefox which disables javascript"
arch=('any')
url="https://noscript.net/"
license=('GPL2')
checkdepends=('jq')
makedepends=('nodejs' 'git' 'npm')
groups=('firefox-addons')
source=("git+https://github.com/hackademix/noscript.git#tag=$pkgver"
	"nscl.git::git+https://github.com/hackademix/nscl.git")
sha256sums=('95b14ff508d07c38e6b2c51d02e5b2e3d990b432e7f8c3f6d793abe3442a5b1d'
            'SKIP')
b2sums=('1935d66d8a05d4d67cf68b6715105cbabee7a1fcb544fdc4581087f707da93cb7112332363a1e9f77f62fce006ffecef43c917ac2bc76c7b39e4dea48f03ed5f'
        'SKIP')

prepare() {
  cd noscript
  git -c protocol.file.allow=always submodule update --init --recursive
}

build() {
  cd noscript
  npm install web-ext decomment
  PATH=`pwd`/node_modules/.bin:$PATH bash ./build.sh
}

package() {
  optdepends=('firefox' 'konform-browser')
  _extension_id="{73a6fe31-595d-460b-a920-fcc0f8843232}"
  _extension_dest="${pkgdir}/usr/lib/firefox/browser/extensions/${_extension_id}"
  install -Dm644 noscript/xpi/noscript-$pkgver.xpi "${_extension_dest}.xpi"
}
