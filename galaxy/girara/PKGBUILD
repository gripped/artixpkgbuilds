# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: mlq <mlq@pwmt.org>

pkgname=girara
pkgver=0.4.3
pkgrel=3
pkgdesc="User interface library focused on simplicity and minimalism"
url="https://pwmt.org/projects/girara"
arch=('x86_64')
license=('Zlib')
provides=('girara-gtk3' 'girara-common')
replaces=('girara-gtk3' 'girara-common')
conflicts=('girara-gtk3' 'girara-common')
depends=('gtk3' 'glib2' 'json-glib' 'pango' 'glibc')
makedepends=('meson' 'ninja' 'doxygen' 'check')
checkdepends=('xorg-server-xvfb')
source=("${pkgname}-${pkgver}.tar.gz::https://git.pwmt.org/pwmt/${pkgname}/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('0dcde66101f5779c879f5847e8cb9d5777c6fa803733b6fe9c05e17385b608cc02f2e119d456aa4810f623290057e9344bbcc6a64b4b36b4733b60b7e8d885b6')

build() {
        cd "${pkgname}-${pkgver}"
        artix-meson build
        ninja -C build
}

check() {
        cd "${pkgname}-${pkgver}"
        ninja -C build test
}

package() {
        cd "${pkgname}-${pkgver}"
        DESTDIR="${pkgdir}" ninja -C build install
        install -Dm 664 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
