# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: mlq <mlq@pwmt.org>

pkgname=girara
pkgver=0.4.5
pkgrel=1
pkgdesc="User interface library focused on simplicity and minimalism"
url="https://pwmt.org/projects/girara"
arch=('x86_64')
license=('Zlib')
provides=('girara-gtk3' 'girara-common')
replaces=('girara-gtk3' 'girara-common')
conflicts=('girara-gtk3' 'girara-common')
depends=('gtk3' 'glib2' 'json-glib' 'pango' 'glibc')
makedepends=('meson' 'ninja' 'doxygen' 'check')
checkdepends=('xorg-server-xvfb')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pwmt/${pkgname}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('38fc0f9b9c232e93786130986e53cbcc83bbcb02575bdbd799026d2dd2f46684c8fe2ac5ea7b1b46012a48f92d78537b50a7e9ac42b036cbbf06eda06de00117')

build() {
        cd "${pkgname}-${pkgver}"
        artix-meson build
        ninja -C build
}

check() {
        cd "${pkgname}-${pkgver}"
        ninja -C build test
}

package() {
        cd "${pkgname}-${pkgver}"
        DESTDIR="${pkgdir}" ninja -C build install
        install -Dm 664 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
