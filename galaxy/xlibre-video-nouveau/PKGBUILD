# Maintainer: artist for Artix Linux

pkgname=xlibre-video-nouveau
_pkgname=xf86-video-nouveau
pkgver=1.0.18.4
pkgrel=1
pkgdesc="XLibre fork of X.Org Open Source 3D acceleration driver for nVidia cards"
arch=('x86_64')
url="https://github.com/X11Libre"
license=('MIT')
depends=('mesa' 'libdrm' 'glibc')
makedepends=('xlibre-xserver-devel' 'X-ABI-VIDEODRV_VERSION=28.0')
conflicts=("xlibre-${_pkgname}" 'xorg-server<21.1.1' 'X-ABI-VIDEODRV_VERSION<28' 'X-ABI-VIDEODRV_VERSION>=29')
provides=("xlibre-${_pkgname}" "${_pkgname}")
replaces=("xlibre-${_pkgname}")
groups=('xlibre-drivers')
source=("${url}/${_pkgname}/archive/refs/tags/xlibre-${_pkgname}-${pkgver}.tar.gz")

build() {
  cd ${_pkgname}-xlibre-${_pkgname}-${pkgver}
  export CFLAGS=${CFLAGS/-fno-plt}
  export CXXFLAGS=${CXXFLAGS/-fno-plt}
  export LDFLAGS=${LDFLAGS/-Wl,-z,now}

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd ${_pkgname}-xlibre-${_pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install

  install -m755 -d "${pkgdir}/usr/share/licenses/xlibre-${_pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/xlibre-${_pkgname}/"
}

sha256sums=('d7de68d4fff6f4dd52b398feb41cdda191a01ac59417a42224fffd8f55c5c02e')
