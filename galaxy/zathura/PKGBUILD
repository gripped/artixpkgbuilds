# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: ML <neldoreth>

pkgname=zathura
pkgver=0.5.5
pkgrel=1.1
pkgdesc="Minimalistic document viewer"
url="https://pwmt.org/projects/zathura/"
arch=('x86_64')
license=('Zlib')
makedepends=('python-sphinx' 'texlive-bin' 'meson' 'ninja' 'appstream-glib' 'check')
checkdepends=('xorg-server-xvfb')
depends=('girara' 'sqlite' 'desktop-file-utils' 'file' 'libsynctex' 'file' 'libseccomp')
optdepends=('zathura-djvu: DjVu support'
            'zathura-pdf-poppler: PDF support using Poppler'
            'zathura-pdf-mupdf: PDF support using MuPDF'
            'zathura-ps: PostScript support'
            'zathura-cb: Comic book support')
# Temporarily switch sources while waiting for upstream to fix sources uploading on the pwmt.org website
# See https://git.pwmt.org/pwmt/zathura/-/issues/407#note_2414
source=(zathura-$pkgver.tar.gz::https://deb.debian.org/debian/pool/main/z/zathura/zathura_$pkgver.orig.tar.xz)
#source=(zathura-$pkgver.tar.gz::https://pwmt.org/projects/zathura/download/zathura-$pkgver.tar.xz)
sha256sums=('eb5d7af54bb0fc88f5f03f6be3fc3a18bfddd28d93ed9e6136bbdb8ff3baf88f')

build() {
  cd zathura-$pkgver
  artix-meson build

  cd build
  ninja
}

check() {
  cd zathura-$pkgver/build

  ninja test
}

package() {
  cd zathura-$pkgver/build
  DESTDIR="$pkgdir" ninja install

  install -D -m664 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
