# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfce4-cpufreq-plugin
pkgver=1.2.9
pkgrel=1
pkgdesc="CPU frequency and governor plugin for the Xfce4 panel"
arch=('x86_64')
license=('GPL-2.0-or-later')
url="https://docs.xfce.org/panel-plugins/xfce4-cpufreq-plugin/start"
groups=('xfce4-goodies')
depends=('xfce4-panel')
makedepends=('git' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/panel-plugins/xfce4-cpufreq-plugin.git#tag=$pkgname-$pkgver")
sha256sums=('676a1f6bc39fda9c46c2aa011de80041edcb953e2f266843ac2bff873d45234f')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
