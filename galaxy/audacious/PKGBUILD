# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Evangelos Foutras <foutrelis@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=audacious
pkgver=4.6.1
pkgrel=2
pkgdesc='Lightweight, advanced audio player focused on audio quality'
arch=(x86_64)
url='https://audacious-media-player.org/'
license=(BSD-2-Clause)
depends=(
  cairo
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  libgcc
  libstdc++
  pango
  qt6-base
  qt6-imageformats
  qt6-svg
)
makedepends=(
  git
  glib2-devel
  meson
)
optdepends=('unzip: zipped skins support')
source=("git+https://github.com/audacious-media-player/audacious.git#tag=$pkgname-$pkgver")
b2sums=(be95274e3ea80103aa61113bc034ecf9c3bcebf55db9146c3e7517adbd1b72bcdda816a9abe73c4da1809e20b7d0d2926f904bc3d19417b4269202bdd5b76749)

build() {
  _PRETTY_NAME=`sed -n 's/^PRETTY_NAME="\([^"]*\)"/\1/p' /etc/os-release`
  artix-meson $pkgname build \
    -D buildstamp="$_PRETTY_NAME"
  meson compile -C build
}

package() {
  depends+=(audacious-plugins)

  meson install -C build --destdir "$pkgdir"
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" $pkgname/COPYING
}
