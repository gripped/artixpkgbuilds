# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=audacious
pkgver=4.4.1
pkgrel=1
pkgdesc="Lightweight, advanced audio player focused on audio quality"
arch=('x86_64')
url="https://audacious-media-player.org/"
license=('BSD')
depends=('qt6-base' 'qt6-svg' 'qt6-imageformats' 'glib2' 'hicolor-icon-theme'
         'desktop-file-utils')
makedepends=('meson' 'glib2-devel')
optdepends=('unzip: zipped skins support')
source=(https://distfiles.audacious-media-player.org/$pkgname-$pkgver.tar.bz2)
sha256sums=('260d988d168e558f041bbb56692e24c535a96437878d60dfd01efdf6b1226416')

build() {
  artix-meson $pkgname-$pkgver build \
    -Dgtk=false \
    -Dbuildstamp='Arch Linux'
  meson compile -C build
}

package() {
  depends+=('audacious-plugins')

  meson install -C build --destdir "$pkgdir"
  install -Dm644 $pkgname-$pkgver/contrib/audacious.appdata.xml -t "$pkgdir/usr/share/metainfo"
  install -Dm644 $pkgname-$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
