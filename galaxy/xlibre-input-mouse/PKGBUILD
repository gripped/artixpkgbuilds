# Maintainer: artist for Artix Linux

pkgname=xlibre-input-mouse
_pkgname=xf86-input-mouse
pkgver=1.9.5.4
pkgrel=1
pkgdesc="XLibre fork of X.Org mouse input driver"
arch=(x86_64)
license=('custom')
url="https://github.com/X11Libre"
depends=('glibc' 'xorgproto')
makedepends=('xlibre-xserver-devel' 'X-ABI-XINPUT_VERSION=26.0')
conflicts=("xlibre-${_pkgname}" 'xorg-server<21.1.1' 'X-ABI-XINPUT_VERSION<26' 'X-ABI-XINPUT_VERSION>=27')
provides=("xlibre-${_pkgname}" "${_pkgname}")
replaces=("xlibre-${_pkgname}")
source=("${url}/${_pkgname}/archive/refs/tags/xlibre-${_pkgname}-${pkgver}.tar.gz")
groups=('xlibre-drivers')

build() {
  cd ${_pkgname}-xlibre-${_pkgname}-${pkgver}
  ./autogen.sh
  ./configure --prefix=/usr 
  make
}

package() {
  cd ${_pkgname}-xlibre-${_pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}"/${_pkgname}-xlibre-${_pkgname}-${pkgver}/COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

sha256sums=('9f2108e435d52dc5b373f4fa2a7503a53738ae911a899ce3bf351cd2b912e6e9')

