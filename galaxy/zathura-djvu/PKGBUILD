# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: karnath <karnathtorjian@gmail.com>

pkgname=zathura-djvu
pkgver=2026.01.30
pkgrel=1
pkgdesc="DjVu support for Zathura"
url="https://pwmt.org/projects/zathura-djvu/"
arch=('x86_64')
license=('Zlib')
depends=('djvulibre' 'zathura' 'desktop-file-utils')
makedepends=('meson' 'ninja' 'appstream' 'appstream-glib')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pwmt/${pkgname}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('3179b19b7540447c73571baf1dda06bb80dc685bf58ef4dfc59d0eec109bbb38dd4a1b4356bc61daa05811403199612acd1794bbbab75562346ee41b7195379f')

build() {
        cd "${pkgname}-${pkgver}"
        artix-meson build
        ninja -C build
}

package() {
        cd "${pkgname}-${pkgver}"
        DESTDIR="${pkgdir}" ninja -C build install
        install -Dm 664 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
