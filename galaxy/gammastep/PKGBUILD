# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=gammastep
pkgver=2.0.10
pkgrel=1
pkgdesc="Adjust the color temperature of your screen according to your surroundings."
arch=(x86_64)
url="https://gitlab.com/chinstrap/gammastep"
license=(GPL-3.0-or-later)
depends=(
  glibc
  hicolor-icon-theme
  libdrm
  libx11
  libxcb
  libxxf86vm
  wayland
)
makedepends=(
  glib2
  intltool
  python
  udev
)
optdepends=(
  'geoclue: for automatic location based on GeoClue2'
  'python-gobject: for gammastep-indicator'
  'python-xdg: for gammastep-indicator'
  'gtk3: for gammastep-indicator'
  'libappindicator-gtk3: for gammastep-indicator'
)
source=($url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('a12e38a2c8e6379d8bfef23810428d3389e16d0ec8d9e35819a21129c802347608f1aaac1946e2e44e4fcc96f82d0607d07554fe89832dffdb6e6b98863ebffe')
b2sums=('df612e87362f3954ca1cafc8521c3be5db0ccf62e801507535ee433cf0d8b61d6f7e2cfc1308f250ba9cce704f946d37e4250574723a491ae6ba2fec3550e227')

prepare() {
  cd $pkgname-v$pkgver
  ./bootstrap
}

build() {
  cd $pkgname-v$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  make -k check -C $pkgname-v$pkgver
}

package() {
  depends+=(
    glib2 libgio-2.0.so libgobject-2.0.so libglib-2.0.so
  )

  make DESTDIR="$pkgdir/" install -C $pkgname-v$pkgver
  install -vDm 644 $pkgname-v$pkgver/{CONTRIBUTING,NEWS,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
