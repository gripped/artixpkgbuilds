# Maintainer: artist for Artix Linux

pkgname=xlibre-input-wacom
_pkgname=xf86-input-wacom
pkgver=1.2.3.3
pkgrel=1
pkgdesc="XLibre fork of X.Org Wacom tablet driver"
arch=(x86_64)
url="https://github.com/X11Libre"
license=('GPL-2.0-or-later')
depends=('libxi' 'libxinerama' 'libxrandr' 'libudev' 'libx11' 'glibc')
makedepends=('xlibre-xserver-devel' 'X-ABI-XINPUT_VERSION=26.0' 'meson' 'gobject-introspection'
            # for tests:
            'python-libevdev' 'python-pytest' 'python-yaml' 'python-gobject' 'python-attrs')
conflicts=("xlibre-${_pkgname}" "$_pkgname" 'xorg-server<21.1.1' 'X-ABI-XINPUT_VERSION<26' 'X-ABI-XINPUT_VERSION>=27')
replaces=("xlibre-${_pkgname}")
provides=("xlibre-${_pkgname}" "x11win-input-wacom" "${_pkgname}")
source=("${url}/${_pkgname}/archive/refs/tags/xlibre-${_pkgname}-${pkgver}.tar.gz")
groups=('xlibre-drivers')

build() {
  artix-meson ${_pkgname}-xlibre-${_pkgname}-${pkgver} build \
    -D xorg-conf-dir=/usr/share/X11/xorg.conf.d/ \
    -D unittests=enabled

  meson configure build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  rm -r ${pkgdir}/usr/lib/systemd
}

sha256sums=('21f25957a0049cd4f001c75996137ed794714e042cb7382d1ac77e23715c51fa')

