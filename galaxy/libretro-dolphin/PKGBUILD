# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-dolphin
pkgver=33059
pkgrel=1
pkgdesc='Nintendo GC/Wii core'
arch=(x86_64)
url=https://github.com/libretro/dolphin
license=(GPL-2.0-only)
groups=(libretro)
depends=(
  alsa-lib
  bluez-libs
  bzip2
  enet
  fmt
  gcc-libs
  glibc
  hidapi
  libevdev
  libgl
  libretro-core-info
  libusb
  libx11
  libxi
  libxrandr
  lzo
  sfml
  libudev.so
  xz
  zlib
  zstd
)
makedepends=(
  clang
  cmake
  git
  libglvnd
  mesa
  ninja
  python
)
_commit=83438f9b1a2c832319876a1fda130a5e33d4ef87
source=(
  libretro-dolphin::git+https://github.com/libretro/dolphin.git#commit=${_commit}
  fmt-10.patch
)
b2sums=('1307455835872b063ba87df95c7258b544a4e2e691a38aaa9b2b620e492f437fb05d6a759572dbce861ba56bbf8e5af186b992f64e534ee9767e1ea304632801'
        'e669c2497beaa42e5bde4033ebac00b85bb4f28ebff910c952dc4354de174267fcf2a1d27d6ca8b421c5c1e81591f7375150b0954d92c172d144e7a89a884186')

prepare() {
  cd libretro-dolphin
  patch -Np1 -i ../fmt-10.patch
}

pkgver() {
  cd libretro-dolphin
  git rev-list --count HEAD
}

build() {
  export CC=clang
  export CXX=clang++
  cmake -S libretro-dolphin -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
    -DENABLE_LTO=ON \
    -DENABLE_NOGUI=OFF \
    -DENABLE_QT=OFF \
    -DENABLE_TESTS=OFF \
    -DLIBRETRO=ON \
    -DUSE_SHARED_ENET=ON \
    -Wno-dev
  cmake --build build
}

package() {
  install -Dm 644 build/dolphin_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
