# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-dolphin
pkgver=20260902.223111.ge1e6d25fa139
pkgrel=1
pkgdesc='Nintendo GC/Wii core'
arch=(x86_64)
url=https://github.com/libretro/dolphin
license=(GPL-2.0-only)
groups=(libretro)
depends=(
  bluez-libs
  bzip2
  curl
  enet
  glibc
  glslang
  hidapi
  libgl
  libretro-core-info
  libspng
  libusb
  libx11
  libxi
  lz4
  lzo
  pugixml
  sfml
  xz
  xxhash
  zlib
  zlib-ng
  zstd
)
makedepends=(
  cmake
  git
  libglvnd
  mesa
  ninja
  python
  vulkan-headers
)
source=(
  libretro-dolphin::git+https://github.com/libretro/dolphin.git#commit=${pkgver##*.g}
  git+https://github.com/mutouyun/cpp-ipc.git
  git+https://github.com/weisslj/cpp-optparse.git
  git+https://github.com/fmtlib/fmt.git
  git+https://github.com/ocornut/imgui.git
  git+https://github.com/epezent/implot.git
  git+https://github.com/mgba-emu/mgba.git
  git+https://github.com/zlib-ng/minizip-ng.git
  git+https://github.com/syoyo/tinygltf.git
  git+https://github.com/GPUOpen-LibrariesAndSDKs/VulkanMemoryAllocator.git
  git+https://github.com/e-dant/watcher.git
)
b2sums=('7d3bb76e891c8a8be551f0fc2e1a4b4377d5e0ce97094a6127b73c7a07197e3fdea90471ed7b1b4228dc20d3902586ce8cf1f58c9731c95b3e3dbfc7c0a1d866'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP')

prepare() {
  cd libretro-dolphin
  for submodule in Externals/{cpp-ipc/cpp-ipc,cpp-optparse/cpp-optparse,fmt/fmt,imgui/imgui,implot/implot,mGBA/mgba,minizip-ng/minizip-ng,tinygltf/tinygltf,VulkanMemoryAllocator,watcher}; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../${submodule##*/}
    git -c protocol.file.allow=always submodule update ${submodule}
  done
}


build() {
  cmake -S libretro-dolphin -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_ANALYTICS=OFF \
    -DENABLE_LTO=ON \
    -DENABLE_QT=OFF \
    -DLIBRETRO=ON \
    -DLIBRETRO_STATIC=ON \
    -Wno-dev
  cmake --build build
}

package() {
  install -Dm 644 build/dolphin_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
