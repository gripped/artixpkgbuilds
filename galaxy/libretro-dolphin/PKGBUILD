# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-dolphin
pkgver=33044
pkgrel=3
pkgdesc='Nintendo GC/Wii core'
arch=(x86_64)
url=https://github.com/libretro/dolphin
license=(GPL2)
groups=(libretro)
depends=(
  alsa-lib
  bluez-libs
  bzip2
  enet
  fmt
  gcc-libs
  glibc
  hidapi
  libevdev
  libgl
  libretro-core-info
  libusb
  libx11
  libxi
  libxrandr
  lzo
  sfml
  libudev.so
  xz
  zlib
  zstd
)
makedepends=(
  clang
  cmake
  git
  libglvnd
  mesa
  ninja
  python
)
_commit=89a4df725d4eb24537728f7d655cddb1add25c18
source=(
  libretro-dolphin::git+https://github.com/libretro/dolphin.git#commit=${_commit}
  libretro-dolphin-missing-headers.patch
  fmt-10.patch
)
b2sums=('3be9f3efc9a41f677543ba27c44d5e60470c93ef210005e76a44b5785381eb7ebf3e54eee6d302e2651feea37aec94f905b4ee213936f303c78c30bda593bd98'
        'b2f532c0b878e300bbe3da9f0113bdb1388fa4621c56eafbf9ba9a7b44de5ed05eaf8a6698dc9558ead2510df8eee1bf8bb609a9b4dc821596d24aa772c415ef'
        'e669c2497beaa42e5bde4033ebac00b85bb4f28ebff910c952dc4354de174267fcf2a1d27d6ca8b421c5c1e81591f7375150b0954d92c172d144e7a89a884186')

prepare() {
  cd libretro-dolphin
  patch -Np1 -i ../libretro-dolphin-missing-headers.patch
  patch -p1 -i ../fmt-10.patch # Fix build with fmt 10
}

pkgver() {
  cd libretro-dolphin
  git rev-list --count HEAD
}

build() {
  export CC=clang
  export CXX=clang++
  cmake -S libretro-dolphin -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_LTO=ON \
    -DENABLE_NOGUI=OFF \
    -DENABLE_QT=OFF \
    -DENABLE_TESTS=OFF \
    -DLIBRETRO=ON \
    -DUSE_SHARED_ENET=ON \
    -Wno-dev
  cmake --build build
}

package() {
  install -Dm 644 build/dolphin_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
