# Maintainer: artist for Artix Linux

pkgname=xlibre-video-vmware
_pkgname=xf86-video-vmware
pkgver=13.4.0.3
pkgrel=1.1
pkgdesc="XLibre fork of X.org vmware video driver"
arch=(x86_64)
url="https://github.com/X11Libre"
license=('MIT AND X11')
depends=('mesa' 'libudev' 'libxext' 'libx11' 'libdrm' 'glibc')
makedepends=('xlibre-xserver-devel' 'X-ABI-VIDEODRV_VERSION=28.0')
conflicts=("xlibre-${_pkgname}" 'xorg-server<21.1.1' 'X-ABI-VIDEODRV_VERSION<28' 'X-ABI-VIDEODRV_VERSION>=29')
replaces=("xlibre-${_pkgname}")
provides=("xlibre-${_pkgname}" "${_pkgname}" 'xf86-video-vmware')   # for virtualbox-guest-utils / nous
groups=('xlibre-drivers')
source=("${url}/${_pkgname}/archive/refs/tags/xlibre-${_pkgname}-${pkgver}.tar.gz")
options=('!emptydirs')

build() {
  cd ${_pkgname}-xlibre-${_pkgname}-${pkgver}
  export CFLAGS=${CFLAGS/-fno-plt}
  export CXXFLAGS=${CXXFLAGS/-fno-plt}
  export LDFLAGS=${LDFLAGS/-Wl,-z,now}

  ./autogen.sh
  ./configure --prefix=/usr --enable-vmwarectrl-client
  make
}

package() {
  cd ${_pkgname}-xlibre-${_pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  
  install -m755 -d "${pkgdir}/usr/share/licenses/xlibre-${_pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/xlibre-${_pkgname}/"
}

sha256sums=('1773016119fc0e7d5992ad95e1e7ad26a103b5f0198516389447bed5dd9d963d')
