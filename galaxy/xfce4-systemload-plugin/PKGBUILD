# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Evangelos Foutras <foutrelis@archlinux.org>
# Contributor: Robin Candau <antiz@archlinux.org>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=xfce4-systemload-plugin
pkgver=1.4.0
pkgrel=1
pkgdesc="System load plugin for the Xfce panel"
arch=('x86_64')
license=('BSD-2-Clause')
url="https://docs.xfce.org/panel-plugins/xfce4-systemload-plugin/start"
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libgtop' 'upower')
makedepends=('git' 'meson' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/panel-plugins/xfce4-systemload-plugin.git#tag=$pkgname-$pkgver")
sha256sums=('4399797f50c907fc297838ef7b8da69654c0ab2bff0cab281a7b9fe925239d67')

build() {
  artix-meson $pkgname build \
    --localstatedir=/var
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dm644 $pkgname/COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
