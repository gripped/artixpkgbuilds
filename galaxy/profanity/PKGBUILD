# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: jason ryan <jasonwryan@gmail.com>
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>

pkgbase=profanity
pkgname=('profanity' 'profanity-gtk')
pkgver=0.18.2
pkgrel=1
epoch=1
pkgdesc='Console based XMPP client'
url='https://profanity-im.github.io'
arch=('x86_64')
license=('GPL3')
_clidepends=(
  'libcurl.so' 'libncursesw.so' 'expat' 'glib2' 'libotr'
  'libnotify.so' 'libgpgme.so' 'python' 'libgcrypt' 'libsignal-protocol-c'
  'glibc' 'libreadline.so' 'libassuan' 'libgpg-error' 'libstrophe.so'
  )
_gtkdepends=(
  'libgtk-3.so' 'libxss' 'libx11' 'gdk-pixbuf2' 'cairo'
  )
makedepends=(
  ${_clidepends[@]} ${_gtkdepends[@]} 'meson'
  )
checkdepends=('cmocka')
source=(https://github.com/profanity-im/profanity/archive/${pkgver}/${pkgbase}-${pkgver}.tar.gz)
sha256sums=('d5431065b4d966a745284e64337ec7e11a3fd20179de00b0f98b9cf6004e2ab5')
sha512sums=('ca1972f4262f8ca92aa268fcba48e50ad8b9685446aafe1ab2097cd750e4b4a17c6ae669feecd8cfff748fe30e9986933d511ad12bd68822c80e8ef5bea41c27')
b2sums=('a8cc82b252a222b1c412aa7100036cac5cb0737aa4fd9727d868c63d5105c1b47bee207dbebcf7845dc8575b742438373bec0b874dfe56c45575d8bacbbde6ca')

prepare() {
  cp -a "${srcdir}"/${pkgname}-${pkgver}{,-gtk}
}

build() {
  echo "Building non-gtk variant..."
  (
    local meson_options=(
      -D icons-and-clipboard=disabled
      -D xscreensaver=disabled
      -D notifications=enabled
      -D python-plugins=enabled
      -D c-plugins=enabled
      -D otr=enabled
      -D omemo=enabled
      -D pgp=enabled
    )

    artix-meson ${pkgname}-${pkgver} build "${meson_options[@]}"
    meson compile -C build
  )
  echo "Building gtk variant..."
  (
    local meson_options=(
      -D icons-and-clipboard=enabled
      -D xscreensaver=enabled
      -D notifications=enabled
      -D python-plugins=enabled
      -D c-plugins=enabled
      -D otr=enabled
      -D omemo=enabled
      -D pgp=enabled
    )

    artix-meson ${pkgname}-${pkgver} build-gtk "${meson_options[@]}"
    meson compile -C build-gtk
  )
}

package_profanity() {
  depends=(
    ${_clidepends[@]}
  )
  meson install -C build --destdir "${pkgdir}"
}

package_profanity-gtk() {
  pkgdesc+=' (gtk support)'
  depends=(
    ${_clidepends[@]} ${_gtkdepends[@]}
  )
  provides=('profanity')
  conflicts=('profanity')
  meson install -C build-gtk --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
