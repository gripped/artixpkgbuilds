# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: jason ryan <jasonwryan@gmail.com>
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>

pkgbase=profanity
pkgname=('profanity' 'profanity-gtk')
pkgver=0.18.1
pkgrel=1
epoch=1
pkgdesc='Console based XMPP client'
url='https://profanity-im.github.io'
arch=('x86_64')
license=('GPL3')
_clidepends=(
  'libcurl.so' 'libncursesw.so' 'expat' 'glib2' 'libotr'
  'libnotify.so' 'libgpgme.so' 'python' 'libgcrypt' 'libsignal-protocol-c'
  'glibc' 'libreadline.so' 'libassuan' 'libgpg-error' 'libstrophe.so'
  )
_gtkdepends=(
  'libgtk-3.so' 'libxss' 'libx11' 'gdk-pixbuf2' 'cairo'
  )
makedepends=(
  ${_clidepends[@]} ${_gtkdepends[@]} 'meson'
  )
checkdepends=('cmocka')
source=(https://github.com/profanity-im/profanity/archive/${pkgver}/${pkgbase}-${pkgver}.tar.gz)
sha256sums=('c6cd7d4497d4c63ce44e6d26afbb227e23a1ac299a1851ce7fdb232209b06e4b')
sha512sums=('de3237a67b0fd9446d100b962139367344fd4d6fc33e6ef4ec61f8e82af3298f0562e7e5dfc1313a02d5c8eb2455313ae6b1fe55025814620ffb709891ca4351')
b2sums=('3bc7dff6efd26132d9d6eb9243a14f92031d2b7bdf20f9a4637fc58961f0e7e0bcdb370aa52bb994f15880ddd2bc8c185574cb4aac1a34f4e7bbaa9562936243')

prepare() {
  cp -a "${srcdir}"/${pkgname}-${pkgver}{,-gtk}
}

build() {
  echo "Building non-gtk variant..."
  (
    local meson_options=(
      -D icons-and-clipboard=disabled
      -D xscreensaver=disabled
      -D notifications=enabled
      -D python-plugins=enabled
      -D c-plugins=enabled
      -D otr=enabled
      -D omemo=enabled
      -D pgp=enabled
    )

    artix-meson ${pkgname}-${pkgver} build "${meson_options[@]}"
    meson compile -C build
  )
  echo "Building gtk variant..."
  (
    local meson_options=(
      -D icons-and-clipboard=enabled
      -D xscreensaver=enabled
      -D notifications=enabled
      -D python-plugins=enabled
      -D c-plugins=enabled
      -D otr=enabled
      -D omemo=enabled
      -D pgp=enabled
    )

    artix-meson ${pkgname}-${pkgver} build-gtk "${meson_options[@]}"
    meson compile -C build-gtk
  )
}

package_profanity() {
  depends=(
    ${_clidepends[@]}
  )
  meson install -C build --destdir "${pkgdir}"
}

package_profanity-gtk() {
  pkgdesc+=' (gtk support)'
  depends=(
    ${_clidepends[@]} ${_gtkdepends[@]}
  )
  provides=('profanity')
  conflicts=('profanity')
  meson install -C build-gtk --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
