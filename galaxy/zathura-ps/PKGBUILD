# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: karnath <karnathtorjian@gmail.com>

pkgname=zathura-ps
pkgver=2026.07.18
pkgrel=1
pkgdesc="Adds ps support to zathura by using the libspectre library"
url="https://pwmt.org/projects/zathura-ps"
arch=('x86_64')
license=('Zlib')
depends=('libspectre' 'zathura')
makedepends=('meson' 'ninja' 'appstream' 'desktop-file-utils')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pwmt/${pkgname}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('6d823b2ad1dee7ffd9c74c77610f871a71e8a1e911ba5fea50650287afdb5c25c63fb1bd65df9a73e6bb469579e91fd6577fb39dde8513bfbaf0b8763a346aec')

build() {
        cd "${pkgname}-${pkgver}"
        artix-meson build
        ninja -C build
}

check() {
	cd "${pkgname}-${pkgver}"
	meson test -C build --print-errorlogs
}

package() {
        cd "${pkgname}-${pkgver}"
        DESTDIR="${pkgdir}" ninja -C build install
        install -Dm 664 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
