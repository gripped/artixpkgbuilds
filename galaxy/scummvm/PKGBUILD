# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: juergen <juergen@archlinux.org>
# Contributor: Manolis Tzanidakis, Petter Gundersen

pkgname=scummvm
pkgver=2026.2.0
pkgrel=1
pkgdesc="A 'virtual machine' for several classic graphical point-and-click adventure games."
arch=('x86_64')
license=('GPL-3.0-only')
url="http://www.scummvm.org/"
depends=('libpng' 'libtheora' 'sdl2' 'sdl2_net' 'fluidsynth' 'flac' 'faad2' 'libvorbis' 'libmad' 'freetype2'
         'libgl' 'glu' 'libjpeg-turbo' 'libmpeg2' 'curl' 'a52dec' 'giflib' 'glew' 'libspeechd' 'gtk3' 'sndio'
         'glib2' 'libstdc++' 'zlib' 'hicolor-icon-theme')
makedepends=('mesa')
source=("https://downloads.scummvm.org/frs/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha512sums=('093369a5e3d849e9d4fb12a247fa8af68314d7bba59bdc2e5270d0e01fdd711761fa9707e8b61acac1967e223f2db12aa208a397000ee6630f5b2adf365d490d')
b2sums=('f6627dd4c517338cae58ddf3cec923b702e76e665b3047cbc0be53a09eb005ae33ca611ccf3397cf911388be45aaeb0dc469f7c8136c953bb8ffacfc0611404b')

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --enable-release \
    --disable-debug \
    --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
