# Maintainer: arc-d3v <arc-d3v@artixlinux.org>
# Contributor: dec05eba <dec05eba@protonmail.com>

pkgname=gpu-screen-recorder-ui
pkgver=1.11.8
pkgrel=1
pkgdesc='A fullscreen overlay UI for GPU Screen Recorder in the style of ShadowPlay'
arch=('x86_64')
url="https://git.dec05eba.com/gpu-screen-recorder-ui"
license=('GPL-3.0-only')
makedepends=(
  git
  meson
  libcap
  linux-api-headers
  gtk-update-icon-cache
  desktop-file-utils
)
depends=(
  libstdc++
  libgcc
  glibc
  dbus
  gpu-screen-recorder
  gpu-screen-recorder-notification
  libglvnd
  libx11
  libxrandr
  libxrender
  libxcomposite
  libxfixes
  libxext
  libxi
  libxcursor
  libpulse
  libdrm
  wayland
  pango
  freetype2
  glib2
)

source=(git+https://repo.dec05eba.com/gpu-screen-recorder-ui#tag=${pkgver})
sha512sums=('a240abeafb1f9ef0ea7b3306de11de66e54f0ea7cdf598a54b43cfaa629970b745c0d831bf6634cc970eaefa784efe9b1ff9d73080096c989a907048a96ba412')
install="${pkgname}.install"

build() {
  cd "$srcdir"/$pkgname
  arch-meson build --buildtype=release -Dstrip=true
  meson compile -C build
}

package() {
  cd "$srcdir"/$pkgname
  meson install -C build --destdir "$pkgdir"
}
