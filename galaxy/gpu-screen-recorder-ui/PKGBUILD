# Maintainer: arc-d3v <arc-d3v@artixlinux.org>
# Contributor: dec05eba <dec05eba@protonmail.com>

pkgname=gpu-screen-recorder-ui
pkgver=1.11.7
pkgrel=1
pkgdesc='A fullscreen overlay UI for GPU Screen Recorder in the style of ShadowPlay'
arch=('x86_64')
url="https://git.dec05eba.com/gpu-screen-recorder-ui"
license=('GPL-3.0-only')
makedepends=(
  git
  meson
  libcap
  linux-api-headers
  gtk-update-icon-cache
  desktop-file-utils
)
depends=(
  libstdc++
  libgcc
  glibc
  dbus
  gpu-screen-recorder
  gpu-screen-recorder-notification
  libglvnd
  libx11
  libxrandr
  libxrender
  libxcomposite
  libxfixes
  libxext
  libxi
  libxcursor
  libpulse
  libdrm
  wayland
  pango
  freetype2
  glib2
)

source=(git+https://repo.dec05eba.com/gpu-screen-recorder-ui#tag=${pkgver})
sha512sums=('d64d09696352e074d335138b6aed8e0ba2834a65393aff24189515b298881884045c73992c362ffe3417e03cabb7f0adf4b0e084fc9d0d02d0945258ea0179b7')
install="${pkgname}.install"

build() {
  cd "$srcdir"/$pkgname
  arch-meson build --buildtype=release -Dstrip=true
  meson compile -C build
}

package() {
  cd "$srcdir"/$pkgname
  meson install -C build --destdir "$pkgdir"
}
