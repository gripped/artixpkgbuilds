# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Simon Perry <aur [at] sanxion [dot] net>
# Contributor: Jonas Nyrén <jonas.nyren*mindkiller.com>

pkgname=libsidplayfp
pkgver=3.1.0
pkgrel=1
pkgdesc='Library to play Commodore 64 music'
arch=(x86_64)
url='https://github.com/libsidplayfp/libsidplayfp'
license=(GPL-2.0-only)
makedepends=(
  git
  vice
  xa
  libusb
  libresidfp
  libexsid
  libgcrypt
)
optdepends=(
  'vice: better SID support'
  'libusb: USBSID support'
)
provides=(
  libsidplayfp.so
  libstilview.so
)
source=(
  "$pkgname::git+$url#tag=v$pkgver"
  'github.com-libsidplayfp-exsid-driver::git+https://github.com/libsidplayfp/exsid-driver'
  'github.com-LouDnl-USBSID-Pico-driver::git+https://github.com/LouDnl/USBSID-Pico-driver'
)
sha512sums=('4c7059e70fe5b335f0691ab00d31a3c2a2cbf6c70964109cbe7f8a06d5f4d17878343d2406ab6a062ceafb96302929ce4ea2c8c08b43c136831711441d3ba069'
            'SKIP'
            'SKIP')
b2sums=('62542dcbb6195746cc861af9fcca9025be4f849694dc3ab70e315ded57a8ad9897751c7d595164b655606342e4e66b5f106a97486261384afa2ba4b42699eda8'
        'SKIP'
        'SKIP')

prepare() {
  cd "$pkgname"

  # prepare git submodule(s)
  git submodule init
  git config submodule.src/builders/exsid-builder/driver.url "$srcdir/github.com-libsidplayfp-exsid-driver"
  git config submodule.src/builders/usbsid-builder/driver.url "$srcdir/github.com-LouDnl-USBSID-Pico-driver"
  git -c protocol.file.allow=always submodule update
}

build() {
  cd "$pkgname"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --with-exsid \
    --with-usbsid

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  depends+=(
    glibc
    libgcc libgcc_s.so
    libstdc++ libstdc++.so
    libresidfp libresidfp.so
    libexsid libexsid.so
  )
  DESTDIR="$pkgdir" make -C "$pkgname" install
}
