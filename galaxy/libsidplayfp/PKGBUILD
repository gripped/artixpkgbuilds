# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Simon Perry <aur [at] sanxion [dot] net>
# Contributor: Jonas Nyrén <jonas.nyren*mindkiller.com>

pkgname=libsidplayfp
pkgver=2.11.0
pkgrel=1
pkgdesc='Library to play Commodore 64 music'
arch=('x86_64')
url='https://github.com/libsidplayfp/libsidplayfp'
license=('GPL-2.0-only')
depends=('glibc' 'gcc-libs' 'libgcrypt')
makedepends=('git' 'vice' 'xa')
optdepends=('vice: better SID support')
source=(
  "$pkgname::git+$url#tag=v$pkgver"
  'github.com-libsidplayfp-exsid-driver::git+https://github.com/libsidplayfp/exsid-driver'
  'github.com-libsidplayfp-resid::git+https://github.com/libsidplayfp/resid'
)
sha512sums=('97d1fff5fcb2c7c617f90f118717b98ce8afa692802e489faa462babd3e6f982d3c73fe03e2279320743e5e3a116e43ac6f01feff498997863cd8f8e14629e14'
            'SKIP'
            'SKIP')
b2sums=('9eaacfc7b4350ea8694e0d3c261a3176ef7b8cb5464ef565ef14d8b74e58a1bbb1486d407fc56786c7a430b169115527a3ddacd849d961ba3d1092efda339705'
        'SKIP'
        'SKIP')

prepare() {
  cd "$pkgname"

  # prepare git submodule(s)
  git submodule init
  git config submodule.src/builders/exsid-builder/driver.url "$srcdir/github.com-libsidplayfp-exsid-driver"
  git config submodule.src/builders/resid-builder/resid.url "$srcdir/github.com-libsidplayfp-resid"
  git -c protocol.file.allow=always submodule update
}

build() {
  cd "$pkgname"

  autoreconf -vfi

  ./configure --prefix=/usr

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  DESTDIR="$pkgdir" make -C "$pkgname" install
}
