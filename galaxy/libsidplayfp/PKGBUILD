# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Simon Perry <aur [at] sanxion [dot] net>
# Contributor: Jonas Nyrén <jonas.nyren*mindkiller.com>

pkgname=libsidplayfp
pkgver=2.16.0
pkgrel=1
pkgdesc='Library to play Commodore 64 music'
arch=(x86_64)
url='https://github.com/libsidplayfp/libsidplayfp'
license=(GPL-2.0-only)
depends=(glibc gcc-libs libgcrypt)
makedepends=(git vice xa libusb)
optdepends=(
  'vice: better SID support'
  'libusb: USBSID support'
)
source=(
  "$pkgname::git+$url#tag=v$pkgver"
  'github.com-libsidplayfp-exsid-driver::git+https://github.com/libsidplayfp/exsid-driver'
  'github.com-libsidplayfp-resid::git+https://github.com/libsidplayfp/resid'
)
sha512sums=('25f4692b6e5feb6c9097ea5d5bd82db2c0dd11db9b1da19962b98211a8de37b667f58a0335867d208dc53fe88da7c2234c4f737b1ad3d8a3661b15b906e1cdbe'
            'SKIP'
            'SKIP')
b2sums=('723a159ac57c159c72ab0eb437f3871eba7836a19d0cbfb346c2d8b9e2e26f5dde2ea7afb97e9c99fe1ea89c133ffec75d7477c43fba700c61eec1e2578bf992'
        'SKIP'
        'SKIP')

prepare() {
  cd "$pkgname"

  # prepare git submodule(s)
  git submodule init
  git config submodule.src/builders/exsid-builder/driver.url "$srcdir/github.com-libsidplayfp-exsid-driver"
  git config submodule.src/builders/resid-builder/resid.url "$srcdir/github.com-libsidplayfp-resid"
  git -c protocol.file.allow=always submodule update
}

build() {
  cd "$pkgname"

  autoreconf -vfi

  ./configure --prefix=/usr

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  DESTDIR="$pkgdir" make -C "$pkgname" install
}
