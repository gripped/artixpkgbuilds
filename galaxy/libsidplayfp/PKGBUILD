# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Simon Perry <aur [at] sanxion [dot] net>
# Contributor: Jonas Nyrén <jonas.nyren*mindkiller.com>

pkgname=libsidplayfp
pkgver=2.13.1
pkgrel=1
pkgdesc='Library to play Commodore 64 music'
arch=('x86_64')
url='https://github.com/libsidplayfp/libsidplayfp'
license=('GPL-2.0-only')
depends=('glibc' 'gcc-libs' 'libgcrypt')
makedepends=('git' 'vice' 'xa')
optdepends=('vice: better SID support')
source=(
  "$pkgname::git+$url#tag=v$pkgver"
  'github.com-libsidplayfp-exsid-driver::git+https://github.com/libsidplayfp/exsid-driver'
  'github.com-libsidplayfp-resid::git+https://github.com/libsidplayfp/resid'
)
sha512sums=('457a4463856a7ac3e3667a8808cebabf9d8979ba4d22ecf4516b7a490ceececd59117a7bb4c5af9eae28b2054a20865848e582ae6a6b513e29deb30b0b969dda'
            'SKIP'
            'SKIP')
b2sums=('3d2df41a8f944875af5feace6b7c072cc51f9ef9663df172c63e02f1c14b1a8b3bc10150685a21d7f0591023076d5096140773516d21b2f2605702defb07827b'
        'SKIP'
        'SKIP')

prepare() {
  cd "$pkgname"

  # prepare git submodule(s)
  git submodule init
  git config submodule.src/builders/exsid-builder/driver.url "$srcdir/github.com-libsidplayfp-exsid-driver"
  git config submodule.src/builders/resid-builder/resid.url "$srcdir/github.com-libsidplayfp-resid"
  git -c protocol.file.allow=always submodule update
}

build() {
  cd "$pkgname"

  autoreconf -vfi

  ./configure --prefix=/usr

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  DESTDIR="$pkgdir" make -C "$pkgname" install
}
