# Maintainer: Peter Jung <ptr1337@archlinux.org>

pkgname=cosmic-osk
pkgver=1.9.0
pkgrel=1
pkgdesc='On-screen keyboard for the COSMIC desktop environment'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-osk
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  libgcc
  glibc
  libxkbcommon
  wayland
)
makedepends=(
  cargo
  clang
  git
  just
  lld
)
source=(
  git+https://github.com/pop-os/cosmic-osk.git#tag=epoch-${pkgver}
)
b2sums=('99ee3bb356522fc0f39851b57586bc0126a1521644eaea78e54e095ab6442a95a090151ebf8c2bade4a96aaf737b8f5e590896d6386da98183f463c28f99825a')

prepare() {
  cd cosmic-osk
  cargo fetch --locked
}

build() {
  cd cosmic-osk
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-osk
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
