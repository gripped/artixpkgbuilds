# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-snes9x
pkgver=2651
pkgrel=1
epoch=1
pkgdesc='Super Nintendo Entertainment System core'
arch=(x86_64)
url=https://github.com/libretro/snes9x
license=(
  custom
  GPL2
  LGPL2.1
)
groups=(libretro)
depends=(libretro-core-info)
makedepends=(git)
_commit=49f484569ff2aec7ff08e7598a97d6c9e6eae72d
source=(libretro-snes9x::git+https://github.com/libretro/snes9x.git#commit=${_commit})
sha256sums=('22d3e98bb7839ca94a2c84c2afb20afda61f4d56af6d77419417b95f4fe70d8b')

pkgver() {
  cd libretro-snes9x

  git rev-list --count HEAD
}

build() {
  make -C libretro-snes9x/libretro
}

package() {
  install -Dm 644 libretro-snes9x/libretro/snes9x_libretro.so -t "${pkgdir}"/usr/lib/libretro/
  install -Dm 644 libretro-snes9x/LICENSE -t "${pkgdir}"/usr/share/licenses/libretro-snes9x/
}

# vim: ts=2 sw=2 et:
