# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Maxime Gauduin <archlinux.org>

pkgname=libretro-snes9x
pkgver=20260816.181733.g890b5d445538
pkgrel=1
epoch=1
pkgdesc='Super Nintendo Entertainment System core'
arch=(x86_64)
url=https://github.com/libretro/snes9x
license=(
  GPL-2.0-or-later
  LGPL-2.1-only
  LicenseRef-custom
)
groups=(libretro)
depends=(
  glibc
  libretro-core-info
  libstdc++
  zlib
)
makedepends=(git)
source=(libretro-snes9x::git+https://github.com/libretro/snes9x.git#commit=${pkgver##*.g})
b2sums=('fab3e270cdfda03ef0f0a4e83f875755b2a493a20e2f0bc9fa5d5fc5a44b1d9220c2e841624f8eb5a420597c8cba3ac3025b50a227ec9913bffdf9cc69c3d1db')

build() {
  make -C libretro-snes9x/libretro
}

package() {
  install -Dm 644 libretro-snes9x/libretro/snes9x_libretro.so -t "${pkgdir}"/usr/lib/libretro/
  install -Dm 644 libretro-snes9x/LICENSE -t "${pkgdir}"/usr/share/licenses/libretro-snes9x/
}
