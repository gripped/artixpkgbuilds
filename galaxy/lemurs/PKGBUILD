# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Robin Candau <antiz@archlinux.org>

pkgname=lemurs
pkgver=0.4.0
pkgrel=1
pkgdesc="A customizable TUI display/login manager written in Rust"
url="https://github.com/coastalwhite/lemurs"
arch=('x86_64')
license=('Apache-2.0 OR MIT')
depends=('udev' 'pam')
makedepends=('cargo')
backup=("etc/${pkgname}/config.toml")
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('05641ea9f0b8fa459c737adccd2f4714fd29e69e07a213c117b35013837bfe3a')

prepare() {
	cd "${pkgname}-${pkgver}"
	export RUSTUP_TOOLCHAIN=stable
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "${pkgname}-${pkgver}"
	export RUSTUP_TOOLCHAIN=stable
	export CARGO_TARGET_DIR=target
	cargo build --frozen --release --all-features
}

check() {
	cd "${pkgname}-${pkgver}"
	export RUSTUP_TOOLCHAIN=stable
	cargo test --frozen --all-features
}

package() {
	cd "${pkgname}-${pkgver}"
	install -Dm 755 "target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

	install -dm 755 "${pkgdir}/etc/lemurs/wms"
	install -dm 755 "${pkgdir}/etc/lemurs/wayland"
	install -Dm 644 extra/config.toml "${pkgdir}/etc/lemurs/config.toml"
	install -Dm 755 extra/xsetup.sh "${pkgdir}/etc/lemurs/xsetup.sh"

	install -Dm 644 extra/lemurs.pam "${pkgdir}/etc/pam.d/lemurs"

	install -Dm 644 LICENSE-MIT "${pkgdir}/usr/share/licenses/${pkgname}/MIT"
	install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
