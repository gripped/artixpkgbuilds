# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Robin Candau <antiz@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Douglas Soares de Andrade <dsandrade@gmail.com>
# Contributor: Michael Fellinger <m.fellinger@gmail.com>
# Contributor: Steffen Jaeckel <arch-pkgs@jaeckel.eu>

pkgname=sqlitebrowser
pkgver=3.13.1
pkgrel=2
pkgdesc="A light GUI editor for SQLite databases, built on top of Qt"
url="https://sqlitebrowser.org"
arch=('x86_64')
license=('GPL-3.0-only')
depends=('glibc' 'gcc-libs' 'qt5-base' 'qt5-svg' 'hicolor-icon-theme' 'sqlcipher'
         'qcustomplot' 'qhexedit2' 'qscintilla-qt5' 'libqcustomplot.so' 'libqhexedit.so')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools')
source=("git+https://github.com/sqlitebrowser/sqlitebrowser#tag=v${pkgver}")
sha256sums=('75739f9667624004259242d64c9ebcc0452e870155afde73b9619a5ef117fcbb')
validpgpkeys=('05E463FDB5802C3D00257F9C79EAD0AB3BD7DD2C'  # Justin Clift (OpenPGP key for signing git commits) <justin@postgresql.org>
              '17ABC291B166F699409851AA9503B162E0416CE3') # SeongTae Jeong (OpenPGP key for signing git commits) <seongtaejg@gmail.com>

prepare() {
	cd "${pkgname}"
	# Remove vendored libraries (we want to use our system libraries instead)
	rm -rv libs/{qcustomplot-source,qhexedit,qscintilla}
}

build() {
	cd "${pkgname}"
	cmake -B build \
		-DCMAKE_INSTALL_PREFIX='/usr' \
		-DCMAKE_BUILD_TYPE='None' \
		-DBUILD_STABLE_VERSION='ON' \
		-DENABLE_TESTING='ON' \
		-DFORCE_INTERNAL_QCUSTOMPLOT='OFF' \
		-DFORCE_INTERNAL_QSCINTILLA='OFF' \
		-DFORCE_INTERNAL_QHEXEDIT='OFF' \
		-Dsqlcipher='1' \
		-Wno-dev \
		-G Ninja
	ninja -C build
}

check() {
	cd "${pkgname}/build"
	ctest -V
}

package() {
	cd "${pkgname}"
	DESTDIR="${pkgdir}" ninja -C build install
}
