# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Evangelos Foutras <foutrelis@archlinux.org>
# Contributor: Robin Candau <antiz@archlinux.org>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgname=thunar-media-tags-plugin
pkgver=0.4.0
pkgrel=3
pkgdesc="Adds special features for media files to the Thunar File Manager"
arch=('x86_64')
license=('GPL-2.0-or-later')
url="https://docs.xfce.org/xfce/thunar/media-tags"
groups=('xfce4-goodies')
depends=('taglib' 'thunar')
makedepends=('git' 'intltool' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/thunar-plugins/thunar-media-tags-plugin.git#tag=$pkgname-$pkgver")
sha256sums=('9ee0e933ecdbb6a3c0b49fd5bef354133d8b331f5cf682db1e174d9b32b624ed')

prepare() {
  cd $pkgname
  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
