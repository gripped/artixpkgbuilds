# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alex Filgueira <alexfilgueira (at antergos.com)>
# Contributor: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xapp
pkgver=3.2.0
pkgrel=2
pkgdesc="Common library for X-Apps project"
arch=(x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL)
depends=(libdbusmenu-gtk3 libgnomekbd xapp-symbolic-icons)
optdepends=('python: for mate-desktop status applet')
makedepends=(git meson gobject-introspection python-gobject vala glib2-devel)
provides=(xapps)
conflicts=(xapps)
replaces=(xapps)
source=(git+${url}#tag=$pkgver)
sha256sums=('60793a54c51b57cea2fd28a7cbd14076476ad1966b14671c369ccc0253d14d24')
b2sums=('21dd7a808439cbdbb7d01facaca7f533abe1bcfa8244de26eafa12db0381c37fcf5e458456e6d70d825d39a6c0b9c8e9a5ea70726ec1cf149e052796b53b3380')

build() {
  artix-meson ${pkgname} build
  meson compile -C build
}

package(){
  meson install -C build --destdir="$pkgdir"

  # byte-compile python modules since meson does not implement autotoolsâ€™ py-compile.
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  python -m compileall -o 0 -o 1 -o 2 --hardlink-dupes -s "${pkgdir}" "${pkgdir}"${site_packages}
}
