# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alex Filgueira <alexfilgueira (at antergos.com)>
# Contributor: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xapp
pkgver=3.2.1
pkgrel=1
pkgdesc="Common library for X-Apps project"
arch=(x86_64)
url='https://github.com/linuxmint/xapp'
license=('GPL-3.0-only AND LGPL-3.0-only')
depends=(
  bash
  cairo
  dconf
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  libdbusmenu-gtk3
  libgnomekbd
  libx11
  libxkbfile
  pango
  python
  python-gobject
  python-setproctitle
  xapp-symbolic-icons
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  meson
  vala
)
provides=(xapps)
conflicts=(xapps)
replaces=(xapps)
source=("git+https://github.com/linuxmint/xapp.git#tag=$pkgver")
sha256sums=(052b06513f205e62ec038c6045612ff228d0966c39f5e47fd83a70dd72846824)
b2sums=(be3e2fc6140abeec0cf5362a1ca63b4a65f0ec50bbddbf0a78331a3713d7b42c62db6df033c0c08e5f7c023b0b7a5ad0080248f0cea7dc9d362a9b530d3e29e6)

build() {
  artix-meson $pkgname build \
    -D docs=true
  meson compile -C build
}

package(){
  meson install -C build --destdir="$pkgdir"

  # byte-compile python modules since meson does not implement autotoolsâ€™ py-compile.
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  python -m compileall -o 0 -o 1 -o 2 --hardlink-dupes -s "$pkgdir" "$pkgdir"$site_packages
}
