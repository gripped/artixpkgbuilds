# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-system-monitor
pkgver=1.28.1
pkgrel=3.1
pkgdesc="A system monitor for MATE"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL-2.0-or-later')
depends=('gtkmm3' 'libgtop' 'libwnck3' 'gettext' 'polkit')
makedepends=('autoconf-archive' 'glib2-devel' 'itstool' 'mate-common' 'python' 'yelp-tools' 'elogind')
conflicts=('mate-system-monitor-gtk3')
replaces=('mate-system-monitor-gtk3')
groups=('mate-extra')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/mate-desktop/mate-system-monitor/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('284f310e2c50b23a3852c4905996a0f74223e5b7681913e54af339940289e75e')

prepare() {
	cd "${pkgname}-${pkgver}"
	sed -i 's/libsystemd/libelogind/g' configure.ac
	./autogen.sh
}

build() {
	cd "${pkgname}-${pkgver}"
	./configure \
	           --prefix=/usr \
	           --libexecdir="/usr/lib/${pkgname}" \
	           --localstatedir=/var \
	           --disable-systemd
	make
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
