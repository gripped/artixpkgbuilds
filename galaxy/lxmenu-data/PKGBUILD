# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=lxmenu-data
pkgver=0.1.7
pkgrel=1
pkgdesc='Freedesktop.org desktop menus for LXDE'
arch=(any)
url='https://github.com/lxde/lxmenu-data'
license=(LGPL-2.1-or-later)
makedepends=(
  git
  intltool
)
source=("git+https://github.com/lxde/lxmenu-data.git#tag=$pkgver")
b2sums=(4de363cce5007310fc4d7282132847e4da65365c3c2291f8753d3acbd78c0a8bffdb3f5651231a5b2e2bb4a71dab2a407db1bdd10c85a871907f06b0300d6bf4)

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
