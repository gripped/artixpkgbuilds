# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=xdg-desktop-portal-cosmic
pkgver=1.4.0
pkgrel=1
epoch=1
pkgdesc='A backend implementation for xdg-desktop-portal for the COSMIC desktop environment'
arch=(x86_64)
url=https://github.com/pop-os/xdg-desktop-portal-cosmic
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  libgcc
  glibc
  libpipewire
  libxkbcommon
  mesa
  wayland
  xdg-desktop-portal
)
makedepends=(
  cargo
  clang
  git
  just
  mold
)
provides=(xdg-desktop-portal-impl)
source=(
  git+https://github.com/pop-os/xdg-desktop-portal-cosmic.git#tag=epoch-${pkgver}
)
b2sums=('58fc6979319ff1ef0804b01bb3e95f70e1f31bca0034b7ac19e12d6890117a91d9f8bc9dfeaafab1754c1bb5981462f65fe9a5a1d220efc3f799da559d5e8caa')

prepare() {
  cd xdg-desktop-portal-cosmic
  cargo fetch --locked
}

build() {
  cd xdg-desktop-portal-cosmic
  RUSTFLAGS+=" -C link-arg=-fuse-ld=mold"
  CARGO_NET_OFFLINE=true just build
}

package() {
  cd xdg-desktop-portal-cosmic
  just rootdir="${pkgdir}" prefix='/usr' libexecdir='/usr/lib' install
}

# vim: ts=2 sw=2 et:
