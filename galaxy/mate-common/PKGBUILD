# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-common
pkgver=1.28.0
pkgrel=3
pkgdesc='Common development macros for MATE'
arch=(any)
url='https://github.com/mate-desktop/mate-common'
license=(GPL-3.0-or-later)
depends=(
  autoconf
  autoconf-archive
  automake
  bash
  gettext
  glib2
  gtk-doc
  intltool
  libtool
  pkgconf
  yelp-tools
)
makedepends=(git)
source=("git+https://github.com/mate-desktop/mate-common.git#tag=v$pkgver")
b2sums=(0147a9b261bed4dbcf1728fe995ee5eed208a2875c797772ee6a209796db4d08bb1875041ddd825f4919abea249dfd346fe02ad7b7e5fe7a0c43b5bb391056ef)

prepare() {
  cd $pkgname
  autoreconf -fiv
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
