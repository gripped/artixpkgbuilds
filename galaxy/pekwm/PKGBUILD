# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Eddie Lozon <almostlucky@attbi.com>

pkgname=pekwm
pkgver=0.4.2
pkgrel=1
pkgdesc='Tabbed X11 window manager'
arch=(x86_64)
license=(GPL-2.0-or-later)
url='https://www.pekwm.se/'
makedepends=(cmake gendesk git)
depends=(curl libjpeg-turbo libpng libxft libxinerama libxpm libxrandr systemd-libs)
backup=(etc/pekwm/autoproperties
        etc/pekwm/autoproperties_typerules
        etc/pekwm/config
        etc/pekwm/config_system
        etc/pekwm/keys
        etc/pekwm/menu
        etc/pekwm/mouse
        etc/pekwm/mouse_click
        etc/pekwm/mouse_sloppy
        etc/pekwm/mouse_system
        etc/pekwm/panel
        etc/pekwm/start
        etc/pekwm/vars)
source=("https://www.pekwm.se/pekwm/uv/pekwm-$pkgver.tar.gz")
b2sums=('0ce50d98eef0c7bcd2a5cde7774f75807ed8e2fd95bbee1ad05d911b284eb5e4de152e6f7a3c08db2fb9ac3fdb027da4c146db2b7fabc6846c6f76530c1e5256')

prepare() {
  gendesk -f -n --pkgname $pkgname --pkgdesc "$pkgdesc" --name PekWM
  gendesk -f -n -wm --pkgname ${pkgname}_kdm --exec $pkgname --name PekWM
}

build() {
  cmake \
    -B build \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_LIBDIR=lib \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -S $pkgname-$pkgver \
    -W no-dev
  make -C build
}

package() {
  DESTDIR="$pkgdir" make -C build install
  install -Dm644 -t "$pkgdir/usr/share/xsessions" $pkgname.desktop
  install -Dm644 -t "$pkgdir/usr/share/apps/kdm/sessions" ${pkgname}_kdm.desktop
}
