# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Vaporeon <vaporeon@vaporeon.io>

pkgname=rgbds
pkgver=0.9.4
pkgrel=1
pkgdesc='Rednex GameBoy Development System'
arch=(x86_64)
url=https://github.com/gbdev/rgbds
license=(MIT)
depends=(
  glibc
  libpng
)
makedepends=(
  cmake
  git
  ninja
)
_tag=d1829ed92327a9f9210ffd45d865331f24dfa4e6
source=(git+https://github.com/gbdev/rgbds.git#tag=${_tag})
b2sums=('d0ebb9629e53df74558dedbae3d6e45c5efe21af8b453b6b4291e9cd936ddad751ab900e7bcdcd9d6c96784237168ff2298df690e0999ca102d3d2c9e6fa3375')

pkgver() {
  cd rgbds

  git describe --tags | sed 's/^v//'
}

build() {
  cmake -S rgbds -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  rm -rf "${pkgdir}"/build
  install -Dm 644 rgbds/LICENSE -t "${pkgdir}"/usr/share/licenses/rgbds/
  find rgbds/contrib/zsh_compl -type f -exec install -Dm 644 {} -t "${pkgdir}"/usr/share/zsh/site-functions/ \;
}

# vim: ts=2 sw=2 et:
