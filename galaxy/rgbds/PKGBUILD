# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Vaporeon <vaporeon@vaporeon.io>

pkgname=rgbds
pkgver=1.0.3
pkgrel=1
pkgdesc='Rednex GameBoy Development System'
arch=(x86_64)
url=https://github.com/gbdev/rgbds
license=(MIT)
depends=(
  glibc
  libpng
  libstdc++
)
makedepends=(
  cmake
  git
  ninja
)
source=(git+https://github.com/gbdev/rgbds.git#tag=v${pkgver})
b2sums=('4d9bb4afed33e2c05e05fd0658ae05b4d1203061f53b7f9955b003600ac9ef14eef6cdf3df1e69ec44682dea3f3b3aa9c6af342a6e5077c73e859b66c3798bdf')

build() {
  cmake -S rgbds -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  rm -rf "${pkgdir}"/build
  install -Dm 644 rgbds/LICENSE -t "${pkgdir}"/usr/share/licenses/rgbds/
  find rgbds/contrib/zsh_compl -type f -exec install -Dm 644 {} -t "${pkgdir}"/usr/share/zsh/site-functions/ \;
}

# vim: ts=2 sw=2 et:
