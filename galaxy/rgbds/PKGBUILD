# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Vaporeon <vaporeon@vaporeon.io>

pkgname=rgbds
pkgver=1.0.0
pkgrel=1
pkgdesc='Rednex GameBoy Development System'
arch=(x86_64)
url=https://github.com/gbdev/rgbds
license=(MIT)
depends=(
  glibc
  libpng
)
makedepends=(
  cmake
  git
  ninja
)
_tag=fb9fa6038cfc39a448f043806219a58c86daf930
source=(git+https://github.com/gbdev/rgbds.git#tag=${_tag})
b2sums=('6dff4d983c8f2a89b157c8482a931dbf6738e474b72b1545c146329ae5f10a6de45c7649e5a04342cbe0a1710b5283a018d1e1595dfb222d5dd213398a7c8445')

pkgver() {
  cd rgbds

  git describe --tags | sed 's/^v//'
}

build() {
  cmake -S rgbds -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  rm -rf "${pkgdir}"/build
  install -Dm 644 rgbds/LICENSE -t "${pkgdir}"/usr/share/licenses/rgbds/
  find rgbds/contrib/zsh_compl -type f -exec install -Dm 644 {} -t "${pkgdir}"/usr/share/zsh/site-functions/ \;
}

# vim: ts=2 sw=2 et:
