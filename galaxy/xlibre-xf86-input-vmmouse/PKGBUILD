# Maintainer: artist for Artix Linux

pkgname=xlibre-xf86-input-vmmouse
_pkgname=xf86-input-vmmouse
pkgver=13.2.0.1
pkgrel=1.1
pkgdesc="XLibre fork of X.Org VMWare Mouse input driver"
arch=(x86_64)
license=('custom')
url="https://github.com/X11Libre"
depends=('libelogind')
makedepends=('xlibre-xserver-devel' 'X-ABI-XINPUT_VERSION=26.0' 'xorgproto')
conflicts=(${_pkgname} 'xorg-server<21.1.1' 'X-ABI-XINPUT_VERSION<26' 'X-ABI-XINPUT_VERSION>=27')
provides=(${_pkgname})
source=("${url}/${_pkgname}/archive/refs/tags/${pkgname}-${pkgver}.tar.gz")
groups=('xlibre-drivers')

build() {
  cd ${_pkgname}-${pkgname}-${pkgver}
  ./autogen.sh
  ./configure --prefix=/usr \
    --with-udev-rules-dir=/usr/lib/udev/rules.d
  make
}

package() {
  cd ${_pkgname}-${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}"/${_pkgname}-${pkgname}-${pkgver}/COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  rm -rfv "${pkgdir}"/usr/{lib,share}/hal
}

sha256sums=('66a6ad55767d0a3169633b542111820370a8e9ac918b8fdd1c55d24320c2144a')
