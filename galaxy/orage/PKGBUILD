# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Robin Candau <antiz@archlinux.org>
# Contributor: 0b100100 <0b100100 at protonmail dot ch>

pkgname=orage
pkgver=4.20.3
pkgrel=2
pkgdesc="A simple calendar application with reminders for Xfce"
arch=('x86_64')
url="https://docs.xfce.org/apps/orage/start"
license=('GPL-2.0-or-later')
depends=('glib2' 'libxfce4ui' 'libnotify' 'libical')
makedepends=('git' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/apps/orage.git#tag=$pkgname-$pkgver"
         libical-4.patch)
sha256sums=('eda1ab1c426169c04601cf5ac423563723926020a8c09108c542257f17c0bd1d'
            '708187f0a724cdccd692bb903c90e592a8a83edbd95628de09ff91c4996fea18')

prepare() {
  cd $pkgname
  patch -p1 -i ../libical-4.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
