# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xreader
pkgver=4.6.5
pkgrel=1
pkgdesc='Document viewer for files like PDF and Postscript'
arch=(x86_64)
url='https://github.com/linuxmint/xreader'
license=(GPL-2.0-or-later)
groups=(x-apps)
depends=(
  at-spi2-core
  cairo
  dconf
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  libarchive
  libice
  libsm
  libtiff
  libx11
  libxml2
  pango
  poppler-glib
  webkit2gtk-4.1
  xapp
  zlib
)
makedepends=(
  djvulibre
  git
  glib2-devel
  gobject-introspection
  intltool
  itstool
  libgxps
  libspectre
  meson
  texlive-bin
)
optdepends=(
  'djvulibre: support for djvu files'
  'libgxps: support for xps files'
  'libspectre: support for dvi and ps files'
  'mathjax2: support for math in epub files'
  'texlive-bin: support for dvi files'
)
source=("git+https://github.com/linuxmint/xreader.git#tag=${pkgver}")
b2sums=('9515c2a6d60d707b8200e41632734f5c2d341ab69f6c38b0d1af604dc8f7d5073f65989a0e892834212cecc74d9e9e722290554b6ac72cf9270d6e6d1b42a4db')

build() {
  artix-meson $pkgname build \
    --libexecdir=lib/$pkgname \
    -D mathjax-directory=/usr/share/mathjax2 \
    -D comics=true \
    -D djvu=true \
    -D dvi=true \
    -D t1lib=true \
    -D pixbuf=true \
    -D help_files=true \
    -D introspection=true
  meson compile -C build
}

package(){
  meson install -C build --destdir="$pkgdir"
}
