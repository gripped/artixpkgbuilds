# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xreader
pkgver=4.6.0
pkgrel=1
pkgdesc="Document viewer for files like PDF and Postscript. X-Apps Project."
arch=(x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL-2.0-or-later)
groups=(x-apps)
depends=(poppler-glib webkit2gtk-4.1 xapp)
optdepends=('djvulibre: support for djvu files'
            'libgxps: support for xps files'
            'libspectre: support for dvi and ps files'
            'mathjax2: support for math in epub files'
            'texlive-bin: support for dvi files')
makedepends=(meson intltool itstool gobject-introspection djvulibre
             git libgxps libspectre texlive-bin glib2-devel)
source=(git+https://github.com/linuxmint/xreader.git#tag=${pkgver})
sha256sums=('486281f9afe2edfd78f601c8ae169333511db098e8320cd77f87d37854f66659')
b2sums=('5b5d10782bbc5346f774483e4f3f8e14ad8cf007d790fdcf54519e2eff5bcf9972d67b540e9db55c9b06ca846a7d28d569f763baea8ca03a030783f384c96617')

build() {
  artix-meson ${pkgname} build \
    --libexecdir=lib/${pkgname} \
    -Dmathjax-directory=/usr/share/mathjax2 \
    -Dcomics=true \
    -Ddjvu=true \
    -Ddvi=true \
    -Dt1lib=true \
    -Dpixbuf=true \
    -Dhelp_files=true \
    -Dintrospection=true
  meson compile -C build
}

package(){
  meson install -C build --destdir="$pkgdir"
}
