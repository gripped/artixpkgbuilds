# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Caleb Maclennan <caleb@alerque.com>
# Contributor: Ilaï Deutel

pkgname=typst
pkgver=0.15.1
pkgrel=2
epoch=1
pkgdesc='A markup-based typesetting system for the sciences'
arch=(x86_64)
url="https://$pkgname.app"
_url="https://github.com/$pkgname/$pkgname"
license=(Apache-2.0)
depends=(glibc # libc.so libm.so
         libgcc libgcc_s.so
         openssl libssl.so libcrypto.so)
replaces=(typst-bin)
makedepends=(cargo
             git)
optdepends=('otf-libertinus: additional weights and variants for default font'
            'ttf-linux-libertine: legacy default font prior to v0.12.0')
_archive="$pkgname-$pkgver"
source=("$_url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('c07909e01a2a6941e52c9b616e48c209c755eed416d62bcf5583c37a4aca01a3')

_srcenv() {
	cd "$_archive"
	export CARGO_HOME="$srcdir"
	export CARGO_PROFILE_RELEASE_DEBUG=2
	export CARGO_PROFILE_RELEASE_STRIP=false
	export CARGO_PROFILE_RELEASE_LTO=true
	export CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1
	export CARGO_PROFILE_RELEASE_OPT_LEVEL=3
	CFLAGS+=' -ffat-lto-objects'
	export OPENSSL_NO_VENDOR=true
}

prepare() {
	_srcenv
	cargo fetch --locked --target host-tuple
}

build() {
	local hash=$(gunzip < "$_archive.tar.gz" | git get-tar-commit-id)
	_srcenv
	export TYPST_VERSION="$pkgver"
	export TYPST_COMMIT_SHA="${hash::8}"
	export GEN_ARTIFACTS=artifacts/
	cargo build -p typst-cli --frozen --release
}

check() {
	cd "$_archive"
	cargo test --workspace --frozen
}

package() {
	cd "$_archive"
	local _artifacts='crates/typst-cli/artifacts'
	install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
	install -Dm0644 -t "$pkgdir/usr/share/man/man1/" "$_artifacts/$pkgname"*.1
	install -Dm0644 -t "$pkgdir/usr/share/zsh/site-functions/" "$_artifacts/_$pkgname"
	install -Dm0644 -t "$pkgdir/usr/share/fish/vendor_completions.d/" "$_artifacts/$pkgname.fish"
	install -Dm0644 "$_artifacts/$pkgname.bash" "$pkgdir/usr/share/bash-completion/completions/$pkgname"
}
