# Maintainer: artist for Artix Linux

pkgname=xlibre-xf86-video-vmware
_pkgname=xf86-video-vmware
pkgver=13.4.0.2
pkgrel=1.3
pkgdesc="XLibre fork of X.org vmware video driver"
arch=(x86_64)
url="https://github.com/X11Libre"
license=('MIT AND X11')
depends=('mesa' 'libudev' 'libxext' 'libx11' 'libdrm' 'glibc')
makedepends=('xlibre-xserver-devel' 'X-ABI-VIDEODRV_VERSION=28.0')
conflicts=('xorg-server<21.1.1' 'X-ABI-VIDEODRV_VERSION<28' 'X-ABI-VIDEODRV_VERSION>=29')
provides=('xf86-video-vmware')   # for virtualbox-guest-utils / nous
groups=('xlibre-drivers')
source=("${url}/${_pkgname}/archive/refs/tags/${pkgname}-${pkgver}.tar.gz")
options=('!emptydirs')

build() {
  cd ${_pkgname}-${pkgname}-${pkgver}
  export CFLAGS=${CFLAGS/-fno-plt}
  export CXXFLAGS=${CXXFLAGS/-fno-plt}
  export LDFLAGS=${LDFLAGS/-Wl,-z,now}

  ./autogen.sh
  ./configure --prefix=/usr --enable-vmwarectrl-client
  make
}

package() {
  cd ${_pkgname}-${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

sha256sums=('3589e2abbde8e74af50fa07bbafc0624b000a35d3bf481b406d10df14fc6c2ba')
