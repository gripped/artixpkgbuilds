# Maintainer: artist for Artix Linux

pkgname=xlibre-xf86-input-synaptics
_pkgname=xf86-input-synaptics
pkgver=1.10.0.1
_pkgver=1.10.0
pkgrel=1.1
pkgdesc="XLibre fork of X.Org Synaptics driver for notebook touchpads"
arch=('x86_64')
license=('MIT')
url="https://github.com/X11Libre"
depends=('libxtst' 'libevdev' 'libx11' 'libxi' 'glibc')
makedepends=('xlibre-xserver-devel' 'X-ABI-XINPUT_VERSION=26.0' 'xorgproto')
conflicts=(${_pkgname} 'X-ABI-XINPUT_VERSION<26' 'X-ABI-XINPUT_VERSION>=27' 'synaptics')
provides=(${_pkgname} 'synaptics')
groups=('xlibre-drivers')
source=("${url}/${_pkgname}/archive/refs/tags/${_pkgname}-${_pkgver}.tar.gz")
install=xf86-input-synaptics.install

build() {
  cd ${srcdir}/${_pkgname}-${_pkgname}-${_pkgver}
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/${_pkgname}-${_pkgname}-${_pkgver}

  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}"/${_pkgname}-${_pkgname}-${_pkgver}/COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

sha256sums=('b2dfcffb7aafada79bfd28dccfbb1e25ff93a1500ccb4b71e995b8fbcd511fe8')
