# Maintainer: arc-d3v <arc-d3v@artixlinux.org>

pkgname=halloy
pkgver=2026.6
pkgrel=1
pkgdesc='An open-source IRC client'
arch=(x86_64)
url='https://halloy.chat'
license=(GPL-3.0-or-later)
depends=(
  alsa-lib
  libgcc
  glibc
  hicolor-icon-theme
  openssl
  libxcb
  sqlite
)
makedepends=(
  git
  rust
)
options=(!lto)
source=("$pkgname::git+https://github.com/squidowl/halloy#tag=$pkgver")
sha512sums=('bda1f3668eb221938864b42755956b889f6cf3f008a8b2f906e064ae50ea6fd7c430fedad7e60bbb2f6358a89d7f1d15326650af8b6e86f7e8b09e12544bcd29')
b2sums=('ef59545f1ae403c4f1c8e5ba647dbe568bf7d59618a97f5f12f7f3b49e47fce07747600d719e1f94fb92197c28af912ced825bd9d2400a0e08b4caebd9f0d1de')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target $(rustc --print host-tuple)
}

build() {
  cd "$pkgname"

  LIBSQLITE3_SYS_USE_PKG_CONFIG=1 cargo build --locked --profile packaging
}

check() {
  cd "$pkgname"

  LIBSQLITE3_SYS_USE_PKG_CONFIG=1 cargo test --locked --profile packaging
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/packaging/halloy

  # desktop files
  pushd assets/linux
  install -vDm644 -t "$pkgdir/usr/share/metainfo" org.squidowl.halloy.appdata.xml
  install -vDm644 -t "$pkgdir/usr/share/applications" org.squidowl.halloy.desktop
  cp -vr icons "$pkgdir/usr/share"
  popd

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
