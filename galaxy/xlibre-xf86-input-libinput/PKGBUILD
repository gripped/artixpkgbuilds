# Maintainer: artist for Artix Linux

pkgname=xlibre-xf86-input-libinput
_pkgname=xf86-input-libinput
pkgver=1.5.1.0
pkgrel=9.1
pkgdesc="XLibre fork of the generic input driver for the X.Org server based on libinput"
arch=('x86_64')
license=('MIT')
url="https://github.com/X11Libre"
depends=('libinput' 'glibc')
makedepends=('xlibre-xserver-devel' 'X-ABI-XINPUT_VERSION=26.0' 'libxi' 'libx11' 'xorgproto' 'libxfont2')
conflicts=('xorg-server-common<25.0.0.0' 'X-ABI-XINPUT_VERSION<26.0' 'X-ABI-XINPUT_VERSION>=27.0')
provides=("x11win-input-libinput")
source=("${url}/${_pkgname}/archive/refs/tags/${pkgname}-${pkgver}.tar.gz")
groups=('xlibre-drivers')

build() {
  cd ${_pkgname}-${pkgname}-${pkgver}
  ./autogen.sh
  ./configure --prefix=/usr \
    --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd ${_pkgname}-${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}"/${_pkgname}-${pkgname}-${pkgver}/COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

sha256sums=('f1be5a443af78307af18103a6bb614021fe163380b0eb43dec820a2389fbd6c8')
