# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=freecell-solver
pkgver=6.10.0
pkgrel=1
pkgdesc='A program that automatically solves layouts of Freecell and similar variants of Card Solitaire'
arch=(x86_64)
url='https://fc-solve.shlomifish.org'
license=(MIT)
depends=(bash
         glibc)
makedepends=(cmake
             gperf
             perl-moo
             perl-path-tiny
             perl-template-toolkit
             python-random2
             python-pysol_cards
             rinutils)
optdepends=('python-pysol_cards: for the Python interface'
            'python-random2: for the Python interface'
            'python-six: for the Python interface')
source=(https://fc-solve.shlomifish.org/downloads/fc-solve/$pkgname-$pkgver.tar.xz)
sha256sums=('443ba29de08be751a60faca42a817b65b342cea93188fd7651741b75048379b4')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DFCS_WITH_TEST_SUITE=OFF \
    -DBUILD_STATIC_LIBRARY=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname-$pkgver/COPYING.html "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.html
}
