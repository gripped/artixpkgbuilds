# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Kristian Mosegaard <kristian@mosen.me>
# Contributor: Max Liebkies <mail@maxliebkies.de>
# Contributor: Krzysztof Bogacki <krzysztof.bogacki@leancode.pl>

pkgbase=dotnet-core-10.0
pkgname=(
  dotnet-runtime-10.0
  aspnet-runtime-10.0
  dotnet-sdk-10.0
  dotnet-targeting-pack-10.0
  aspnet-targeting-pack-10.0
  dotnet-source-built-artifacts-10.0
)
pkgver=10.0.10.sdk110
pkgrel=1
arch=(x86_64)
url=https://dotnet.microsoft.com
license=(MIT)
makedepends=(
  bash
  clang20
  cmake
  dotnet-sdk-10.0
  dotnet-source-built-artifacts-10.0
  git
  icu
  krb5
  libgit2
  libunwind
  libxml2
  lldb
  llvm20
  lttng-ust2.12
  nodejs
  openssl
  udev
  zlib
)
optdepends=('bash-completion: Bash completion support')
options=(
  !lto
  staticlibs
)
source=(git+https://github.com/dotnet/dotnet.git#tag=v${pkgver/.*.sdk/.0.})
b2sums=('550f21787221b23756c5958b0b613bcd2a727c5ea9a230174cb6fdedac9c475f43995e5152ec19283f4a212c1deb9ae7a76f0fee2cf8521bc2c015d876d3ea90')

prepare() {
  cd dotnet

  # fix bootstrap
  git remote set-url origin https://github.com/dotnet/dotnet.git

  local _bootstrapver=$(xmllint --xpath "//*[local-name()='PrivateSourceBuiltSdkVersion']/text()" eng/Versions.props)
  local _previousver=$(pacman -Q dotnet-source-built-artifacts | sed -r 's/.*([0-9]+\.[0-9]+)\.[0-9]+\.sdk([0-9]+)-.*/\1.\2/')

  if [[ $_bootstrapver == $_previousver ]]; then
    cp -r /usr/share/dotnet .dotnet
    ln -sf /usr/share/dotnet/source-built-artifacts/Private.SourceBuilt.Artifacts.*.tar.gz prereqs/packages/archive/
  fi
  ./prep-source-build.sh
}

build() {
  export DOTNET_CLI_TELEMETRY_OPTOUT=1
  cd dotnet

  export COMPlus_LTTng=0
  export VERBOSE=1
  export OPENSSL_ENABLE_SHA1_SIGNATURES=1

  export PATH="/usr/lib/llvm20/bin:$PATH"

  # this uses malloc_usable_size, which is incompatible with fortification level 3
  CFLAGS="${CFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"
  CXXFLAGS="${CXXFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"

  CFLAGS=$(echo $CFLAGS  | sed -e 's/-fstack-clash-protection//' )
  CXXFLAGS=$(echo $CXXFLAGS  | sed -e 's/-fstack-clash-protection//' )
  export EXTRA_CFLAGS="$CFLAGS"
  export EXTRA_CXXFLAGS="$CXXFLAGS"
  export EXTRA_LDFLAGS="$LDFLAGS"
  unset CFLAGS
  unset CXXFLAGS
  unset LDFLAGS

  # rtm branding strips out the "prerelease" tag from the version (see https://github.com/dotnet/dotnet/blob/b0f34d51fccc69fd334253924abd8d6853fad7aa/build.sh#L18)
  ./build.sh --clean-while-building --online --source-build
}

package_dotnet-runtime-10.0() {
  pkgdesc='The .NET Core runtime'
  depends=(
    dotnet-host
    glibc
    libgcc
    libstdc++
    icu
    krb5
    libunwind
    zlib
    openssl
  )
  optdepends=('lttng-ust2.12: CoreCLR tracing')
  provides=(dotnet-runtime=${pkgver%.*.sdk*})

  install -dm 755 "${pkgdir}"/usr/share/{dotnet,licenses}
  bsdtar -xf dotnet/artifacts/assets/Release/dotnet-sdk-${pkgver%.*.sdk*}.${pkgver#*sdk}-artix-*.tar.gz -C "${pkgdir}"/usr/share/dotnet/ --no-same-owner shared/Microsoft.NETCore.App
  ln -s dotnet-host "${pkgdir}"/usr/share/licenses/dotnet-runtime-10.0
}

package_aspnet-runtime-10.0() {
  pkgdesc='The ASP.NET Core runtime'
  depends=(dotnet-runtime-10.0)
  provides=(aspnet-runtime=${pkgver%.*.sdk*})

  install -dm 755 "${pkgdir}"/usr/share/{dotnet,licenses}
  bsdtar -xf dotnet/artifacts/assets/Release/dotnet-sdk-${pkgver%.*.sdk*}.${pkgver#*sdk}-artix-*.tar.gz -C "${pkgdir}"/usr/share/dotnet/ --no-same-owner shared/Microsoft.AspNetCore.App
  ln -s dotnet-host "${pkgdir}"/usr/share/licenses/aspnet-runtime-10.0
}

package_dotnet-sdk-10.0() {
  pkgdesc='The .NET Core SDK'
  depends=(
    dotnet-runtime-10.0
    dotnet-targeting-pack-10.0
    glibc
    libgcc
    libstdc++
  )
  optdepends=('aspnet-targeting-pack: Build ASP.NET Core applications')
  provides=(dotnet-sdk=${pkgver%.*.sdk*})

  install -dm 755 "${pkgdir}"/usr/share/{dotnet,licenses}
  bsdtar -xf dotnet/artifacts/assets/Release/dotnet-sdk-${pkgver%.*.sdk*}.${pkgver#*sdk}-artix-*.tar.gz -C "${pkgdir}"/usr/share/dotnet/ --no-same-owner sdk sdk-manifests templates
  install -dm 755 "${pkgdir}"/usr/share/dotnet/metadata/workloads/${pkgver%.*.sdk*}.${pkgver#*.sdk}
  touch "${pkgdir}"/usr/share/dotnet/metadata/workloads/${pkgver%.*.sdk*}.${pkgver#*.sdk}/userlocal
  ln -s dotnet-host "${pkgdir}"/usr/share/licenses/dotnet-sdk-10.0
}

package_dotnet-targeting-pack-10.0() {
  pkgdesc='The .NET Core targeting pack'
  provides=(dotnet-targeting-pack=${pkgver%.*.sdk*})

  install -dm 755 "${pkgdir}"/usr/share/{dotnet,licenses}
  bsdtar -xf dotnet/artifacts/assets/Release/dotnet-sdk-${pkgver%.*.sdk*}.${pkgver#*sdk}-artix-*.tar.gz -C "${pkgdir}"/usr/share/dotnet/ --no-same-owner packs/Microsoft.NETCore.App.{Host.artix-*,Ref}
  ln -s dotnet-host "${pkgdir}"/usr/share/licenses/dotnet-targeting-pack-10.0
}

package_aspnet-targeting-pack-10.0() {
  pkgdesc='The ASP.NET Core targeting pack'
  depends=(dotnet-targeting-pack-10.0)
  provides=(aspnet-targeting-pack=${pkgver%.*.sdk*})

  install -dm 755 "${pkgdir}"/usr/share/{dotnet,licenses}
  bsdtar -xf dotnet/artifacts/assets/Release/dotnet-sdk-${pkgver%.*.sdk*}.${pkgver#*sdk}-artix-*.tar.gz -C "${pkgdir}"/usr/share/dotnet/ --no-same-owner packs/Microsoft.AspNetCore.App.Ref
  ln -s dotnet-host "${pkgdir}"/usr/share/licenses/aspnet-targeting-pack-10.0
}

package_dotnet-source-built-artifacts-10.0() {
  pkgdesc='Internal package for building the .NET Core SDK'
  provides=(dotnet-source-built-artifacts=${pkgver%.*.sdk*})

  install -Dm 644 dotnet/artifacts/assets/Release/Private.SourceBuilt.Artifacts.*.tar.gz -t "${pkgdir}"/usr/share/dotnet/source-built-artifacts/
}
