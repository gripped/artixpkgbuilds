# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Filipe Laíns (FFY00) <lains@archlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Robin Candau <antiz@archlinux.org>
# Contributor: Frederik “Freso” S. Olesen <freso.dk@gmail.com>

pkgname=lutris
pkgver=0.5.19
pkgrel=8
pkgdesc='Open Gaming Platform'
arch=(any)
url=https://lutris.net
license=(GPL-3.0-only)
depends=(
  cabextract
  curl
  gdk-pixbuf2
  glib2
  gnome-desktop
  gobject-introspection-runtime
  gtk3
  hicolor-icon-theme
  mesa-utils
  p7zip
  pango
  psmisc
  python
  python-cairo
  python-certifi
  python-dbus
  python-distro
  python-evdev
  python-gobject
  python-lxml
  python-moddb
  python-pillow
  python-requests
  python-urllib3
  python-yaml
  unzip
  webkit2gtk-4.1
  xdg-desktop-portal-impl
  xorg-xrandr
)
makedepends=(
  git
  meson
  ninja
  protobuf
  python-poetry-core
)
checkdepends=(
  python-nose2
  xorg-server-xvfb
  xterm
  fluidsynth
  vulkan-tools
  pciutils
  procps-ng
  wine
)
optdepends=(
  'fluidsynth: For games that have MIDI music'
  'gamemode: Allows games to request a temporary set of optimisations'
  'gvfs: GVFS backend'
  'innoextract: Extract Inno Setup installers'
  'libayatana-appindicator: tray icon support'
  'lib32-gamemode: Allows games to request a temporary set of optimisations'
  'lib32-gnutls: Required to login to some game platforms (e.g. Ubisoft Connect, EA Origin and GOG)'
  'lib32-mesa-libgl: OpenGL support'
  'lib32-vkd3d: DirectX 12 support'
  'lib32-vulkan-icd-loader: Vulkan support'
  'python-pefile: Display icons of Microsoft executable files'
  'python-protobuf: Battle.net support'
  'vkd3d: DirectX 12 support'
  'vulkan-icd-loader: Vulkan support'
  'vulkan-tools: Vulkan support'
  'wine: Windows support'
  'winetricks: Use system winetricks'
  'xorg-xgamma: Restore gamma on game exit'
  'umu-launcher: For running games through Proton'
)
source=("git+https://github.com/lutris/lutris.git#tag=v${pkgver}"
        "git+https://github.com/bartok765/galaxy_blizzard_plugin.git")
sha256sums=('e0349f6c127e9da4a935d84fa73370c353cd08e513160e28b1da13db94cc9d8c'
            'SKIP')

prepare() {
  # Regenerate protos to fix BattleNet plugin
  # See https://gitlab.archlinux.org/archlinux/packaging/packages/lutris/-/issues/4
  # And https://github.com/lutris/lutris/issues/5659
  protoc --proto_path=galaxy_blizzard_plugin/src --python_out=. product_db.proto
  cp -vf product_db_pb2.py lutris/lutris/util/battlenet/product_db_pb2.py
}

build() {
  artix-meson lutris build
  ninja -C build
}

check() {
  cd lutris
  xvfb-run nose2
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
