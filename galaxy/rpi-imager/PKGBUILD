# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Christian Heusel <gromit@archlinux.org>
# Contributor: Iyán Méndez Veiga <me (at) iyanmv (dot) com>
# Contributor: Dmytro Aleksandrov <alkersan@gmail.com>

pkgname=rpi-imager
pkgver=2.0.9
pkgrel=1
pkgdesc="Raspberry Pi Imaging Utility"
depends=(
    'curl'
    'glibc'
    'gnutls'
    'hicolor-icon-theme'
    'libarchive'
    'libgcc'
    'libstdc++'
    'liburing'
    'libusb'
    'polkit'
    'qt6-base'
    'qt6-declarative'
    'xz'
    'zstd'
)
optdepends=(
    'dosfstools: SD card bootloader support'
)
makedepends=(
    'git'
    'cmake'
    'qt6-tools'
    'qt6-svg'
)
arch=('x86_64')
url="https://github.com/raspberrypi/rpi-imager"
license=("Apache-2.0")

source=("git+https://github.com/raspberrypi/rpi-imager.git#tag=v${pkgver}"
        "remove-vendoring.patch")

b2sums=('2951f48bbfdb32540434de4326ef64deb7762c119cfaa673dbc3a2d59f275edfdda136b2cc9e43f2bd5c734a6c752f3087318165d9f5d7c9e457df077bc62748'
        'c1d0b984ea68dc2f809c11989d159562773b336e6b255f3298a5ce22d85aafc4ea0a65e7de71f1dafcd94b016b1e682b9ba5e48678e39a7a6eda158dc9622807')

prepare() {
    cd "${pkgname}" 
    # https://github.com/raspberrypi/rpi-imager/issues/924
    patch -Np1 -i "$srcdir/remove-vendoring.patch"
}

build() {
    local cmake_options=(
        -B build
        -S "${pkgname}/src"
        -Wno-dev
        -D CMAKE_BUILD_TYPE=None
        -D CMAKE_INSTALL_PREFIX=/usr
        -D ENABLE_CHECK_VERSION=OFF
    )
    cmake "${cmake_options[@]}"
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
    install -Dm644 ${srcdir}/${pkgname}/doc/man/rpi-imager.1 "${pkgdir}/usr/share/man/man1/rpi-imager.1"
    install -Dm644 ${srcdir}/${pkgname}/debian/changelog "${pkgdir}/usr/share/doc/rpi-imager/changelog"
    install -Dm644 ${srcdir}/${pkgname}/debian/com.raspberrypi.rpi-imager.policy "${pkgdir}/usr/share/polkit-1/actions/com.raspberrypi.rpi-imager.policy"
    install -Dm644 ${srcdir}/${pkgname}/license.txt "$pkgdir/usr/share/licenses/$pkgname/license.txt"
}
