# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Christian Heusel <gromit@archlinux.org>
# Contributor: Iyán Méndez Veiga <me (at) iyanmv (dot) com>
# Contributor: Dmytro Aleksandrov <alkersan@gmail.com>

pkgname=rpi-imager
pkgver=2.0.5
pkgrel=1
pkgdesc="Raspberry Pi Imaging Utility"
depends=(
    'curl'
    'gcc-libs'
    'glibc'
    'gnutls'
    'hicolor-icon-theme'
    'libarchive'
    'polkit'
    'qt6-base'
    'qt6-declarative'
    'xz'
    'zstd'
)
optdepends=(
    'dosfstools: SD card bootloader support'
    'udisks2: Needed if you want to be able to run rpi-imager as a regular user'
)
makedepends=(
    'git'
    'cmake'
    'qt6-tools'
    'qt6-svg'
)
arch=('x86_64')
url="https://github.com/raspberrypi/rpi-imager"
license=("Apache-2.0")

source=("git+https://github.com/raspberrypi/rpi-imager.git#tag=v${pkgver}"
        "remove-vendoring.patch")

b2sums=('9e199b803445cde35d99b94de47f6c495853359bed530e03a8eadc08ee1885312680fb8754ab3d8a779caa6fcc19d5297015b50d6a6311fad8b1bdb7ccc752f0'
        '0e1f638695ffe702efd7a8a66bc822787119af98eee6fc21dc6f134765de89d12f661b34a444e16b466b7397fb9098cee968520fd4d9e95c86e18f5708c496b1')

prepare() {
    # https://github.com/raspberrypi/rpi-imager/issues/924
    patch -Np1 -d "${pkgname}" -i "$srcdir/remove-vendoring.patch"
}

build() {
    cmake -B build -S "${pkgname}/src" \
        -DCMAKE_BUILD_TYPE='None' \
        -DCMAKE_INSTALL_PREFIX='/usr' \
        -DENABLE_CHECK_VERSION=OFF
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
    install -Dm644 ${srcdir}/${pkgname}/doc/man/rpi-imager.1 "${pkgdir}/usr/share/man/man1/rpi-imager.1"
    install -Dm644 ${srcdir}/${pkgname}/debian/changelog "${pkgdir}/usr/share/doc/rpi-imager/changelog"
    install -Dm644 ${srcdir}/${pkgname}/debian/com.raspberrypi.rpi-imager.policy "${pkgdir}/usr/share/polkit-1/actions/com.raspberrypi.rpi-imager.policy"
    install -Dm644 ${srcdir}/${pkgname}/license.txt "$pkgdir/usr/share/licenses/$pkgname/license.txt"
}
