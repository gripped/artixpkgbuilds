# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-play
pkgver=20260622.165905.g1e4f393f3c52
pkgrel=1
pkgdesc='Sony PlayStation 2 core'
arch=(x86_64)
url=https://github.com/jpd002/Play-
license=(MIT)
groups=(libretro)
depends=(
  bzip2
  glew
  glibc
  glu
  icu
  libgl
  libretro-core-info
  zlib
)
makedepends=(
  cmake
  git
  libglvnd
)
source=(
  libretro-play::git+https://github.com/jpd002/Play-.git#commit=${pkgver##*.g}
  play-CodeGen::git+https://github.com/jpd002/Play--CodeGen.git
  play-Dependencies::git+https://github.com/jpd002/Play-Dependencies.git
  git+https://github.com/gulrak/filesystem.git
  play-Framework::git+https://github.com/jpd002/Play--Framework.git
  play-libchdr::git+https://github.com/jpd002/libchdr.git
  play-Nuanceur::git+https://github.com/jpd002/Nuanceur.git
  git+https://github.com/SDWebImage/SDWebImage.git
  git+https://github.com/Cyan4973/xxHash.git
  git+https://github.com/facebook/zstd.git
)
b2sums=('2da1c813496dd2ee338598c0bd4f6579fbc2966955dce508031469b8c5984cbc81a702a41fc68d5f2402a46fb59c277b34e81ae5c96aec720176e759078dbbce'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP')

prepare() {
  cd libretro-play
  for submodule in deps/{CodeGen,Dependencies,Framework,Nuanceur,libchdr}; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../play-${submodule#*/}
  done
  git -c protocol.file.allow=always submodule update
  cd deps/Dependencies
  git submodule init SDWebImage
  git config submodule.SDWebImage.url ../../../SDWebImage
  git submodule init ghc_filesystem
  git config submodule.ghc_filesystem.url ../../../filesystem
  git submodule init xxHash
  git config submodule.xxHash.url ../../../xxHash
  git submodule init zstd
  git config submodule.zstd.url ../../../zstd
  git -c protocol.file.allow=always submodule update
}

build() {
  cmake -S libretro-play -B build \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
    -DBUILD_LIBRETRO_CORE=ON \
    -DBUILD_PLAY=OFF \
    -DBUILD_TESTS=OFF \
    -DENABLE_AMAZON_S3=OFF \
    -Wno-dev
  make -C build
}

package() {
  install -Dm 644 build/Source/ui_libretro/play_libretro.so -t "${pkgdir}"/usr/lib/libretro/
  install -Dm 644 libretro-play/License.txt -t "${pkgdir}"/usr/share/licenses/libretro-play/
}

# vim: ts=2 sw=2 et:
