# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-play
pkgver=8459
pkgrel=1
pkgdesc='Sony PlayStation 2 core'
arch=(x86_64)
url=https://github.com/jpd002/Play-
license=(MIT)
groups=(libretro)
depends=(
  bzip2
  glew
  glibc
  glu
  icu
  libgl
  libretro-core-info
  zlib
)
makedepends=(
  cmake
  git
  libglvnd
)
_commit=b15a49e31172f05dfdb1b1a15ea71e8a870c27ae
source=(
  libretro-play::git+https://github.com/jpd002/Play-.git#commit=${_commit}
  play-CodeGen::git+https://github.com/jpd002/Play--CodeGen.git
  play-Dependencies::git+https://github.com/jpd002/Play-Dependencies.git
  git+https://github.com/gulrak/filesystem.git
  play-Framework::git+https://github.com/jpd002/Play--Framework.git
  play-libchdr::git+https://github.com/jpd002/libchdr.git
  play-Nuanceur::git+https://github.com/jpd002/Nuanceur.git
  git+https://github.com/SDWebImage/SDWebImage.git
  git+https://github.com/Cyan4973/xxHash.git
  git+https://github.com/facebook/zstd.git
)
b2sums=('6209f9af830fa64f67644afb5bdd97cdd14674348de600773a492f48b02c19a599412d8f6a1f2c1b3dff51404a2878965a9d846ac36ff2289771b53f56b11bba'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP')

pkgver() {
  cd libretro-play
  git rev-list --count HEAD
}

prepare() {
  cd libretro-play
  git config --global protocol.file.allow always
  for submodule in deps/{CodeGen,Dependencies,Framework,Nuanceur,libchdr}; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../play-${submodule#*/}
  done
  git submodule update
  cd deps/Dependencies
  git submodule init SDWebImage
  git config submodule.SDWebImage.url ../../../SDWebImage
  git submodule init ghc_filesystem
  git config submodule.ghc_filesystem.url ../../../filesystem
  git submodule init xxHash
  git config submodule.xxHash.url ../../../xxHash
  git submodule init zstd
  git config submodule.zstd.url ../../../zstd
  git submodule update
}

build() {
  cmake -S libretro-play -B build \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_LIBRETRO_CORE=ON \
    -DBUILD_PLAY=OFF \
    -DBUILD_TESTS=OFF \
    -DENABLE_AMAZON_S3=OFF \
    -Wno-dev
  make -C build
}

package() {
  install -Dm 644 build/Source/ui_libretro/play_libretro.so -t "${pkgdir}"/usr/lib/libretro/
  install -Dm 644 libretro-play/License.txt -t "${pkgdir}"/usr/share/licenses/libretro-play/
}

# vim: ts=2 sw=2 et:
