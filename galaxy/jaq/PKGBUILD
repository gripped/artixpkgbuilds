# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Caleb Maclennan <caleb@alerque.com>
# Contributor: Sanpi <sanpi+aur@homecomputing.fr>

pkgname=jaq
pkgver=3.1.1
pkgrel=1
pkgdesc='A jq clone focussed on correctness, speed, and simplicity'
url="https://github.com/01mf02/$pkgname"
arch=(x86_64)
license=(MIT)
depends=(glibc # libc.so libm.so
         libgcc libgcc_s.so)
makedepends=(cargo
             jotdown)
_archive="$pkgname-$pkgver"
source=("$url/archive/refs/tags/v$pkgver/$_archive.tar.gz")
sha256sums=('9b8587436be48b5791c8276573321a3d4f404e0dc77ea6503d05725a55edd266')

_srcenv() {
	cd "$_archive"
	export CARGO_HOME="$srcdir"
	export CARGO_PROFILE_RELEASE_DEBUG=2
	export CARGO_PROFILE_RELEASE_STRIP=false
	export CARGO_PROFILE_RELEASE_LTO=true
	export CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1
	export CARGO_PROFILE_RELEASE_OPT_LEVEL=3
	CFLAGS+=' -ffat-lto-objects'
}

prepare() {
	_srcenv
	cargo fetch --locked --target host-tuple
}

build() {
	_srcenv
	cargo build --frozen --release
	make -C docs "$pkgname.1"
}

check() {
	_srcenv
	cargo test --frozen
}

package() {
	cd "$_archive"
	install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE-MIT
	install -Dm0644 -t "$pkgdir/usr/share/man/man1/" "docs/$pkgname.1"
}
