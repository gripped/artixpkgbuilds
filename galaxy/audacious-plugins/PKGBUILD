# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Evangelos Foutras <foutrelis@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=audacious-plugins
pkgver=4.6.1
pkgrel=4
pkgdesc='Plugins for Audacious'
arch=(x86_64)
url='https://audacious-media-player.org/'
license=(BSD-2-Clause)
depends=(
  alsa-lib
  cairo
  curl
  faad2
  ffmpeg
  flac
  fluidsynth
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  jack
  json-glib
  lame
  libbs2b
  libcddb
  libcdio
  libcdio-paranoia
  libcue
  libgcc
  libglvnd
  libmms
  libmodplug
  libnotify
  libogg
  libopenmpt
  libpipewire
  libpulse
  libsamplerate
  libsidplayfp
  libsndfile
  libsoxr
  libstdc++
  libvorbis
  libx11
  libxcomposite
  libxml2
  libxrender
  lirc
  mpg123
  neon
  opusfile
  pango
  qt6-base
  sdl3
  sndio
  wavpack
  zlib
)
makedepends=(
  "audacious>=$pkgver"
  git
  glib2-devel
  meson
)
source=(
  "git+https://github.com/audacious-media-player/audacious-plugins.git#tag=$pkgname-$pkgver"
  sidplay-rom-paths.patch
)
b2sums=(
  41574a2bc9b954c1cbb27f7a071319cc8390c846895702e23a8faf744a32c1debd2af5ceb4c6121c9384debb32a5dde418a4f5e0bb2550e9b5f561d803619cd8
  1b8fa3ecc1954de27ecd41fa364d9c59f07810dd6265e9b2865b267792040082852955b6819fb627c4f9a5146f13051685dfd84b149bdee6699a304a807e4197
)

prepare() {
  cd $pkgname

  # Set paths for ROM files from vice for sidplay
  patch -Np1 -i ../sidplay-rom-paths.patch
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" $pkgname/COPYING
}
