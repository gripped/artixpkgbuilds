# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=audacious-plugins
pkgver=4.4.1
pkgrel=1
pkgdesc="Plugins for Audacious"
arch=('x86_64')
url="https://audacious-media-player.org/"
license=('BSD' 'GPL')
depends=('alsa-lib' 'curl' 'faad2' 'ffmpeg' 'flac' 'fluidsynth'
         'jack' 'lame' 'libcdio-paranoia' 'libcue' 'libmms' 'libmodplug'
         'libmtp' 'libpulse' 'libnotify' 'libsamplerate' 'libsidplayfp'
         'libvorbis' 'lirc' 'mpg123' 'neon' 'wavpack' 'libbs2b' 'libopenmpt'
         'libcddb' 'opusfile' 'libpipewire')
makedepends=("audacious>=$pkgver" 'meson' 'glib2-devel')
source=(https://distfiles.audacious-media-player.org/$pkgname-$pkgver.tar.bz2
        sidplay-rom-paths.patch)
sha256sums=('484ed416b1cf1569ce2cc54208e674b9c516118485b94ce577d7bc5426d05976'
            'c32cd36f75dd18db082f9b9447f1c0982279703b9d648f5695295ff25c9b678d')

prepare() {
  cd $pkgname-$pkgver

  # set paths for ROM files from vice for sidplay
  patch -Np1 -i ../sidplay-rom-paths.patch
}

build() {
  artix-meson $pkgname-$pkgver build \
    -Dgtk=false
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dm644 $pkgname-$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
