# Maintainer: arc-d3v <arc-d3v@artixlinux.org>
# Contributor: dec05eba <dec05eba@protonmail.com>

pkgname=gpu-screen-recorder
pkgver=5.13.4
pkgrel=1
pkgdesc='A shadowplay-like screen recorder for Linux. The fastest screen recorder for Linux'
arch=('x86_64')
url="https://git.dec05eba.com/gpu-screen-recorder"
license=('GPL-3.0-only')
makedepends=(
  meson
  vulkan-headers
  git
)
depends=(
  bash
  glibc
  libstdc++
  libgcc
  ffmpeg
  libglvnd
  libxcomposite
  libxrandr
  libxfixes
  libxdamage
  libx11
  libpulse
  libva
  libdrm
  libcap
  wayland
  libpipewire
  dbus
  libjpeg-turbo
)
optdepends=(
  'nvidia-utils: Required to record your screen on NVIDIA'
  'libxnvctrl: Required when using the -oc option to overclock the NVIDIA GPU to workaround NVIDIA p2 state bug'
  'mesa: Required to record your screen on AMD/Intel'
  'libva-mesa-driver: Required to record your screen on AMD'
  'libva-intel-driver: Required to record your screen on Intel G45 and HD Graphics family'
  'intel-media-driver: Required to record your screen on Intel Broadwell or later iGPUs or Intel Arc'
  'linux-firmware-intel: Required to record your screen on Intel Skylake or later iGPUs'
)
source=(git+https://repo.dec05eba.com/gpu-screen-recorder#tag=$pkgver)
sha512sums=('066ba993eab69a513f641aa2ad820d8813f1a367497a6d8482a907703d83c01c3a0c18c3f10b18a17482202c97c484b407d19b08994802e58a27164051ee4442')

build() {
  cd "$srcdir"/$pkgname
  arch-meson build -Dsystemd=true --buildtype=release -Dstrip=true
  meson compile -C build
}

package() {
  cd "$srcdir"/$pkgname
  meson install -C build --destdir "$pkgdir"
  setcap CAP_SYS_ADMIN=+ep "$pkgdir"/usr/bin/gsr-kms-server
}
