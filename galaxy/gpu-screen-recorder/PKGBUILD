# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: dec05eba <dec05eba@protonmail.com>

pkgname=gpu-screen-recorder
pkgver=6.1.2
pkgrel=1
pkgdesc='A shadowplay-like screen recorder for Linux. The fastest screen recorder for Linux'
arch=('x86_64')
url="https://git.dec05eba.com/gpu-screen-recorder"
license=('GPL-3.0-only')
install=gpu-screen-recorder.install
makedepends=(
  meson
  vulkan-headers
  git
)
depends=(
  bash
  glibc
  libstdc++
  libgcc
  ffmpeg
  libglvnd
  libxcomposite
  libxrandr
  libxfixes
  libxdamage
  libx11
  libpulse
  libva
  libdrm
  libcap
  wayland
  libpipewire
  dbus
  libjpeg-turbo
  ffmpeg
)
optdepends=(
    'nvidia-utils: Required to record your screen on NVIDIA'
    'libxnvctrl: Required when using the -oc option to overclock the NVIDIA GPU to workaround NVIDIA p2 state bug'
    'mesa: Required to record your screen on AMD/Intel'
    'libva-mesa-driver: Required to record your screen on AMD'
    'libva-intel-driver: Required to record your screen on Intel G45 and HD Graphics family'
    'intel-media-driver: Required to record your screen on Intel Broadwell or later iGPUs or Intel Arc'
    'linux-firmware-intel: Required to record your screen on Intel Skylake or later iGPUs'
)
source=(git+https://repo.dec05eba.com/gpu-screen-recorder#tag=$pkgver)
sha512sums=('7626639ba5cb0533d9b3639c38fb938c70a3a42654d0aac422ba110b4aa9b1bec82f689cb8dc559bf409843e263a58506acc900f75aae3e12d64d45062f67aa9')

build() {
  cd "$srcdir"/$pkgname
  artix-meson build -Dsystemd=false -Dffmpeg_static=false --buildtype=release -Dstrip=true
  meson compile -C build
}

package() {
  cd "$srcdir"/$pkgname
  meson install -C build --destdir "$pkgdir"
}
