# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgname=vhba-module
pkgver=20260313
pkgrel=28
pkgdesc="Virtual SCSI adapter - Linux modules"
url="https://cdemu.sourceforge.io/"
arch=(x86_64)
license=(GPL-2.0-or-later)
makedepends=(linux-headers "$pkgname-dkms=$pkgver")
provides=(VHBA-MODULE)
options=(!debug !strip)

build() {
  _kernver=$(</usr/src/linux/version)
  fakeroot dkms build --dkmstree "$srcdir" -m $pkgname/$pkgver -k $_kernver
}

package() {
  depends=(linux)

  _kernver="$(</usr/src/linux/version)"
  _extramodules="/usr/lib/modules/$_kernver/extramodules"
  install -Dm644 -t "$pkgdir$_extramodules" \
    $pkgname/$pkgver/$_kernver/$CARCH/module/*.ko

  # compress kernel modules
  find "$pkgdir" -name '*.ko' -exec zstd --rm -19 {} +
}

# vim:set sw=2 sts=-1 et:
