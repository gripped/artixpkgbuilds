# Maintainer: artist for Artix Linux

pkgname=xlibre-input-vmmouse
_pkgname=xf86-input-vmmouse
pkgver=13.2.0.4
pkgrel=1
pkgdesc="XLibre fork of X.Org VMWare Mouse input driver"
arch=(x86_64)
license=('custom')
url="https://github.com/X11Libre"
depends=('libelogind')
makedepends=('xlibre-xserver-devel' 'X-ABI-XINPUT_VERSION=26.0' 'xorgproto')
conflicts=("xlibre-${_pkgname}" 'xorg-server<21.1.1' 'X-ABI-XINPUT_VERSION<26' 'X-ABI-XINPUT_VERSION>=27')
provides=("xlibre-${_pkgname}" "${_pkgname}")
replaces=("xlibre-${_pkgname}")
source=("${url}/${_pkgname}/archive/refs/tags/xlibre-${_pkgname}-${pkgver}.tar.gz")
groups=('xlibre-drivers')

build() {
  cd ${_pkgname}-xlibre-${_pkgname}-${pkgver}
  ./autogen.sh
  ./configure --prefix=/usr \
    --with-udev-rules-dir=/usr/lib/udev/rules.d
  make
}

package() {
  cd ${_pkgname}-xlibre-${_pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}"/${_pkgname}-xlibre-${_pkgname}-${pkgver}/COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  rm -rfv "${pkgdir}"/usr/{lib,share}/hal
}

sha256sums=('4166bd8225590c93c26f9be90c4cf0fa924a28e1898e1435d4fc3e4395151327')
