# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=anubis
pkgver=1.17.1
pkgrel=3
pkgdesc="Reverse proxy to protect against scraper bots"
url="https://anubis.techaro.lol/"
arch=(x86_64)
license=(MIT)
depends=(glibc)
makedepends=(
  brotli
  esbuild
  git
  go
  gzip
  npm
  zstd
)
backup=(etc/anubis/default.env)
options=(
  # Reproducibility fix
  # https://github.com/golang/go/pull/53528
  !lto
)
source=(
  "git+https://github.com/TecharoHQ/anubis?signed#tag=v${pkgver/[a-z]/-&}"
  0001-fix-web-Avoid-timestamping-main.mjs.gz.patch
)
b2sums=('723989641b4ab37588e052c6147ce16836211a8a1a170eb89f36504c63bac2ea061ab275f83c75aeef4b1e0949bb5ee35160c7a46f85b0fc957791bbc9f7514f'
        '00f0e216eb328d9305932a5a96c1542861a59b48fd95e02d1b0f7568f8c5afbdf9f6c44c89c60a83986db47028f00474fccccf2dc366b4c5f5e941804ee89103')
validpgpkeys=(
  833F64161167B501058C394756375DA2DF02ABFF # Techaro Packages Signatures <gpg+packages@techaro.lol>
)

prepare() {
  cd anubis

  # Reproducibility fix
  git apply -3 ../0001-fix-web-Avoid-timestamping-main.mjs.gz.patch

  make deps
}

build() {
  cd anubis

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  make assets
  make prebaked-build
}

package() {
  cd anubis

  install -D var/anubis -t "$pkgdir/usr/bin"

  install -Dm644 run/default.env -t "$pkgdir/etc/anubis"

  install -Dm644 README.md data/botPolicies.json -t "$pkgdir/usr/share/doc/$pkgname"
  cp -a docs/docs "$pkgdir/usr/share/doc/$pkgname/docs"
  find "$pkgdir/usr/share/doc" -name _category_.json -delete

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 sts=-1 et:
