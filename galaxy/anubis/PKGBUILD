# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=anubis
pkgver=1.24.0
pkgrel=1
pkgdesc="Reverse proxy to protect against scraper bots"
url="https://anubis.techaro.lol/"
arch=(x86_64)
license=(MIT)
depends=(glibc)
makedepends=(
  brotli
  esbuild
  git
  go
  gzip
  npm
  zstd
)
backup=(etc/anubis/default.env)
options=(
  # Reproducibility fix
  # https://github.com/golang/go/pull/53528
  !lto
)
source=(
  "git+https://github.com/TecharoHQ/anubis?signed#tag=v${pkgver/[a-z]/-&}"
)
b2sums=('57031fd4a5cb39f5e8e5863436b457a57f1c0cbd3d15fb7c04cd62864ee2688cbd50d7f970f04e565fef3fa1e5b57703f276e89e02bfd4cd8b112c0fa4c9a19a')
validpgpkeys=(
  833F64161167B501058C394756375DA2DF02ABFF # Techaro Packages Signatures <gpg+packages@techaro.lol>
)

prepare() {
  cd anubis

  make deps
}

build() {
  cd anubis

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  make assets
  make prebaked-build
}

package() {
  cd anubis

  install -D var/anubis -t "$pkgdir/usr/bin"
  install -D var/robots2policy "$pkgdir/usr/bin/anubis-robots2policy"

  install -Dm644 run/default.env -t "$pkgdir/etc/anubis"

  local docdir="$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 README.md -t "$docdir"
  cp -a docs/docs data -t "$docdir"
  find "$docdir" \( -name _category_.json -o -name '*.go' \) -delete

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 sts=-1 et:
