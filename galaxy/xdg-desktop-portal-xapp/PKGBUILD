# Maintainer:
# Contributor: Mark Wagie

pkgname=xdg-desktop-portal-xapp
pkgver=1.1.3
pkgrel=1
pkgdesc='A backend implementation for xdg-desktop-portal using GTK and various pieces of Cinnamon/MATE/Xfce4 infrastructure'
arch=(x86_64)
url='https://github.com/linuxmint/xdg-desktop-portal-xapp'
license=(LGPL-2.1)
depends=(xdg-desktop-portal xdg-desktop-portal-gtk xapp xapp-symbolic-icons)
makedepends=(git meson glib2-devel)
provides=(xdg-desktop-portal-impl)
source=(git+https://github.com/linuxmint/xdg-desktop-portal-xapp#tag=$pkgver
        xapp-portals.conf)
sha256sums=('98b66c57abbc315823b2f07ed95cc247b7e126a1b9dcda4354f9cf0bc83cbb6d'
            '86f7bad3112e2bccfe07eb9345e95ed4716c126de5e9397966c6de30ae056c11')

build() {
  artix-meson -D systemduserunitdir=/usr/lib/systemd $pkgname build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
  # fix detection with x-d-p 1.18
  install -Dm644 ${srcdir}/xapp-portals.conf  ${pkgdir}/usr/share/xdg-desktop-portal/xapp-portals.conf
}
