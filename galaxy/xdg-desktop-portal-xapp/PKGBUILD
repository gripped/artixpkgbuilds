# Maintainer:
# Contributor: Mark Wagie

pkgname=xdg-desktop-portal-xapp
pkgver=1.1.0
pkgrel=2
pkgdesc='A backend implementation for xdg-desktop-portal using GTK and various pieces of Cinnamon/MATE/Xfce4 infrastructure'
arch=(x86_64)
url='https://github.com/linuxmint/xdg-desktop-portal-xapp'
license=(LGPL-2.1)
depends=(xdg-desktop-portal xdg-desktop-portal-gtk xapp)
makedepends=(meson glib2-devel)
provides=(xdg-desktop-portal-impl)
source=(https://github.com/linuxmint/xdg-desktop-portal-xapp/archive/$pkgver/$pkgname-$pkgver.tar.gz
        xapp-portals.conf)
sha256sums=('2d0746ad1021d8848fb60f1444d09c81c5a4da90df334b44418d6533e6811c13'
            '86f7bad3112e2bccfe07eb9345e95ed4716c126de5e9397966c6de30ae056c11')

build() {
  artix-meson -D systemduserunitdir=/usr/lib/systemd $pkgname-$pkgver build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
  # fix detection with x-d-p 1.18
  install -Dm644 ${srcdir}/xapp-portals.conf  ${pkgdir}/usr/share/xdg-desktop-portal/xapp-portals.conf
}
