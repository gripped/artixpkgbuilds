# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Maintainer: Noctalia Team <team@noctalia.dev>

_pkgname=noctalia
pkgname=noctalia
pkgver=5.2.1
pkgrel=1
pkgdesc='A sleek, customizable desktop shell crafted for Wayland'
arch=('x86_64' 'aarch64')
url='https://github.com/noctalia-dev/noctalia'
license=('MIT')
depends=(
  'bash'
  'cairo'
  'curl'
  'fontconfig'
  'freetype2'
  'git'
  'glib2'
  'glibc'
  'harfbuzz'
  'hicolor-icon-theme'
  'jemalloc'
  'libgcc'
  'libglvnd'
  'libical'
  'libjxl'
  'libpipewire'
  'libqalculate'
  'librsvg'
  'libsecret'
  'libsndfile'
  'libsodium'
  'libstdc++'
  'libwebp'
  'libwireplumber'
  'libxkbcommon'
  'libxml2'
  'md4c'
  'pam'
  'polkit'
  'pango'
  'sdbus-cpp'
  'tomlplusplus'
  'wayland'
)
optdepends=(
  'ddcutil: monitor brightness control'
  'power-profiles-daemon: power profile management'
  'upower: battery and power device integration'
)

makedepends=(
  'meson'
  'ninja'
  'nlohmann-json'
  'pkgconf'
  'stb'
  'wayland-protocols'
)
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver//_/-}.tar.gz")
sha256sums=('5418f6b759de96e56a4bbca809c5a4c60a8a9594bdf67ecb8c5a341f97c221d1')

build() {
  CXXFLAGS+=" -Wno-unused-result"
  artix-meson "${_pkgname}-${pkgver//_/-}" build-release \
    -Db_ndebug=true \
    -Dtests=disabled
  meson compile -C build-release
}

package() {
  meson install -C build-release --destdir "${pkgdir}"

  install -Dm644 "${_pkgname}-${pkgver//_/-}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "${_pkgname}-${pkgver//_/-}/README.md" "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
