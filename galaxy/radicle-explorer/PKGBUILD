# Maintainer: Ivan Shapovalov <intelfx@intelfx.name>

pkgbase=radicle-explorer
pkgname=(radicle-{explorer,httpd})
pkgver=0.28.0
pkgrel=1
pkgdesc="open source, peer-to-peer code collaboration stack built on Git"
arch=('x86_64')
license=('GPL-3.0-only' 'Apache-2.0 OR MIT')
_node="seed.radicle.xyz"
_rid="rad:z4V1sjrXqjvFdnCUbxPFqd5p4DtH5"
url="https://app.radicle.xyz/nodes/$_node/$_rid"
makedepends=(
	'git'
	'cargo'
	'asciidoctor'
	'nodejs'
	'npm'
	'libgit2'
)
_tag="releases/$pkgver"
source=(
	"radicle-explorer::git+https://$_node/${_rid#rad:}.git#tag=$_tag"
	"radicle-explorer.config.json"
	"radicle-explorer.nginx.conf"
)
b2sums=('1fb8fa2ef52265829c9ffcbaec8bd042e8fe349a333fda02d5e1513fbf849e2a04e0a6a2797a3674e3ffada1a004a810d1f2062d28e3a3f3bd31baf7ad843f2a'
        'd29bf8a4344d407cdc19cce3d6d8ef2f28e97454c07978301ef1009a995ba8f352ad706b7230f33d290d7b055d8a8c80c80164625463adc4e0b1191b1c4573f2'
        '5735a8bae977e1fde93a294de1a7f738542f8c4d12d8efeed940c0a8f79f05a59c70101cf9faaa7207f034915e2ac189b0e4af7f0285610dbd9ecc0305d2601c')

prepare() {
	cd radicle-explorer
	npm ci --ignore-scripts

	cd crates/radicle-httpd
  export CARGO_TARGET_DIR="$PWD/target"
	cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
	cd radicle-explorer
	export VITE_RUNTIME_CONFIG=true
	npm run \
		build

	# _Disable_ cross-toolchain LTO because we are using different toolchains
	# for C/C++ and Rust code (i.e., LLVM LTO is incompatible with GCC LTO).
	# In this project, C/C++ code is linked into Rust code. Therefore, apply
	# a workaround to force generation of normal object code on C side:
	CFLAGS+=" -ffat-lto-objects"
	CXXFLAGS+=" -ffat-lto-objects"

	export LIBGIT2_NO_VENDOR=1
	export RADICLE_VERSION="$pkgver"

	cd crates/radicle-httpd
  export CARGO_TARGET_DIR="$PWD/target"
	cargo build \
		--frozen \
		--release \
		--bins \
		# EOL

	mkdir -p target/release/man
	for _man in *.adoc; do
		asciidoctor --doctype manpage --backend manpage --destination-dir target/release/man "$_man"
	done

	# XXX: tests rebuild and overwrite some of the binaries
	cp -a target/release -T target/dist
}

check() {
	cd radicle-explorer/crates/radicle-httpd
  export CARGO_TARGET_DIR="$PWD/target"
	(
	# Ideally, we'd use `env -i`, but `cargo test` forces a recompilation
	# if build flags don't match (+ we want to test what we ship anyway).
	# As a stop-gap, unset variables that are known to break tests
	# (and might have been set in makepkg.conf).
	unset "${!GIT_@}"
	cargo test \
		--frozen \
		# EOL
	)
}

package_radicle-explorer() {
	pkgdesc+=" - explorer (frontend)"
	license=('GPL-3.0-only')
	depends=()
	optdepends=(
		'radicle-httpd: local backend for radicle-explorer'
	)

	cd radicle-explorer

	install -dm755 \
		"$pkgdir/usr/share/radicle-explorer"
	cp -dR --preserve=timestamps \
		build \
		-T "$pkgdir/usr/share/radicle-explorer"
	install -Dm644 \
		config/default.json \
		-T "$pkgdir/usr/share/radicle-explorer/config.json.example"
	# TODO: install into /etc?
	install -Dm644 \
		"$srcdir/radicle-explorer.config.json" \
		-T "$pkgdir/usr/share/radicle-explorer/config.json"

	install -Dm644 \
		LICENSE \
		-t "$pkgdir/usr/share/licenses/$pkgname"
}

package_radicle-httpd() {
	pkgdesc+=" - explorer (backend)"
	license=('Apache-2.0 OR MIT')
	depends=(
		'glibc'
		'libgcc'
		'libgit2' 'libgit2.so'
		'zlib'
		'radicle-node'
	)

	cd radicle-explorer/crates/radicle-httpd

	install -Dm755 \
		target/dist/radicle-httpd \
		-t "$pkgdir/usr/bin"

	install -Dm644 \
		target/dist/man/radicle-httpd.1 \
		-t "$pkgdir/usr/share/man/man1"

	install -Dm644 \
		"$srcdir/radicle-explorer.nginx.conf" \
		"$pkgdir/usr/share/doc/$pkgname/nginx/radicle-explorer.conf"

	install -Dm644 \
		LICENSE-APACHE \
		LICENSE-MIT \
		-t "$pkgdir/usr/share/licenses/$pkgname"
}
