# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira (faidoc) <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-settings-daemon
pkgver=6.4.2
pkgrel=1
pkgdesc="The Cinnamon Settings daemon"
arch=(x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL)
depends=(cinnamon-desktop colord dbus-glib libcanberra-pulse libcups
         libgnomekbd libgudev libnotify librsvg libwacom nss polkit
         pulse-native-provider upower)
optdepends=('cinnamon-translations: i18n')
makedepends=(meson samurai xf86-input-wacom glib2-devel)
options=(!emptydirs)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('78f353cb8bd1dc3e8586dee009cbb71be4b4b388e7511118d08aa0e966b8b4d939f1971dfb2f5d48d2d9bad142e41e87c72f933d9d6d7875b3ac8d6602eb4730')
b2sums=('0368d2977fcc5756fc3f58aab2fabcddecadb3016cc5fbdf7c08ea2199b3e4314221572bff48a55217bc47c9e3d99568a8c6a6672a14788d45480c3dac424afa')

build() {
  artix-meson --libexecdir=lib/${pkgname} ${pkgname}-${pkgver} build
  samu -C build
}

package() {
  DESTDIR="${pkgdir}" samu -C build install
}
