# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira (faidoc) <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-settings-daemon
pkgver=6.4.0
pkgrel=1
pkgdesc="The Cinnamon Settings daemon"
arch=(x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL)
depends=(cinnamon-desktop colord dbus-glib libcanberra-pulse libcups
         libgnomekbd libgudev libnotify librsvg libwacom nss polkit
         pulse-native-provider upower)
optdepends=('cinnamon-translations: i18n')
makedepends=(meson samurai xf86-input-wacom glib2-devel)
options=(!emptydirs)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('8820c168b18779227e46494b62f8b90f86a5cfa2731e60e79efc7e382ffaef5331feff64a2a58487f626b0ad16674b1867041ef21ce5b5488685a5ea76d6b22c')
b2sums=('de548745d835f62f92547965ef231c9cc432724502e915380ba7d28ddc2c8550f8465dac9e65e326583af5d6576a01728d345ffff1fd2290e30250cd5f837dc9')

build() {
  artix-meson --libexecdir=lib/${pkgname} ${pkgname}-${pkgver} build
  samu -C build
}

package() {
  DESTDIR="${pkgdir}" samu -C build install
}
