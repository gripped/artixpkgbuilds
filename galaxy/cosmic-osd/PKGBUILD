# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-osd
pkgver=1.2.0
pkgrel=1
epoch=1
pkgdesc='COSMIC On-Screen Display'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-osd
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  libgcc
  glibc
  libinput
  libpipewire
  libpulse
  libxkbcommon
  sound-theme-freedesktop
  libelogind
  wayland
)
makedepends=(
  cargo
  clang
  git
  just
  lld
)
source=(
  git+https://github.com/pop-os/cosmic-osd.git#tag=epoch-${pkgver}
)
b2sums=('83e7286e5861085668d58cde2f053a9bdd8618aac47e2facb3ba8e8ea3df4584f5fdef955906118e4993803faa299517d897f89339bb30a9725a4547d748831d')

prepare() {
  cd cosmic-osd
  cargo fetch --locked
}

build() (
  export RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  cd cosmic-osd
  just build-release
)

package() {
  cd cosmic-osd
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
