# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Evangelos Foutras <foutrelis@archlinux.org>
# Contributor: Robin Candau <antiz@archlinux.org>

pkgname=libxfce4windowing
pkgver=4.19.3
pkgrel=4
pkgdesc="Windowing concept abstraction library for X11 and Wayland"
arch=('x86_64')
url="https://docs.xfce.org/xfce/libxfce4windowing/start"
license=('LGPL-2.1-only')
depends=('gdk-pixbuf2' 'glib2' 'gtk3' 'libwnck3' 'libx11' 'wayland')
makedepends=('gobject-introspection' 'gtk-doc')
source=(https://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('3bf6ce5511ef7fb04ddcea79dfdec7a500d8e0b4b7566b28b4cd662adc74bb5a')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-gtk-doc \
    --enable-wayland \
    --enable-x11 \
    --disable-debug
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
