# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Evangelos Foutras <foutrelis@archlinux.org>
# Contributor: Robin Candau <antiz@archlinux.org>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfdesktop
pkgver=4.20.1
pkgrel=3
pkgdesc="Xfce's desktop manager"
arch=('x86_64')
url="https://docs.xfce.org/xfce/xfdesktop/start"
license=('GPL-2.0-or-later')
groups=('xfce4')
depends=('libxfce4ui' 'libxfce4windowing' 'libyaml' 'gtk-layer-shell' 'exo' 'thunar' 'garcon' 'hicolor-icon-theme')
makedepends=('git' 'glib2-devel' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/xfce/xfdesktop.git#tag=$pkgname-$pkgver"
        'fix_log_spam_with_trashed_files.patch')
sha256sums=('f19dca4adf0c82975b3c8ae58e8745e8d07b0328c603413ebf199e1e0ad37b99'
            'bf75f2fec0402a685feccf3e273e86f1b6f446c1c823dd86d97f46a5fc742b7c')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh

  # Fix log spam issue with trashed files
  # See https://gitlab.xfce.org/xfce/thunar/-/issues/1527
  # and https://gitlab.xfce.org/xfce/xfdesktop/-/commit/03f8af7daf70bf08dbcbc5c04ebe63fad616fb76
  patch -Np1 -i "$srcdir/fix_log_spam_with_trashed_files.patch"
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-thunarx \
    --enable-x11 \
    --enable-wayland \
    --enable-notifications \
    --disable-debug \
    --enable-maintainer-mode
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
