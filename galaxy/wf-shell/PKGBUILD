# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Adrian Perez de Castro <aperez@igalia.com>

pkgname=wf-shell
pkgver=0.11.0
pkgrel=1
pkgdesc='GTK-based panel for Wayfire'
arch=(x86_64)
url='https://github.com/WayfireWM/wf-shell'
license=(MIT)
depends=(
  alsa-lib
  cairomm-1.16
  ddcutil
  glib2
  glibc
  glibmm-2.68
  gtk4
  gtk4-layer-shell
  gtkmm-4.0
  hicolor-icon-theme
  libdbusmenu-glib
  libdrm
  libepoxy
  libgcc
  libpulse
  libsigc++-3.0
  libstdc++
  libwireplumber
  libxkbcommon
  mesa
  openssl
  pam
  "wayfire>=${pkgver%.*}.0"
  wayland
  "wf-config>=${pkgver%.*}.0"
  yyjson
)
makedepends=(
  git
  glib2-devel
  glm
  meson
  wayland-protocols
)
source=(
  "git+https://github.com/WayfireWM/wf-shell.git#tag=v$pkgver"
  git+https://gitlab.gnome.org/GNOME/libgnome-volume-control.git
  git+https://github.com/soreau/wayland-logout.git
  git+https://github.com/wmww/gtk4-layer-shell.git
  git+https://github.com/WayfireWM/wf-json.git
)
b2sums=(
  f863d60a7e4b944addc969a2e6b3f5cd0de1607042b657c341bcfb44193e89a3fc31bfe78b8a2fef869a5a8d5aa0cce49178f0f4c46922612509543ed5308aba
  SKIP
  SKIP
  SKIP
  SKIP
)

prepare() {
  cd $pkgname

  git submodule init
  git config submodule.subprojects/wlroots.update none
  git submodule set-url subprojects/gvc "$srcdir/libgnome-volume-control"
  git submodule set-url subprojects/wayland-logout "$srcdir/wayland-logout"
  git submodule set-url subprojects/gtk4-layer-shell "$srcdir/gtk4-layer-shell"
  git submodule set-url subprojects/wf-json "$srcdir/wf-json"
  git -c protocol.file.allow=always submodule update
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dm644 -t "$pkgdir/usr/share/doc/$pkgname" $pkgname/wf-shell.ini.example
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" $pkgname/LICENSE
}
