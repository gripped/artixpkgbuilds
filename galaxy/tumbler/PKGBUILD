# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Evangelos Foutras <foutrelis@archlinux.org>
# Contributor: Robin Candau <antiz@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan Jezek <honzin.jezek@gmail.com>

pkgname=tumbler
pkgver=4.18.2
pkgrel=3
pkgdesc="Thumbnail service implementing the thumbnail management D-Bus specification"
arch=('x86_64')
url="https://docs.xfce.org/xfce/tumbler/start"
license=('GPL-2.0-or-later')
groups=('xfce4')
depends=('libxfce4util' 'gdk-pixbuf2')
makedepends=('git' 'glib2-devel' 'intltool' 'python' 'ffmpegthumbnailer' 'poppler-glib'
             'libgsf' 'libgepub' 'libopenraw' 'freetype2' 'xfce4-dev-tools')
optdepends=('ffmpegthumbnailer: video thumbnails'
            'poppler-glib: PDF thumbnails'
            'libgsf: ODF thumbnails'
            'libgepub: EPUB thumbnails'
            'libopenraw: RAW thumbnails'
            'freetype2: font thumbnails')
backup=('etc/xdg/tumbler/tumbler.rc')
source=("git+https://gitlab.xfce.org/xfce/tumbler.git#tag=$pkgname-$pkgver")
sha256sums=('83eb70ba554f701883c1fab8b5d0d1a1b29d9f931717bab9391e92d028017d1c')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-debug \
    --enable-gtk-doc \
    --disable-gstreamer-thumbnailer
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  rm -r $pkgdir/usr/lib/systemd
}

# vim:set ts=2 sw=2 et:
