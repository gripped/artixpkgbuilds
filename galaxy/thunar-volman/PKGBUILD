# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Evangelos Foutras <foutrelis@archlinux.org>
# Contributor: Robin Candau <antiz@archlinux.org>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=thunar-volman
pkgver=4.18.0
pkgrel=3
pkgdesc="Automatic management of removable drives and media for Thunar"
arch=('x86_64')
url="https://docs.xfce.org/xfce/thunar/thunar-volman"
license=('GPL-2.0-or-later')
groups=('xfce4')
depends=('thunar' 'libxfce4ui' 'hicolor-icon-theme')
makedepends=('git' 'intltool' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/xfce/thunar-volman.git#tag=$pkgname-$pkgver")
sha256sums=('a4812673cac2b94c9b1603f9c1d45972a5a4ebd193c2cf4f7213c0d4e4ee7783')

prepare() {
  cd $pkgname
  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
