# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-calc
pkgver=1.28.0
pkgrel=3.1
pkgdesc="Calculator for the MATE desktop environment"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL-2.0-or-later')
depends=('gtk3' 'gettext' 'libmpc')
makedepends=('autoconf-archive' 'mate-common' 'glib2-devel' 'itstool' 'yelp-tools')
groups=('mate-extra')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/mate-desktop/mate-calc/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('afefad23bee76901ee8b84e10db42e29a1f3630d754b16960fe021402538a9b2')

prepare() {
	cd "${pkgname}-${pkgver}"
	./autogen.sh
}

build() {
    	cd "${pkgname}-${pkgver}"
   	./configure \
        	--prefix=/usr
    	make
}

check() {
	cd "${pkgname}-${pkgver}"
	make check
}

package() {
    	cd "${pkgname}-${pkgver}"
    	make DESTDIR="${pkgdir}" install
}
