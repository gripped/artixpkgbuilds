# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Adrian Perez de Castro <aperez@igalia.com>

pkgname=wcm
pkgver=0.11.0
pkgrel=1
pkgdesc='GTK application to configure Wayfire'
arch=(x86_64)
url='https://github.com/WayfireWM/wcm'
license=(MIT)
depends=(
  atkmm
  fmt
  glib2
  glibc
  glibmm
  gtk3
  gtkmm3
  hicolor-icon-theme
  libevdev
  libgcc
  libsigc++
  libstdc++
  libxkbcommon
  libxml2
  wayland
  "wf-config>=${pkgver%.*}.0"
)
makedepends=(
  git
  glm
  meson
  "wayfire>=${pkgver%.*}.0"
  wayland-protocols
  "wf-shell>=${pkgver%.*}.0"
)
optdepends=('wdisplays: Display configuration tool')
source=(
  "git+https://github.com/WayfireWM/wcm.git#tag=v$pkgver"
  wcm-install-icon.patch
)
b2sums=(
  c554f436cb2a887d7c594af9f77a67a719d2c5f8ec3b4f53195e57e765044b6917dfb9d25d44037571c695d60f3ba3bbf541ac89d1e75661584febb0181daf88
  cc5fc8a58e8f109730ed8cbda8ea33b5a878a16595bd4cde871f39018218d8a5bff6e19deed572ef09deacdeae092614e3a9482d2fddab80a0888836606a9f9a
)

prepare() {
  cd $pkgname

  # Install icons into private directory
  # https://github.com/WayfireWM/wcm/pull/120
  git apply -3 ../wcm-install-icon.patch
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" $pkgname/LICENSE
}
