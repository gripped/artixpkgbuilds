# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Adrian Perez de Castro <aperez@igalia.com>

pkgname=wcm
pkgver=0.10.0
pkgrel=4
pkgdesc='GTK application to configure Wayfire'
arch=(x86_64)
url='https://github.com/WayfireWM/wcm'
license=(MIT)
depends=(
  atkmm
  glib2
  glibc
  glibmm
  gtk3
  gtkmm3
  libevdev
  libgcc
  libsigc++
  libstdc++
  libxkbcommon
  libxml2
  wayland
  "wf-config>=${pkgver%.*}.0"
)
makedepends=(
  git
  glm
  meson
  "wayfire>=${pkgver%.*}.0"
  wayland-protocols
  "wf-shell>=${pkgver%.*}.0"
)
optdepends=('wdisplays: Display configuration tool')
source=("git+https://github.com/WayfireWM/wcm.git#tag=v$pkgver")
b2sums=(b76e5467d02cc5926ae7efe300c6d017aa9a0410b2c720e16ebb53fce03560c111ab9e664f4e164881bee2c1237d45e14c5b8ab18ddc0265e319f718867985a5)

prepare() {
  cd $pkgname

  # Install wcm.svg to the expected location
  # https://github.com/WayfireWM/wcm/pull/118
  sed -i "s/join_paths(share_dir, 'icons')/join_paths(share_dir, 'icons', 'hicolor', 'scalable', 'apps')/" icons/desktop/meson.build
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" $pkgname/LICENSE
}
