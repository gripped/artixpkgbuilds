# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=pcmanfm-qt
pkgver=2.3.0
pkgrel=2
pkgdesc="The LXQt file manager, Qt port of PCManFM"
arch=("x86_64")
groups=("lxqt")
url="https://github.com/lxqt/$pkgname"
# https://github.com/lxqt/pcmanfm-qt/blob/1.4.1/README.md?plain=1#L12
license=("GPL-2.0-or-later")
depends=("desktop-file-utils" "libfm-qt" "libfm-qt6.so" "layer-shell-qt")
makedepends=("lxqt-build-tools")
optdepends=(
	"gvfs: for various features like mounting without root, remote filesystems, encrypted filesystems and many others mentioned on https://github.com/lxqt/pcmanfm-qt/wiki"
	"gvfs-mtp: MTP support (Media Transfer Protocol)"
	"lxqt-archiver: archive management"
	"qt6-translations: allow keyboard shortcuts to function in some languages (e.g. German)"
)
source=(
	"https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
)
sha256sums=('30ab4cc0f36aedd3f454cec2468d7b8a5820f12de1dd9e69d2e65d7299716d09'
            'SKIP')
validpgpkeys=(
	"169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
	"7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
	"19DFDF3A579BD509DBB572D8BE793007AD22DF7E"  # Pedram Pourang <tsujan2000@gmail.com>
)

prepare() {
	cd "$srcdir/$pkgname-$pkgver"

	# Show desktop preferences only in LXQt
	# https://github.com/lxqt/pcmanfm-qt/pull/2110
	echo 'OnlyShowIn=LXQt;' >> pcmanfm/pcmanfm-qt-desktop-pref.desktop.in
}

build() {
	cmake -B build -S "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=None
	make -C build
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
