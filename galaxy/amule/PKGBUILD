# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Henrik Ronellenfitsch <searinox@web.de>
# Contributor: Alessio Sergi <sergi.alessio {at} gmail.com>
# Contributor: Dario 'Dax' Vilardi <dax [at] deelab [dot] org>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=amule
epoch=1
pkgver=3.0.0
pkgrel=1
pkgdesc='An eMule-like client for ed2k p2p network'
arch=(x86_64)
url='http://www.amule.org'
license=(GPL-2.0-or-later)
depends=(binutils
         crypto++
         curl
         gd
         glib2
         glibc
         gtk3
         libayatana-appindicator
         libgcc
         libmaxminddb
         libpng
         libstdc++
         libupnp
         readline
         wxwidgets-common
         wxwidgets-gtk3
         zlib
         zstd)
makedepends=(boost
             cmake
             git)
provides=(amule-daemon)
source=(git+https://github.com/amule-project/amule#tag=$pkgver
        amule.sysusers
        amule.tmpfiles)
options=(!lto)
sha256sums=('4ebc7b98840d91ad43d4f4952837b770fdfdac9a772a02adf2b117ddd2198503'
            '1a1780d3010f338b16ace33fac6af8480bb0830402001f95f12c2e98afbd1b60'
            'e9d1b7019c7075b0f8616c6507a767b87de8f899936680e9ff5829d8cbba224d')

prepare() {
  cd $pkgname
  rm -fr .git
}

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_ALC=ON \
    -DBUILD_ALCC=ON \
    -DBUILD_AMULECMD=ON \
    -DBUILD_CAS=ON \
    -DBUILD_DAEMON=ON \
    -DBUILD_REMOTEGUI=ON \
    -DBUILD_WEBSERVER=ON \
    -DBUILD_WXCAS=ON \
    -DBUILD_FILEVIEW=ON \
    -DENABLE_IP2COUNTRY=ON \
    -DENABLE_NLS=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 "$srcdir"/amule.sysusers "$pkgdir"/usr/lib/sysusers.d/amule.conf
  install -Dm644 "$srcdir"/amule.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/amule.conf
  rm "$pkgdir"/usr/share/LICENSE.md
}
