# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Based on gnome-screensaver package:
# Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Jan de Groot <jgc@archlinux.org>

pkgname=cinnamon-screensaver
pkgver=6.2.1
pkgrel=1
pkgdesc="Screensaver designed to integrate well with the Cinnamon desktop."
arch=(x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL)
depends=(accountsservice cinnamon-desktop libgnomekbd
         python-cairo python-gobject python-setproctitle python-xapp xapp xdotool)
optdepends=('cinnamon-translations: i18n')
makedepends=(gobject-introspection meson samurai glib2-devel)
backup=(etc/pam.d/cinnamon-screensaver)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('4033d1170b4ef359dc8a6d529b40e40fa5bb630f4900eaf5d45c73d7caadde00345811bc5b8f347bfc184f16abbdb232ebf71bb911c713bb00ced557115fd300')
b2sums=('86159f06e44bd58ca443457109c1345ba3021d1b8aa339d315da45fad994e8fccf59fffdc8bef3edefebf42a6ea4ab632354ef5a8f7c10f6d4d70ab517fce3bb')

build() {
  artix-meson --libexecdir=lib/cinnamon-screensaver build ${pkgname}-${pkgver}
  samu -C build
}

package() {
  DESTDIR="${pkgdir}" samu -C build install
}
