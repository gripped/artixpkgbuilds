# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Based on gnome-screensaver package:
# Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Jan de Groot <jgc@archlinux.org>

pkgname=cinnamon-screensaver
pkgver=6.4.1
pkgrel=1
pkgdesc="Screensaver designed to integrate well with the Cinnamon desktop."
arch=(x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL)
depends=(accountsservice cinnamon-desktop libgnomekbd
         python-cairo python-gobject python-setproctitle python-xapp xapp xdotool)
optdepends=('cinnamon-translations: i18n')
makedepends=(gobject-introspection meson samurai glib2-devel)
backup=(etc/pam.d/cinnamon-screensaver)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('62125b6c8a0cfa8b969acd2be5f4ce0ae8f092565c45807b7ccedcf626a2c9bb2208838872eeea3ef2c418d902e55a58fab54e7f57cf2e6da00bbf19fd440783')
b2sums=('83a9fda11bc0e1a809b43a2b7c3c4b6b540b0c6415faaa21f31534c550b956b4c67bd67108f2d1c8b66780dbedecf6f726dd9e3e29779ede9a496fce9a59237a')

build() {
  artix-meson --libexecdir=lib/cinnamon-screensaver build ${pkgname}-${pkgver}
  samu -C build
}

package() {
  DESTDIR="${pkgdir}" samu -C build install
}
