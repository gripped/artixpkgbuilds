# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Evangelos Foutras <foutrelis@archlinux.org>
# Contributor: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Robin Candau <antiz@archlinux.org>
# Contributor: tobias [tobias at archlinux.org]

pkgname=mousepad
pkgver=0.6.3
pkgrel=1
pkgdesc="Simple text editor for Xfce"
arch=('x86_64')
url="https://docs.xfce.org/apps/mousepad/start"
license=('GPL-2.0-or-later')
groups=('xfce4-goodies')
depends=('gtksourceview4' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('git' 'glib2-devel' 'gspell' 'libxfce4ui' 'polkit' 'xfce4-dev-tools')
optdepends=('gspell: spell checking plugin'
            'libxfce4ui: shortcuts editor plugin')
source=("git+https://gitlab.xfce.org/apps/mousepad.git#tag=$pkgname-$pkgver")
sha256sums=('1f2ab5b7652d6289f2643b32b4e067dddb11b7065e48b6203aa1d2c19c131f21')

prepare() {
  cd $pkgname
  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
