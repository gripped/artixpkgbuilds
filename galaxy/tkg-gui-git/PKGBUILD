# Maintainer: superphenotype

pkgname=tkg-gui-git
pkgver=r39
pkgrel=1
pkgdesc="Graphical interface for building custom Linux kernels using the linux-tkg build system"
arch=('x86_64')
url="https://github.com/MasterGenotype/tkg-gui"
license=('unknown')
depends=('gcc-libs' 'glibc' 'libxkbcommon' 'libxcb' 'wayland' 'mesa' 'openssl')
makedepends=('cargo' 'git')
optdepends=(
    'base-devel: required by makepkg for kernel builds'
)
provides=('tkg-gui')
conflicts=('tkg-gui')
source=("tkg-gui::git+https://git@github.com/MasterGenotype/tkg-gui.git")
sha256sums=('SKIP')

prepare() {
    cd tkg-gui
    git submodule update --init --recursive
    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd tkg-gui
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --frozen --release
}

package() {
    cd tkg-gui
    install -Dm755 "target/release/tkg-gui" "$pkgdir/usr/bin/tkg-gui"

    # Install submodules to /usr/share/tkg-gui so the binary can locate them
    install -dm755 "$pkgdir/usr/share/tkg-gui"
    cp -a submodules/linux-tkg "$pkgdir/usr/share/tkg-gui/linux-tkg"

    install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"

    # Install desktop entry
    sed 's|%BINDIR%|/usr/bin|g' tkg-gui.desktop > tkg-gui.desktop.out
    install -Dm644 tkg-gui.desktop.out "$pkgdir/usr/share/applications/tkg-gui.desktop"

    # Install polkit policy
    install -Dm644 com.tkg-gui.policy "$pkgdir/usr/share/polkit-1/actions/com.tkg-gui.policy"
    sed -i 's|%BINDIR%|/usr/bin|g' "$pkgdir/usr/share/polkit-1/actions/com.tkg-gui.policy"
}
