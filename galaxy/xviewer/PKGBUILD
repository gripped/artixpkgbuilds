# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=xviewer
pkgver=3.4.17
pkgrel=1
pkgdesc='Simple image viewer'
arch=(x86_64)
url='https://github.com/linuxmint/xviewer'
license=(GPL-2.0-or-later)
groups=(x-apps)
depends=(
  at-spi2-core
  cairo
  cinnamon-desktop
  dconf
  exempi
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  lcms2
  libexif
  libjpeg-turbo
  libpeas
  librsvg
  libx11
  xapp
  zlib
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  itstool
  meson
)
optdepends=('xviewer-plugins: Additional features')
source=(
  "git+https://github.com/linuxmint/xviewer.git#tag=$pkgver"
  xviewer-update-state.patch
)
b2sums=(
  bf003c59b201c1b7f3078fb59f4a43b6497c673dbb5f2c06ec8feaa813c6e2bf8e8e83c27f88f742f5f38fda15cf1dcc8cfd90bc23cfbac97547e679ea3a898a
  515efac7b2fd626ec244dd419a0da873d503ce3c155e75a0ae82e1aedf13ffc3058133c52a652cead4577ff6af143a9803db1f36b92aace1eaa3e748a087830e
)

prepare() {
  cd $pkgname

  # Update state after opening images
  # https://github.com/linuxmint/xviewer/pull/238
  git apply -3 ../xviewer-update-state.patch
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
