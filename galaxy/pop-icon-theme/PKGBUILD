# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Christopher Reimer <mail@c-reimer.de>

pkgname=pop-icon-theme
pkgver=3.5.1
pkgrel=1.1
pkgdesc='System76 Pop Icon Theme'
arch=(any)
url=https://github.com/pop-os/icon-theme
license=(CCPL)
depends=(adwaita-icon-theme)
makedepends=(
  git
  meson
)
_tag=1a575a8e99b4ae629b9b16543a3a04d148632ba9
source=(pop-icon-theme::git+https://github.com/pop-os/icon-theme.git#tag=${_tag})
b2sums=('31018a78e3e141b76f6533d668d0cec0aae96e71bb157ddae1b0c01f946a66b496319ef511c441a8ed0a788d0c215189ef17aacfd191b46a0276da08b70e39ad')

pkgver() {
  cd pop-icon-theme
  git describe --tags | sed 's/^v//;s/-/.r/;s/-g/./'
}

build() {
  artix-meson pop-icon-theme build
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
