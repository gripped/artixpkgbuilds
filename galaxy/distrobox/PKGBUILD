# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Robin Candau <antiz@archlinux.org>
# Contributor: robertfoster

pkgname=distrobox
pkgver=1.8.2.3
pkgrel=1
pkgdesc="Use any linux distribution inside your terminal."
url="https://distrobox.it"
arch=('any')
license=('GPL-3.0-only')
depends=('bash' 'hicolor-icon-theme')
optdepends=('docker: Docker support'
	    'podman: Podman support')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/89luca89/distrobox/archive/${pkgver}.tar.gz")
sha256sums=('b4fcc60c0b856aac93385ea0eb88fb27fdaeb00b365c75e0eed497c306069837')

package() {
	cd "${pkgname}-${pkgver}"
	./install -P "${pkgdir}/usr"
	find docs -name "*.md" -exec install -Dm 644 -t "${pkgdir}/usr/share/doc/${pkgname}" {} +
}
