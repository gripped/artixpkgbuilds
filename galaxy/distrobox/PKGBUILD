# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Robin Candau <antiz@archlinux.org>
# Contributor: robertfoster

pkgname=distrobox
pkgver=1.8.2.2
pkgrel=1
pkgdesc="Use any linux distribution inside your terminal."
url="https://distrobox.it"
arch=('any')
license=('GPL-3.0-only')
depends=('bash' 'hicolor-icon-theme')
optdepends=('docker: Docker support'
	    'podman: Podman support')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/89luca89/distrobox/archive/${pkgver}.tar.gz")
sha256sums=('0c797689c0b8c7c7c9fa53d1f5550657af95e64d8b8bbdc0fe374f341ebf6cd0')

package() {
	cd "${pkgname}-${pkgver}"
	./install -P "${pkgdir}/usr"
	find docs -name "*.md" -exec install -Dm 644 -t "${pkgdir}/usr/share/doc/${pkgname}" {} +
}
