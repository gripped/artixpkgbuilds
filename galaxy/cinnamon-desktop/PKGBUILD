# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueria <alexfilgueira@antergos.com>
# Contributor: Jan de Groot <jan@archlinux.org>

pkgname=cinnamon-desktop
pkgver=6.6.2
pkgrel=1
pkgdesc='Library with common API for various Cinnamon modules'
arch=(x86_64)
url='https://github.com/linuxmint/cinnamon-desktop'
license=('GPL-2.0-or-later AND LGPL-2.0-or-later')
depends=(
  cairo
  dconf
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  libpulse
  libx11
  libxkbfile
  libelogind
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  meson
)
source=("git+https://github.com/linuxmint/cinnamon-desktop.git#tag=$pkgver")
b2sums=('2d9762a60bc6c43f43e5bbcae4b07f60406dcf4de9ea82ca0c397a4ef0728a48702d4dc40deb6fae0e9329e6a16d589c3d0f1a54fa11ce8a9648407730f4c420')

build() {
  artix-meson build $pkgname -Dsystemd=disabled
  meson compile -C build
}

package() {
  meson install -C build --destdir="$pkgdir"
}
