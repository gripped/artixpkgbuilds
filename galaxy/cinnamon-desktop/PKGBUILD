# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueria <alexfilgueira@antergos.com>
# Contributor: Jan de Groot <jan@archlinux.org>

pkgname=cinnamon-desktop
pkgver=6.6.0
pkgrel=1
pkgdesc="Library with common API for various Cinnamon modules"
arch=(x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL LGPL)
depends=(accountsservice gtk3 libxkbfile libpulse)
makedepends=(git meson gobject-introspection intltool glib2-devel)
source=(git+${url}#tag=$pkgver
        config.patch)
sha256sums=('a41b080077b61a6ad16f4a787d77dc8c4b6b3d04209bf82117f4de55f64cca60'
            'e70227c64eca6857fe9adc21c6aee851df40b3a54bf34a145e0dc4b9819a7b6a')
b2sums=('ad5ed3fb5966625ab5a84176cf33f631ffbbee3b221f8e37432af82221aaac663a18a09c50ff8210d99f48848546f4348235ac9c89b55aa5c6e1d096f6eb5042'
        '5ae28fca1f3fee35222569afdba3f6f2df00f2b27709d1135589f33a3b80546ca541c391f5ee02759467fcaffe380dc1cbc4117dffe3c10cdf7046fd8588e384')

prepare() {
  cd ${pkgname}
  # Adjust configuration
  patch -p1 < ../config.patch
  mkdir -p build
}

build() {
  artix-meson build $pkgname -Dsystemd=disabled
  meson compile -C build
}

package() {
  meson install -C build --destdir="$pkgdir"
}
