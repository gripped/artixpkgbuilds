# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: uastasi <uastasi@archlinux.us>

pkgname=gnote
pkgver=50.2
pkgrel=1
pkgdesc="A note taking application"
url="https://gitlab.gnome.org/GNOME/gnote/-/wikis/Gnote"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  cairomm-1.16
  dconf
  glib2
  glibc
  glibmm-2.68
  gtk4
  gtkmm-4.0
  hicolor-icon-theme
  libadwaita
  libgcc
  libsecret
  libsigc++-3.0
  libstdc++
  libxml2
  libxslt
  pango
  pangomm-2.48
  util-linux-libs
)
makedepends=(
  git
  meson
  yelp-tools
)
source=("git+https://gitlab.gnome.org/GNOME/gnote.git#tag=${pkgver/[a-z]/.&}")
b2sums=('b55237bae73cdd38de69a14964d37fc9e8c825e6a0ba19ed2597e69d3c5b169983205091fcd3dec12ce5c5570adbcdb6277c408b07e9d4fd72973c79041a56b8')

build() {
  artix-meson gnote build
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
