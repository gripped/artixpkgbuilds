# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-screensaver
pkgver=1.28.0
pkgrel=3
pkgdesc='Screensaver for MATE'
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL-2.0-or-later')
depends=('mate-panel' 'libmatekbd' 'gettext' 'libnotify' 'libxss' 'mate-desktop' 'mate-menus' 'mate-session-manager')
makedepends=('autoconf-archive' 'glib2-devel' 'mate-common' 'elogind')
groups=('mate-extra')
conflicts=('mate-screensaver-gtk3')
replaces=('mate-screensaver-gtk3')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/mate-desktop/mate-screensaver/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('bbbe2bd247ffba1116098d23b5aec57c6cb132c08287f344e1e144ebf06317dc')

prepare() {
	cd "${pkgname}-${pkgver}"
	./autogen.sh
}

build() {
	cd "${pkgname}-${pkgver}"
	./configure \
	           --prefix=/usr \
	           --libexecdir="/usr/lib/${pkgname}" \
	           --sysconfdir=/etc \
	           --with-xscreensaverdir=/usr/share/xscreensaver/config \
	           --with-xscreensaverhackdir=/usr/lib/xscreensaver \
	           --with-mit-ext \
	           --with-libnotify \
	           --enable-locking \
	           --without-console-kit \
	           --with-elogind=yes
	make
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
