# Maintainer: Artoo <artoo@artixlinux.org>
# Contributor: MatMoul <matmoul at the google email domain which is .com>

_url=https://github.com/aarnt/alpm_octopi_utils
_commit=1e735c3a27803ca5b6470e946f9cac708143dfd9

pkgname=alpm-octopi-utils
pkgver=1.0.2
pkgrel=10
pkgdesc="Alpm utils for Octopi"
arch=('x86_64')
url="https://tintaescura.com/projects/octopi/"
license=('GPL-3.0-or-later')
provides=(alpm_octopi_utils libalpm_octopi_utils.so)
depends=(glibc glib2 pacman)
makedepends=(cmake git vala)
conflicts=(alpm_octopi_utils)
replaces=(alpm_octopi_utils)
source=("$pkgname::git+$_url.git#commit=$_commit"
        0001-set-soversion.patch)
sha256sums=('SKIP'
            '0a479b0194ed31686786250d060276aa26a1c638a0bcf38d34f9109db0d648ad')

prepare() {
    patch -d "$pkgname" -Np 1 -i ../0001-set-soversion.patch
}

build() {
    cmake -S "$pkgname" -B build \
        -DCMAKE_INSTALL_PREFIX=/usr
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
}
