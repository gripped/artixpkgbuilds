# Maintainer: artist for Artix Linux

pkgname=xlibre-xf86-video-ati
_pkgname=xf86-video-ati
pkgver=22.0.0.3
pkgrel=1
pkgdesc="XLibre fork of X.Org ati video driver"
arch=('x86_64')
url="https://github.com/X11Libre"
license=('MIT')
depends=('libudev' 'mesa' 'libpciaccess' 'libdrm' 'glibc')
makedepends=('xlibre-xserver-devel' 'udev' 'X-ABI-VIDEODRV_VERSION=28.0')
conflicts=('X-ABI-VIDEODRV_VERSION<28' 'X-ABI-VIDEODRV_VERSION>=29')
groups=('xlibre-drivers')
source=("${url}/${_pkgname}/archive/refs/tags/${pkgname}-${pkgver}.tar.gz")

build() {
  cd ${_pkgname}-${pkgname}-${pkgver}
  export CFLAGS=${CFLAGS/-fno-plt}
  export CXXFLAGS=${CXXFLAGS/-fno-plt}
  export LDFLAGS=${LDFLAGS/-Wl,-z,now}

  ./autogen.sh --prefix=/usr --enable-glamor --disable-debug
  ./configure --prefix=/usr
  make
}

check() {
  cd ${_pkgname}-${pkgname}-${pkgver}
  make check
}

package() {
  cd ${_pkgname}-${pkgname}-${pkgver}
  make "DESTDIR=${pkgdir}" install

  install -Dm644 "${srcdir}"/${_pkgname}-${pkgname}-${pkgver}/COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

sha256sums=('4d9925766f18d244b8e5d2e3c8737b9cfb8c86eb48ab7a6aa649992ac836b44f')
