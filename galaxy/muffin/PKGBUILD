# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: Adam Hani Schakaki <adam@schakaki.net>
# Contributor: Ner0

pkgname=muffin
pkgver=6.4.0
pkgrel=1
pkgdesc="Cinnamon window manager based on Mutter"
arch=(x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL)
depends=(cinnamon-desktop gobject-introspection-runtime libcanberra libinput
         libsm libxkbcommon-x11 startup-notification zenity graphene pipewire xorg-xwayland)
makedepends=(meson intltool json-glib gobject-introspection wayland-protocols glib2-devel egl-wayland)
options=(!emptydirs)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('15af3d82396bfc5e5ee68896ba026ec80646dba31ea91ecb4661e69c5cbf80f2')
b2sums=('66f6d4b201e32be7602ae4fab4199404f30a1bbfa6a0d96419e3265cce31f1562508b8d67f88bf57fb78a6279421effa3a2f2cb8ea489f59c7dbaa4da84bcc6a')

build() {
  artix-meson build ${pkgname}-${pkgver} -D egl_device=true -D wayland_eglstream=true
  meson compile -C build
}

package() {
  meson install --destdir="${pkgdir}" -C build
}
