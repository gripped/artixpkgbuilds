# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: Adam Hani Schakaki <adam@schakaki.net>
# Contributor: Ner0

pkgname=muffin
pkgver=6.6.2
pkgrel=1
pkgdesc='Cinnamon window manager based on Mutter'
arch=(x86_64)
url='https://github.com/linuxmint/muffin'
license=(GPL-2.0-or-later)
depends=(
  at-spi2-core
  cairo
  cinnamon-desktop
  cinnamon-settings-daemon
  dconf
  fontconfig
  fribidi
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  graphene
  gtk3
  json-glib
  libcanberra
  libdrm
  libglvnd
  libgudev
  libice
  libinput
  libpipewire
  libsm
  libwacom
  libx11
  libxau
  libxcb
  libxcomposite
  libxcursor
  libxdamage
  libxext
  libxfixes
  libxi
  libxinerama
  libxkbcommon
  libxkbcommon-x11
  libxkbfile
  libxrandr
  libxtst
  mesa
  pango
  pipewire
  startup-notification
  systemd-libs
  wayland
  xorg-xwayland
  zenity
)
makedepends=(
  egl-wayland
  git
  glib2-devel
  gobject-introspection
  meson
  wayland-protocols
)
source=(
  "git+https://github.com/linuxmint/muffin.git#tag=$pkgver"
  muffin-menu-icon.patch
  muffin-wayland-wm-capabilities.patch
)
b2sums=('9a9842d27075fd2f3d07c110f8e13129388e6a7c07391694e7a123d416a8555f74a49799cc22851f61036644dec008bebe4f49a2e17c9fea804cc5a08ab57644'
        '3e511043e740ecc08ca611ec67b4e50d5a13bd4b3e0bb0f300d4ada57a7f4346afa303493bcfa43242f6715c7dc50fb7005a54a9def0079505da1846f8bb4f7c'
        '90e1dde17c970cc1e87e0517c8fcef3f6f53e20969bec34437aa99a042cc7d27eaf8ed08e722f534c758adf80548055948d6ed3efb06489d30559b3243372034')

prepare() {
  cd $pkgname

  # Use window icon for the menu button if available
  # https://github.com/linuxmint/muffin/pull/778
  git apply -3 ../muffin-menu-icon.patch

  # Implement support for xdg_toplevel.wm_capabilities
  # https://github.com/linuxmint/muffin/pull/780
  git apply -3 ../muffin-wayland-wm-capabilities.patch
}

build() {
  artix-meson $pkgname build \
    -D egl_device=true \
    -D wayland_eglstream=true
  meson compile -C build
}

package() {
  meson install -C build --destdir="$pkgdir"
}
