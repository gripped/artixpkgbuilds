# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Frederik Schwan <freswa at archlinux dot org>

pkgname=autotiling-rs
pkgver=0.2.0
pkgrel=1
pkgdesc='Automatically alternates container layouts between horizontal and vertical'
arch=('x86_64')
url='https://github.com/ammgws/autotiling-rs'
license=('MIT')
makedepends=('rust')
source=("https://github.com/ammgws/autotiling-rs/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
b2sums=('2224206b5857b9c6ab1457bc8f438919783162da5b596ff4722af073d71ae1e0d1d250d6ad62afd2d7d51fffdcd9995450df6bfeec5f2fd31706f0eb1b261392')

prepare() {
  cd ${pkgname}-${pkgver}
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 target/release/${pkgname} "${pkgdir}"/usr/bin/${pkgname}
}
