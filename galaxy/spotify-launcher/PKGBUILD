# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=spotify-launcher
pkgver=0.6.6
pkgrel=1
pkgdesc="Client for spotify's apt repository in Rust for Arch Linux"
url='https://github.com/kpcyrd/spotify-launcher'
arch=('x86_64')
license=('Apache-2.0' 'MIT')
depends=(
  'alsa-lib>=1.0.14'
  'at-spi2-atk'
  'glibc'
  'gtk3'
  'hicolor-icon-theme'
  'libayatana-appindicator'
  'libcurl-gnutls'
  'libgcc' 'libgcc_s.so'
  'libsm'
  'libxss'
  'nss'
  'sequoia-sqv'
  'xz' 'liblzma.so'
  'zenity'
)
makedepends=('cargo')
backup=('etc/spotify-launcher.conf')
source=(https://github.com/kpcyrd/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/kpcyrd/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz.asc)
sha256sums=('aa24c78bfe851699c5d19a98f6a8b7dfe94d9d55c7572bb8577cc651b0f80f61'
            'SKIP')
b2sums=('01a14d490a0ece0d06866a2a367f97a5e3914497258f8cfd9f22f9e160d06846283bba8d56489299f9379506d006e55f0485d44eba9b62ea94eae0438bf9aa5c'
        'SKIP')
options=('!lto')

validpgpkeys=("64B13F7117D6E07D661BBCE0FE763A64F5E54FD6")

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --frozen --release
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --frozen
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/spotify-launcher
  install -Dm 644 data/pubkey_5384CE82BA52C83A.gpg \
    "${pkgdir}/usr/share/spotify-launcher/keyring.pgp"

  install -Dm644 contrib/spotify-launcher.desktop -t "${pkgdir}/usr/share/applications"
  install -Dm644 contrib/icons/spotify-linux-512.png "${pkgdir}/usr/share/pixmaps/spotify-launcher.png"
  install -Dm644 contrib/spotify-launcher.conf -t "${pkgdir}/etc"
  install -Dm644 LICENSE-* -t "${pkgdir}/usr/share/licenses/${pkgname}"

  for size in 22 24 32 48 64 128 256 512; do
    install -Dm644 "contrib/icons/spotify-linux-${size}.png" \
      "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/spotify-launcher.png"
  done
}

# vim: ts=2 sw=2 et:
