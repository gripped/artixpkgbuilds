# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=spotify-launcher
pkgver=0.6.4
pkgrel=1
pkgdesc="Client for spotify's apt repository in Rust for Arch Linux"
url='https://github.com/kpcyrd/spotify-launcher'
arch=('x86_64')
license=('Apache-2.0' 'MIT')
depends=(
  'alsa-lib>=1.0.14'
  'at-spi2-atk'
  'gcc-libs'
  'glibc'
  'gtk3'
  'hicolor-icon-theme'
  'libayatana-appindicator'
  'libcurl-gnutls'
  'libsm'
  'libxss'
  'nss'
  'sequoia-sqv'
  'xz' 'liblzma.so'
  'zenity'
)
makedepends=('cargo')
backup=('etc/spotify-launcher.conf')
source=(https://github.com/kpcyrd/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/kpcyrd/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz.asc)
sha256sums=('80e0a883f71c7f56987757d7e0cd69125a0fc6c62b52bbfe5e706cc5d11883e7'
            'SKIP')
b2sums=('033b8e12058a290da98f0ab6dee809d3c999116213219cfdf5861621f6e4132b94331894ede32feb685182fcabfceac5f44c9ad3f61d51d676ef0ef1db2cc4a3'
        'SKIP')
options=('!lto')

validpgpkeys=("64B13F7117D6E07D661BBCE0FE763A64F5E54FD6")

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --frozen --release
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --frozen
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/spotify-launcher
  install -Dm 644 data/pubkey_C85668DF69375001.gpg \
    "${pkgdir}/usr/share/spotify-launcher/keyring.pgp"

  install -Dm644 contrib/spotify-launcher.desktop -t "${pkgdir}/usr/share/applications"
  install -Dm644 contrib/icons/spotify-linux-512.png "${pkgdir}/usr/share/pixmaps/spotify-launcher.png"
  install -Dm644 contrib/spotify-launcher.conf -t "${pkgdir}/etc"
  install -Dm644 LICENSE-* -t "${pkgdir}/usr/share/licenses/${pkgname}"

  for size in 22 24 32 48 64 128 256 512; do
    install -Dm644 "contrib/icons/spotify-linux-${size}.png" \
      "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/spotify-launcher.png"
  done
}

# vim: ts=2 sw=2 et:
