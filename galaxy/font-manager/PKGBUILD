# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Joaquin <joaquingc123 at gmail dot com>
# Contributor: Dies <JerryCasiano(at)gmail(dot)com>
# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>
# Contributor: Guan 'kuno' Qing <neokuno(at)gmail(dot)com>
# Contributor: Guten Ye <ywzhaifei(at)gmail(dot)com>
# Contributor: i_magnific0 <i_magnific0(at)yahoo(dot)com>
# Contributor: Pavlo <pavlofreemen(at)gmail(dot)com>

pkgname=font-manager
pkgver=0.8.9
pkgrel=1
pkgdesc='A simple font management application for GTK+ Desktop Environments'
url=https://fontmanager.github.io
arch=(x86_64 i686)
license=(GPL-3.0-only)
depends=(gtk3
         json-glib
         libsoup
         libxml2
         sqlite
         webkit2gtk-4.1)
optdepends=(file-roller
            libnautilus-extension)
makedepends=(gobject-introspection
             intltool
             meson
             nautilus
             ninja
             vala
             yelp-tools)
_archive="$pkgname-$pkgver"
_url="https://github.com/FontManager/font-manager"
source=("$_url/releases/download/$pkgver/$_archive.tar.xz")
sha256sums=('4e17df355d2390aa8ac6833599c886bc64ad412193e020be081a077cc723a0df')

build() {
	artix-meson "$_archive" build \
		-Dnautilus=true \
		-Dreproducible=true
	ninja -C build
}

package() {
	DESTDIR="$pkgdir" ninja -C build install
}
