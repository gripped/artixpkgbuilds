# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Joaquin <joaquingc123 at gmail dot com>
# Contributor: Dies <JerryCasiano(at)gmail(dot)com>
# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>
# Contributor: Guan 'kuno' Qing <neokuno(at)gmail(dot)com>
# Contributor: Guten Ye <ywzhaifei(at)gmail(dot)com>
# Contributor: i_magnific0 <i_magnific0(at)yahoo(dot)com>
# Contributor: Pavlo <pavlofreemen(at)gmail(dot)com>

pkgname=font-manager
pkgver=0.9.4
pkgrel=1
pkgdesc='A simple font management application for GTK+ Desktop Environments'
url=https://fontmanager.github.io
_url="https://github.com/FontManager/$pkgname"
arch=(x86_64 i686)
license=(GPL-3.0-only)
depends=(cairo libcairo.so
         fontconfig libfontconfig.so
         freetype2 libfreetype.so
         gcc-libs # libgcc_s.so
         glibc # libc.so
         glib2 libgio-2.0.so libglib-2.0.so libgobject-2.0.so
         graphene libgraphene-1.0.so
         gtk4
         harfbuzz libharfbuzz.so
         json-glib
         libsoup3 libsoup-3.0.so
         libxml2 libxml2.so
         pango libpango-1.0.so libpangoft2-1.0.so
         sqlite libsqlite3.so
         webkitgtk-6.0 libwebkitgtk-6.0.so)
optdepends=(file-roller
            libnautilus-extension)
makedepends=(gobject-introspection
             intltool
             meson
             nautilus
             vala
             yelp-tools)
_archive="$pkgname-$pkgver"
source=("$_url/releases/download/$pkgver/$_archive.tar.xz")
sha256sums=('6a0899ac753f50d0779751828c62fa6220024213ebd2179b6f64a3a9bff51f96')

build() {
	local meson_opts=(
		nautilus=true
		reproducible=true
	)
	artix-meson "$_archive" build ${meson_opts[@]/#/-D }
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
