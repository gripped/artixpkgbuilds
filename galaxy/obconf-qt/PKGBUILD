# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Jerome Leclanche <jerome@leclan.ch>

pkgname=obconf-qt
pkgver=0.16.6
pkgrel=1
pkgdesc="Openbox configuration tool. Qt port of ObConf"
arch=("x86_64")
groups=("lxqt")
url="https://github.com/lxqt/$pkgname"
# See discussions about the license at https://github.com/lxqt/obconf-qt/issues/210
license=("GPL-2.0-or-later")
depends=("hicolor-icon-theme" "openbox" "qt6-base")
makedepends=("lxqt-build-tools")
source=(
	"https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
)
sha512sums=('e6845afbf1c3540840f5411d342d72fa39c1540f9f76243964a62a974271e50874188d65ce458a5e95af1ac2590b6730b4c0b4407615fae2f69c3942f3413b2e'
            'SKIP')
validpgpkeys=(
	"19DFDF3A579BD509DBB572D8BE793007AD22DF7E"  # https://github.com/tsujan.gpg
)


build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
