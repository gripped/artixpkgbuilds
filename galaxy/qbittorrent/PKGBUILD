# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG
# Contributor: Jkkyll Wu <adaptee at gmail [dot] com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: Evine Deng <evinedeng@hotmail.com>

pkgbase=qbittorrent
pkgname=(qbittorrent
         qbittorrent-nox)
pkgver=5.1.1
pkgrel=2
arch=(x86_64)
url='https://www.qbittorrent.org'
license=(GPL-2.0-or-later
         GPL-3.0-or-later)
depends=(gcc-libs
         glibc
         libtorrent-rasterbar
         openssl
         qt6-base
         zlib)
makedepends=(boost
             cmake
             git
             qt6-svg
             qt6-tools)
optdepends=('python: needed for torrent search tab')
source=(git+https://github.com/qbittorrent/qBittorrent/#tag=release-$pkgver
        sysusers.conf
        tmpfiles.conf)
sha256sums=('81b30c25718f91ae3f8e0a4647d6f728c1ef1d3ca6527f1cfa773db7bdf41f67'
            '0f148c97cc5fae83fc5022b5f2da374b60a1e2f62a4faf01265e73a9f208825a'
            '8bd2274ba9a6d414cd0170c8855cd6823fa026158ce7ed5eb74d661f21457238')
validpgpkeys=('D8F3DA77AAC6741053599C136E4A2D025B7CC9A2') # sledgehammer999 <sledgehammer999@qbittorrent.org>

prepare() {
  # Fix bug where webui cannot save preferences, see:
  # - https://github.com/qbittorrent/qBittorrent/issues/22909
  # - https://github.com/qbittorrent/qBittorrent/pull/22910
  # - https://github.com/qbittorrent/qBittorrent/commit/101f35dcf2898afd52c6721066e1d71f6cef9c6e
  git -C qBittorrent cherry-pick -n 101f35dcf2898afd52c6721066e1d71f6cef9c6e
}

build() {
  cmake -B build -S qBittorrent \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build

  cmake -B build-nox -S qBittorrent \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DGUI=OFF \
    -DSYSTEMD=OFF
  cmake --build build-nox
}

package_qbittorrent() {
  pkgdesc='An advanced BitTorrent client programmed in C++, based on Qt toolkit and libtorrent-rasterbar'
  depends+=(qt6-svg hicolor-icon-theme)

  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 qBittorrent/COPYING -t "$pkgdir"/usr/share/licenses/$pkgname
}

package_qbittorrent-nox() {
  pkgdesc='An advanced BitTorrent client programmed in C++, based on Qt toolkit and libtorrent-rasterbar, w/o gui'

  DESTDIR="$pkgdir" cmake --install build-nox
  install -Dm644 qBittorrent/COPYING -t "$pkgdir"/usr/share/licenses/$pkgname

  install -Dm644 sysusers.conf "$pkgdir/usr/lib/sysusers.d/qbittorrent.conf"
  install -Dm644 tmpfiles.conf "$pkgdir/usr/lib/tmpfiles.d/qbittorrent.conf"
}
