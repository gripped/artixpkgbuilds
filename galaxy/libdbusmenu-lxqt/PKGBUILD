# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Forked from extra/libdbusmenu-qt. Original contributors:
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libdbusmenu-lxqt
pkgver=0.2.0
pkgrel=1
pkgdesc='A library that provides a Qt implementation of the DBusMenu spec'
arch=(x86_64)
url='https://github.com/lxqt/libdbusmenu-lxqt'
# https://github.com/lxqt/libdbusmenu-lxqt/blob/master/COPYING
# All source files contain "any later"
license=('LGPL-2.0-or-later')
depends=(qt6-base)
makedepends=(cmake doxygen git)
checkdepends=(dbus xorg-server-xvfb)
source=("git+https://github.com/lxqt/libdbusmenu-lxqt?signed#tag=$pkgver")
sha256sums=('74188f1dd665a1e9e0114ecee5d394518ceebd325c182f3c5b0eb4990fecc3c4')
validpgpkeys=(
  "19DFDF3A579BD509DBB572D8BE793007AD22DF7E"  # https://github.com/tsujan
)

build() {
  cmake -B build -S libdbusmenu-lxqt \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

check() {
  dbus-launch xvfb-run --auto-display cmake --build build --target check
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
