# Maintainer: artist for Artix Linux

pkgname=xlibre-xf86-video-amdgpu
_pkgname=xf86-video-amdgpu
pkgver=23.0.0.1
pkgrel=1.1
pkgdesc="XLibre fork of X.Org amdgpu video driver"
arch=('x86_64')
url="https://github.com/X11Libre"
license=('MIT')
depends=('libudev' 'mesa' 'libdrm' 'glibc')
makedepends=('xlibre-xserver-devel' 'udev' 'X-ABI-VIDEODRV_VERSION=28.0')
conflicts=(${_pkgname} 'X-ABI-VIDEODRV_VERSION<28' 'X-ABI-VIDEODRV_VERSION>=29')
provides=(${_pkgname})
source=("${url}/${_pkgname}/archive/refs/tags/${pkgname}-${pkgver}.tar.gz")
groups=('xlibre-drivers')

build() {
  cd ${_pkgname}-${pkgname}-${pkgver}
  export CFLAGS=${CFLAGS/-fno-plt}
  export CXXFLAGS=${CXXFLAGS/-fno-plt}
  export LDFLAGS=${LDFLAGS/-Wl,-z,now}

  ./autogen.sh
  ./configure --prefix=/usr \
    --enable-glamor
  make
}

check() {
  cd ${_pkgname}-${pkgname}-${pkgver}
  make check
}

package() {
  cd ${_pkgname}-${pkgname}-${pkgver}
  make "DESTDIR=${pkgdir}" install
  install -Dm644 "${srcdir}"/${_pkgname}-${pkgname}-${pkgver}/COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

sha256sums=('c23e6a51bcdd16a455851cc2417554b032c545d30466687a4f8c20f3fa68f96b')
