# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=pluma
pkgver=1.28.1
pkgrel=1
pkgdesc="A powerful text editor for MATE"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL-2.0-or-later')
depends=('iso-codes' 'mate-desktop' 'zenity' 'gtksourceview4' 'libpeas' 'python' 'gettext' 'enchant' 'libsm')
makedepends=('itstool' 'git' 'glib2-devel' 'gobject-introspection' 'python' 'mate-common' 'yelp-tools' 'autoconf-archive')
optdepends=('python-gobject: to use the python plugins')
groups=('mate-extra')
conflicts=('pluma-gtk3')
replaces=('pluma-gtk3')
source=("git+https://github.com/mate-desktop/pluma.git#tag=v${pkgver}"
        git+https://github.com/mate-desktop/mate-submodules.git)
sha512sums=('8e6f152c92cfba1736f1a95ef027ba8e857ba554955ce6198f083058ee602c3531e3a5ee3e69f191be81a543780c4b4aa0a2c6c8728984f8256e42f3481fdd92'
            'SKIP')

prepare() {
	cd "${pkgname}"
	git submodule init
	git config submodule.mate-submodules.url "${srcdir}/mate-submodules"
	git -c protocol.file.allow=always submodule update
	NOCONFIGURE=1 ./autogen.sh
}

build() {
	cd "${pkgname}"
	./configure \
	            --prefix=/usr \
	            --libexecdir="/usr/lib/${pkgname}" \
	            --enable-gtk-doc=no \
	            --enable-python
	make
}

package() {
	cd "${pkgname}"
	make DESTDIR="${pkgdir}" install
}
