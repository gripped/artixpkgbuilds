# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Evangelos Foutras <foutrelis@archlinux.org>
# Contributor: Robin Candau <antiz@archlinux.org>
# Contributor: Tobias Kieslich <tobias funnychar archlinux.org>
# Contributor: Alois Nespor alois.nespor@gmail.com

pkgname=xfburn
pkgver=0.8.0
pkgrel=1
pkgdesc="A simple CD/DVD burning tool based on libburnia libraries"
arch=('x86_64')
url="https://docs.xfce.org/apps/xfburn/start"
license=('GPL-2.0-or-later')
groups=('xfce4-goodies')
depends=('libburn' 'libisofs' 'gtk3' 'libxfce4ui' 'exo' 'gst-plugins-base-libs'
         'libgudev' 'desktop-file-utils')
makedepends=('git' 'glib2-devel' 'meson' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/apps/xfburn.git#tag=$pkgname-$pkgver")
sha256sums=('f928ca59342c586c9138a686f8a6861281794fceda1b459abf00621308fca7c3')

build() {
  artix-meson $pkgname build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set ts=2 sw=2 et:
