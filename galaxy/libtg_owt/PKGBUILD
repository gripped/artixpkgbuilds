# Maintainer: Jiachen YANG <farseerfc at archlinux dot org>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>

pkgname=libtg_owt
# It seems the currently desired version can be gotten from
# https://github.com/telegramdesktop/tdesktop/blob/dev/Telegram/build/docker/centos_env/Dockerfile
_commit=62321fd7128ab2650b459d4195781af8185e46b5
pkgver=0.git30.${_commit:0:7}
pkgrel=2
pkgdesc='WebRTC library'
arch=('x86_64')
url='https://github.com/desktop-app/tg_owt'
license=('BSD-3-Clause')
# libdrm is dynamically loaded via dlopen
depends=('libdrm' 'libxdamage')
makedepends=('git' 'ninja' 'cmake' 'protobuf' 'libxrandr' 'libxcomposite' 'openssl' 'glibc'
             'ffmpeg' 'libva' 'opus' 'yasm' 'libjpeg-turbo' 'pipewire' 'libxtst' 'libepoxy' 'openh264')
options=('staticlibs')
source=("git+${url}.git#commit=${_commit}"
        0001-Replace-absl-template-nullability-annotations.patch
        "git+https://chromium.googlesource.com/libyuv/libyuv/"
        "git+https://github.com/google/crc32c"
        "git+https://github.com/abseil/abseil-cpp"
        "git+https://github.com/cisco/libsrtp")
b2sums=('73232699174a27e794efbdd51926d2ef515384f0a95eb498f1f6f41b5c3ee986fd964f5f5017f7f5368a784aafc11433910b6a9e412ecf6c21be8e8ceae8deaf'
        'b7505db537f72da43fdc648d181387a550a683e204a12df593d68ff51ae2f2a924352adf11ad2fa794e5de87ed96d0d698257b442ca0fd2fbb5fb09ac627a7b6'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP')

prepare() {
  cd tg_owt
  git submodule init
  git submodule set-url src/third_party/libyuv "$srcdir"/libyuv
  git submodule set-url src/third_party/crc32c/src "$srcdir"/crc32c
  git submodule set-url src/third_party/abseil-cpp "$srcdir"/abseil-cpp
  git submodule set-url src/third_party/libsrtp "$srcdir"/libsrtp
  git -c protocol.file.allow=always submodule update

  # Fix for abseil 20250814; see https://github.com/desktop-app/tg_owt/pull/164.
  patch -p1 -i ../0001-Replace-absl-template-nullability-annotations.patch
}

build() {
  cd tg_owt

  export CFLAGS+=" -ffat-lto-objects"
  export CXXFLAGS+=" -ffat-lto-objects -I/usr/include/libdrm"
  # Upstream stated that only static builds are really supported so that's what we'll do.
  # https://github.com/desktop-app/tg_owt/issues/75#issuecomment-992061171
  cmake \
    -B build \
    -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=OFF
  ninja -C build
}

package() {
  cd tg_owt
  DESTDIR="${pkgdir}/" ninja -C build install
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
