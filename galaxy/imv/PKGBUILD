# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Harry Jeffery <harry@exec64.co.uk>
# Contributor: Aleksei Bavshin <alebastr89@gmail.com>

pkgname=imv
pkgver=5.0.0
pkgrel=1
pkgdesc='Image viewer for Wayland and X11'
url='https://sr.ht/~exec64/imv/'
arch=(x86_64)
license=(MIT)
depends=(glu libheif libinih libjxl libnsbmp librsvg libxkbcommon libxkbcommon-x11 pango qoi wayland-protocols)
makedepends=(asciidoc cmake cmocka meson)
conflicts=(renameutils)
source=("$pkgname-$pkgver.tar.gz::https://git.sr.ht/~exec64/$pkgname/archive/v$pkgver.tar.gz")
b2sums=('bc65c1c0006a22dc09d01d914aa1eb32bdf6e54344053a94c9f1c56955d8f5056b57bacc180375aaa426db097335548692264b7f9fd47e9854196ec9e2e0deec')

prepare() {
  sed -i 's/icu-io/icu-uc/g' $pkgname-v$pkgver/meson.build
}

build() {
  export CFLAGS+=' -w'
  artix-meson build $pkgname-v$pkgver -D libnsgif=disabled
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 $pkgname-v$pkgver/LICENSE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
