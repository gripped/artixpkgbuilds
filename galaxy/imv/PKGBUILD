# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Harry Jeffery <harry@exec64.co.uk>
# Contributor: Aleksei Bavshin <alebastr89@gmail.com>

pkgname=imv
pkgver=4.5.0
pkgrel=6
pkgdesc='Image viewer for Wayland and X11'
url='https://sr.ht/~exec64/imv/'
arch=(x86_64)
license=(MIT)
depends=(glu libheif libinih libjxl librsvg libxkbcommon libxkbcommon-x11 pango)
makedepends=(asciidoc cmake cmocka meson)
conflicts=(renameutils)
source=("$pkgname-$pkgver.tar.gz::https://git.sr.ht/~exec64/$pkgname/archive/v$pkgver.tar.gz")
b2sums=('100174071049ee711a868832ea297cc7fd43450145db9e71a631afb97d7d77574f37501b749734f47b3f2699d159ed05e5159fd9060c6f0a5de18b92708d9e9c')

prepare() {
  sed -i 's/icu-io/icu-uc/g' $pkgname-v$pkgver/meson.build
}

build() {
  export CFLAGS+=' -w'
  artix-meson build $pkgname-v$pkgver -D libnsgif=disabled -D freeimage=disabled
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 $pkgname-v$pkgver/LICENSE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
