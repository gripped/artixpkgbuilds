# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=dillo
pkgver=3.2.0
pkgrel=6.1
pkgdesc="A small, fast graphical web browser built on FLTK"
arch=(x86_64)
url="https://dillo-browser.org/"
license=('GPL-3.0-or-later')
depends=(
  'fltk1.3'
  'gcc-libs'
  'glibc'
  'libjpeg-turbo'
  'libpng'
  'libwebp'
  'libx11'
  'openssl'
  'perl'
  'zlib'
)
makedepends=(
  git
)
backup=(
  etc/dillo/dillorc
  etc/dillo/domainrc
  etc/dillo/dpidrc
  etc/dillo/keysrc
)
source=(
  "git+https://git.dillo-browser.org/dillo?signed#tag=v$pkgver"
  $pkgname-3.2.0-fltk1.3.patch
)
sha512sums=('08d56317f9ed6ef931a37589cc181574438cb433b664763f453230d9cc1128c2cec87a1c3f65748c4e6a2fb3e86a0c39ea2e5bff273a214ec598f24bbccb1f43'
            '67b328d9d9c4af6c6d1d180c8ec2ea9bd3ac9c125bedb26345cd3363d3c989a1b853fa4485d1df9e9a8f0c9e05b29aecfab31e330328dd74cfe3d5dd22db8635')
validpgpkeys=('32E65EC501A1B6FDF8190D293EE6BA977EB2A253') # Rodrigo Arias Mallo <rodarima@gmail.com>

prepare() {
  cd $pkgname
  # Use fltk1.3 as upstream is not yet compatible with fltk >= 1.4
  patch -Np1 -i ../$pkgname-3.2.0-fltk1.3.patch

  autoreconf -fiv
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc \
    --enable-cookies \
    --enable-tls \
    --enable-ipv6
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -vDm644 -t "$pkgdir/usr/share/pixmaps" icons/128x128/dillo.png
  install -vDm644 -t "$pkgdir/usr/share/applications" dillo.desktop
}
