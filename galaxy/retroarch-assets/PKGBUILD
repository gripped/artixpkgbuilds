# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>

pkgbase=retroarch-assets
pkgname=(
  retroarch-assets-glui
  retroarch-assets-ozone
  retroarch-assets-xmb
)
pkgver=522
pkgrel=1
epoch=1
pkgdesc='XMB menu assets for RetroArch'
arch=(any)
url=http://www.libretro.com/
license=(GPL)
groups=(libretro)
depends=(retroarch)
makedepends=(git)
_commit=c4f1ec8bddba15e1b1a00a7e56c50cf0eca8b5c9
source=(git+https://github.com/libretro/retroarch-assets.git#commit=${_commit})
sha256sums=('fe604cd6e1b8838c62106705509a09de8d3779bef86072b6652055095dc47911')

pkgver() {
  cd retroarch-assets

  git rev-list --count HEAD
}

package_retroarch-assets-glui() {
  depends+=(retroarch-assets-xmb)

  cd retroarch-assets

  install -dm 755 "${pkgdir}"/usr/share/retroarch/assets
  cp -dr --no-preserve=ownership glui "${pkgdir}"/usr/share/retroarch/assets/
}

package_retroarch-assets-ozone() {
  depends+=(retroarch-assets-xmb)

  cd retroarch-assets

  install -dm 755 "${pkgdir}"/usr/share/retroarch/assets
  cp -dr --no-preserve=ownership ozone "${pkgdir}"/usr/share/retroarch/assets/
}

package_retroarch-assets-xmb() {
  cd retroarch-assets

  install -dm 755 "${pkgdir}"/usr/share/retroarch/assets
  cp -dr --no-preserve=ownership xmb "${pkgdir}"/usr/share/retroarch/assets/
}

# vim: ts=2 sw=2 et:
