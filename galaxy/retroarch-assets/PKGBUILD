# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>

pkgbase=retroarch-assets
pkgname=(
  retroarch-assets-glui
  retroarch-assets-ozone
  retroarch-assets-xmb
)
pkgver=20260411.201626.gcd17f64cff4e
pkgrel=1
epoch=1
pkgdesc='UI/UX assets for RetroArch'
arch=(any)
url=http://www.libretro.com/
license=(CC-BY-4.0)
groups=(libretro)
depends=(retroarch)
makedepends=(git)
source=(git+https://github.com/libretro/retroarch-assets.git#commit=${pkgver##*.g})
b2sums=('6ba368af18b4d5f7ff3870412b3153f67549747ffb2528748e411fb98395c8fd60bf199e7582fc92e4f9396b41fa21c15937f3e267f8b730917abd556b20cc2c')

package_retroarch-assets-glui() {
  depends+=(retroarch-assets-xmb)
  pkgdesc="GLUI ${pkgdesc}"
  cd retroarch-assets

  install -dm 755 "${pkgdir}"/usr/share/retroarch/assets
  cp -dr --no-preserve=ownership glui "${pkgdir}"/usr/share/retroarch/assets/
}

package_retroarch-assets-ozone() {
  depends+=(retroarch-assets-xmb)
  pkgdesc="Ozone ${pkgdesc}"
  cd retroarch-assets

  install -dm 755 "${pkgdir}"/usr/share/retroarch/assets
  cp -dr --no-preserve=ownership ozone "${pkgdir}"/usr/share/retroarch/assets/
}

package_retroarch-assets-xmb() {
  cd retroarch-assets
  pkgdesc="XMB ${pkgdesc}"
  install -dm 755 "${pkgdir}"/usr/share/retroarch/assets
  cp -dr --no-preserve=ownership xmb "${pkgdir}"/usr/share/retroarch/assets/
}
