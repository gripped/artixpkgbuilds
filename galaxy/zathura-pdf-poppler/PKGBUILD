# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com)
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: karnath <karnathtorjian@gmail.com>

pkgname=zathura-pdf-poppler
pkgver=0.3.4
pkgrel=1
pkgdesc="Adds pdf support to zathura by using the poppler engine"
url="https://pwmt.org/projects/zathura-pdf-poppler/"
arch=('x86_64')
license=('Zlib')
depends=('poppler-glib' 'zathura' 'desktop-file-utils')
makedepends=('meson' 'ninja' 'appstream' 'appstream-glib')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pwmt/${pkgname}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('e4c219eecd59b7e60013e37a7ab89e6afd425fd4fb66ea219070045999fa541ab5e597b91ad202d81f614e7a82bd623b7c69314b301cbd33cd92acbeef216ae5')

build() {
        cd "${pkgname}-${pkgver}"
        artix-meson build
        ninja -C build
}

package() {
        cd "${pkgname}-${pkgver}"
        DESTDIR="${pkgdir}" ninja -C build install
        install -Dm 664 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
