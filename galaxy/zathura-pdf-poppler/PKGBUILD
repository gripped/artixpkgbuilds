# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com)
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: karnath <karnathtorjian@gmail.com>

pkgname=zathura-pdf-poppler
pkgver=2026.02.03
pkgrel=1
pkgdesc="Adds pdf support to zathura by using the poppler engine"
url="https://pwmt.org/projects/zathura-pdf-poppler/"
arch=('x86_64')
license=('Zlib')
depends=('poppler-glib' 'zathura' 'desktop-file-utils')
makedepends=('meson' 'ninja' 'appstream' 'appstream-glib')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pwmt/${pkgname}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('4c6c32bb213f99b6f3d61fcce317d6be289530ad6ec0a6df4f599abcb4109c75da44822563346ee55828ead504d0a49f9fdbe846c2f4f17d2718d0631d2cee88')

build() {
        cd "${pkgname}-${pkgver}"
        artix-meson build
        ninja -C build
}

package() {
        cd "${pkgname}-${pkgver}"
        DESTDIR="${pkgdir}" ninja -C build install
        install -Dm 664 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
