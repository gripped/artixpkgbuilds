# Maintainer: Johannes Löthberg <johannes@kyriasis.com)
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: karnath <karnathtorjian@gmail.com>

pkgname=zathura-pdf-poppler
pkgver=2026.07.18
pkgrel=1
pkgdesc="Adds pdf support to zathura by using the poppler engine"
url="https://pwmt.org/projects/zathura-pdf-poppler/"
arch=('x86_64')
license=('Zlib')
depends=('poppler-glib' 'zathura')
makedepends=('meson' 'ninja' 'appstream' 'desktop-file-utils')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pwmt/${pkgname}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('6a1ca54a3041f1bcc591cd26c496d6e8868bd0f25336d78268724e4b65fa1fb4839660aca6fb93e5b8a0367da656185868186d1f7d033a3cd90075f197409c2e')

build() {
        cd "${pkgname}-${pkgver}"
        artix-meson build
        ninja -C build
}

check() {
	cd "${pkgname}-${pkgver}"
	meson test -C build --print-errorlogs
}

package() {
        cd "${pkgname}-${pkgver}"
        DESTDIR="${pkgdir}" ninja -C build install
        install -Dm 664 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
