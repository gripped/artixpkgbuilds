# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Duck Hunt <vaporeon@tfwno.gf>

pkgname=libretro-mgba
pkgver=9839
pkgrel=1
pkgdesc='Nintendo Game Boy Advance core'
arch=(x86_64)
url=https://github.com/libretro/mgba
license=(custom:MPL2)
groups=(libretro)
depends=(libretro-core-info)
makedepends=(git)
_commit=affc86e4c07b6e1e8363e0bc1c5ffb813a2e32c9
source=(libretro-mgba::git+https://github.com/libretro/mgba.git#commit=${_commit})
sha256sums=('37dac929267b635f7cb387b3b537b9399eecd42487afb803bad77d300b3c24d6')

pkgver() {
  cd libretro-mgba

  git rev-list --count HEAD
}

build() {
  make -C libretro-mgba -f Makefile.libretro
}

package() {
  install -Dm 644 libretro-mgba/mgba_libretro.so -t "${pkgdir}"/usr/lib/libretro/
  install -Dm 644 libretro-mgba/LICENSE -t "${pkgdir}"/usr/share/licenses/libretro-mgba/
}

# vim: ts=2 sw=2 et:
