# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Duck Hunt <vaporeon@tfwno.gf>

pkgname=libretro-mgba
pkgver=20260917.021300.g7a12d6d4b9ac
pkgrel=1
pkgdesc='Nintendo Game Boy Advance core'
arch=(x86_64)
url=https://github.com/libretro/mgba
license=(MPL-2.0)
groups=(libretro)
depends=(
  glibc
  libretro-core-info
)
makedepends=(
  cmake
  git
  ninja
)
source=(libretro-mgba::git+https://github.com/libretro/mgba.git#commit=${pkgver##*.g})
b2sums=('27124d8a74be979810c860fcc086e16b516d61e85c83babbcd7f7d8a51a8d4023ee01ef5ba3b51bc6c3a9cbe5087c846fdaaaf9b757659ebc73df3e187e4200e')

build() {
  cmake -S libretro-mgba -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_LIBRETRO=ON \
    -DLIBMGBA_ONLY=ON
  cmake --build build
}

package() {
  install -Dm 644 build/mgba_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}
