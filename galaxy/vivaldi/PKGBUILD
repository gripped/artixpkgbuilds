# Maintainer: arc-d3v <arc-d3v@artixlinux.org>
# Contributor: TZ86

pkgname=vivaldi
_rpmversion=7.9.3970.64-1
pkgver=7.9.3970.64
pkgrel=1
pkgdesc='An advanced browser made with the power user in mind.'
url="https://vivaldi.com"
options=(!strip !zipman)
license=('custom')
arch=('x86_64' 'aarch64')
provides=('vivaldi-stable')
depends=(
  'alsa-lib'
  'at-spi2-core'
  'cairo'
  'libcups'
  'libxcomposite'
  'libxdamage'
  'libxkbcommon'
  'libxrandr'
  'libxss'
  'mesa'
  'nss'
  'pango'
  'ttf-font'
  'desktop-file-utils'
  'shared-mime-info'
  'hicolor-icon-theme'
)
makedepends=('w3m' 'imagemagick')
optdepends=(
  'vivaldi-ffmpeg-codecs: playback of proprietary video/audio'
  'org.freedesktop.secrets: better secret storage in gnome-keyring or kwallet'
  'libnotify: native notifications'
  'pipewire: WebRTC desktop sharing under Wayland'
  'gtk3: gtk3 integration'
  'gtk4: for use with --gtk-version=4'
  'qt5-base: qt5 integration'
  'qt6-base: qt6 integration'
  'upower: Battery Status API support'
)
source_x86_64=("https://downloads.vivaldi.com/stable/vivaldi-stable-${_rpmversion}.x86_64.rpm")
source_aarch64=("https://downloads.vivaldi.com/stable/vivaldi-stable-${_rpmversion}.aarch64.rpm")
source=('0001-add-support-for-user-flags.patch')
sha512sums=('a9bdab0fb0f394af17d1b126bae2d0cdc55e63eaa6eaf36eb994735047d3d7bc5000d75ab930f74b76b283c5addefbd4e4e63c5e4f2908bd53c270e73641c19d')
sha512sums_x86_64=('cb5036ff52fc6e74c42b5dc9f3c8ba27ff5212fd12302522122f5bf88533b005a30517fcacdc095f8931c5bab50486b3c29758d54d1829eb9852f063362bd6f0')
sha512sums_aarch64=('a5392fc53d82ff6692df3f1b2c41438899813d524f0a86bc0c523a66a2b7172630348a147ddb0aa518c7c885a0d8fe0d36fb9060e8934687e1e2988e769f35ce')

package() {
  cp --parents -a {opt,usr/bin,usr/share} "$pkgdir"

  # add support for ~/.config/vivaldi-stable.conf
  patch -p1 -i "$srcdir/0001-add-support-for-user-flags.patch" \
    "$pkgdir/opt/$pkgname/$pkgname"
  # remove "orig" leftover if it is there
  rm -f "$pkgdir/opt/$pkgname/$pkgname.orig"

  # suid sandbox
  chmod 4755 "$pkgdir/opt/$pkgname/vivaldi-sandbox"

  install -dm755 "$pkgdir/usr/bin"

  # make /usr/bin/vivaldi-stable available
  binf="$pkgdir/usr/bin/vivaldi-stable"
  if [[ ! -e "$binf" ]] && [[ ! -f "$binf" ]] && [[ ! -L "$binf" ]]; then
    ln -s /opt/vivaldi/vivaldi "$binf"
  fi
  # make /usr/bin/vivaldi available
  binf="$pkgdir/usr/bin/vivaldi"
  if [[ ! -e "$binf" ]] && [[ ! -f "$binf" ]] && [[ ! -L "$binf" ]]; then
    ln -s /opt/vivaldi/vivaldi "$binf"
  fi

  # Vivaldi has different design for each size of icons. Avoid using them.
  # hicolor xdg fallback
  install -Dm644 "$pkgdir/opt/$pkgname/resources/vivaldi/resources/welcomepage-vivaldi.svg" \
    "$pkgdir/usr/share/icons/hicolor/scalable/${pkgname}.svg"
  install -Dm644 "$pkgdir/opt/$pkgname/product_logo_256.png" \
    "$pkgdir/usr/share/icons/hicolor/256x256/apps/$pkgname.png"
  for _res in 128 64 48 32 22; do
    install -d "$pkgdir/usr/share/icons/hicolor/${_res}x${_res}/apps"
    magick "$pkgdir/opt/$pkgname/product_logo_256.png" \
      -resize ${_res}x${_res} \
      "$pkgdir/usr/share/icons/hicolor/${_res}x${_res}/apps/$pkgname.png"
    chmod 0644 \
      "$pkgdir/usr/share/icons/hicolor/${_res}x${_res}/apps/$pkgname.png"
  done
  install -d "$pkgdir/usr/share/pixmaps"
  install -Dm644 "$pkgdir/opt/$pkgname/product_logo_256.png" \
    "$pkgdir/usr/share/pixmaps/${pkgname}.png"

  rm -f "$pkgdir/opt/$pkgname/product_logo_"*.png

  # license
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  w3m -dump "$pkgdir/opt/$pkgname/LICENSE.html" |
    head -n 5 \
      >"$pkgdir/usr/share/licenses/$pkgname/license.txt"

  # https://archlinux.org/todo/legacy-path-for-metainfo-files/
  install -Dm644 "usr/share/appdata/$pkgname.appdata.xml" -t \
    "$pkgdir/usr/share/metainfo/"
  rm -rv "$pkgdir/usr/share/appdata"
}
