# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>

pkgbase=libretro-beetle-psx
pkgname=(
  libretro-beetle-psx
  libretro-beetle-psx-hw
)
pkgver=20260907.040201.g82d8e051d1c7
pkgrel=1
pkgdesc='Sony PlayStation core'
arch=(x86_64)
url=https://github.com/libretro/beetle-psx-libretro
license=(GPL-2.0-only)
groups=(libretro)
depends=(
  libgcc
  libretro-core-info
  libstdc++
  zlib
)
makedepends=(
  git
  libglvnd
  mesa
  vulkan-icd-loader
)
source=(libretro-beetle-psx::git+https://github.com/libretro/beetle-psx-libretro.git#commit=${pkgver##*.g})
b2sums=('a53e7c534b130fb4d95e699849b6d499e2f64324ca0eedf1d8f13e0eca4b68883293e81c8d931878c3a3c0de0a006d949544a2d6fd98f375110b9fa0a6a8d92a')

prepare() {
  cp -r libretro-beetle-psx{,-hw}
}

build() {
  make \
    HAVE_LIGHTREC=1 \
    SYSTEM_LIBCHDR=0 \
    SYSTEM_ZLIB=1 \
    -C libretro-beetle-psx
  make \
    HAVE_HW=1 \
    HAVE_LIGHTREC=1 \
    SYSTEM_LIBCHDR=0 \
    SYSTEM_ZLIB=1 \
    -C libretro-beetle-psx-hw
}

package_libretro-beetle-psx() {
  install -Dm 644 libretro-beetle-psx/mednafen_psx_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

package_libretro-beetle-psx-hw() {
  depends+=(libgl)

  install -Dm 644 libretro-beetle-psx-hw/mednafen_psx_hw_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
