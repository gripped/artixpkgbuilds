# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: twa022 <twa022 at gmail dot com>

pkgbase=libstaroffice
pkgname=(libstaroffice libstaroffice-doc)
pkgver=0.0.7
pkgrel=4
pkgdesc="filter for old StarOffice documents(.sdc, .sdw, ...) based on librevenge"
url="https://github.com/fosnola/libstaroffice"
arch=('x86_64')
license=('LGPL-2.1-or-later OR MPL-2.0')
makedepends=('doxygen' 'librevenge' 'zlib' 'gcc-libs' 'glibc')
source=(https://github.com/fosnola/$pkgname/releases/download/$pkgver/libstaroffice-$pkgver.tar.xz)
sha256sums=('f94fb0ad8216f97127bedef163a45886b43c62deac5e5b0f5e628e234220c8db')

build() {
  cd "${pkgbase}-${pkgver}"
  ./configure --prefix=/usr
  make
}

check() {
  cd "${pkgbase}-${pkgver}"
  make check
}

package_libstaroffice() {
  depends=('librevenge' 'zlib' 'gcc-libs' 'glibc')

  cd "${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install-exec install-pkgconfDATA
  make -C inc/libstaroffice DESTDIR="${pkgdir}" install
}

package_libstaroffice-doc() {
  pkgdesc+=" - documentation"

  cd "${pkgbase}-${pkgver}"
  make -C docs/doxygen/ DESTDIR="${pkgdir}" install
}
