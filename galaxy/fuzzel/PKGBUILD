# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Caleb Maclennan <caleb@alerque.com>
# Contributor: Daniel Eklöf <daniel at ekloef dot se>

pkgname=fuzzel
pkgver=1.14.1
pkgrel=2
pkgdesc='Application launcher for wlroots based Wayland compositors'
url="https://codeberg.org/dnkl/$pkgname"
arch=(x86_64)
license=(MIT)
depends=(cairo
         fcft
         fontconfig
         libfcft.so
         libpng
         libxkbcommon
         pixman
         wayland
         resvg) # resvg is preferred by upstream
makedepends=(meson
             scdoc
             tllist
             wayland-protocols)
backup=('etc/xdg/fuzzel/fuzzel.ini')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('4b8a914d7a065e34da7db4cc6ae4f02c773445e41b724b28b8b7385636b449ee')

build() {
	cd "$pkgname"
	# Between meson-1.10.1 and 1.11.2 the header finder stopped finding resvg.h in
	# the place it has always been which made this package unreproducible (even
	# accounting for the checksum change that happened at the same time thanks to
	# Codeberg's gzip backend change).
	CFLAGS+=' -I/usr/include/resvg'
	artix-meson \
		-Denable-cairo=enabled \
		-Dpng-backend=libpng \
		-Dsvg-backend=resvg \
		. build
	meson compile -C build
}

package() {
	cd "$pkgname"
	meson install -C build --destdir "$pkgdir"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
