# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Ali H. Caliskan <ali.h.caliskan AT gmail DOT com>
# Contributor: Ryan Coyner <rcoyner@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=mixxx
pkgver=2.5.4
pkgrel=1
pkgdesc="Free, open source software for digital DJing"
arch=(x86_64)
url="https://www.mixxx.org"
_url="https://github.com/mixxxdj/mixxx"
license=(GPL-2.0-or-later)
groups=(pro-audio)
# TODO: package libshout-idjc
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  hidapi
  lame
  libebur128
  libkeyfinder
  libmad
  libmodplug
  libx11
  openssl
  opus
  opusfile
  qt6-5compat
  qt6-base
  qt6-declarative
  qt6-svg
  qtkeychain-qt6
  soundtouch
  sqlite
  taglib1
  upower
  wavpack
)
makedepends=(
  benchmark
  chromaprint
  cmake
  faad2
  ffmpeg
  flac
  glib2
  glu
  gperftools
  gtest
  libid3tag
  libogg
  libsndfile
  libusb
  libvorbis
  lilv
  lv2
  microsoft-gsl
  qt6-shadertools
  qt6-tools
  portaudio
  portmidi
  protobuf
  rubberband
)
source=(
  $_url/archive/$pkgver/$pkgname-$pkgver.tar.gz
)
sha512sums=('ded35b6c3e6934de2f6d36c7dedc8c36b5d37cea675faf1adba870b1c1d64d8b86f12783fe5bb42828b6fbd2029fec5b2725752c295d71da70c3aa125cc13b9c')
b2sums=('130073f0484dc34e76e3ea306c71281dc153ffb731e8a64c51ede2d42d8b00f39778b50dc3290de008311b7365485e40604e19d8312858e682e479b86940421b')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=Release
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-$pkgver
    -W no-dev
  )

  export PKG_CONFIG_PATH=/usr/lib/taglib1/pkgconfig
  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  depends+=(
    chromaprint libchromaprint.so
    flac libFLAC.so
    ffmpeg libavcodec.so libavformat.so libavutil.so
    glib2 libgobject-2.0.so
    libglvnd libOpenGL.so
    libid3tag libid3tag.so
    libogg libogg.so
    libsndfile libsndfile.so
    libusb libusb-1.0.so
    libvorbis libvorbis.so libvorbisenc.so libvorbisfile.so
    lilv liblilv-0.so
    portaudio libportaudio.so
    portmidi libportmidi.so
    protobuf libprotobuf-lite.so
    rubberband librubberband.so
    zlib libz.so
  )

  DESTDIR="$pkgdir" cmake --install build
}
