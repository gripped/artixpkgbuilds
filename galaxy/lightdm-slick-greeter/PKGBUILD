# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Sam Burgos <santiago.burgos1089@gmail.com>

pkgname=lightdm-slick-greeter
pkgver=2.2.3
pkgrel=1
pkgdesc='A slick-looking LightDM greeter'
arch=('x86_64')
url="https://github.com/linuxmint/slick-greeter"
license=('GPL3.0-only')
depends=('cairo' 'freetype2' 'gtk3' 'libcanberra' 'libxext' 'lightdm' 'pixman'
         'python' 'xorg-server' 'xapp')
optdepends=('numlockx: enable numerical keypad on supported keyboard')
makedepends=('intltool' 'vala' 'gnome-common' 'meson')
source=("${pkgname}-${pkgver}.tar.gz::$url/archive/${pkgver}.tar.gz")
sha512sums=('3cfd1699178d860d8e2de8d40e0049c63b5b846cce3606a7ba135616aefba39c199b5d1419c7ebaf79ed8ba545e4ad3f9e44147d35faa07dca0686399b40ca14')
b2sums=('62bf8071fb49b086ccd17eeb67ee6205003d26573373efef2f5b4bcb9c16aee0d28034a10bf25d71d1aa6c6b65a68c30c7de0a27f38b7b79ed30c5f97459ebe8')

build() {
	cd slick-greeter-${pkgver}
	artix-meson . build
	meson compile -C build
}

check() {
	cd slick-greeter-${pkgver}
	meson test -C build
}

package() {
	cd slick-greeter-${pkgver}
	meson install -C build --destdir "$pkgdir"
	# adjust launcher name
	mv $pkgdir/usr/share/xgreeters/slick-greeter.desktop \
    $pkgdir/usr/share/xgreeters/lightdm-slick-greeter.desktop
}
