# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Sam Burgos <santiago.burgos1089@gmail.com>

pkgname=lightdm-slick-greeter
pkgver=2.2.6
pkgrel=1
pkgdesc='A slick-looking LightDM greeter'
arch=('x86_64')
url="https://github.com/linuxmint/slick-greeter"
license=('GPL3.0-only')
depends=('cairo' 'freetype2' 'gtk3' 'libcanberra' 'libxext' 'lightdm' 'pixman'
         'python' 'xorg-server' 'xapp')
optdepends=('numlockx: enable numerical keypad on supported keyboard')
makedepends=('intltool' 'vala' 'gnome-common' 'meson')
source=("${pkgname}-${pkgver}.tar.gz::$url/archive/${pkgver}.tar.gz")
sha512sums=('7c6ad8acd5886b41d5a4aef7eb46f12f86d2739f1add215e5907d310b0e9a747e8093b2dcb41d81117ef8e1d69e9f4a73f89fb206f1ea15783e6da0efc74536b')
b2sums=('a6807f070453a75508beef05c03b0885202ccbed7d6be57ecddfdf1f354169eebc8811cd8a40665cba02e325430d5c6ee3b4292057a5c6b2a69641941ae11789')

build() {
	cd slick-greeter-${pkgver}
	artix-meson . build
	meson compile -C build
}

check() {
	cd slick-greeter-${pkgver}
	meson test -C build
}

package() {
	cd slick-greeter-${pkgver}
	meson install -C build --destdir "$pkgdir"
	# adjust launcher name
	mv $pkgdir/usr/share/xgreeters/slick-greeter.desktop \
    $pkgdir/usr/share/xgreeters/lightdm-slick-greeter.desktop
}
