# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=cjs
pkgver=128.0
pkgrel=1
pkgdesc="Javascript Bindings for Cinnamon"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('LGPL-2.0-or-later')
depends=('gobject-introspection-runtime' 'js128' 'readline' 'gcc-libs'
         'libffi' 'cairo' 'glib2' 'libgirepository' 'libx11' 'glibc')
makedepends=('dbus' 'meson' 'gobject-introspection')
checkdepends=('xorg-server-xvfb' 'gtk3')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        fixes-from-master.patch)
sha512sums=('e7ad28a085788f05bc40dd8a86799c068e54e25d7ed349a610a8935c9de3bc556b72e4b7b2c158f84a4a62a4b10789a3b812ca9e3ae2b3e1b1db471e91ddee97'
            'ef85371a0e065c70978a0755d2202b4e0bb83997db1d6bf7caaedff797fdbb9d9372422388e9a6ac9ab07b6064224ec43ce8477fdb71ebd477e6b9c1f27dab19')
b2sums=('d4b516a8d02fcb297f23f277d8ac12f7cf3c8bf0eec41d22e01ea4ffd3995aedc1b20a173d44c3fc6827e4cb3366c277347fcd11e6362a66d682c47919f84fec'
        'f4c9abdd21f013f27d400b030e71b69213048cbb91705f92a81d99c326d857009ff4294b187bed687400809e8b123a676235cc1675ad7eb087ec7fe5af8b08cd')

prepare() {
    cd ${pkgname}-${pkgver}

    # Fixes from master up to 1f39576bafe6bc05bce960e590dc743dd7990e39
    # Required with glib 2.86.0
    patch -Np1 -i ../fixes-from-master.patch
}

build() {
    artix-meson ${pkgname}-${pkgver} build -D installed_tests=false
    meson compile -C build
}

check() {
    dbus-run-session xvfb-run -s '-nolisten local' \
        meson test -C build --print-errorlogs
}

package() {
    meson install -C build --destdir "${pkgdir}"

    # cjs doesn't properly disable installs from gobject-introspection-tests
    rm -r "${pkgdir}/usr/lib/installed-tests"
}
