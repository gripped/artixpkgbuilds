# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Lauri Niskanen <ape@ape3000.com>

pkgname=libretro-gambatte
pkgver=1064
pkgrel=1
pkgdesc='Nintendo Game Boy/Game Boy Color core'
arch=(x86_64)
url=https://github.com/libretro/gambatte-libretro
license=(GPL2)
groups=(libretro)
depends=(libretro-core-info)
makedepends=(git)
_commit=cd1e180b1edf6e6853cf4d501adac0538076de34
source=(libretro-gambatte::git+https://github.com/libretro/gambatte-libretro.git#commit=${_commit})
sha256sums=('014dde635847ff611e0509fca4fc7acce88d7b9aab1bb55160e9e8d7fd4dc6f7')

pkgver() {
  cd libretro-gambatte

  git rev-list --count HEAD
}

build() {
  make -C libretro-gambatte -f Makefile.libretro
}

package() {
  install -Dm 644 libretro-gambatte/gambatte_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
