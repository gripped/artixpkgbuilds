# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Lauri Niskanen <ape@ape3000.com>

pkgname=libretro-gambatte
pkgver=1076
pkgrel=1
pkgdesc='Nintendo Game Boy/Game Boy Color core'
arch=(x86_64)
url=https://github.com/libretro/gambatte-libretro
license=(GPL2)
groups=(libretro)
depends=(libretro-core-info)
makedepends=(git)
_commit=13b7af780e9893ae62cc24d567591b5eb6a6dd72
source=(libretro-gambatte::git+https://github.com/libretro/gambatte-libretro.git#commit=${_commit})
sha256sums=('7f8297a01dfe733cae42fd2ccfe7395962f9311600e98ce4d7d57736cbe097d3')

pkgver() {
  cd libretro-gambatte

  git rev-list --count HEAD
}

build() {
  make -C libretro-gambatte -f Makefile.libretro
}

package() {
  install -Dm 644 libretro-gambatte/gambatte_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
