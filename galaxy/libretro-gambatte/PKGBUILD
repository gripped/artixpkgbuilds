# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Lauri Niskanen <ape@ape3000.com>

pkgname=libretro-gambatte
pkgver=1049
pkgrel=1
pkgdesc='Nintendo Game Boy/Game Boy Color core'
arch=(x86_64)
url=https://github.com/libretro/gambatte-libretro
license=(GPL2)
groups=(libretro)
depends=(libretro-core-info)
makedepends=(git)
_commit=1fbeee0a46b12d3ab28c4795172929ebb31808ea
source=(libretro-gambatte::git+https://github.com/libretro/gambatte-libretro.git#commit=${_commit})
sha256sums=('997ff200ae34dc4eae4c20a6712ae323fcd450428fb0663b4fb8bb83db954300')

pkgver() {
  cd libretro-gambatte

  git rev-list --count HEAD
}

build() {
  make -C libretro-gambatte -f Makefile.libretro
}

package() {
  install -Dm 644 libretro-gambatte/gambatte_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
