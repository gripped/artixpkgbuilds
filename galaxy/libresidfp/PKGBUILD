# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=libresidfp
pkgver=1.2.1
pkgrel=1
pkgdesc='Cycle exact SID emulation'
arch=(x86_64)
url='https://github.com/libsidplayfp/libresidfp'
license=(GPL-2.0-only)
makedepends=(git)
provides=(libresidfp.so)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('6357be53cad9268ed09a7f1b83b21038578f82a9faa5b3631b9a8f79d88992bf722a18e7c004fbf55aed13e89007b0e72d062e3792bf0638ebfaa93f7f8def02')
b2sums=('aaa90568961d7b93a8a49342d1ec6d488a6bdede3598f5d91bef5c1d4fe7d67508627b83565f5b39954e16c0230f7a247dbcf0af81b7b93aa9c70c23a3d69b2c')

prepare() {
  cd "$pkgname"

  autoreconf -vfi
}

build() {
  cd "$pkgname"

  ./configure --prefix=/usr

  make
}

package() {
  depends+=(
    glibc
    libgcc libgcc_s.so
    libstdc++ libstdc++.so
  )

  cd "$pkgname"

  DESTDIR="$pkgdir" make install
}
