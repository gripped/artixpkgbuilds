# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=libresidfp
pkgver=1.1.2
pkgrel=1
pkgdesc='Cycle exact SID emulation'
arch=(x86_64)
url='https://github.com/libsidplayfp/libresidfp'
license=(GPL-2.0-only)
makedepends=(git)
provides=(libresidfp.so)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('06db22898a3b2f95cd927aadfb91965135b0de293fff1746ca197839ee4b0d5233e73034b5650d66558c8c305534b808a2676f539eff8ed621a35cc0c0172c5c')
b2sums=('c84b8833bf63f079c25f14499e25eebda1e24a0c599aef41d1db18211c15019373238929a5f0069105877fb1527e705967f6d2a86df87d2d77045b99798575d2')

prepare() {
  cd "$pkgname"

  autoreconf -vfi
}

build() {
  cd "$pkgname"

  ./configure --prefix=/usr

  make
}

package() {
  depends+=(
    glibc
    libgcc libgcc_s.so
    libstdc++ libstdc++.so
  )

  cd "$pkgname"

  DESTDIR="$pkgdir" make install
}
