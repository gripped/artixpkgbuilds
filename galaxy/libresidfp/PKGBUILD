# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=libresidfp
pkgver=1.2.0
pkgrel=1
pkgdesc='Cycle exact SID emulation'
arch=(x86_64)
url='https://github.com/libsidplayfp/libresidfp'
license=(GPL-2.0-only)
makedepends=(git)
provides=(libresidfp.so)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('befad11eb91a7efadba39a961c64898c1adf0ac12a726affc43e9a7b801da0d0631341212f21849f9a90753e13d84974bebd239166a47b0e07bf0fb558d154d6')
b2sums=('015b2556a218be768b45d38ccfaf9684197795eb588d93dfd4fc874e5e4b3c125a256611df682dbb8221b50b61b63d1427b3efa22f291a9b2463acae457d44af')

prepare() {
  cd "$pkgname"

  autoreconf -vfi
}

build() {
  cd "$pkgname"

  ./configure --prefix=/usr

  make
}

package() {
  depends+=(
    glibc
    libgcc libgcc_s.so
    libstdc++ libstdc++.so
  )

  cd "$pkgname"

  DESTDIR="$pkgdir" make install
}
