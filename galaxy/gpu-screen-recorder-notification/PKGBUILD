# Maintainer: arc-d3v <arc-d3v@artixlinux.org>
# Contributor: dec05eba <dec05eba@protonmail.com>

pkgname=gpu-screen-recorder-notification
pkgver=1.2.3
pkgrel=1
pkgdesc='Notification in the style of ShadowPlay'
arch=(x86_64)
url="https://git.dec05eba.com/gpu-screen-recorder-notification"
license=(GPL-3.0-only)
makedepends=(
  git
  meson
)
depends=(
  glibc
  gcc-libs
  libglvnd
  libx11
  libxrandr
  libxrender
  libxext
  wayland
  pango
  freetype2
)
source=(git+https://repo.dec05eba.com/gpu-screen-recorder-notification#tag=${pkgver})
sha512sums=('f78ac048f7a3c44b8e5597bdc5644361aa21684926d07e16921156a6cba223599204bc88d64a21c4f145697b9b9adbae48eeb448ed75d469600d9fefcf866d47')

build() {
  cd "$srcdir"/$pkgname
  arch-meson build --buildtype=release -Dstrip=true
  meson compile -C build
}

package() {
  cd "$srcdir"/$pkgname
  meson install -C build --destdir "$pkgdir"
}
