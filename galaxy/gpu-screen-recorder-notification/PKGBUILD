# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: dec05eba <dec05eba@protonmail.com>

pkgname=gpu-screen-recorder-notification
pkgver=1.3.2
pkgrel=1
pkgdesc='Notification in the style of ShadowPlay'
arch=(x86_64)
url="https://git.dec05eba.com/gpu-screen-recorder-notification"
license=(GPL-3.0-only)
makedepends=(
  git
  meson
)
depends=(
  glibc
  gcc-libs
  libglvnd
  libx11
  libxkbcommon
  libxrandr
  libxrender
  libxext
  wayland
  pango
  freetype2
)
source=(git+https://repo.dec05eba.com/gpu-screen-recorder-notification#tag=${pkgver})
sha512sums=('8a205d1f4afdd571fd98fed417d810711f146d77bd86b6f7903bfd04de84ff5b1a3270164c5ba76869ddf1951cd97f87d382cfe2d363d7e1363e3a7108cba6c6')

build() {
  cd "$srcdir"/$pkgname
  artix-meson build --buildtype=release -Dstrip=true
  meson compile -C build
}

package() {
  cd "$srcdir"/$pkgname
  meson install -C build --destdir "$pkgdir"
}
