# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Diego Principe <cdprincipe@gmail.com>
# Contributor: paterbrown <paterbrown@silberhelme.de>
# Contributor: Josip <josipponjavic@gmail.com>


pkgname=elementary-icon-theme
pkgver=8.2.0
pkgrel=1
pkgdesc='Named, vector icons for elementary OS'
arch=(any)
url=https://github.com/elementary/icons
license=(GPL3)
groups=(pantheon)
depends=(hicolor-icon-theme)
makedepends=(
  git
  librsvg
  meson
  xorg-xcursorgen
)
_tag=6ddbb535af70a5ceeff5e1fec05d9b0562641b2a
source=(elementary-icon-theme::git+https://github.com/elementary/icons.git#tag=${_tag})
sha256sums=('910e2e8cb75cc9d5a66e69fc9b2d5a40e98cdb2fd001d18b65e7b86281f07122')

pkgver() {
  cd elementary-icon-theme

  git describe --tags
}

build() {
  artix-meson elementary-icon-theme build \
    -Dvolume_icons=false
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
