# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-mesen
pkgver=2904
pkgrel=1
pkgdesc='Nintendo Entertainment System core'
arch=(x86_64)
url=https://github.com/SourMesen/Mesen
license=(GPL3)
groups=(libretro)
depends=(
  gcc-libs
  libretro-core-info
)
makedepends=(git)
_commit=e1180957b31979d4b82d613af9263fcb9442a685
source=(git+https://github.com/SourMesen/Mesen.git#commit=${_commit})
sha256sums=('5070dacee307d6840529e679243f986eb5675e8a6557ad9d4d8b19f19747c665')

pkgver() {
  cd Mesen

  git rev-list --count HEAD
}

build() {
  make -C Mesen/Libretro
}

package() {
  install -Dm 644 Mesen/Libretro/mesen_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
