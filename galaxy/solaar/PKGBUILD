# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Arnaud Taffanel <dev@taffanel.org>
# Contributor: Victor HÃ¤ggqvist <aur a snilius d com>

_name=Solaar
pkgname=solaar
pkgver=1.1.16
pkgrel=1
pkgdesc="Linux device manager for a wide range of Logitech devices"
url="https://pwr-solaar.github.io/Solaar/"
_url="https://github.com/pwr-Solaar/Solaar"
license=(GPL-2.0-or-later)
arch=(any)
depends=(
  glib2
  gobject-introspection-runtime
  gtk3
  hicolor-icon-theme
  libnotify
  python
  python-dbus
  python-evdev
  python-gobject
  python-psutil
  python-pyudev
  python-typing_extensions
  python-xlib
  python-yaml
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
optdepends=('libayatana-appindicator: Display tray icon')
source=(
  "git+$_url.git?signed#tag=$pkgver"
)
sha512sums=('db112f86c5954492d5700cbf79cab632973c52b4e0dd67ba60c307bdf204cb8682d07a8792114a639ded1001ea2a800c5f75b1d2d68b319b7399e9a93ff6fcc4')
validpgpkeys=('2FF2ACA9D97FF7B42CF8494A1E32E75672D6A196') # Peter F. Patel-Schneider (key generated for github) <pfpschneider@gmail.com>

prepare() {
  cd $_name
  git cherry-pick -n 924684b610f831ceeb659bbc9a8321e38954870e  # https://github.com/pwr-Solaar/Solaar/pull/2931
  tools/po-compile.sh
}

build() {
  cd $_name
  python -m build --wheel --no-isolation
}

package() {
  cd $_name
  python -m installer --destdir="$pkgdir" dist/*.whl

  # udev
  rm -rf "$pkgdir/etc"
  install -vDm 644 rules.d/42-logitech-unify-permissions.rules -t "$pkgdir/usr/lib/udev/rules.d/"
  # docs
  install -vDm 644 {CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
