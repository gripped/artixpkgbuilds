# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Christian Heusel <christian@heusel.eu>
# Contributor: Andrew Rabert <ar@nullsum.net>

pkgname=scrcpy
pkgver=4.0
pkgrel=1
pkgdesc='Display and control your Android device'
arch=(x86_64)
url='https://github.com/Genymobile/scrcpy'
license=(Apache-2.0)
depends=(android-tools ffmpeg sdl2 glibc libusb)
makedepends=(git meson)
source=("git+$url#tag=v$pkgver"
        "$pkgname-server-$pkgver.apk::$url/releases/download/v$pkgver/$pkgname-server-v$pkgver")
b2sums=('91db0a30421685e44a1501564608cf55f5dc017de7e816663af2e4d7289f0615b0884b05e5a2302220700a7692ebed1e5f11088c4e36091d95075804d50c9cf5'
        '3896c93fe893985485500937e05127016987954ead6e0d5fe057dd9cb5a20f20eeb81af29f074a07aaeaaae6235eafd8e568eef5a6a91c94ab92bf3e12723d88')

build() {
  mkdir -p build
  artix-meson build $pkgname --buildtype release \
    -D b_lto=true \
    -D b_ndebug=true \
    -D prebuilt_server=../$pkgname-server-$pkgver.apk
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 $pkgname/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
