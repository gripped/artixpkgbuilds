# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Merk Matthias <macem@chello.at>

pkgname=xfce4-sensors-plugin
pkgver=1.4.5
pkgrel=1
pkgdesc="Sensors plugin for the Xfce panel"
arch=('x86_64')
url="https://docs.xfce.org/panel-plugins/xfce4-sensors-plugin/start"
license=('GPL-2.0-or-later')
groups=('xfce4-goodies')
depends=('xfce4-panel' 'lm_sensors' 'libnotify' 'hicolor-icon-theme')
makedepends=('git' 'hddtemp' 'netcat' 'xfce4-dev-tools')
optdepends=('hddtemp: for monitoring the temperature of hard drives')
source=("git+https://gitlab.xfce.org/panel-plugins/xfce4-sensors-plugin.git#tag=$pkgname-$pkgver")
sha256sums=('6db9c517ba6de86fdfcca513fe1b50aabc006fe62a4ded82e49d9fa5c9441d2b')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-pathchecks \
    --disable-debug
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
