# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Evangelos Foutras <foutrelis@archlinux.org>
# Contributor: Robin Candau <antiz@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Merk Matthias <macem@chello.at>

pkgname=xfce4-sensors-plugin
pkgver=1.4.4
pkgrel=3
pkgdesc="Sensors plugin for the Xfce panel"
arch=('x86_64')
url="https://docs.xfce.org/panel-plugins/xfce4-sensors-plugin/start"
license=('GPL-2.0-or-later')
groups=('xfce4-goodies')
depends=('xfce4-panel' 'lm_sensors' 'libnotify' 'hicolor-icon-theme')
makedepends=('git' 'intltool' 'hddtemp' 'netcat' 'xfce4-dev-tools')
optdepends=('hddtemp: for monitoring the temperature of hard drives')
source=("git+https://gitlab.xfce.org/panel-plugins/xfce4-sensors-plugin.git#tag=$pkgname-$pkgver")
sha256sums=('af044d6d46daf45cc2465216d96860bbc8b533c01833a27843a543157fd69559')

prepare() {
  cd $pkgname
  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-pathchecks \
    --disable-debug
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
