# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Christian Rebischke <Chris.Rebischke[at]archlinux[dot]org>
# Contributor: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=packer
pkgver=1.15.4
pkgrel=1
pkgdesc='tool for creating machine images for platforms from a single source configuration'
arch=('x86_64')
url="https://github.com/hashicorp/packer"
license=('LicenseRef-BSL-1.1')
makedepends=('git' 'go' 'go-tools')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/hashicorp/packer/archive/v${pkgver}.tar.gz")
sha512sums=('198b164d8f2b347c6a0fe1e512fd12ebb4a3ad0ce293824fda3c3c3ed89646f18efe38c1f168c9f7f2dd5c51dd1c0ad62de8fdf4a9db107c4a159efe6106f0fb')

prepare() {
	export GOPATH="${srcdir}"
	export PATH="$PATH:$GOPATH/bin"
	mkdir -p "src/github.com/hashicorp"
	mv "${pkgname}-${pkgver}" "src/github.com/hashicorp/${pkgname}"
	sed -e '/VersionPrerelease = ""/ {N;N;N;d;}' -i "${srcdir}/src/github.com/hashicorp/${pkgname}/Makefile"
}

build() {
  cd "src/github.com/hashicorp/${pkgname}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw -ldflags=-linkmode=external"
	go build -o packer-binary
}

# Currently broken
# check() {
#   cd "src/github.com/hashicorp/${pkgname}"
#   go test ./...
# }

package() {
	cd "src/github.com/hashicorp/${pkgname}"
	install -Dm755 packer-binary "${pkgdir}/usr/bin/packer"
	install -Dm644 contrib/zsh-completion/_packer "${pkgdir}"/usr/share/zsh/site-functions/_packer
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set et sw=2 ts=2 tw=79:
