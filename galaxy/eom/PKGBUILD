# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=eom
pkgver=1.28.1
pkgrel=1
pkgdesc="An image viewer for MATE"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL-2.0-or-later')
depends=('dbus-glib' 'gobject-introspection-runtime' 'gettext' 'exempi' 'lcms2' 'libexif' 'libjpeg-turbo' 'mate-desktop' 'libpeas')
makedepends=('autoconf-archive' 'glib2-devel' 'gobject-introspection' 'mate-common' 'itstool' 'yelp-tools')
optdepends=('webp-pixbuf-loader: webp image format support')
groups=('mate-extra')
conflicts=('eom-gtk3')
replaces=('eom-gtk3')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/mate-desktop/eom/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('5adc899e98f48d95aef95b02981c30fd0ecadac194bf0764b0aa157595922046')

prepare() {
    	cd "${pkgname}-${pkgver}"
	./autogen.sh
}

build() {
    	cd "${pkgname}-${pkgver}"
    	./configure \
        	--prefix=/usr \
        	--localstatedir=/var \
        	--with-librsvg
    	make
}

package() {
    	cd "${pkgname}-${pkgver}"
    	make DESTDIR="${pkgdir}" install
}
