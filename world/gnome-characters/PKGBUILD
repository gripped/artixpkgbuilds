# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-characters
pkgver=48.0
pkgrel=1
pkgdesc="A character map application"
url="https://apps.gnome.org/Characters/"
arch=(x86_64)
license=("BSD-3-Clause AND GPL-2.0-or-later")
depends=(
  dconf
  emoji-font
  gjs
  glib2
  glibc
  gnome-desktop-4
  gtk4
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  appstream
  git
  glib2-devel
  gobject-introspection
  gperf
  meson
)
checkdepends=(weston)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/gnome-characters.git#tag=${pkgver/[a-z]/.&}")
b2sums=('c44bd88c42ee00cdd0191eca6ed3572016dd91b2b6ad78c620761be2c596b9c6ff36b83fdfc205c28536e9fef187ac976baf6d8ac55c5303818adec836b0e753')

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() (
  export XDG_RUNTIME_DIR="$PWD/runtime-dir"
  mkdir -p -m 700 "$XDG_RUNTIME_DIR"

  export WAYLAND_DISPLAY=wayland-5
  weston --backend=headless-backend.so --socket=$WAYLAND_DISPLAY --idle-time=0 &
  _w=$!

  trap "kill $_w; wait" EXIT

  meson test -C build --print-errorlogs
)

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname/COPYING
}

# vim:set sw=2 sts=-1 et:
