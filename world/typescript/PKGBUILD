# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruno Galeotti <bgaleotti at gmail dot com>

_name=TypeScript
pkgname=typescript
pkgver=5.9.2
pkgrel=1
pkgdesc='JavaScript with syntax for types'
arch=(any)
url=http://www.typescriptlang.org
license=(Apache-2.0)
depends=(nodejs)
makedepends=(
  git
  npm
  rsync
)
source=("git+https://github.com/microsoft/$_name.git#tag=v$pkgver")
b2sums=('0f700079c0428dbcfb4c74640d3081cbfdad28d72bfa465e332cde26f269c84abcf0b7dd332b108123c3f8c5decbc8bd18fb9460f62d91d489830428bc804ca6')

prepare() {
  cd $_name
  npm ci
}

build() {
  cd $_name
  npx hereby LKG
}

check() {
  cd $_name
  npm run test
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname

  install -d "$pkgdir"/{usr/bin,$mod_dir}
  ln -s $mod_dir/bin/{tsc,tsserver} "$pkgdir"/usr/bin

  cd $_name
  rsync -r --exclude=.gitattributes bin lib package.json "$pkgdir"/$mod_dir
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md SECURITY.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname \
    ThirdPartyNoticeText.txt
}
