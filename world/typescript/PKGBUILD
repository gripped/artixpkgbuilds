# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruno Galeotti <bgaleotti at gmail dot com>

_name=TypeScript
pkgname=typescript
pkgver=5.9.3
pkgrel=1
pkgdesc='JavaScript with syntax for types'
arch=(any)
url=http://www.typescriptlang.org
license=(Apache-2.0)
depends=(nodejs)
makedepends=(
  git
  npm
  rsync
)
source=("git+https://github.com/microsoft/$_name.git#tag=v$pkgver")
b2sums=('6a70af179b0cabe63fd220f9f6166602778f57e7d0033d6dd45c9c191958e0d9198f3f959affdeb0aef00d85e245ec8264779de9e4a9d8fb738d45d518993fbe')

prepare() {
  cd $_name
  npm ci
}

build() {
  cd $_name
  npx hereby LKG
}

check() {
  cd $_name
  npm run test
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname

  install -d "$pkgdir"/{usr/bin,$mod_dir}
  ln -s $mod_dir/bin/{tsc,tsserver} "$pkgdir"/usr/bin

  cd $_name
  rsync -r --exclude=.gitattributes bin lib package.json "$pkgdir"/$mod_dir
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md SECURITY.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname \
    ThirdPartyNoticeText.txt
}
