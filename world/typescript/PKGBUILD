# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Bruno Galeotti <bgaleotti at gmail dot com>

_name=TypeScript
pkgname=typescript
pkgver=5.4.4
pkgrel=1
pkgdesc='JavaScript with syntax for types'
arch=('any')
url=http://www.typescriptlang.org
license=('Apache-2.0')
depends=('nodejs')
makedepends=('git' 'npm' 'rsync')
source=("git+https://github.com/microsoft/$_name.git#tag=v$pkgver")
b2sums=('057214661240b8edc4805a0852e806391ce07b06daa6568c7de5fa327cc3f87b4a0a555c776bc0a76c2fb2ae16667867fd680346c6de7b35d70034ed2c64ce06')

prepare() {
  cd $_name
  npm ci
}

build() {
  cd $_name
  npx hereby LKG
}

check() {
  cd $_name
  npm run test
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname
  install -d "$pkgdir"/{usr/bin,$mod_dir}
  ln -s $mod_dir/bin/{tsc,tsserver} "$pkgdir"/usr/bin

  cd $_name
  rsync -r --exclude=.gitattributes bin lib package.json "$pkgdir"/$mod_dir
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md SECURITY.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname \
    ThirdPartyNoticeText.txt
}
