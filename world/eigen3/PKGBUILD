# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Ronald van Haren <ronald@archlinux.org>
# Contributor: Chirantan Ekbote <chirantan.ekbote at gmail.com>
# Contributor: Kringel

pkgname=eigen3
pkgver=3.4.1
pkgrel=1
pkgdesc='Lightweight C++ template library for vector and matrix math, a.k.a. linear algebra (legacy version)'
arch=(any)
url='https://eigen.tuxfamily.org'
license=(MPL-2.0 Apache-2.0
         BSD-3-Clause Minpack
         'LGPL-2.1-only OR LGPL-2.1-or-later')
makedepends=(cmake
             git)
conflicts=(eigen)
provides=(eigen=$pkgver)
source=(git+https://gitlab.com/libeigen/eigen#tag=$pkgver)
sha256sums=('155ef5ef8b9bdf599007f85cc2332a3a218b7b50037c6efee7b5ac1971e4a52d')

build() {
  cmake -B build -S eigen \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DEIGEN_BUILD_BLAS=OFF \
    -DEIGEN_BUILD_LAPACK=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # install custom licenses
  install -Dm644 eigen/COPYING.* -t "$pkgdir/usr/share/licenses/$pkgname"
}
