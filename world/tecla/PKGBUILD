# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=tecla
pkgver=48.0.2
pkgrel=1
pkgdesc="Keyboard layout viewer"
url="https://gitlab.gnome.org/GNOME/tecla"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  libxkbcommon
  pango
  wayland
)
makedepends=(
  git
  meson
)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/tecla.git#tag=${pkgver/[a-z]/.&}")
b2sums=('a782a59ede500070ea1d3048375a4a35a918ee8cafbe339ea928116d353abca367fa26d43cc1156d9940fbaa5bc13b6258a8d15c263c5196f08cc237de823c2f')

prepare() {
  cd tecla
}

build() {
  artix-meson tecla build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
