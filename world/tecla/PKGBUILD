# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=tecla
pkgver=47.0
pkgrel=1
pkgdesc="Keyboard layout viewer"
url="https://gitlab.gnome.org/GNOME/tecla"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  libxkbcommon
  pango
  wayland
)
makedepends=(
  git
  meson
)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/tecla.git#tag=${pkgver/[a-z]/.&}")
b2sums=('aa25059fab60df80fbef146373f9240e83cd36b384a327a04c325de25a7d276e4d4e337046265b0e8da2dc28e89b158e4df855badc1c285c1d9522a8167ecbd0')

prepare() {
  cd tecla
}

build() {
  artix-meson tecla build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
