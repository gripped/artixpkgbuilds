# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkexiv2
pkgver=23.04.1
pkgrel=2
pkgdesc='A library to manipulate pictures metadata'
url='https://www.kde.org/'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(qt5-base exiv2)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/graphics/libkexiv2/-/commit/68c21bba.patch)
sha256sums=('3a15410e0eaea3b3b0ca0fa274448d97e8172b5167a82f106c29dff6ba19e275'
            'SKIP'
            'bb23a1d9162e2332fd9810829274d6d5f36009ee2a568fc003fba9f06056d7ed')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < 68c21bba.patch # Fix build with exiv2 0.28
}

build() {
  artix-cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
