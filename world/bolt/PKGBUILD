# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>

pkgname=bolt
pkgver=0.9.9
pkgrel=1
pkgdesc="Thunderbolt 3 device manager"
arch=('x86_64')
url="https://gitlab.freedesktop.org/bolt/bolt"
license=('LGPL-2.1-or-later')
depends=('polkit' 'udev')
makedepends=('asciidoc' 'meson' 'python-setuptools' 'patch' 'glib2-devel')
checkdepends=('umockdev')
source=(https://gitlab.freedesktop.org/$pkgname/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('d2b05e3ee1ffa9b4fc1c4a11138c69bf1f60acba86f07b1b4b40b4d456048936')

build() {
  cd $pkgname-$pkgver

  artix-meson ../build \
    -Dman=true
  meson compile -v -C ../build
}

check() {
  cd $pkgname-$pkgver

  meson test -C ../build --print-errorlogs
}

package() {
  cd $pkgname-$pkgver

  meson install -C ../build --destdir "${pkgdir}"
}
