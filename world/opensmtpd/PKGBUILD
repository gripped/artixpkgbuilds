# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: SÃ©bastien Luttringer
# Contributor: parchd <parchd@archlinux.info>

pkgname=opensmtpd
pkgver=7.6.0p1
pkgrel=1
pkgdesc="Free implementation of the server-side SMTP protocol"
arch=(x86_64)
url="https://www.opensmtpd.org/"
license=(custom)
depends=(libevent libxcrypt openssl pam zlib)
optdepends=('opensmtpd-filter-rspamd: rspamd integration')
makedepends=(signify)
provides=(smtp-server smtp-forwarder)
conflicts=(smtp-server smtp-forwarder)
backup=(etc/smtpd/smtpd.conf etc/smtpd/aliases)
options=(emptydirs)
#"https://www.opensmtpd.org/archives/${pkgname}-${pkgver}.tar.gz"
source=("https://github.com/OpenSMTPD/OpenSMTPD/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        ${pkgname}-${pkgver}.signify::"${url}/archives/${pkgname}-${pkgver}.sum.sig"
        opensmtpd-20181026.pub
        opensmtpd.sysusers)
sha256sums=('b27c806982a6653a2637f810ae7a45372b9a7ff99350ee1003746503ff0e4a97'
            '61f6362c6457ac11cf7c1d737303759e96f6962de4465a9ad35cbaaae18f52f2'
            'b74dca53567cd5070905a0a1acd77041805b6c0c4a0e1285830ea13654e1dcd5'
            '5a6e0e2f1ceb4f6fe69aaa7871291af3b4ee1c55a96a667e72a309f961c8bd2d')

prepare() {
  signify -Cp ${pkgname}-20181026.pub \
           -x ${pkgname}-${pkgver}.signify \
              ${pkgname}-${pkgver}.tar.gz

  cd ${pkgname}-${pkgver}
  sed -ri 's,/etc/mail,/etc/smtpd,g' usr.sbin/smtpd/smtpd.conf
  autoreconf -vfi
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/smtpd \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/smtpd \
    --without-libbsd \
    --with-path-mbox=/var/spool/mail \
    --with-path-empty=/var/empty \
    --with-path-socket=/run \
    --with-path-CAfile=/etc/ssl/certs/ca-certificates.crt \
    --with-user-smtpd=smtpd \
    --with-user-queue=smtpq \
    --with-group-queue=smtpq \
    --with-auth-pam

  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install SMTPD_QUEUE_USER=92

  ln -s /usr/bin/smtpctl "${pkgdir}"/usr/bin/sendmail
  ln -s /usr/bin/smtpctl "${pkgdir}"/usr/bin/mailq
  ln -s /usr/bin/smtpctl "${pkgdir}"/usr/bin/newaliases
  ln -s /usr/bin/smtpctl "${pkgdir}"/usr/bin/makemap

  install -Dm644 etc/aliases -t "${pkgdir}"/etc/smtpd/

  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/

  install -Dm644 ../opensmtpd.sysusers "${pkgdir}"/usr/lib/sysusers.d/opensmtpd.conf
}
