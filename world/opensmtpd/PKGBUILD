# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: SÃ©bastien Luttringer
# Contributor: parchd <parchd@archlinux.info>

pkgname=opensmtpd
pkgver=7.8.0p0
pkgrel=1
pkgdesc="Free implementation of the server-side SMTP protocol"
arch=(x86_64)
url="https://www.opensmtpd.org"
license=(custom)
depends=(libevent libxcrypt openssl pam zlib)
optdepends=('opensmtpd-filter-rspamd: rspamd integration')
makedepends=(git)
provides=(smtp-server smtp-forwarder)
conflicts=(smtp-server smtp-forwarder)
backup=(etc/smtpd/smtpd.conf etc/smtpd/aliases)
options=(emptydirs)
source=("git+https://github.com/OpenSMTPD/OpenSMTPD.git#tag=${pkgver}"
        opensmtpd.sysusers)
sha256sums=('10131c5cc2dfce51d520e6847cb43d08e0043d87fafeb74b3531ca617ab4aced'
            '955e25043dbbc266a496db10c5d5f25f70d5342bba0550915932c04068a4ca01')
install="${pkgname}.install"

prepare() {
  cd OpenSMTPD
  sed -ri 's,/etc/mail,/etc/smtpd,g' usr.sbin/smtpd/smtpd.conf
  autoreconf -vfi
}

build() {
  cd OpenSMTPD

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/smtpd \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/smtpd \
    --without-libbsd \
    --with-path-mbox=/var/spool/mail \
    --with-path-empty=/var/empty \
    --with-path-socket=/run \
    --with-path-CAfile=/etc/ssl/certs/ca-certificates.crt \
    --with-user-smtpd=smtpd \
    --with-user-queue=smtpq \
    --with-group-queue=smtpq \
    --with-auth-pam

  make
}

package() {
  cd OpenSMTPD

  make DESTDIR="${pkgdir}" install SMTPD_QUEUE_USER=92

  ln -s /usr/bin/smtpctl "${pkgdir}"/usr/bin/sendmail
  ln -s /usr/bin/smtpctl "${pkgdir}"/usr/bin/mailq
  ln -s /usr/bin/smtpctl "${pkgdir}"/usr/bin/newaliases
  ln -s /usr/bin/smtpctl "${pkgdir}"/usr/bin/makemap

  install -Dm644 etc/aliases -t "${pkgdir}"/etc/smtpd/

  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/

  install -Dm644 ../opensmtpd.sysusers "${pkgdir}"/usr/lib/sysusers.d/opensmtpd.conf
}
