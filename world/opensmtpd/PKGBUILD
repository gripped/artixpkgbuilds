# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: SÃ©bastien Luttringer
# Contributor: parchd <parchd@archlinux.info>

pkgname=opensmtpd
pkgver=7.3.0p2
pkgrel=1
pkgdesc="Free implementation of the server-side SMTP protocol"
arch=(x86_64)
url="https://www.opensmtpd.org/"
license=(custom)
depends=(libevent openssl pam libxcrypt zlib)
optdepends=('opensmtpd-filter-rspamd: rspamd integration')
makedepends=(signify)
provides=(smtp-server smtp-forwarder)
conflicts=(smtp-server smtp-forwarder)
backup=(etc/smtpd/smtpd.conf etc/smtpd/aliases)
options=(emptydirs)
source=("https://www.opensmtpd.org/archives/${pkgname}-${pkgver}.tar.gz"
        ${pkgname}-${pkgver}.signify::"${url}/archives/${pkgname}-${pkgver}.sum.sig"
        opensmtpd-20181026.pub
        opensmtpd.sysusers)
sha256sums=('fccdfbc5b98d150012bf9ccdef51752c18a862ed10888b56289826b83a2b5a4e'
            '0f5f4373584e8047f26ce6216e320d139c85de584b42941c5e11c23af6e0de98'
            'b74dca53567cd5070905a0a1acd77041805b6c0c4a0e1285830ea13654e1dcd5'
            'b38b64f1457b7227d55585edc711cce525e089516d16b093e6c44387380e6b13')
install=${pkgname}.install

prepare() {
  signify -Cp ${pkgname}-20181026.pub \
           -x ${pkgname}-${pkgver}.signify \
              ${pkgname}-${pkgver}.tar.gz

  sed -ri 's,/etc/mail,/etc/smtpd,g' ${pkgname}-${pkgver}/usr.sbin/smtpd/smtpd.conf
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/smtpd \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/smtpd \
    --with-bundled-libtls \
    --with-path-mbox=/var/spool/mail \
    --with-path-empty=/var/empty \
    --with-path-socket=/run \
    --with-path-CAfile=/etc/ssl/certs/ca-certificates.crt \
    --with-user-smtpd=smtpd \
    --with-user-queue=smtpq \
    --with-group-queue=smtpq \
    --with-auth-pam

  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install SMTPD_QUEUE_USER=92

  ln -s /usr/bin/smtpctl "${pkgdir}"/usr/bin/sendmail
  ln -s /usr/bin/smtpctl "${pkgdir}"/usr/bin/mailq
  ln -s /usr/bin/smtpctl "${pkgdir}"/usr/bin/newaliases
  ln -s /usr/bin/smtpctl "${pkgdir}"/usr/bin/makemap

  install -Dm644 etc/aliases -t  "${pkgdir}"/etc/smtpd/

  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/

  install -Dm644 ../opensmtpd.sysusers "${pkgdir}"/usr/lib/sysusers.d/opensmtpd.conf
}
