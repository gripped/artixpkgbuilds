# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=async-dns
pkgname=ruby-${_gemname}
pkgver=1.4.1
pkgrel=1
pkgdesc='Asynchronous DNS resolver and server'
url='https://github.com/socketry/async-dns'
arch=('any')
license=('MIT')
depends=(
  ruby
  ruby-async
  ruby-io-endpoint
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-decode
  ruby-sus
  ruby-sus-fixtures-async
)
options=('!emptydirs')
source=(https://github.com/socketry/async-dns/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('4faee2aea6c3218ae97f65e6d625e4fad9707f3c3eac225ffc47439a9b795089855e786d6af1631c69ff056bdd1de60f7c281c6de95b9e3898da1c01cef00b73')
b2sums=('cbe42f6b1b8d4ec17ac4437cfb40dcad8a60f4a43a6d85a6aa721109887a0c104549002773c313bed3b8ff5dd4cb7b91e56d0ae0001f38d11ff0c18d931ef1a6')

prepare() {
  cd ${_gemname}-${pkgver}

  sed 's|git ls-files|find|' -i ${_gemname}.gemspec

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended \
    --expression 's|~>|>=|g' \
    --expression '/signing_key/d' \
    ${_gemname}.gemspec

  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unreproducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" bake test
}

package() {
  cd "${_gemname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 license* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
