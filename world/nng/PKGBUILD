# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Vincent Bernardoff <vb AT luminar.eu.org>

pkgname=nng
pkgver=1.12.0
pkgrel=1
pkgdesc='A lightweight, broker-less messaging library'
arch=('x86_64')
url='https://nng.nanomsg.org/'
license=('MIT')
depends=('glibc' 'wolfssl')
makedepends=(
  'git'
  'cmake'
  'ninja'
)
source=("$pkgname::git+https://github.com/nanomsg/nng#tag=v$pkgver")
sha512sums=('3383abadf0527b19d2f0a5b61bebbeecde275a6a62fdf3745e8e737da545157aff8529f6c2d111e8941334a3a7e270233e1d0cdcef1d71f7f843bc7dc315550c')
b2sums=('bbd2e1dd283152122fa3edcdb10e255594a826187a27ecb28aa6005aeb549b74c23db6fdc28a13ee66825b63e9e6c1dbf86c0bc9ea7c5de80a4745d1abc556a0')

build() {
  local cmake_options=(
    -B build
    -S "$pkgname"
    -G Ninja
    -W no-dev
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D BUILD_SHARED_LIBS=ON
    -D NNG_ENABLE_TLS=ON
    -D NNG_TLS_ENGINE=wolf
    -D NNG_ENABLE_DOC=OFF
    -D NNG_TESTS=OFF
  )
  cmake "${cmake_options[@]}"

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname/LICENSE.txt"
}
