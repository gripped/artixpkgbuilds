# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Vincent Bernardoff <vb AT luminar.eu.org>

pkgname=nng
pkgver=1.12.3
pkgrel=1
pkgdesc='A lightweight, broker-less messaging library'
arch=('x86_64')
url='https://nng.nanomsg.org/'
license=('MIT')
depends=('glibc' 'wolfssl')
makedepends=(
  'git'
  'cmake'
  'ninja'
)
source=("$pkgname::git+https://github.com/nanomsg/nng#tag=v$pkgver")
sha512sums=('7129caac4368722e050855f23961dcd17516c49bfb3387eae89cf21d2f65f6d46d6907158f5c81c5cdd74e7c87de0309c888d30bd1fa674e6e47c23f6b425fbf')
b2sums=('e54dba632e46a1a157d4a838829d240e1ebefbdcbfcc1e0b5530317d1946ca603f925d8502b6ea6e0aaf852342ea4dde5e8942e79ef4be7f92ffb0f9ec6314b7')

build() {
  local cmake_options=(
    -B build
    -S "$pkgname"
    -G Ninja
    -W no-dev
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D BUILD_SHARED_LIBS=ON
    -D NNG_ENABLE_TLS=ON
    -D NNG_TLS_ENGINE=wolf
    -D NNG_ENABLE_DOC=OFF
    -D NNG_TESTS=OFF
  )
  cmake "${cmake_options[@]}"

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname/LICENSE.txt"
}
