# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Ivy Foster <iff@archlinux.org>
# Contributor: Alex Szczuczko <alex at szc dot ca>
# Contributor: Devin Cofer <ranguvar{AT]archlinux[DOT}us>
# Contributor: Imanol Celaya <ilcra1989@gmail.com>

pkgname=bmake
pkgver=20260508
pkgrel=1
pkgdesc='Portable version of the NetBSD make build tool'
arch=(x86_64)
url='https://www.crufty.net/help/sjg/bmake.html'
license=(BSD-3-Clause)
makedepends=(bc)
optdepends=('python: for meta2deps.py script')
#source=("https://ftp.netbsd.org/pub/NetBSD/misc/sjg/bmake-$pkgver.tar.gz"{,.asc})
source=("https://www.crufty.net/ftp/pub/sjg/bmake-$pkgver.tar.gz"{,.asc})
sha256sums=('7b1eea90abc767430cd1a612529d5f301c47fd6a35bee7585778c951e891a82c'
            'SKIP')
validpgpkeys=(7E228507C26D8DC164F531BFBA54C8AF755A2A99)

prepare() {
	cd bmake
	sed -i 's/cmd-interrupt//g' unit-tests/Makefile
	sed -i 's/deptgt-interrupt//g' unit-tests/Makefile
}

build() {
	cd bmake
	./boot-strap --prefix=/usr op=build
}

package() {
	cd bmake
	./boot-strap --install-destdir="$pkgdir" --prefix=/usr op=install
	install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
