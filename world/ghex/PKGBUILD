# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgbase=ghex
pkgname=(
  ghex
  ghex-docs
)
pkgver=46.2
pkgrel=1
pkgdesc="A simple binary editor for the Gnome desktop"
url="https://wiki.gnome.org/Apps/Ghex"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  pango
)
makedepends=(
  gi-docgen
  git
  gobject-introspection
  meson
  vala
  yelp-tools
)
source=("git+https://gitlab.gnome.org/GNOME/ghex.git#tag=${pkgver/[a-z]/.&}")
b2sums=('21f4608873e948b4464b9bfcd2308b39e48d05ba362fd8d56a4d09a2ae87642c042397adb6e9e9d3223aa0412b4522cfa47637ac419dc1b181ee200ff47e76ae')

prepare() {
  cd ghex
}

build() {
  local meson_options=(
    -D gtk_doc=true
    -D vapi=true
  )

  artix-meson ghex build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_ghex() {
  provides=(libgtkhex-4.so)
  groups=(gnome-extra)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_ghex-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
