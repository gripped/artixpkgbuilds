# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=optimist
pkgname=ruby-${_gemname}
pkgver=3.2.0
pkgrel=1
pkgdesc='Commandline option parser for Ruby that just gets out of your way'
url='https://github.com/ManageIQ/optimist'
arch=('any')
license=('MIT')
depends=('ruby')
makedepends=('ruby-rdoc' 'ruby-rake' 'ruby-bundler')
checkdepends=('ruby-minitest' 'ruby-chronic')
options=('!emptydirs')
source=(https://github.com/ManageIQ/optimist/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('c08c4a7329e82152449758bfc4dfc5befc69ad89e13fa5dbb9c13ca0553dae0902135d6a4b598c8102add36980dcdc2b652861e36c2c0e73700d2ac93dc6b61c')
b2sums=('30cb6016c3e2e83d2fc9a82dadaaed99243172c40123dac324bd76d9f741cd63b70ef229ba1d4c455f864c7733e74aa34b16d9930f773715cb6c3ce26a4e716e')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec
  sed 's|git ls-files -z|find -print0|' -i ${_gemname}.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

check() {
  cd ${_gemname}-${pkgver}
  MT_COMPAT="true" rake test
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" \
    -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/licenses/${pkgname}"

  cd "${pkgdir}/${_gemdir}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
  rm -r "${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/test"
}

# vim: ts=2 sw=2 et:
