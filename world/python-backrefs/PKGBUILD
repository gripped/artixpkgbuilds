# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-backrefs
pkgver=5.9
pkgrel=1
pkgdesc="Wrapper around re or regex that adds additional back references"
arch=('any')
url="https://github.com/facelessuser/backrefs"
license=('MIT')
depends=('python')
makedepends=(
  'python-build'
  'python-hatchling'
  'python-installer'
  'python-wheel'
)
checkdepends=(
  'python-pytest'
  'python-regex'
)
optdepends=('python-regex: use regex instead of re')
source=("$url/archive/$pkgver/${pkgname#python-}-$pkgver.tar.gz")
sha256sums=('cee408e5b10331b062a81b75f40475e43c71cb8e4bdb1db4ec06c45298772b75')

build() {
  cd ${pkgname#python-}-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}-$pkgver
  pytest
}

package() {
  cd ${pkgname#python-}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
