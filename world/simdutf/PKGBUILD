# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Hu <integral@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=simdutf
pkgver=9.2.0
pkgrel=1
pkgdesc="Unicode routines (UTF8, UTF16, UTF32) and Base64"
arch=('x86_64')
url="https://${pkgname}.github.io/${pkgname}/"
license=('Apache-2.0 OR MIT')
depends=('glibc' 'libgcc' 'libstdc++')
makedepends=('cmake' 'git')
provides=('libsimdutf.so')
source=("git+https://github.com/${pkgname}/${pkgname}.git#tag=v${pkgver}")
sha256sums=('af3bbee73b5de8eb2e4a58be87b7347f09d0ebe5282100500064e6d861ae084c')

build() {
	cmake -B build \
		-S "${pkgname}" \
		-D CMAKE_BUILD_TYPE=None \
		-D CMAKE_INSTALL_PREFIX=/usr \
		-D BUILD_SHARED_LIBS=ON

	cmake --build build
}

check() {
	ctest --test-dir build --output-on-failure
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm644 "${pkgname}/LICENSE-MIT" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
