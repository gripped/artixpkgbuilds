# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor:

pkgname=simdutf
pkgver=9.1.2
pkgrel=1
pkgdesc='Unicode routines (UTF8, UTF16, UTF32) and Base64'
arch=(x86_64)
url='https://simdutf.github.io/simdutf/'
license=(Apache-2.0
         MIT)
depends=(glibc
         libgcc
         libstdc++)
makedepends=(cmake
             git)
source=(git+https://github.com/simdutf/simdutf#tag=v$pkgver)
sha256sums=('f450bf3c8c1f8cedd10c62a90bcadbcdf4e7aa8c7c348b2036770d6a90a5082d')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname/LICENSE* -t "$pkgdir"/usr/share/licenses/$pkgname
}
