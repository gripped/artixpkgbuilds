# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor:

pkgname=simdutf
pkgver=9.1.1
pkgrel=1
pkgdesc='Unicode routines (UTF8, UTF16, UTF32) and Base64'
arch=(x86_64)
url='https://simdutf.github.io/simdutf/'
license=(Apache-2.0
         MIT)
depends=(glibc
         libgcc
         libstdc++)
makedepends=(cmake
             git)
source=(git+https://github.com/simdutf/simdutf#tag=v$pkgver)
sha256sums=('e8f952ad78e3206fd59e3e34d703b22afd93c2eda923f9f77f08f67e568d5411')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname/LICENSE* -t "$pkgdir"/usr/share/licenses/$pkgname
}
