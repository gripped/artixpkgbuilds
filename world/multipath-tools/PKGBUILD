# Maintainer: Robin Broda <robin@broda.me>
# Contributor: Michael Lass <bevan@bi-co.net>
# Contributor: Justin Dray <justin@dray.be>
# Contributor: Patrick McCarty <pnorcks at gmail dot com>
# Contributor: Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Thomas S Hatch <thatch45 ar gmail dot com>
# Contributor: Michael P <ptchinster@archlinux.us>
# Contributor: Matt Heagney <matt@heagney.com>

pkgname=multipath-tools
pkgver=0.15.0
pkgrel=1
pkgdesc='Multipath tools for Linux (including kpartx)'
arch=('x86_64')
url='https://github.com/opensvc/multipath-tools'
license=('GPL-2.0-or-later' 'GPL-3.0-or-later' 'LGPL-2.0-or-later' 'LGPL-2.1-or-later')
depends=('glibc'
         'device-mapper' 'libdevmapper.so'
         'json-c' 'libjson-c.so'
         'libaio' 'libaio.so'
         'libgcc' 'libgcc_s.so'
         'liburcu' #'liburcu.so'
         'libudev' 'libudev.so' 'libudev.so'
         'util-linux-libs' 'libmount.so')
makedepends=('git' 'udev')
source=("multipath-tools::git+https://github.com/opensvc/multipath-tools#tag=${pkgver}")
sha256sums=('8089da3cf93d8c1d3bc5b9337db0f46e2545e31583b45df1d5640b29abf5fe65')

_make_opts=(
  prefix='/usr/'
  bindir='/usr/bin'
  etc_prefix=""
  configfile='/etc/multipath.conf'
  statedir='/etc/multipath'
  LIB='lib'
)


build() {
  cd "${pkgname}"

  make \
    "${_make_opts[@]}" \
    FORTIFY_OPT=''
}

package() {
  cd "${pkgname}"

  make -j1 \
    "${_make_opts[@]}" \
    DESTDIR="${pkgdir}" \
    install

  install -D -m0644 -t "${pkgdir}/usr/share/doc/${pkgname}" README*
}
