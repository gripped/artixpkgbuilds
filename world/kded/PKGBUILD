# Maintainer: nikolar <nikolar@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kded
pkgver=5.110.0
pkgrel=1
pkgdesc='Extensible deamon for providing system level services'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kservice kcrash)
makedepends=(extra-cmake-modules kdoctools)
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('d4bfecd640bed1abdcbd5008e505ba8cea1f807ad362a8ceaaa4670cf9fc10d9'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  #remove systemd service   <<==== Not needed for kded-5.102.0-1
  rm -r $pkgdir/usr/lib/systemd
}
