# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt6-wayland
_pkgver=6.8.0
pkgver=6.8.0
pkgrel=2
arch=(x86_64)
url='https://www.qt.io'
license=(GPL-3.0-only
         LGPL-3.0-only
         LicenseRef-Qt-Commercial
         Qt-GPL-exception-1.0)
pkgdesc='Provides APIs for Wayland'
depends=(gcc-libs
         glibc
         libglvnd
         libxkbcommon
         qt6-base
         qt6-declarative
         qt6-svg
         wayland)
makedepends=(cmake
             git
             ninja)
groups=(qt6)
_pkgfn=${pkgname/6-/}
source=(git+https://code.qt.io/qt/$_pkgfn#tag=v$_pkgver)
sha256sums=('09927060c5595c33176d7a9828b427a0934484c0741d910437a49c6cad5165d4')

prepare() {
  cd $_pkgfn
  # Cherry picks for DDE
  git cherry-pick -n 67f121cc4c3865aa3a93cf563caa1d9da3c92695
  git cherry-pick -n 070414dd4155e13583e5e8b16bed1a5b68d32910
}

build() {
  cmake -B build -S $_pkgfn -G Ninja \
    -DCMAKE_MESSAGE_LOG_LEVEL=STATUS
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}
