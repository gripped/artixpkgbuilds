# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=ruby-unicode-version
_pkgname="${pkgname#ruby-}"
pkgver=1.4.0
pkgrel=3
pkgdesc='Returns Unicode/Emoji versions of current and previous Rubies'
arch=('any')
url='https://github.com/janlelis/unicode-version'
license=('MIT')
depends=('ruby')
makedepends=('git')
checkdepends=('ruby-rake' 'ruby-minitest')
options=('!emptydirs')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('78c0a3dcb6e0195d41403faa8eced4487d458873b0d38cfafc44665f106cb6c73a109285f20cd3bda9ea14b2e464f0565bffe06f47c7aabcfae98ea018136937')
b2sums=('e1dcf8360cc60ffe1182a7d1ed2ce748c971907bcf96d9e75142a256a131bb64fd800ebd792f572120ec3e170b559e0409575a7d0de0e39d9f9f72a7f175a0ea')

build() {
  cd "$pkgname"

  gem build "$_pkgname.gemspec"
}

check() {
  cd "$pkgname"

  rake spec
}

package() {
  cd "$pkgname"

  local _gemdir="$(gem env gemdir)"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$_pkgname-$pkgver.gem"

  # delete cache
  rm -rf "$pkgdir/$_gemdir/cache"

  # license
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  ln -sf "$_gemdir/gems/$_pkgname-$pkgver/MIT-LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname"
}
