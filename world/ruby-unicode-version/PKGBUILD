# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=ruby-unicode-version
_pkgname="${pkgname#ruby-}"
pkgver=1.5.0
pkgrel=1
pkgdesc='Returns Unicode/Emoji versions of current and previous Rubies'
arch=('any')
url='https://github.com/janlelis/unicode-version'
license=('MIT')
depends=('ruby')
makedepends=('git')
checkdepends=('ruby-rake' 'ruby-minitest')
options=('!emptydirs')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('35c943ca3f15ec79c31434a22fb1a4c982e16eeb7376599c408682ae71ee335fd96c53e0ce8a2554947ff9ae29eced7b623d14cdad447a05c819cb4e061a4700')
b2sums=('dbde315662beeaa92950660cfde90881df717af3d776af9d6b10dccabdc50ae9142fd573614e1d0b84d582d0be1a7cccd4d9b9d11052382fe213577b49193a3b')

build() {
  cd "$pkgname"

  gem build "$_pkgname.gemspec"
}

check() {
  cd "$pkgname"

  rake spec
}

package() {
  cd "$pkgname"

  local _gemdir="$(gem env gemdir)"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$_pkgname-$pkgver.gem"

  # delete cache
  rm -rf "$pkgdir/$_gemdir/cache"

  # license
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  ln -sf "$_gemdir/gems/$_pkgname-$pkgver/MIT-LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname"
}
