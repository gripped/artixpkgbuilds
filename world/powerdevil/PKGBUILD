# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=powerdevil
pkgver=6.7.5
_dirver=$(echo $pkgver | cut -d. -f1-3)
pkgrel=2
pkgdesc='Manages the power consumption settings of a Plasma Shell'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL-2.0-or-later)
depends=(ddcutil
         glibc
         plasma-activities
         kauth
         kcmutils
         kconfig
         kcoreaddons
         kcrash
         kdbusaddons
         kglobalaccel
         ki18n
         kidletime
         kirigami
         kitemmodels
         knotifications
         kpackage
         kservice
         kxmlgui
         libkscreen
         libplasma
         libstdc++
         libxcb
         plasma-workspace
         qcoro
         qt6-base
         qt6-declarative
         solid
         libelogind
         wayland)
makedepends=(extra-cmake-modules
             kdoctools
             plasma-wayland-protocols)
optdepends=('kinfocenter: for the Energy Information KCM'
            'power-profiles-daemon: power profiles support'
            'tlp: alternative power profiles support')
groups=(plasma)
source=(https://download.kde.org/stable/plasma/$_dirver/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/plasma/powerdevil/-/commit/54f8a4a8.patch)
sha256sums=('6a738fccf01e4d1b10fea68ff8b404a406f95bc36fcb593bcc70fafb1c53258b'
            'SKIP'
            'f92b339712096d623c862e31b58d87ce72f381462efeab30893f1937a6634f88')
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D'  # Jonathan Esk-Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  patch -d $pkgname-$pkgver -p1 < 54f8a4a8.patch # Adapt to solid 6.30 changes
}

build() {
  cmake -B build  -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  rm -r $pkgdir/usr/lib/systemd
}
