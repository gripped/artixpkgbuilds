# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-calendar
pkgver=47.0
pkgrel=1
pkgdesc="Simple and beautiful calendar application designed to perfectly fit the GNOME desktop"
url="https://apps.gnome.org/Calendar"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  dconf
  evolution-data-server
  gcc-libs
  geoclue
  geocode-glib-2
  glib2
  glibc
  graphene
  gsettings-desktop-schemas
  gtk4
  hicolor-icon-theme
  libadwaita
  libedataserverui4
  libgweather-4
  libical
  libsoup3
  pango
)
makedepends=(
  git
  glib2-devel
  meson
)
optdepends=(
  'gnome-control-center: Manage online accounts'
  'xdg-desktop-portal-impl: Various user settings (e.g. 24-hour clock)'
)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/gnome-calendar.git#tag=${pkgver/[a-z]/.&}")
b2sums=('a57675325b33c6babfb4d551b396f7403a77910f7e70ab338f68085925aaddb7c03011381d94b162e08df383509034764d9522cac5818493913698053f65922b')

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
