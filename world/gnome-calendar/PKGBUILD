# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-calendar
pkgver=49.0.1
pkgrel=2
pkgdesc="Simple and beautiful calendar application designed to perfectly fit the GNOME desktop"
url="https://apps.gnome.org/Calendar"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  dconf
  evolution-data-server
  gcc-libs
  geoclue
  glib2
  glibc
  graphene
  gsettings-desktop-schemas
  gtk4
  hicolor-icon-theme
  libadwaita
  libedataserverui4
  libgweather-4
  libical
  libsoup3
  pango
)
makedepends=(
  blueprint-compiler
  git
  glib2-devel
  meson
)
optdepends=(
  'gnome-control-center: Manage online accounts'
  'xdg-desktop-portal-impl: Various user settings (e.g. 24-hour clock)'
)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/gnome-calendar.git#tag=${pkgver/[a-z]/.&}")
b2sums=('7e6adde4b5452b8257e68ce62a0b796a2aba7b9d4a6348858c504a602c96a2f56187e136d367c1b6110e38db745c2acee8496a31365ec68b0ce1fcd73d2ef385')

prepare() {
  cd $pkgname

  # Backport fixes from gnome-49 branch
  git cherry-pick -n ..26892365a20eb02f55a8cdba293c52a282765959
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
