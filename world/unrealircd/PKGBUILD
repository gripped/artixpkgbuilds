# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Zerial <fernando@zerial.org>

pkgname=unrealircd
pkgver=6.1.8.1
pkgrel=1
pkgdesc="Open Source IRC Server"
arch=('x86_64')
url="https://www.unrealircd.org"
license=('GPL2')
depends=('openssl' 'curl' 'c-ares' 'tre' 'pcre2' 'libnsl' 'argon2' 'libsodium')
conflicts=('ircd')
provides=('ircd')
backup=('etc/unrealircd/unrealircd.conf')
install=unrealircd.install
validpgpkeys=('1D2D2B03A0B68ED11D68A24BA7A21B0A108FF4A9')
source=(https://www.unrealircd.org/unrealircd4/unrealircd-$pkgver.tar.gz{,.asc}
	unrealircd.tmpfiles.d
	unrealircd.sysusers.d)
sha256sums=('f8f7a0b738614a527e2420ca3970bc735c6c5346dfa8dd9e2d5bd2eeaf992e93'
            'SKIP'
            '91b5e1d623b51ffd4734d73e35cead09be596460c41b9440406f92c9e2b4b9b1'
            '9e595176e63b301476982b1456d6ed065c479ff913b6743417ab8a9efdda0e3a')

build() {
  cd unrealircd-$pkgver
  ./configure \
    --with-pidfile=/run/unrealircd/ircd.pid \
    --enable-ssl=/usr \
    --with-bindir=/usr/bin \
    --with-datadir=/var/lib/unrealircd \
    --with-controlfile=/run/unrealircd/unrealircd.ctl \
    --with-confdir=/etc/unrealircd \
    --with-modulesdir=/usr/lib/unrealircd \
    --with-logdir=/var/log/unrealircd \
    --with-cachedir=/var/cache/unrealircd \
    --with-docdir=/usr/share/doc/unrealircd \
    --with-tmpdir=/tmp \
    --with-scriptdir=/usr \
    --with-nick-history=2000 \
    --with-permissions=0644 \
    --enable-dynamic-linking
  make
}

package() {
  cd unrealircd-$pkgver

  make install DESTDIR="$pkgdir"
  mv "$pkgdir"/usr/unrealircd "$pkgdir"/etc/unrealircd/unrealircd
  cp "$pkgdir"/etc/unrealircd/examples/example.conf "$pkgdir"/etc/unrealircd/unrealircd.conf
  rm -rf "$pkgdir"/tmp

  install -Dm0644 "$srcdir"/unrealircd.tmpfiles.d "$pkgdir"/usr/lib/tmpfiles.d/unrealircd.conf
  install -Dm0644 "$srcdir"/unrealircd.sysusers.d "$pkgdir"/usr/lib/sysusers.d/unrealircd.conf
}
