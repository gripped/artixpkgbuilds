# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bert Peters <bertptrs@archlinux.org>

pkgname=ruby-cucumber
pkgver=11.0.0
pkgrel=1
pkgdesc="Behaviour Driven Development with elegance and joy"
arch=(any)
url='https://github.com/cucumber/cucumber-ruby'
license=(MIT)
depends=(
  ruby
  ruby-base64
  ruby-builder
  ruby-cucumber-ci-environment
  ruby-cucumber-core
  ruby-cucumber-cucumber-expressions
  ruby-cucumber-gherkin
  ruby-cucumber-html-formatter
  ruby-cucumber-messages
  ruby-diff-lcs
  ruby-mime-types
  ruby-mini_mime
  ruby-multi_test
  ruby-sys-uname
  ruby-rake
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bundler
  ruby-nokogiri
  ruby-rspec
  ruby-webrick
)
options=(!emptydirs)
source=(
  "https://github.com/cucumber/cucumber-ruby/archive/v$pkgver/cucumber-cucumber-ruby-$pkgver.tar.gz"
)
sha512sums=('1f0bd9108fda58e7f020fbcf0fadf89695155af993f08c06095509ecb6e1c0ac3419d6a27d80c52990bf247975ff57027fa09e118687b83603c54a87cc62bea3')
b2sums=('adf5ba73b7ed42eabc5da1576ff0b1bb1de4646242744e8ea0e34dd6a525f4ed39880b1dc74e9a0e64de48686db33301d6257e16481372bd05094039eb13204b')

prepare() {
  cd cucumber-ruby-$pkgver

  sed -i \
    -e '/capybara/d' \
    -e 's|~>|>=|' \
    -e "s/, '< [0-9]\+'//" \
    cucumber.gemspec
  sed -i '/[rR]ubo[cC]op/d' Rakefile

  rm Gemfile

  sed -i -e 's/bundle exec //' gem_tasks/cck.rake compatibility/cck_spec.rb
  sed -i \
    -e "s/Cucumber::Messages::TimeConversion/Cucumber::Messages::Helpers::TimeConversion/" \
    -e "s/Cucumber::Messages::IdGenerator::Incrementing/Cucumber::Messages::Helpers::IdGenerator::Incrementing/" \
    -e "s/Cucumber::Messages::IdGenerator::UUID/Cucumber::Messages::Helpers::IdGenerator::UUID/" \
    -e "s/require 'cucumber\/messages\/id_generator'/require 'cucumber\/messages\/helpers\/id_generator'/" \
    lib/cucumber/configuration.rb \
    lib/cucumber/formatter/message_builder.rb \
    spec/cucumber/formatter/message_spec.rb \
    spec/cucumber/runtime/hooks_examples.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd cucumber-ruby-$pkgver
  gem build cucumber.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    cucumber-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd cucumber-ruby-$pkgver
  # cck disabled: "ci" is not supported in latest cucumber-messages
  PATH="$PWD/tmp_install/usr/bin:$PATH" GEM_HOME="tmp_install/$_gemdir" rake spec cucumber
}

package() {
  cd cucumber-ruby-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
