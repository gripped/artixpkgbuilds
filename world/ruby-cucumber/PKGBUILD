# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bert Peters <bertptrs@archlinux.org>

pkgname=ruby-cucumber
pkgver=10.2.0
pkgrel=1
pkgdesc="Behaviour Driven Development with elegance and joy"
arch=(any)
url='https://github.com/cucumber/cucumber-ruby'
license=(MIT)
depends=(
  ruby
  ruby-base64
  ruby-builder
  ruby-cucumber-ci-environment
  ruby-cucumber-core
  ruby-cucumber-cucumber-expressions
  ruby-cucumber-gherkin
  ruby-cucumber-html-formatter
  ruby-cucumber-messages
  ruby-diff-lcs
  ruby-mime-types
  ruby-mini_mime
  ruby-multi_test
  ruby-rake
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bundler
  ruby-nokogiri
  ruby-rspec
  ruby-webrick
)
options=(!emptydirs)
source=(
  "https://github.com/cucumber/cucumber-ruby/archive/v$pkgver/cucumber-cucumber-ruby-$pkgver.tar.gz"
  "${pkgname}_remove-sys-uname.patch"
)
sha512sums=('d52d89b2940c799dc6e32e859c8de89b1821ae5790c66ab81f98c19d907efc802e56f3c62c6eb8eb48caf9d098adf12eef8d86e84baa28f164169e970155ed51'
            '7a93b2d3dfd9866b83ee6b21fb86f601309ccbdd744e8162fb5d47dd636eb3c472ec76f5ec43820040f2fd58e8c3de6d9b3cf89f0962be22d3731dcde7cdedf1')
b2sums=('9813bf8f08c99f423f517875d8f2daf5af9f2081e33a191c8bef78ee4fa6e2ce00e7ff8a41c0f0ded400bd9207c3d180b7eb4a4f61dba84f75bc4e45b118b7a0'
        '975fbbb90686f3b9b97d3226608303b2776898f3614486a928cc12eb7cd13615c2ea472e134403948d6a4f8bfd79e999cf8dd48bedffa5f0d539ba108e34ffe1')

prepare() {
  cd cucumber-ruby-$pkgver

  patch --verbose --strip=1 --input="../${pkgname}_remove-sys-uname.patch"

  sed -i \
    -e '/capybara/d' \
    -e 's|~>|>=|' \
    -e "s/, '< [0-9]\+'//" \
    cucumber.gemspec
  sed -i '/[rR]ubo[cC]op/d' Rakefile

  rm Gemfile

  sed -i -e 's/bundle exec //' gem_tasks/cck.rake compatibility/cck_spec.rb
  sed -i \
    -e "s/Cucumber::Messages::TimeConversion/Cucumber::Messages::Helpers::TimeConversion/" \
    -e "s/Cucumber::Messages::IdGenerator::Incrementing/Cucumber::Messages::Helpers::IdGenerator::Incrementing/" \
    -e "s/Cucumber::Messages::IdGenerator::UUID/Cucumber::Messages::Helpers::IdGenerator::UUID/" \
    -e "s/require 'cucumber\/messages\/id_generator'/require 'cucumber\/messages\/helpers\/id_generator'/" \
    lib/cucumber/configuration.rb \
    lib/cucumber/formatter/message_builder.rb \
    spec/cucumber/formatter/message_spec.rb \
    spec/cucumber/runtime/hooks_examples.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd cucumber-ruby-$pkgver
  gem build cucumber.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    cucumber-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd cucumber-ruby-$pkgver
  # cck disabled: "ci" is not supported in latest cucumber-messages
  PATH="$PWD/tmp_install/usr/bin:$PATH" GEM_HOME="tmp_install/$_gemdir" rake spec cucumber
}

package() {
  cd cucumber-ruby-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
