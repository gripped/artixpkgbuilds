# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>

pkgname=libopenmpt
pkgver=0.8.5
pkgrel=1
pkgdesc="A library to render tracker music to a PCM audio stream"
arch=(x86_64)
url="https://lib.openmpt.org/libopenmpt/"
license=(BSD-3-Clause)
depends=(
  flac
  glibc
  libgcc
  libogg  # required by pkgconf
  libpulse
  libsndfile
  libstdc++
  libvorbis
  mpg123
  portaudio
  zlib
)
makedepends=(
  autoconf-archive
  doxygen
  help2man
)
provides=(libopenmpt.so)
source=($pkgname-$pkgver.tar.gz::https://lib.openmpt.org/files/libopenmpt/src/$pkgname-$pkgver+release.autotools.tar.gz)
sha512sums=('c8d4cc8b8c7fe9f3d341762166c73c4c42c73423a0064a7c904fd6ad6d6f9fecc6ac2f33ab8cff4bc3c364bbe55479c9edb59c32cec79c2d8e6e349f812be260')
b2sums=('9b829edb1513244fdf2c577ca2ae0d3e64b686f0a6bc47156c73e9cb76a58d00e184ce3e5704e580f3ad6833f92f3a980ecc2dc0e57cd718be889a924b7eb64c')

prepare() {
  cd $pkgname-$pkgver+release.autotools
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver+release.autotools
  ./configure --prefix=/usr
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  make -k check -C $pkgname-$pkgver+release.autotools
}

package() {
  cd $pkgname-$pkgver+release.autotools
  make DESTDIR="$pkgdir" install
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
