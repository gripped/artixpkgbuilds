# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-system-monitor
pkgver=47.0
pkgrel=3
pkgdesc="View current processes and monitor system state"
url="https://apps.gnome.org/SystemMonitor"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  glibmm-2.68
  graphene
  gtk4
  gtkmm-4.0
  hicolor-icon-theme
  libadwaita
  libgtop
  librsvg
  libsigc++-3.0
  pango
  polkit
)
makedepends=(
  appstream-glib
  git
  glib2-devel
  meson
  yelp-tools
)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/gnome-system-monitor.git#tag=${pkgver/[a-z]/.&}")
b2sums=('39ab0f7ce6c924f333610ad7469701cb8bea75ff2ff11e25f8ca79c9e77cbc852f04a4c53bd49e0aebbd5be376b09f946c73404b24ee91afbd2d8e9359e5cda9')

prepare() {
  cd $pkgname

  # Fix resuming from suspended state
  # https://gitlab.gnome.org/GNOME/gnome-system-monitor/-/merge_requests/166
  git cherry-pick -n 714faa9fad2a41f6353a242d48e5cb59b6ecdaac
}

build() {
  artix-meson $pkgname build -Dsystemd=false
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
