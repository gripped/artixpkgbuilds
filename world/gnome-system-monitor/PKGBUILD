# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-system-monitor
pkgver=48.0
pkgrel=1
pkgdesc="View current processes and monitor system state"
url="https://apps.gnome.org/SystemMonitor"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  glibmm-2.68
  graphene
  gtk4
  gtkmm-4.0
  hicolor-icon-theme
  libadwaita
  libgtop
  librsvg
  libsigc++-3.0
  pango
  polkit
)
makedepends=(
  appstream
  catch2
  git
  glib2-devel
  meson
  yelp-tools
)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/gnome-system-monitor.git#tag=${pkgver/[a-z]/.&}")
b2sums=('5abbca55cc12993848ea784559233d5a1a0df115a70224ab9440a6a333896a89a08dc90320a052079a3f36f8614e413cbf8918a7c41dbe37e02bdad33f4b25ee')

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build -Dsystemd=false
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
