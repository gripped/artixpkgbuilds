# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-system-monitor
pkgver=48.1
pkgrel=1
pkgdesc="View current processes and monitor system state"
url="https://apps.gnome.org/SystemMonitor"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  glibmm-2.68
  graphene
  gtk4
  gtkmm-4.0
  hicolor-icon-theme
  libadwaita
  libgtop
  librsvg
  libsigc++-3.0
  pango
  polkit
)
makedepends=(
  appstream
  catch2
  git
  glib2-devel
  meson
  yelp-tools
)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/gnome-system-monitor.git#tag=${pkgver/[a-z]/.&}")
b2sums=('8ab8d6ed3f821f5adb094ba04affb055eb1f2637b0182621631eede2264c5738f3f3df3cf17b2a17d53b90c8f3b1a941a985965f99d266b754d1ce6077e6533c')

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build -Dsystemd=false
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
