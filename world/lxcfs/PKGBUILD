# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Christian Brauner <christianvanbrauner@gmail.com>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=lxcfs
pkgver=6.0.3
pkgrel=2
pkgdesc="FUSE filesystem for LXC"
arch=(x86_64)
url="https://linuxcontainers.org/"
license=('GPL')
depends=('lxc' 'libnih' 'fuse3' 'dbus-glib')
makedepends=('help2man' 'meson' 'python-jinja')
options=('emptydirs')
source=("https://linuxcontainers.org/downloads/lxcfs/lxcfs-$pkgver.tar.gz"{,.asc})
validpgpkeys=('602F567663E593BCBD14F338C638974D64792D67')
sha256sums=('886484d4a74d33dfa285635d5ebef20b0686a64901d23e00dc0f4496c10c1114'
            'SKIP')

build() {
    cd "$pkgname-$pkgver"
    artix-meson -Dinit-script=sysvinit ../build
    meson compile -C ../build
}

package() {
    cd "$pkgname-$pkgver"
    DESTDIR="$pkgdir" meson install -C ../build
    install -dm0755 "$pkgdir"/var/lib/lxcfs
    # Artix
    rm -rf "$pkgdir"/etc/init.d
}
