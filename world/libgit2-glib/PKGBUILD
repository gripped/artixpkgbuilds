# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=libgit2-glib
pkgname=(
  libgit2-glib
  libgit2-glib-docs
)
pkgver=1.2.0
pkgrel=6
pkgdesc="GLib wrapper for libgit2"
url="https://gitlab.gnome.org/GNOME/libgit2-glib"
license=(LGPL-2.1-or-later)
arch=(x86_64)
makedepends=(
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  libgit2
  meson
  python-gobject
  vala
)
source=(
  "git+$url.git#tag=v$pkgver"
  0001-Unbreak-micro-version.patch
  0002-build-Fix-build-against-and-require-libgit2-1.8.0.patch
  0003-Adjust-API-for-libgit2-1.9.0.patch
)
b2sums=('d31e3edff3b663b0ba754868b719afd231067dd55aef05d4deec995dfab7eb7ba45c16ef2094dfa0f0c605731732b78e78bfa283a70fb18ab7d24a6f12fb00a8'
        '44ea6fb63b10c31042d6cea588c099ff2fe8c3d2bdc7fd1ec2acdd74a07832954cca942de130f4461a124639db4e785362ac188782e2e2d7d22a89dff0442a8d'
        'b4419fd17c3569e2c2f74febc59fcd1ddcbdbe3186c001f49efbd02de7f744e94b79a4c5b8cf3c747ba1ee61744fed6dd36516d99599ba490d9956cc0af047df'
        '1b70e9acc922ee65f1a46cd7e4490a9fa5974408f375028c0768ea64350d8cd5444267f3085b7d6bc7092c68b938b5abaffd54d574403b2e8df6522415bae2da')

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

prepare() {
  # fix a micro issue
  git -C $pkgname apply -3 ../0001-Unbreak-micro-version.patch

  # use libgit2 1.8
  # https://gitlab.gnome.org/GNOME/libgit2-glib/-/merge_requests/40
  git -C $pkgname apply -3 ../0002-build-Fix-build-against-and-require-libgit2-1.8.0.patch

  # use libgit2 1.9
  git -C $pkgname apply -3 ../0003-Adjust-API-for-libgit2-1.9.0.patch
}

build() {
  artix-meson $pkgname build -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_libgit2-glib() {
  depends=(
    glib2 libgio-2.0.so libglib-2.0.so libgobject-2.0.so
    glibc
    libgit2 libgit2.so
  )
  optdepends=(
    'libgit2-glib-docs: for developer documentation'
    'python-gobject: for Python integration'
  )
  provides=(libgit2-glib-1.0.so)

  meson install -C build --destdir "$pkgdir"
  (
    cd "$pkgdir"
    _pick libgit2-glib-docs usr/share/gtk-doc
  )

  install -vDm 644 $pkgname/{AUTHORS,ChangeLog,NEWS,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}

package_libgit2-glib-docs() {
  pkgdesc+=" - documentation"

  mv -v $pkgname/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
