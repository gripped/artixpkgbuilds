# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>
# Contributor: Mario Finelli <mario at finel dot li>

_gemname='rubocop-performance'
pkgname="ruby-${_gemname}"
pkgver=1.26.1
pkgrel=1
pkgdesc='An extension of RuboCop focused on code performance checks'
arch=('any')
url='https://docs.rubocop.org/rubocop-performance/'
license=('MIT')
depends=(
  ruby
  rubocop
  ruby-rubocop-ast
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bundler
  ruby-parallel
  ruby-rake
  ruby-rexml
  ruby-rubocop-rspec
  ruby-rspec
  ruby-test-queue
  ruby-yard
)
options=('!emptydirs')
source=("https://github.com/rubocop/${_gemname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('6d114a5d295e3f9eb55c32f0ee666bda5ad4af8a421c508f27e42b36f3884ce3c4801aa59be05e8402823aa43b5d2997d3dfafccbfdd06674a8408e766c1b63c')
b2sums=('0b495d42e327de3921d5fcec09f829b396580f87688261ca12bb817452b46b770fbdd471246af13ba8a53b57a49f0c696d70c3c89c2f25577c9bedc4bead43d3')

prepare() {
  cd "${_gemname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec" Gemfile

  # Remove dependency on bump, simplecov and yard
  sed --in-place --regexp-extended '/bump|simplecov/d' Gemfile
  rm tasks/{cops_documentation,cut_release}.rake

  # we have rubocop as a package so we don't need a git checkout
  sed --in-place --regexp-extended "s|, github: 'rubocop/rubocop'||g" Gemfile
}

build() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install/${_gemdir}/cache/" \
    "tmp_install/${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install/${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install/${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install/${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install/${_gemdir}" rake spec
}

package() {
  cd "${_gemname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
