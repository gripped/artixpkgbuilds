# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-multipart
pkgver=2.0.1
pkgrel=1
pkgdesc='Parser for multipart/form-data'
arch=(any)
url='https://github.com/defnull/multipart'
license=(MIT)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-flit-core
)
checkdepends=(python-pytest)
source=(
  "$pkgname::git+$url#tag=v$pkgver"
  remove-version-constraints.patch
)
sha512sums=('637795750a4ef007bfff52234ad2166a31b7734f06afd78174b217506f9bc554f00b81888a3434c88134699aa0b5491dfcc57a89108e69e510c3de4b26364f90'
            '07b8c04a06554ec7cb34a993655eb3b06807c4dbaf454fd211599a648d9e9a11efd5a3d5075182d294f8f94c73daf8c2baa5b65dd4ed40bc7a985d2e2a740d5e')
b2sums=('5aa49ea4497f9b907b6c9d389f51e2b91fd0e8570ee21976ed8fc6f65338bb2fe489009bc8fc1a2d30234d404de6c28ef981f897591e3c618545652834978976'
        '834aed3057f544e64f7fe927fc56ac093cc0c20668e701c3fbdae2369ae5e88bf00a58ce0d1d022471aabbceeda0d81723535c332d9b7ebe59f7bf253e7a90e0')

prepare() {
  cd "$pkgname"

  patch -p1 -i "$srcdir/remove-version-constraints.patch"
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  local pytest_options=(
    -vv
  )

  pytest "${pytest_options[@]}"
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim:set ts=2 sw=2 et:
