# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Remi Gacogne <rgacogne[at]archlinux[dot]org>
# Contributor: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Kevin Mihelich <kevin@archlinuxarm.org>
# Contributor: Pieter Lexis <pieter[dot]lexis[at]powerdns[dot]com>

pkgname=powerdns
pkgver=4.9.1
pkgrel=2
pkgdesc='Authoritative DNS server'
url='https://www.powerdns.com/'
arch=('x86_64')
license=('GPL-2.0-only')
depends=('boost-libs' 'gnutls' 'libcurl.so' 'libsodium' 'lmdb' 'luajit'
         'openssl' 'sqlite' 'esysusers')
makedepends=('boost' 'geoip' 'libldap' 'mariadb-libs' 'curl' 'libmaxminddb'
             'postgresql-libs' 'tinycdb' 'unixodbc' 'yaml-cpp')
optdepends=('geoip: GeoIP backend'
            'mariadb-libs: MariaDB/MySQL and MyDNS backend'
            'libldap: LDAP backend'
            'libmaxminddb: MaxMindDB GeoIP2 database for the GeoIP backend'
            'postgresql-libs: PostgreSQL backend'
            'unixodbc: ODBC backend'
            'tinycdb: TinyDNS backend'
            'yaml-cpp: GeoIP backend')
provides=('pdns')
conflicts=('pdns')
backup=('etc/powerdns/pdns.conf')
source=(https://downloads.powerdns.com/releases/pdns-${pkgver}.tar.bz2{,.asc}
        sysusers.conf
        powerdns-4_9_1-2_fix-build-with-boost-1_86_0.patch::https://github.com/PowerDNS/pdns/commit/eed56000b1d68ac083b8e8bea4ff0ea30a1579c4.patch)
sha512sums=('8be784615a4b35dd04af7584d1faf3e2b25665b31f83d8f64057ca164a2c4f0604c92c768b88a7b778aa13148fb2d0cedc328551b8c467b7b21011cfc3b33b1a'
            'SKIP'
            '522bde8b76367db6edd461fca8f81d50304a4d764addf33774b3e2c5c93a9e72d93993f54ddd4ee887ceeccf70280aceea3ffaf6370d26310e0a7ebbbf61f26a'
            '4fe99a537d26dc204012b4116a3768741474fdefa57d740d9f29340b1805f6fc68311d5393157429d30ff0f830299e0a675356b0b52a5869d229b346117c7bd5')
validpgpkeys=('B76CD4671C0968BAA87DE61C5E50715BF2FFE1A7'   # Winkels, Erik <erik.winkels@open-xchange.com>
              '16E12866B7738C73976A57436FFC33439B0D04DF' ) # Peter van Dijk <peter.van.dijk@powerdns.com>

prepare() {
  cd pdns-${pkgver}
  # Patch the Makefile.in's so /powerdns is used instead of /pdns (for e.g. $LIBDIR)
  # This allows for running pdns_server without setting `module-dir` in the config.
  find . -name 'Makefile.in' -exec sed -i 's,pkglibdir = \$(libdir)/@PACKAGE@,pkglibdir = $(libdir)/powerdns,' {} \;

  patch -Np1 < ../powerdns-4_9_1-2_fix-build-with-boost-1_86_0.patch
}

build() {
  cd pdns-${pkgver}
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/powerdns \
    --sbindir=/usr/bin \
    --with-modules='' \
    --with-dynmodules="bind geoip gmysql godbc gpgsql gsqlite3 ldap lmdb lua2 pipe remote tinydns" \
    --docdir=/usr/share/doc/powerdns \
    --with-libsodium \
    --enable-tools \
    --enable-ixfrdist \
    --enable-dns-over-tls \
    --disable-dependency-tracking \
    --disable-silent-rules \
    --enable-reproducible \
    --enable-unit-tests \
    --disable-systemd \
    --enable-lto=thin \
    --enable-auto-var-init=zero \
    --with-service-user=powerdns \
    --with-service-group=powerdns \
    --with-libcrypto=/usr # workaround for https://github.com/PowerDNS/pdns/issues/14084
  make
}

check() {
  make -C pdns-${pkgver} check
}

package() {
  make -C pdns-${pkgver} DESTDIR="${pkgdir}" install
  install -Dm 644 sysusers.conf "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  mv "${pkgdir}/etc/powerdns/pdns.conf"{-dist,}
  # Ensure we run as the powerdns user
  sed -i "s,^# \(set.id=\),\1${pkgname}," "${pkgdir}/etc/powerdns/pdns.conf"
}

# vim: ts=2 sw=2 et:
