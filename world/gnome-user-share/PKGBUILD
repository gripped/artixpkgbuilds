# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=gnome-user-share
pkgver=48.0
pkgrel=1
pkgdesc="Easy to use user-level file sharing for GNOME"
url="https://gitlab.gnome.org/GNOME/gnome-user-share"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  apache
  dconf
  gcc-libs
  glib2
  glibc
  mod_dnssd
)
makedepends=(
  git
  meson
  rust
 
)
groups=(gnome)
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-user-share.git#tag=${pkgver/[a-z]/.&}"
  0001-build-Use-cargo-release-for-all-buildtypes-except-de.patch
)
b2sums=('1294180e23983e01ee02c0bb45bfe6c6be2fdbf3f1f24779f6e1841701616e636e0c48c52f0f5f1365e094a961c8faa9303a9dc9264039520b99b8d9b4ff8f10'
        '936f92722d7d189b36c47dad2f0c70c0771923b7c0e9c141006f94a26fe9f41bc7faed529ca2df869bfbad0f8379847bd2d96b2bcbaf2c2c4bd8681ef5aef9e6')

# Use LTO
export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

# Use debug
export CARGO_PROFILE_RELEASE_DEBUG=2

prepare() {
  cd $pkgname

  # Build Rust components in release mode
  git apply -3 ../0001-build-Use-cargo-release-for-all-buildtypes-except-de.patch

  CARGO_HOME="$srcdir/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  local meson_options=(
    -D systemduserunitdir=/usr/lib/systemd
    -D httpd=/usr/bin/httpd
    -D modules_path=/usr/lib/httpd/modules
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild

  rm -r $pkgdir/usr/lib/systemd
}

# vim:set sw=2 sts=-1 et:
