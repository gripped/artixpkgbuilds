# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=four-in-a-row
pkgver=3.38.1
pkgrel=4
pkgdesc="Make lines of the same color to win"
url="https://gitlab.gnome.org/GNOME/four-in-a-row"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gsound
  gtk3
  hicolor-icon-theme
  librsvg
)
makedepends=(
  git
  gobject-introspection
  meson
  vala
  yelp-tools
)
source=(
  "git+$url.git#tag=$pkgver"
  0001-Don-t-assert-when-the-last-winner-is-nobody.patch
)
b2sums=('e44d55f543a1944bb5c9a5d473b41f25877cac7071060b794a5cb825fe53097347e3ee2a8b5dc05ded922fe8a6829982d0718c12256a244e31725446b282e7b5'
        'bde0095f79e15e2c09cd436d69e3cf5e5d429ee89f2c90d415ada0e277cc491d6aa0a365e90e3eaa3011976dbc01e10fc40cc4954f5b8f2a6d5b44dbd6948ad3')

prepare() {
  cd $pkgname

  # Don't assert when the last winner is nobody
  # https://gitlab.gnome.org/GNOME/four-in-a-row/-/merge_requests/34
  git apply -3 ../0001-Don-t-assert-when-the-last-winner-is-nobody.patch
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
