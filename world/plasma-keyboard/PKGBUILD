# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=plasma-keyboard
pkgver=6.7.2
_dirver=$(echo $pkgver | cut -d. -f1-3)
pkgrel=1
arch=(x86_64)
pkgdesc='Virtual Keyboard for Qt based desktops'
url='https://kde.org/plasma-desktop/'
license=(GPL-2.0-or-later)
depends=(glibc
         kcmutils
         kconfig
         kcoreaddons
         kcrash
         ki18n
         kirigami
         kirigami-addons
         kitemmodels
         libplasma
         libstdc++
         libxkbcommon
         qt6-base
         qt6-declarative
         qt6-virtualkeyboard
         wayland)
makedepends=(extra-cmake-modules
             qt6-wayland
             wayland-protocols)
groups=(plasma)
source=(https://download.kde.org/stable/plasma/$_dirver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('ca46e412e7530481904d4cd3f622c5ba2d03504bd7bce77ea3e1221c01c01c07'
            'SKIP')
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D'  # Jonathan Esk-Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
