# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=evince
pkgname=(
  evince
  evince-lib-docs
)
pkgver=48.0
pkgrel=3
pkgdesc="Document viewer (PDF, PostScript, XPS, djvu, dvi, tiff, cbr, cbz, cb7, cbt)"
url="https://apps.gnome.org/Evince"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  at-spi2-core
  cairo
  dconf
  djvulibre
  exempi
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gnome-desktop
  gsettings-desktop-schemas
  gsfonts
  gspell
  gst-plugins-base-libs
  gstreamer
  gtk3
  gvfs
  hicolor-icon-theme
  libarchive
  libgxps
  libhandy
  libsecret
  libspectre
  libsynctex
  libtiff
  libxml2
  pango
  poppler-glib
)
makedepends=(
  appstream
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  meson
  texlive-bin
  yelp-tools
)
source=("git+https://gitlab.gnome.org/GNOME/evince.git?signed#tag=${pkgver/[a-z]/.&}"
        evince-prgname.patch)
b2sums=('01e7c46f617012fc267a215464773c9c804ea87681237747a3a4581728b2e29bf2a5478cb93ace64b318be6473088020094a5339be24e08c5862272d2f269d47'
        'bd126def7c4f94a0f0278ed712fc995bc8dc2a8644dae54ad931a2daccd2feace6e586dc8377f38ae5c1575764cbd0992f472e71e791bbab65fb5a13cb8f3c66')
validpgpkeys=(
  3289FDCF12AC2321BB41E9EF342B349ED9FE78F0 # Germán Poo-Caamaño <gpoo@gnome.org>
)

prepare() {
  cd evince

  # Set application ID to match with .desktop
  # https://gitlab.gnome.org/GNOME/evince/-/merge_requests/725
  git apply -3 ../evince-prgname.patch
}
  
build() {
  local meson_options=(
    -D ps=enabled
  )

  artix-meson evince build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_evince() {
  provides=(libev{document,view}3.so)
  optdepends=('texlive-bin: DVI support')
  groups=(gnome)

  meson install -C build --destdir "$pkgdir"

  _pick lib-docs "$pkgdir"/usr/share/doc/lib*
}

package_evince-lib-docs() {
  pkgdesc+=" (library API documentation)"
  depends=()

  mv lib-docs/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
