# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=konquest
pkgver=26.08.1
pkgrel=1
pkgdesc='The KDE version of Gnu-Lactic'
url='https://apps.kde.org/konquest/'
arch=(x86_64)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(glibc
         kcolorscheme
         kconfig
         kcoreaddons
         kcrash
         kdbusaddons
         kguiaddons
         ki18n
         kwidgetsaddons
         kxmlgui
         libkdegames
         libstdc++
         qt6-base
         qt6-scxml
         qt6-svg)
makedepends=(extra-cmake-modules
             kdoctools)
groups=(kde-applications
        kde-games)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('ff2eb6e383b4d8e9c4091f24d5bd630b953dc797f3c06707c8be548b550babeb'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
