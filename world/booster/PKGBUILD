# Maintainer: Anatol Pomozov

pkgname=booster
pkgver=0.12
pkgrel=1.1
pkgdesc='Fast and secure initramfs generator'
arch=(x86_64)
url='https://github.com/anatol/booster'
license=(MIT)
depends=(bash)
makedepends=(go ruby-ronn-ng)
#checkdepends=(qemu-headless linux tang)
optdepends=(
  'busybox: to enable emergency shell at the boot time'
  'yubikey-personalization: for clevis Yubikey challenge-response support'
  'libfido2: for systemd-enroll with FIDO2'
)
backup=(etc/booster.yaml)
provides=(initramfs)
source=(booster-$pkgver.zip::https://github.com/anatol/booster/archive/$pkgver.zip
        universal-img.patch)
sha512sums=('c66f783b5c7569e18cb2bf3d66c089e129fb835e250feccb63e6772614e78144b2e422649a9744bf5cf42fbeb13d2004939d098974f446244b8b2ceca928776b'
            '5bc0eb9c1273d1e5f0392abc1e7bee09b80ee528787798576bea9ad484dd11db0f48645cd158b7d2a8313bef24a28412eac7a42c6ea7cb35fc608d0df3b8ab2f')

prepare() {
  cd booster-$pkgver
  patch -Np1 -i ../universal-img.patch
}

build() {
  cd booster-$pkgver

  cd generator
  CGO_CPPFLAGS="${CPPFLAGS}" CGO_CFLAGS="${CFLAGS}" CGO_CXXFLAGS="${CXXFLAGS}" CGO_LDFLAGS="${LDFLAGS}" \
    go build -trimpath \
      -buildmode=pie \
      -mod=readonly \
      -modcacherw \
      -ldflags "-linkmode external -extldflags \"${LDFLAGS}\""

  cd ../init
  CGO_ENABLED=0 go build -trimpath -mod=readonly -modcacherw

  cd ..
  ronn docs/manpage.md
}

check() {
  cd booster-$pkgver

  # (cd init; go test)
  # (cd generator; go test)

  # arch chroot does not allow access to KVM
  # TEST_DISABLE_KVM=1 go test -v # disable integration tests for now as it require a lot of time and space to build 10G images
}

package() {
  cd booster-$pkgver
  mkdir "$pkgdir/etc/"
  touch "$pkgdir/etc/booster.yaml"
  install -Dp -m755 generator/generator "$pkgdir/usr/bin/booster"
  install -Dp -m644 docs/manpage.1 "$pkgdir/usr/share/man/man1/booster.1"
  install -Dp -m755 init/init "$pkgdir/usr/lib/booster/init"
  install -Dp -m755 packaging/arch/regenerate_images "$pkgdir/usr/lib/booster/regenerate_images"
  install -Dp -m755 packaging/arch/regenerate_uki "$pkgdir/usr/lib/booster/regenerate_uki"

  install -Dp -m644 packaging/arch/90-booster-install.hook "$pkgdir/usr/share/libalpm/hooks/90-booster-install.hook"
  install -Dp -m755 packaging/arch/booster-install "$pkgdir/usr/share/libalpm/scripts/booster-install"
  install -Dp -m644 packaging/arch/60-booster-remove.hook "$pkgdir/usr/share/libalpm/hooks/60-booster-remove.hook"
  install -Dp -m755 packaging/arch/booster-remove "$pkgdir/usr/share/libalpm/scripts/booster-remove"
  install -Dp -m755 contrib/completion/bash "$pkgdir/usr/share/bash-completion/completions/booster"
}
