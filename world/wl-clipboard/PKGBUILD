# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: Adam Brunnmeier

pkgname=wl-clipboard
pkgver=2.2.1
pkgrel=3
epoch=1
pkgdesc="Command-line copy/paste utilities for Wayland"
arch=('x86_64')
url="https://github.com/bugaevc/wl-clipboard"
license=('GPL-3.0-or-later')
depends=('glibc' 'wayland')
optdepends=(
    'xdg-utils: for content type inference in wl-copy'
    'mailcap: for type inference in wl-paste'
)
makedepends=('meson' 'wayland-protocols')
source=("$pkgname-$pkgver.tar.gz::https://github.com/bugaevc/wl-clipboard/archive/v$pkgver.tar.gz")
sha256sums=('6eb8081207fb5581d1d82c4bcd9587205a31a3d47bea3ebeb7f41aa1143783eb')

build() {
    artix-meson "$pkgname-$pkgver" build
    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
}
