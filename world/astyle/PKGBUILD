# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Thomas Mader <thezema@gmail.com>
# Contributor: Vinay S Shastry <vinayshastry@gmail.com>
# Contributor: tardo <tardo@nagi-fanboi.net>

# TODO rebuild reverse-deps on every pkgver bump
pkgname=astyle
pkgver=3.6.19
pkgrel=1
pkgdesc='A free, fast and small automatic formatter for C, C++, C#, and Java source code'
arch=(x86_64)
url='http://astyle.sourceforge.net/'
license=(LGPL-3.0-only)
depends=(
  glibc
  libgcc
  libstdc++
)
makedepends=(
  git
  jdk8-openjdk
)
optdepends=(java-environment-openjdk)
source=(
  "$pkgname::git+https://gitlab.com/saalen/astyle.git#tag=$pkgver"
  simplify-makefile.patch
)
sha512sums=('06c6283d18e5f3c015c5512999c949eb51b94fc2bc4c168caf15124750ddbc5d5471ed526cd5d086687f385e7d674f1061ed327a0c96f47ce3ffbc616f1457bf'
            '4a533c6b073a55206ea2c8351d6631ba6e056d59823c8988eae63a47a762e920ad852442b57ed1860a648199c34fbc08c31bb553f0dc3f1ed145c2c702ad0290')
b2sums=('924f05fb4cbb9d0e0b4cd25ed0fd933bf5f85cd30371235e022e9ec6fd4d061601d67476ea78999b4e505b338209cec58df111e5576394bb18b00fa75587e40e'
        '155dca3520e1669713efbf42431e4ba2c9c05006dcd2a14f58252d9e6913aab799af7b64c9040201e99ff8d12f953918124c83845fcf64ec025fc131e7acdf1f')

prepare() {
  cd "$pkgname"

  patch -p1 -i "$srcdir/simplify-makefile.patch"
}

build() {
  cd "$pkgname/AStyle/build/gcc"

  JAVA_HOME=/usr/lib/jvm/java-8-openjdk make shared release java
}

package() {
  cd "$pkgname/AStyle/build/gcc"

  DESTDIR="$pkgdir" make install
}
