# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Maxime Schmitt <maxime.schmitt91@gmail.com>

pkgname=nvtop
pkgver=3.2.0
pkgrel=1
pkgdesc="GPUs process monitoring for AMD, Intel and NVIDIA"
arch=('x86_64')
url="https://github.com/Syllo/nvtop"
license=('GPL3')
depends=('ncurses' 'libudev')
makedepends=('git' 'cmake' 'libdrm')
source=("git+https://github.com/Syllo/nvtop.git#tag=$pkgver")
sha512sums=('15edd1f83a78ade76942bcf8f4baf63903bb88641dc82fceef44999dc3452d7ffe49339f019a8a25df4f510bed10ce32f693414e5e5b5495c789359e4708579b')

build() {
  cd $pkgname

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DNVIDIA_SUPPORT=ON -DAMDGPU_SUPPORT=ON .
  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir/" install
}
