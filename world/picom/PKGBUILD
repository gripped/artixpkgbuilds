# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributor: OK100 <ok100@lavabit.com>
# Contributor: Valère Monseur <valere.monseur@ymail.com>

pkgname=picom
pkgver=12.1
pkgrel=3
pkgdesc="A lightweight compositor for X11"
url="https://picom.app"
arch=('x86_64')
license=('MPL-2.0 AND MIT')
depends=('hicolor-icon-theme' 'libconfig' 'libdbus' 'libev' 'libgl' 'pcre2' 'pixman'
         'xcb-util-image' 'xcb-util-renderutil' 'libepoxy')
makedepends=('asciidoctor' 'git' 'mesa' 'meson' 'ninja' 'setconf' 'uthash')
optdepends=('dbus: for controlling picom via D-Bus'
            'python: for running picom-convgen.py'
            'rtkit: for realtime scheduling priority'
            'xorg-xprop: for picom-trans'
            'xorg-xwininfo: for picom-trans')
backup=('etc/xdg/picom.conf')
source=("git+https://github.com/yshui/picom.git?signed#tag=v${pkgver}")
validpgpkeys=('A22F5C0F4FCF9C7C89A167462C965E9E5D45D730') # Yuxuan Shui <yshuiv7@gmail.com>
b2sums=('91ae49db5581023adbb221b80a9b710c7c432cf371c34e568c18932518887688d7280e4c27e6fd72b65dbc0c76b06259e279284ab3f5570d6530b303670204e9')

prepare() {
	cd "${pkgname}"
	# More subtle transparency in the default settings
	setconf picom.sample.conf 'frame-opacity=0.9;'
	setconf picom.sample.conf 'inactive-opacity=0.95;'
	# Improve performance (recommended option)
	setconf -u picom.sample.conf 'glx-no-stencil=true;'
	# Force xrender as the default backend (default upstream value)
	setconf -u picom.sample.conf 'backend="xrender"'
}

build() {
	cd "${pkgname}"
	mkdir -p build
	artix-meson --buildtype=release -D with_docs=true build
	ninja -v -C build
}

check() {
	cd "${pkgname}"
	ninja -C build test
}

package() {
	cd "${pkgname}"
	DESTDIR="${pkgdir}" ninja -C build install

	# Default and example configuration
	install -Dm 644 "${pkgname}.sample.conf" "${pkgdir}/etc/xdg/picom.conf"
	install -Dm 644 "${pkgname}.sample.conf" "${pkgdir}/usr/share/doc/picom/picom.conf.example"

	# License
	install -Dm 644 "LICENSES/MIT" "${pkgdir}/usr/share/licenses/${pkgname}/MIT"
}
