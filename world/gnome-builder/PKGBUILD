# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-builder
pkgver=49.0
pkgrel=1
pkgdesc="An IDE for writing GNOME-based software"
url="https://apps.gnome.org/Builder/"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  autoconf-archive
  cairo
  clang
  cmark
  ctags
  dconf
  desktop-file-utils
  editorconfig-core-c
  flatpak
  gcc-libs
  gdk-pixbuf2
  gjs
  glib2
  glibc
  gobject-introspection-runtime
  gom
  gtk4
  gtksourceview5
  hicolor-icon-theme
  json-glib
  jsonrpc-glib
  libadwaita
  libdex
  libgirepository
  libgit2-glib
  libpanel
  libpeas-2
  libportal
  libportal-gtk4
  libsoup3
  libspelling
  libxml2
  libyaml
  meson
  pango
  python
  python-lxml
  python-sphinx
  sysprof
  template-glib
  vala
  vte4
  webkitgtk-6.0
)
makedepends=(
  appstream
  git
  glib2-devel
  gobject-introspection
  llvm
  mm-common
  python-lsp-server
  python-sphinx_rtd_theme
  yelp-tools
)
checkdepends=(weston)
optdepends=(
  'bash-language-server: Shell code assistance'
  'python-lsp-server: Python code assistance'
)
conflicts=(
  gnome-builder-clang
  gnome-builder-flatpak
)
replaces=(
  'gnome-builder-clang<=46.1-4'
  'gnome-builder-flatpak<=46.1-4'
)
groups=(gnome-extra)
source=("git+https://gitlab.gnome.org/GNOME/gnome-builder.git#tag=${pkgver/[a-z]/.&}")
b2sums=('dfeeab9070f3085b138bcbe29a960beabca1dd1f1e318a68e7ba35a4a09b1374bb8e6f075bcb2cd4b542cba9b20c8bd50d7d9a635ab512589219973d62d7278f')

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D help=true
    -D network_tests=false
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() (
  export XDG_RUNTIME_DIR="$PWD/runtime-dir"
  mkdir -p -m 700 "$XDG_RUNTIME_DIR"

  export WAYLAND_DISPLAY=wl-$pkgname-$RANDOM
  weston --backend=headless --socket=$WAYLAND_DISPLAY --idle-time=0 &
  _w=$!

  trap "kill $_w; wait" EXIT

  export NO_AT_BRIDGE=1 GTK_A11Y=none
  dbus-run-session meson test -C build --print-errorlogs
)

package() {
  depends+=(libgit2.so)

  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
