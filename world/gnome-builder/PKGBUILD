# Maintainer: Fabian Bornschein <fabiscafe-at-mailbox-dot-org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=gnome-builder
pkgname=(
  gnome-builder
  gnome-builder-clang
  gnome-builder-flatpak
)
pkgver=45.0
pkgrel=4
pkgdesc="An IDE for writing GNOME-based software"
url="https://wiki.gnome.org/Apps/Builder"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  autoconf-archive
  cairo
  cmark
  ctags
  d-spy
  dconf
  desktop-file-utils
  devhelp
  editorconfig-core-c
  enchant
  gdk-pixbuf2
  gjs
  glib2
  gobject-introspection-runtime
  gtk4
  gtksourceview5
  hicolor-icon-theme
  icu
  json-glib
  jsonrpc-glib
  libadwaita
  libdex
  libgirepository
  libgit2-glib
  libpanel
  libpeas-2
  libportal
  libportal-gtk4
  libsoup3
  libxml2
  meson
  pango
  python
  python-lxml
  python-sphinx
  sysprof
  template-glib
  vala
  vte4
  webkitgtk-6.0
)
makedepends=(
  appstream-glib
  clang
  flatpak
  git
  gobject-introspection
  llvm
  mm-common
  python-lsp-server
  python-sphinx_rtd_theme
  yelp-tools
)
checkdepends=(weston)
_commit=82b055c8cc4dbcf171f8236e2a7d15cd19225fe1  # tags/45.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-builder.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd $pkgbase
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgbase
  git cherry-pick -n 7aaaecefc2ea8a37eaeae8b4d726d119d4eb8fa3 # Fix build
}

build() {
  local meson_options=(
    -D help=true
  )

  artix-meson $pkgbase build "${meson_options[@]}"
  meson compile -C build
}

check() (
  export XDG_RUNTIME_DIR="$PWD/runtime-dir"
  mkdir -p -m 700 "$XDG_RUNTIME_DIR"

  export WAYLAND_DISPLAY=wayland-5
  weston --backend=headless-backend.so --socket=$WAYLAND_DISPLAY --idle-time=0 &
  _w=$!

  trap "kill $_w; wait" EXIT

  dbus-run-session meson test -C build --print-errorlogs
)

package_gnome-builder() {
  depends+=(libgit2.so)
  optdepends=(
    'bash-language-server: Shell code assistance'
    'gnome-builder-clang: Clang integration'
    'gnome-builder-flatpak: Flatpak integration'
    'python-lsp-server: Python code assistance'
  )
  groups=(gnome-extra)

  meson install -C build --destdir "$pkgdir"

  # Move the external plugins to sub-packages
  mkdir -p {clang,flatpak}/usr/lib
  mv {"$pkgdir",clang}/usr/lib/gnome-builder-clang
  mv {"$pkgdir",flatpak}/usr/lib/gnome-builder-flatpak
}

package_gnome-builder-clang() {
  pkgdesc+=" (clang module)"
  depends=(
    clang
    gcc-libs
    glib2
    gtk4
    gtksourceview5
    jsonrpc-glib
    libdex
    libpeas-2
  )
  groups=(gnome-extra)

  mv clang/* "$pkgdir"
}

package_gnome-builder-flatpak() {
  pkgdesc+=" (flatpak module)"
  depends=(
    flatpak
    gcc-libs
    glib2
  )
  groups=(gnome-extra)

  mv flatpak/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
