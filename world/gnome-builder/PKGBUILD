# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-builder
pkgver=47.2
pkgrel=4
pkgdesc="An IDE for writing GNOME-based software"
url="https://apps.gnome.org/Builder/"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  autoconf-archive
  cairo
  clang
  cmark
  ctags
  d-spy
  dconf
  desktop-file-utils
  devhelp
  editorconfig-core-c
  flatpak
  gcc-libs
  gdk-pixbuf2
  gjs
  glib2
  glibc
  gobject-introspection-runtime
  gom
  gtk4
  gtksourceview5
  hicolor-icon-theme
  json-glib
  jsonrpc-glib
  libadwaita
  libdex
  libgirepository
  libgit2-glib
  libpanel
  libpeas-2
  libportal
  libportal-gtk4
  libsoup3
  libspelling
  libxml2
  meson
  pango
  python
  python-lxml
  python-sphinx
  sysprof
  template-glib
  vala
  vte4
  webkitgtk-6.0
)
makedepends=(
  appstream-glib
  git
  glib2-devel
  gobject-introspection
  llvm
  mm-common
  python-lsp-server
  python-sphinx_rtd_theme
  yelp-tools
)
checkdepends=(weston)
optdepends=(
  'bash-language-server: Shell code assistance'
  'python-lsp-server: Python code assistance'
)
conflicts=(
  gnome-builder-clang
  gnome-builder-flatpak
)
replaces=(
  'gnome-builder-clang<=46.1-4'
  'gnome-builder-flatpak<=46.1-4'
)
groups=(gnome-extra)
source=("git+https://gitlab.gnome.org/GNOME/gnome-builder.git#tag=${pkgver/[a-z]/.&}")
b2sums=('57ce8187399405832868ea274326bc11b7379fb150685330e9ee47e342365c21a9e4f1bd4b64b74a393128cafaab3a3f853ceab13d2ac7058b466eb8c43888a0')

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D help=true
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() (
  export XDG_RUNTIME_DIR="$PWD/runtime-dir"
  mkdir -p -m 700 "$XDG_RUNTIME_DIR"

  export WAYLAND_DISPLAY=wl-$pkgname-$RANDOM
  weston --backend=headless --socket=$WAYLAND_DISPLAY --idle-time=0 &
  _w=$!

  trap "kill $_w; wait" EXIT

  export NO_AT_BRIDGE=1 GTK_A11Y=none
  dbus-run-session meson test -C build --print-errorlogs
)

package() {
  depends+=(libgit2.so)

  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
