# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=libslirp
pkgver=4.9.2
pkgrel=1
pkgdesc='General purpose TCP-IP emulator'
arch=(x86_64)
url='https://gitlab.freedesktop.org/slirp/libslirp'
license=(BSD MIT)
depends=(glib2)
makedepends=(meson)
provides=('libslirp.so')
source=("${url}/-/archive/v${pkgver}/libslirp-v${pkgver}.tar.gz")
sha512sums=('93d3f429849dc5e75d5ca5eccba3b781d5cb95cd2443636c5c0d68a0aefdc2192946d196d0535dfffeed41b15b09e1d67b81da99f7bf8d4762b53d8f62d0a84e')
b2sums=('4713d611f62119a1e4f3970d95e64ec92a7aee2670e533a60e458d46abd9820e25585cfb7f344acb8dc64cd9ae55b9ce1153ac1b37ee27b316ed1014c377a685')

build() {
  artix-meson "$pkgname-v${pkgver}" build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -vDm 644 "$pkgname-v${pkgver}"/COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
