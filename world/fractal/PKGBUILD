# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=fractal
pkgver=14
pkgrel=1
pkgdesc="Matrix group messaging app"
arch=(x86_64)
url="https://gitlab.gnome.org/World/fractal"
license=(GPL-3.0-only)
depends=(
  dconf
  emoji-font
  fontconfig
  gdk-pixbuf2
  glib2
  glibc
  glycin
  glycin-gtk4  # dlopen'ed
  graphene
  gst-plugins-bad-libs
  gst-plugins-base-libs
  gst-plugin-gtk4
  gstreamer
  gtk4
  gtksourceview5
  hicolor-icon-theme
  lcms2
  libadwaita
  libgcc
  libpipewire
  libseccomp
  libshumate
  openssl
  org.freedesktop.secrets
  pango
  sqlite
)
makedepends=(
  blueprint-compiler
  clang
  cmake
  dart-sass
  libwebp
  meson
  rust
  xdg-desktop-portal
)
optdepends=(
  'gst-plugins-good: for video support'
)
source=(
  $url/-/archive/$pkgver/$pkgname-$pkgver.tar.gz
)
sha512sums=('2b0cf2d94f6b0ddb6bea2246a52332130fdf954a165471c1fbb120223808692ca8aa7982b92ae6c3ee91f22179800387d8780998330a3b41a27170929551c1b8')
b2sums=('2a9411166975d5247f8b771ece1d0b6549da6fa57bf1725ad6f1705de6bfb489c36d2bbbb1766d7ac043b21e3b77bcd36e0903e1334127055ffc99ac6c984a82')

prepare() {
  # NOTE: usptream uses a custom CARGO_HOME from within meson
  export CARGO_HOME="$(pwd)/build/cargo-home"
  cd $pkgname-$pkgver
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  CFLAGS+=' -ffat-lto-objects'
  artix-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  # NOTE: explicitly prevent rebuild: https://gitlab.gnome.org/GNOME/fractal/-/issues/1327
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
