# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=fractal
pkgver=12.1
pkgrel=1
pkgdesc="Matrix group messaging app"
arch=(x86_64)
url="https://gitlab.gnome.org/World/fractal"
license=(GPL-3.0-only)
depends=(
  dconf
  fontconfig
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  glycin  # dlopen'ed
  graphene
  gst-plugins-bad-libs
  gst-plugins-base-libs
  gst-plugin-gtk4
  gstreamer
  gtk4
  gtksourceview5
  hicolor-icon-theme
  lcms2
  libadwaita
  libpipewire
  libseccomp
  libshumate
  openssl
  org.freedesktop.secrets
  pango
  sqlite
)
makedepends=(
  clang
  cmake
  dart-sass
  libwebp
  meson
  rust
  xdg-desktop-portal
)
source=(
  $url/-/archive/$pkgver/$pkgname-$pkgver.tar.gz
)
sha512sums=('93739acdeabbf4ec6c3d9abd2fb4ee2ec7f01faf69aa58c73c89ed7fd801cdd308b623a7e9bde1106df77c95cbb8c9b6461bfd5fdc7ef4bc4e2fdad48490ac49')
b2sums=('62180f229af221221a32aa1ac866b0ae8e66d43e0de1dd1993702a2cda607ccb3a5590fade4c235e21afee5b9725534b15e23796b4742e557aa5163b74e183b8')

prepare() {
  # NOTE: usptream uses a custom CARGO_HOME from within meson
  export CARGO_HOME="$(pwd)/build/cargo-home"
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  CFLAGS+=' -ffat-lto-objects'
  artix-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  # NOTE: explicitly prevent rebuild: https://gitlab.gnome.org/GNOME/fractal/-/issues/1327
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
