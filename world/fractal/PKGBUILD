# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=fractal
pkgver=10.1
pkgrel=1
pkgdesc="Matrix group messaging app"
arch=(x86_64)
url="https://gitlab.gnome.org/World/fractal"
license=(GPL-3.0-only)
depends=(
  dconf
  fontconfig
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  glycin  # dlopen'ed
  graphene
  gst-plugins-bad-libs
  gst-plugins-base-libs
  gst-plugin-gtk4
  gstreamer
  gtk4
  gtksourceview5
  hicolor-icon-theme
  lcms2
  libadwaita
  libpipewire
  libseccomp
  libshumate
  openssl
  org.freedesktop.secrets
  pango
  sqlite
)
makedepends=(
  clang
  cmake
  dart-sass
  libwebp
  meson
  rust
  xdg-desktop-portal
)
source=(
  $url/-/archive/$pkgver/$pkgname-$pkgver.tar.gz
)
sha512sums=('93793a9d6e6f8999a488655ee0b6dbad7b96e1eef33801dc0b0d885afe1820baf35e97c2e0fed6ca46ad119bfa86a8e56435fa7daf3d03c0734409c56a3618c7')
b2sums=('c5899533ae062adb946580ab70e48e9f580da8a3b72a1aa5e11c33969a5c1abd55755202cf0e78d8af187faef349ea36b13761d7e4d8a7c1fb6d19e1d0ddf3b1')

prepare() {
  # NOTE: usptream uses a custom CARGO_HOME from within meson
  export CARGO_HOME="$(pwd)/build/cargo-home"
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  CFLAGS+=' -ffat-lto-objects'
  artix-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  # NOTE: explicitly prevent rebuild: https://gitlab.gnome.org/GNOME/fractal/-/issues/1327
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
