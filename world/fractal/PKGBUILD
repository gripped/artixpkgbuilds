# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=fractal
pkgver=11.1
pkgrel=1
pkgdesc="Matrix group messaging app"
arch=(x86_64)
url="https://gitlab.gnome.org/World/fractal"
license=(GPL-3.0-only)
depends=(
  dconf
  fontconfig
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  glycin  # dlopen'ed
  graphene
  gst-plugins-bad-libs
  gst-plugins-base-libs
  gst-plugin-gtk4
  gstreamer
  gtk4
  gtksourceview5
  hicolor-icon-theme
  lcms2
  libadwaita
  libpipewire
  libseccomp
  libshumate
  openssl
  org.freedesktop.secrets
  pango
  sqlite
)
makedepends=(
  clang
  cmake
  dart-sass
  libwebp
  meson
  rust
  xdg-desktop-portal
)
source=(
  $url/-/archive/$pkgver/$pkgname-$pkgver.tar.gz
)
sha512sums=('8c9ad68cc42fd96b254eb0cd6084e3b0e69c3b30684af71c8d136a109004ef2cd7c18d10b64a2c6d1b17aff3cecf627f07ffe0071572a55ee6efaa21781f383c')
b2sums=('6fab0907691b2332d9492a82b6534022df26930a5c7c3cf115f7ca363186a3ea2c3a0fdc9c72894421008afb68ea78a0aad01eb4d39464eb097c929f0b358f94')

prepare() {
  # NOTE: usptream uses a custom CARGO_HOME from within meson
  export CARGO_HOME="$(pwd)/build/cargo-home"
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  CFLAGS+=' -ffat-lto-objects'
  artix-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  # NOTE: explicitly prevent rebuild: https://gitlab.gnome.org/GNOME/fractal/-/issues/1327
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
