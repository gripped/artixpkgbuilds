# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

pkgname=viskores
pkgver=1.1.0
pkgrel=1
pkgdesc="A visualization library for many-threaded devices"
arch=(x86_64)
url="https://github.com/Viskores/viskores"
license=(LicenseRef-viskores)
depends=(
  gcc-libs
  glibc
  onetbb
  openmpi libmpi.so
)
makedepends=(
  cmake
  ninja
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
)
b2sums=('016e697a693cfa909864307dbff34f06a095b40f3454e3aba883d2384752ead4493aa1bc51108e7130ec47b5ba4dab3825eef8b7e599206ca54152364d74815b')

build() {
  local cmake_options=(
    -B build
    -S $pkgname-$pkgver
    -G Ninja
    -W no-dev
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    # make sure the options are compatible with VTK requirements
    # https://gitlab.kitware.com/vtk/vtk/-/blob/master/ThirdParty/viskores/vtkviskores/CMakeLists.txt
    -D Viskores_ENABLE_OPENMP=ON
    -D Viskores_ENABLE_TBB=ON
    -D Viskores_ENABLE_MPI=ON
    -D Viskores_USE_DEFAULT_TYPES_FOR_VTK=ON
    # do not install in versioned subdirectories
    -D Viskores_INSTALL_INCLUDE_DIR=include
    -D Viskores_INSTALL_CONFIG_DIR=lib/cmake/viskores
    -D Viskores_INSTALL_SHARE_DIR=share/viskores
  )
  cmake "${cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
