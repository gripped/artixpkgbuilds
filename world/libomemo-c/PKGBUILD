# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=libomemo-c
pkgver=0.5.1
pkgrel=1
pkgdesc='Fork of libsignal-protocol-c adding support for OMEMO XEP-0384 0.5.0+'
arch=(x86_64)
url='https://github.com/dino/libomemo-c'
license=(GPL-3.0-or-later)
depends=(glibc
         protobuf-c)
makedepends=(cmake
             git)
source=(git+https://github.com/dino/libomemo-c#tag=v$pkgver
        cmake-4.patch)
sha256sums=('997e56d1ca8f8d2d3644cf51a7fabbe805947fb892fff9da109ce597d952915d'
            '667523eab8a91621787334bf99c7519ef46be4a0c9b14e567189d76a601ab74c')

prepare() {
  patch -d $pkgname -p1 < cmake-4.patch
}

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
