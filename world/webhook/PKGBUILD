# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=webhook
pkgver=2.8.3
pkgrel=1
pkgdesc="A lightweight incoming webhook server to run shell commands"
arch=('x86_64')
url="https://github.com/adnanh/webhook"
license=('MIT')
depends=('glibc')
makedepends=('go' 'git')
source=("git+https://github.com/adnanh/webhook.git#tag=$pkgver")
sha512sums=('4978ec2620428f94fd6e70f0c5854d5f18a7607b41f918eaed2806fb5262086097c7137def5a9ba93f68cc7ba9ac10727038c8f9738b1d0a5651e85f0db50ff5')

prepare() {
  cd webhook

  mkdir -p .gopath/src/github.com/adnanh
  ln -sf "$PWD" .gopath/src/github.com/adnanh/webhook
  export GOPATH="$PWD/.gopath:/usr/share/gocode"

  rm -r vendor

  go get github.com/codegangsta/negroni github.com/gorilla/mux github.com/satori/go.uuid \
         gopkg.in/fsnotify.v1 github.com/ghodss/yaml
}

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  cd webhook
  go build -ldflags="-s -w -buildid=''" -o build/webhook
}

package() {
  cd webhook

  install -Dm755 build/webhook "$pkgdir"/usr/bin/webhook
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/

}
