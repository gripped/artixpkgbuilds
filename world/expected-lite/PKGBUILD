# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Hu <integral@archlinux.org>

pkgname=expected-lite
pkgver=0.10.0
pkgrel=1
pkgdesc="Expected objects in C++11 and later in a single-file header-only library"
url="https://github.com/martinmoene/${pkgname}"
arch=('any')
license=('BSL-1.0')
makedepends=('cmake')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('cfe082e4ffedeeedac47763504102646a39c080599c7c1fe99299d6a1f99af92')

build() {
	cmake -B build \
		-S "${pkgname}-${pkgver}" \
		-D CMAKE_BUILD_TYPE=None \
		-D CMAKE_INSTALL_PREFIX=/usr

	cmake --build build
}

check() {
	ctest --test-dir build --output-on-failure
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
}
