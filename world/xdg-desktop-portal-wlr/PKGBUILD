# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Maxim Baz <archlinux at maximbaz dot com>
# Contributor: Stefan Tatschner <stefan@rumpelsepp.org>

pkgname=xdg-desktop-portal-wlr
pkgver=0.8.4
pkgrel=1
pkgdesc='xdg-desktop-portal backend for wlroots'
arch=('x86_64')
url="https://github.com/emersion/xdg-desktop-portal-wlr"
license=('MIT')
depends=(
  'glibc'
  'grim'
  'libdrm'
  'libinih'
  'libpipewire'
  'mesa'
  'pipewire'
  'pipewire-session-manager'
  'slurp'
  'libelogind'
  'wayland'
  'xdg-desktop-portal'
)
makedepends=(
  'meson'
  'scdoc'
  'wayland-protocols'
)
provides=('xdg-desktop-portal-impl')
source=(
  "$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
  wlr-portals.conf
)
sha512sums=('05c4ec1148aec480f38d83cfe99be19144315e9848584664956dd56e36c07792df90807d0e674318dda87be012c0ecdea58f49e54a5401aba4f8673c63714221'
            '71f3383c3f62100dc669c519d107614a4566e808deddf66a179dd2bf175dc272a2390965cf38cd11dc4f0db0dc5cc7104712805034b9bced5874d62b03a579c2')

build() {
  cd "$pkgname-$pkgver"
  artix-meson -Dsystemd=disabled -Dsd-bus-provider=libelogind build
  ninja -C build
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" ninja -C build install
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # fix detection with x-d-p 1.18
  install -vDm644 -t "$pkgdir/usr/share/xdg-desktop-portal" "$srcdir/wlr-portals.conf"
}
