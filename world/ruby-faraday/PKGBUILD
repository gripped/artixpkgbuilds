# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Tim Meusel <tim@bastelfreak.de>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Alfredo Palhares <masterkorp@masterkorp.net>
# Contributor: Jochen Schalanda <jochen+aur@schalanda.name>

_gemname='faraday'
pkgname="ruby-${_gemname}"
pkgver=2.13.0
pkgrel=3
pkgdesc='HTTP/REST API client library.'
arch=('any')
url="https://github.com/lostisland/${_gemname}"
license=('MIT')
depends=(
  ruby
  ruby-faraday-net_http
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bundler
  ruby-pry
  ruby-rack
  ruby-rake
  ruby-rspec
  ruby-webmock
)
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${_gemname}-v${pkgver}.tar.gz")
sha512sums=('c43ea8a2bab64a2403cc9a42ea25554c08491b99ad46ea5406ef29f7a2c729478c0dca751fb671875a53868df44b69f3d789650c826abe5363ceb4d7a97557ea')
b2sums=('d43cd20ce9d0f3893e61c048d5dc9fc760dbfa5e73f33f01c993ec52ff65f407076df10b5f1bf11c3d47070c225daabe0ff639bc179f8ea32a5f8e8b43a2db16')

prepare() {
  cd "${_gemname}-${pkgver}"

  # allow latest dependencies
  sed --in-place --regexp-extended \
    --expression 's|~>|>=|g' \
    --expression "s|, '< [0-9\.]+'||g" \
    "${_gemname}.gemspec"

  # we don't need test coverage
  sed --in-place --regexp-extended \
    --expression '/SimpleCov\.start/,/end/d' \
    --expression '/simplecov/Id' \
    --expression '/coveralls/d' \
    spec/spec_helper.rb
}

build() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rake spec
}

package() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
