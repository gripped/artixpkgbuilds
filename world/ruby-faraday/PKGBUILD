# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Tim Meusel <tim@bastelfreak.de>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Alfredo Palhares <masterkorp@masterkorp.net>
# Contributor: Jochen Schalanda <jochen+aur@schalanda.name>

_gemname='faraday'
pkgname="ruby-${_gemname}"
pkgver=2.12.1
pkgrel=1
pkgdesc='HTTP/REST API client library.'
arch=('any')
url="https://github.com/lostisland/${_gemname}"
license=('MIT')
depends=(
  ruby
  ruby-faraday-net_http
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bundler
  ruby-pry
  ruby-rack
  ruby-rake
  ruby-rspec
  ruby-webmock
)
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${_gemname}-v${pkgver}.tar.gz")
sha512sums=('df136a91bd0bdbece9bc05b6ad67c226ddc38695bc97049949b16d07d1dbcec69f85b1ce17e509dd7083cdd7181561a20313b283fad7eb20fd91032639c3138c')
b2sums=('980694eb3ddfe4cc2491da98d0620d5e41a635b28d390e83c40bcca1e195aa5502e082301606cb7647683363cd17dde2a0e96f3d2aa162d451313fe81727aef7')

prepare() {
  cd "${_gemname}-${pkgver}"

  # allow latest dependencies
  sed --in-place --regexp-extended \
    --expression 's|~>|>=|g' \
    --expression "s|, '< [0-9\.]+'||g" \
    "${_gemname}.gemspec"

  # we don't need test coverage
  sed --in-place --regexp-extended \
    --expression '/SimpleCov\.start/,/end/d' \
    --expression '/simplecov/Id' \
    --expression '/coveralls/d' \
    spec/spec_helper.rb
}

build() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rake spec
}

package() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
