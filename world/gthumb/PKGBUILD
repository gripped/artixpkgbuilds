# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=gthumb
pkgver=3.12.10
pkgrel=1
pkgdesc="Image browser and viewer for the GNOME Desktop"
url="https://wiki.gnome.org/Apps/Gthumb"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  exiv2
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gsettings-desktop-schemas
  gst-plugin-gtk
  gst-plugins-base-libs
  gstreamer
  gtk3
  hicolor-icon-theme
  lcms2
  libcolord
  libheif
  libjpeg-turbo
  libjxl
  libpng
  librsvg
  libtiff
  libwebp
  libx11
  pango
  zlib
)
makedepends=(
  appstream
  brasero
  docbook-xsl
  git
  glib2-devel
  libraw
  meson
  yelp-tools
)
optdepends=(
  'brasero: burn discs'
  'libraw: read RAW files'
)
source=(
  "git+https://gitlab.gnome.org/GNOME/gthumb.git#tag=$pkgver"
)
b2sums=('71c6117d28e08d2704eddc0824e2d1f38aaaebf4eee25c501d4adb69c2a6877ffa02509368ef6887f731be8c9c598ef7a55f3feb96cc934fd9fc8149ee4bf329')

prepare() {
  cd gthumb
}

build() {
  artix-meson gthumb build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
