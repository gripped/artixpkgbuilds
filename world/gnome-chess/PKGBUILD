# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-chess
pkgver=50.1
pkgrel=1
pkgdesc="Play the classic two-player boardgame of chess"
url="https://wiki.gnome.org/Apps/Chess"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  librsvg
  pango
)
makedepends=(
  git
  gobject-introspection
  meson
  vala
  yelp-tools
)
optdepends=('gnuchess: Play against computer')
groups=(gnome-extra)
source=("git+https://gitlab.gnome.org/GNOME/gnome-chess.git#tag=${pkgver/[a-z]/.&}")
b2sums=('f1dfe86a3cc6bd274b644ece297348e90007f002f333e2ec0b0672a442aef9d2224e2508440130d4c148424c6fa8b5b28159c137bed743049ec9e3628e530afa')
validpgpkeys=(
  EF033B6B1044EEE7F743A1737F71B64279363298 # Michael Catanzaro <mcatanzaro@innerfocus.xyz>
)

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
