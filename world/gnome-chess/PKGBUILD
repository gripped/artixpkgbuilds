# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-chess
pkgver=49.0
pkgrel=1
pkgdesc="Play the classic two-player boardgame of chess"
url="https://wiki.gnome.org/Apps/Chess"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  librsvg
  pango
)
makedepends=(
  git
  gobject-introspection
  meson
  vala
  yelp-tools
)
optdepends=('gnuchess: Play against computer')
groups=(gnome-extra)
source=("git+https://gitlab.gnome.org/GNOME/gnome-chess.git?signed#tag=${pkgver/[a-z]/.&}")
b2sums=('9768ace572f982c1ed2a574ae17de0fa1c9c13c76bb50165ad89d1237a3413693c58e44fa50701133c2d4b375b11d1fb87a4f8c434b94a13518e7d87da8b59e3')
validpgpkeys=(EF033B6B1044EEE7F743A1737F71B64279363298) # Michael Catanzaro <mcatanzaro@innerfocus.xyz>

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
