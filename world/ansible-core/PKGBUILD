# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Chris <seitz.christoph@gmail.com>
# Contributor: m0ikz <ndelatorre@moikz.com.ar>
# Contributor: atweiden <archbaum@gmail.com>

pkgname=ansible-core
_pkgname=ansible
pkgver=2.18.7
pkgrel=1
pkgdesc='Radically simple IT automation platform'
arch=('any')
url='https://www.ansible.com'
license=('GPL-3.0-or-later AND BSD-2-Clause AND PSF-2.0 AND MIT AND Apache-2.0')
depends=(
  'python'
  'python-cryptography'
  'python-jinja'
  'python-packaging'
  'python-pyyaml'
  'python-resolvelib'
  # not directly required, but either convenient or indirectly required
  'python-paramiko'
  'python-typing_extensions'
)
makedepends=(
  'python-build'
  'python-docutils'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'git'
  'openssh'
  'python-bcrypt'
  'python-botocore'
  'python-passlib'
  'python-pexpect'
  'python-pytest'
  'python-pytest-mock'
  'python-pytest-xdist'
  'python-pytest-forked'
  'python-pywinrm'
  'python-voluptuous'
)
optdepends=(
  'python-argcomplete: shell completions'
  'python-dnspython: for dig lookup'
  'python-jmespath: json_query support'
  'python-netaddr: for the ipaddr filter'
  'python-passlib: crypt values for vars_prompt'
  'python-pip: for module to manage Python libarary dependencies'
  'python-pywinrm: connect to Windows machines'
  'python-setuptools: for module to manage Python libarary dependencies'
  'sshpass: for ssh connections with password'
)
provides=('python-ansible' 'ansible-base')
replaces=('ansible-base')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ansible/ansible/archive/refs/tags/v${pkgver}.tar.gz"
        'bump_resolvelib_upper_version_bound.patch'
        'relax_setuptools_and_wheel_version_requirements.patch')
sha512sums=('577e6939d4ad309209c6d2d683695d288ece26034aeadfa7cbd4c461795482373b79f4f6255390d50fc782015460a22207b9fd5c65daf664492acb3055169631'
            '87553991bd3e9b7b485819e9041dfdd194dd2746699f26cb0ef7f650d5c7c88a573f3517c48ac2dd49a467ccf9838fec53c4a7e9d4d860bc2441d245032a1ce7'
            '556d2a9ba7927a0700114ed218fac6271c8a663e238c0c221f7f9a8b724fdc9206c6e81c50a129be960a6c2adfe8b18aaa3a4644e51d8bddbca0551351b31cf1')
b2sums=('eb8e24004de5ea4fc7561789aeb4d0ef143195f1c9713ea93bea7e2a59fb8434ab62026e76ca7b1aedd8462869342da0447735f5a866eb6263b3e7c917979414'
        'a9178d94e0a7862a72123d00cf87c19d52c1bfce51c874401ec0b0491c5acd94f4d83dcc3567779010debe4c2d4e048d9de0676169b2beaacd12a0ff527562e3'
        '16cd25adc66b4ecc1438991c084444a8da1abe16b6e09d12811d9a41439f4b40834f968099482d831d760eab28a656c0de755223fa245fcd3114d52003c088c8')

prepare() {
  cd "${_pkgname}-${pkgver}"

  # Bump resolvelib upper bound version requirement to allow building / running with latest resolvelib
  patch -Np1 -i "${srcdir}/bump_resolvelib_upper_version_bound.patch"

  # Upstream is applying very strict upper bound version requirements for setuptools and wheel
  # We relax those to avoid unnecessary build failures
  patch -Np1 -i "${srcdir}/relax_setuptools_and_wheel_version_requirements.patch"
}

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
  python packaging/cli-doc/build.py man --output-dir man
}

check() {
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')

  # tests require upstream wrapper to find ansible-core internals: https://github.com/ansible/ansible/issues/80472
  cd "${_pkgname}-${pkgver}"
  # we do not have libselinux packaged
  rm -v test/units/module_utils/basic/test_selinux.py
  bin/ansible-test units --python "${python_version}" --truncate 0
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl

  install -Dm 644 man/ansible*1 -t "${pkgdir}/usr/share/man/man1/"
  install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  install -Dm 644 licenses/MIT-license.txt "${pkgdir}/usr/share/licenses/${pkgname}/MIT-license.txt"
  install -Dm 644 licenses/simplified_bsd.txt "${pkgdir}/usr/share/licenses/${pkgname}/simplified_bsd.txt"
}
