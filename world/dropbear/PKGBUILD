# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Bartlomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Jason Pierce <pierce.json@gmail.com>
# Contributor: Jeremy Cowgar <jeremy@cowgar.com>
# Contributor: Simon Perry <aur@sanxion.net>

pkgbase=dropbear
pkgname=(dropbear dropbear-scp)
pkgver=2025.87
pkgrel=1
pkgdesc='Lightweight SSH server'
arch=(x86_64)
url='https://github.com/mkj/dropbear'
license=(MIT)
options=(emptydirs)
makedepends=(git)
validpgpkeys=('F7347EF2EE2E07A267628CA944931494F29C6773')
source=("git+$url#tag=DROPBEAR_$pkgver"
        localoptions.h)
b2sums=('aafd9009135b62f5a99e53a2a410909ac264dce37f1df78f10384bc6e285d0e090fd789f7903188aef5a15c1deb0c7bdf99de8fcbe7dcadf7c12bbd58c4f0ec2'
        '49628bbce0d26eb33598de276ede705bd592a38b7892c9db0ceebfb0598a1ee6e7e84ed03dbf255ee1adcad0c5214d38dc03e6c04176a405f59a19765cb28388')

prepare() {
  cp -fv localoptions.h $pkgname/localoptions.h
}

build() {
  cd $pkgname
  autoreconf
  ./configure --bindir=/usr/bin --prefix=/usr --sbindir=/usr/bin
  make PROGRAMS='dbclient dropbear dropbearconvert dropbearkey scp' SCPPROGRESS=1
}

package_dropbear() {
  depends=(libxcrypt zlib)
  install -d "$pkgdir/etc/$pkgname"
  make -C $pkgbase install DESTDIR="$pkgdir"
  install -Dm644 $pkgbase/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_dropbear-scp() {
  pkgdesc='Lightweight application for copying files over SSH'
  provides=(scp)
  conflicts=(openssh)
  make -C $pkgbase install PROGRAMS=scp DESTDIR="$pkgdir"
  install -Dm644 $pkgbase/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  rmdir "$pkgdir/usr/share/man/"{man1,}
}
