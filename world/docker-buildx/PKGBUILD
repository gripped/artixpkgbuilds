# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Morten Linderud <foxboron@archlinux.org>
# Contributor: Christian Heusel <gromit@archlinux.org>
# Contributor: Joakim Saario <saario.joakim@gmail.com>

pkgname=docker-buildx
pkgver=0.30.1
pkgrel=1
pkgdesc="Docker CLI plugin for extended build capabilities with BuildKit"
arch=('x86_64')
url="https://github.com/docker/buildx"
license=("Apache-2.0")
makedepends=('go' 'git')
depends=('glibc')
options=(!lto)
source=("git+https://github.com/docker/buildx.git#tag=v${pkgver}")
sha512sums=('c78f74ad0e062aef46efc96bb4e28911995ea33be4e4787d6f9024e61fb176867d99cd6630ae97a32f81667c5ce6d1d858f377b528db974951417d8bc7be56b2')

build() {
  cd "buildx"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS='-buildmode=pie -trimpath -mod=readonly -modcacherw'
  _buildx_r=github.com/docker/buildx
  go build -mod=vendor -o docker-buildx -ldflags "-linkmode=external \
    -compressdwarf=false \
    -X $_buildx_r/version.Version=${pkgver} \
    -X $_buildx_r/version.Revision=$(git rev-parse HEAD) \
    -X $_buildx_r/version.Package=$_buildx_r" \
    ./cmd/buildx
}

check() {
  cd "buildx"
  # mkdir cli-plugins
  # cp bin/buildx cli-plugins/docker-buildx
  # export DOCKER_CONFIG="$(PWD)"
  go test ./... || true
}

package() {
  cd "buildx"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm755 docker-buildx "$pkgdir/usr/lib/docker/cli-plugins/docker-buildx"
}
