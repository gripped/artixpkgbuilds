# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-async-utilization
pkgver=0.4.0
pkgrel=1
pkgdesc='High-performance utilization metrics for Async services using shared memory'
arch=(any)
url='https://github.com/socketry/async-utilization'
license=(MIT)
depends=(
  ruby
  ruby-console
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-sus
  ruby-sus-fixtures-console
)
options=(!emptydirs)
source=("git+https://github.com/socketry/async-utilization.git#tag=v$pkgver")
sha512sums=('6a55506b36bebab710c87fcab2a2c414453aa064ef63a359a12af6872d61868ecc10c69ca952e6c1d215f0128282f5fcd06ddfdc2166909b0d1535dd87834606')
b2sums=('bae9e4f984d5d1fb068127a6ddbbeae3d373a7e5ca029a415d52369b0a3eb6783486d0fb0a4da5ecbf0e4f56b42db24385269b1e4e7e4a077eaf64493d33dee6')

prepare() {
  cd async-utilization

  sed -r -e 's|~>|>=|g' -e '/signing_key/d' -i async-utilization.gemspec
  sed -i \
    -e '/group :maintenance/,/end/d' \
    -e '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd async-utilization

  gem build async-utilization.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    async-utilization-$pkgver.gem

  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  rm -r "tmp_install/$_gemdir/cache"
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd async-utilization

  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd async-utilization

  cp -a tmp_install/* "$pkgdir"/
  install -vDm 644 readme.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 license.md -t "$pkgdir/usr/share/licenses/$pkgname/"
}
