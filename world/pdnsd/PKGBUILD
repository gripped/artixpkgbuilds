# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Henrik Nymann Jensen <h@henriknj.dk>

pkgname=pdnsd
pkgver=1.2.9.a
_pkgver=${pkgver/.a/a}
pkgrel=16
pkgdesc="Proxy DNS server with permanent caching"
arch=(x86_64)
url="http://members.home.nl/p.a.rombouts/pdnsd/"
license=('GPL-3.0-only')
depends=()
source=(https://repo.p5n.pp.ru/~sergej/dl/2024/pdnsd-${_pkgver}.tar.xz
	build-fix.patch
	pdnsd.sysusers
	pdnsd.tmpfiles)
sha512sums=('e41e7127900c02ea9f36eefa48e78b1382f3d8eb8be64085163d55c5100485178e538d34cdf269c707d7dcbdfa8dacfc06ae90767ae44b11ab79a52133f3e216'
            '251e81089aeac517693deaef837e56af752bccddff452906fee3bae01eb4af0dcc8706c3a791bdb3f55678ae7ab96a32e89a2730b9d4d3012458cb8c1641053d'
            '9221d57a0ab45ad30078e071b4e163ff75d4e13b74e2425bf5dbe6cd067391f3f392aef046286449d73303bb01a9d1f72f29bc50bfc56e51e50d0b97cd6737b3'
            'dbd30b02c8cc21531133573e10704e02ceea5d5cee18c96542398a815ea8b1292aa025664f0cd2c10d248578058e0afb9b0215f6c421a501c2555940d56d4373')

prepare() {
  cd "$srcdir/$pkgname-$_pkgver"
  patch -p1 <../build-fix.patch
}

build() {
  cd "$srcdir/$pkgname-$_pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin --enable-ipv6
  make
}

package() {
  cd "$srcdir/$pkgname-$_pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm0644 ../pdnsd.sysusers "$pkgdir/usr/lib/sysusers.d/pdnsd.conf"
  install -Dm0644 ../pdnsd.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/pdnsd.conf"

  install -dm0755 "$pkgdir/usr/share/doc/pdnsd"
  sed -i 's/"nobody"/"pdnsd"/' "$pkgdir/etc/pdnsd.conf.sample"
  mv "$pkgdir/etc/pdnsd.conf.sample" "$pkgdir/usr/share/doc/pdnsd/pdnsd.conf"

  rm -r "$pkgdir"/{etc,var}
}
