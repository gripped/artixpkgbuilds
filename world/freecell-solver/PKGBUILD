# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=freecell-solver
pkgver=6.12.0
pkgrel=3
pkgdesc='A program that automatically solves layouts of Freecell and similar variants of Card Solitaire'
arch=(x86_64)
url='https://fc-solve.shlomifish.org'
license=(MIT)
depends=(bash
         glibc)
makedepends=(cmake
             gperf
             perl-moo
             perl-path-tiny
             perl-template-toolkit
             python-pysol_cards
             python-six
             rinutils)
optdepends=('python-pysol_cards: for the Python interface'
            'python-six: for the Python interface')
source=(https://fc-solve.shlomifish.org/downloads/fc-solve/$pkgname-$pkgver.tar.xz
        no-random2.patch)
sha256sums=('a2b89e804ce4b918ef749031676210f2095fea3a8cb129805602843c7c4884a0'
            '1546187f3ae12c8128d079a926a997cee533fb4a1a3c66599c6bcc0ed5554b72')

prepare() {
  patch -d $pkgname-$pkgver -p3 < no-random2.patch # Stop searching for unused random2
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DFCS_WITH_TEST_SUITE=OFF \
    -DBUILD_STATIC_LIBRARY=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname-$pkgver/COPYING.html "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.html
}
