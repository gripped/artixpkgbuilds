# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Christian Heusel <gromit@archlinux.org>

_name=orjson
pkgname=python-orjson
pkgver=3.13.0
pkgrel=1
pkgdesc="Fast, correct Python JSON library supporting dataclasses and datetimes"
arch=(x86_64)
url="https://github.com/ijl/orjson"
license=('Apache-2.0 OR MIT')
depends=(
  glibc
  libgcc
  python
)
makedepends=(
  maturin
  python-installer
  rust
)
checkdepends=(
  python-arrow
  python-pendulum
  python-psutil
  python-pytest
  python-pytz
  python-xxhash
)
source=($url/archive/$pkgver/$_name-$pkgver.tar.gz)
sha512sums=('d468ada08735183710d03ec8415bacd5ddcf8a95b00c9b04964fe13ac046872c795512cb98777133690ffcdd7ffcf6c8183f195172e0e169b33fd5efe2437960')
b2sums=('f5b1fbc2793cc2c5fe87a5d0b67e215830f5d19a7d35896e93cba571219890ca6c455e464c5886ac42ea97265f4ec0b6059d0601206a457e3443fed15c0d1c64')

build() {
  # Full LTO removes symbols from the resulting library.
  # https://github.com/ijl/orjson/issues/613
  CFLAGS+=" -ffat-lto-objects"

  cd $_name-$pkgver
  maturin build --release --strip
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir target/wheels/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" target/wheels/*.whl
  install -vDm 644 {CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE-* -t "$pkgdir/usr/share/licenses/$pkgname/"
}
