# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=geocode-glib
pkgname=(
  geocode-glib-common
  geocode-glib
  geocode-glib-2
)
pkgver=3.26.4
pkgrel=5
pkgdesc="Helper library for geocoding services"
url="https://gitlab.gnome.org/GNOME/geocode-glib"
arch=(x86_64)
license=(LGPL-2.0-or-later)
makedepends=(
  gcc-libs
  git
  glib2
  glib2-devel
  glibc
  gobject-introspection
  gtk-doc
  hicolor-icon-theme
  json-glib
  libsoup
  libsoup3
  meson
)
checkdepends=(glibc-locales)
source=("git+https://gitlab.gnome.org/GNOME/geocode-glib.git#tag=$pkgver")
b2sums=('c235a8ebf5d5b7ef19d8a6ff0b8b4f2d7e1609350ffc915cf4c9252b9ee3401383e0aab1ebde4aed8ab02c8e594f83a33a411a4b28093f091bc25d3cf6a74eea')

prepare() {
  cd geocode-glib
}

build() {
  local meson_options=(
    -D enable-installed-tests=false
  )

  artix-meson geocode-glib build2 "${meson_options[@]}" -D soup2=true
  artix-meson geocode-glib build3 "${meson_options[@]}" -D soup2=false
  meson compile -C build2
  meson compile -C build3
}

check() {
  meson test -C build2 --print-errorlogs
  meson test -C build3 --print-errorlogs
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_geocode-glib-common() {
  pkgdesc+=" (common files)"
  depends=(hicolor-icon-theme)

  meson install -C build2 --destdir "$pkgdir"
  meson install -C build3 --destdir "$pkgdir"

  cd "$pkgdir"

  _pick soup2 usr/include/geocode-glib-1.0
  _pick soup2 usr/lib/libgeocode-glib.so*
  _pick soup2 usr/lib/pkgconfig/geocode-glib-1.0.pc
  _pick soup2 usr/lib/girepository-1.0/GeocodeGlib-1.0.typelib
  _pick soup2 usr/share/gir-1.0/GeocodeGlib-1.0.gir
  _pick soup2 usr/share/gtk-doc/html/geocode-glib

  _pick soup3 usr/include/geocode-glib-2.0
  _pick soup3 usr/lib/libgeocode-glib-2.so*
  _pick soup3 usr/lib/pkgconfig/geocode-glib-2.0.pc
  _pick soup3 usr/lib/girepository-1.0/GeocodeGlib-2.0.typelib
  _pick soup3 usr/share/gir-1.0/GeocodeGlib-2.0.gir
  _pick soup3 usr/share/gtk-doc/html/geocode-glib-2
}

package_geocode-glib() {
  pkgdesc+=" (legacy)"
  depends=(
    "geocode-glib-common=$pkgver-$pkgrel"
    gcc-libs
    glib2
    glibc
    json-glib
    libsoup libsoup-2.4.so
  )
  provides=(libgeocode-glib.so)

  mv soup2/* "$pkgdir"
}

package_geocode-glib-2() {
  depends=(
    "geocode-glib-common=$pkgver-$pkgrel"
    gcc-libs
    glib2
    glibc
    json-glib
    libsoup3 libsoup-3.0.so
  )
  provides=(libgeocode-glib-2.so)

  mv soup3/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
