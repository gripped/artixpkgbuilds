# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-notes
pkgver=40.1
pkgrel=8
pkgdesc="Write out notes, every detail matters"
url="https://wiki.gnome.org/Apps/Notes"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  curl
  evolution-data-server
  glib2
  gnome-online-accounts
  gtk3
  json-glib
  libhandy
  libxml2
  tracker3
  webkit2gtk-4.1
)
makedepends=(
  git
  glib2-devel
  meson
  yelp-tools
)
provides=("bijiben=$pkgver")
conflicts=(bijiben)
replaces=(bijiben)
groups=(gnome-extra)
source=("git+https://gitlab.gnome.org/GNOME/gnome-notes.git#tag=BIJIBEN_${pkgver//./_}"
        gnome-notes-activate.patch
        gnome-notes-invalid-search.patch
        gnome-notes-prgname.patch
        gnome-notes-new-note.patch
        gnome-notes-note-trashed.patch)
b2sums=('794f39b637b48a8a64a0fdb98fbd1d346f0a22dc2c752dfbba97f3ffe1c3c6b5d44cd3bbe64fdcc4877065140d6e7b3d3f3961ca872df05c4df22b617729a83d'
        'a198a7af2695dafd3cd39987eac066674e42820e7c774296a887c68225262afb6bfa8bd0e97c0935e9d691d5753c8cc3bc2407162fc1cec34775f4dd4846714f'
        '67f9f875c2847dab0b6a9215ab42b65f47744a5a31b9b802371efd4df3e4a44f8a6e2065279f234e26e76d4f9ae9f0a725cb70f66a0ac311eeb93d37139657ec'
        'e521d8c0d57aba276749cd6e95d9c1e4441c4488a117f57594c79d5857b5bb5e14855a6b2228ec845cb0f1d2630bb9823a8703d65e48729298e3fc676a2a2574'
        '47b4f9403a0edeb376f8117004b3364808b9ed7b728cbbc02121eafc0916610a44405e05b0dde5353d68eff2b6b224332602fdf066032187c8e1decfc0315eac'
        'f5de2fa18f5643dfb5819842cec60e9ddf602d7bdf8ab26846386a58f6ce43669c8b12bca6ed2666f42cc33054fb9c24febf59fbd423c31372eea8a88a0582d2')

prepare() {
  cd $pkgname

  # Fix build with Meson 0.61
  git cherry-pick -n 994af76ce5144062d55d141129bf6bf5fab002ee

  # Remove libuuid dep
  git cherry-pick -n cbadb5e8b7d3e5e6672e8acab82276c174999a12

  # Soup 3
  git cherry-pick -n 8471369c54f9aa777857e2a42d82ab502c071520

  # Fix crash in shell search provider
  # https://gitlab.gnome.org/GNOME/gnome-notes/-/merge_requests/193
  git cherry-pick -n 438678f007f129b6ed636a577917223a503a484b

  # Pass open uri to the primary application instance
  # https://gitlab.gnome.org/GNOME/gnome-notes/-/merge_requests/194
  git cherry-pick -n 747ff6119895c2e9bc6560414d8b8091e2d03ee1

  # Fix activate
  # https://gitlab.gnome.org/GNOME/gnome-notes/-/merge_requests/195
  git apply -3 ../gnome-notes-activate.patch

  # Don't build result when invalid ID specified
  # https://gitlab.gnome.org/GNOME/gnome-notes/-/merge_requests/196
  git apply -3 ../gnome-notes-invalid-search.patch

  # Set prgname to application ID
  # https://gitlab.gnome.org/GNOME/gnome-notes/-/merge_requests/197
  git apply -3 ../gnome-notes-prgname.patch

  # Fix creating new note from remote instance
  # https://gitlab.gnome.org/GNOME/gnome-notes/-/merge_requests/198
  git apply -3 ../gnome-notes-new-note.patch

  # Don't try to switch to main view if detached
  # https://gitlab.gnome.org/GNOME/gnome-notes/-/merge_requests/199
  git apply -3 ../gnome-notes-note-trashed.patch
}

build() {
  local meson_options=(
    -D update_mimedb=false
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
