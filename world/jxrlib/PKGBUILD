# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor:
# Contributor: Filipe Laíns (FFY00) <lains@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=jxrlib
pkgver=1.4.4
pkgrel=1
pkgdesc="Open source implementation of jpegxr"
arch=('x86_64')
url='https://github.com/mircomir/jxrlib'
license=(BSD-2-Clause)
depends=(glibc)
makedepends=(cmake
             git)
source=(git+https://github.com/mircomir/jxrlib#tag=$pkgver
        CMakeLists.txt)
sha256sums=('2352722428e94b62c5ef526384a922f8f259e76763dddbc6d69ba098608fb37f'
            '574ff4c9fb5244c134184335fdd79422cee75bc323c1beca75e066f7f116e50d')

prepare() {
  mv CMakeLists.txt $pkgname
}

build() {
  CFLAGS+=" -Wno-incompatible-pointer-types" \
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
