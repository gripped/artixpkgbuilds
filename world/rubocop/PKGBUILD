# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

pkgname="rubocop"
pkgver=1.74.0
pkgrel=2
pkgdesc='A Ruby code style checking and code formatting tool'
arch=('any')
url='https://rubocop.org/'
license=('MIT')
provides=('ruby-rubocop')
depends=(
  ruby
  ruby-language_server-protocol
  ruby-lint_roller
  ruby-parallel
  ruby-parser
  ruby-rainbow
  ruby-regexp_parser
  ruby-rubocop-ast
  ruby-ruby-progressbar
  ruby-unicode-display_width
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  asciidoctor
  procps-ng
  ruby-bundler
  ruby-irb
  ruby-lsp
  ruby-memory_profiler
  ruby-rake
  ruby-rspec
  ruby-rubocop-performance
  ruby-rubocop-rake
  ruby-rubocop-rspec
  ruby-stackprof
  ruby-test-queue
  ruby-webmock
  ruby-yard
)
options=('!emptydirs')
source=(
  "https://github.com/rubocop/rubocop/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
  "${pkgname}_fix_tests.patch::https://github.com/rubocop/rubocop/pull/14004.patch"
)
sha512sums=('81cad3d39464d1f9e6d9ff57fa67b5ebe9ed3091c3ff764c3bd131b5aec125b57f3252ce5ee2b02962dded18cf4d5d1ce66b1a7684351a0a0b029feddca3abdc'
            'a4c4589b966af442935c95196b103f2f540cb4227f99972f40cf34318836dba1567e097f370f577b4cc09c6c630a38553a8ed8ac003b153c4ae11318fc9e3dc8')
b2sums=('2663d9e46c15496d1e973af0a92c19032a89c68990462bc9f0cc4d1d39bd0c309796be3995b4d5b6766faa8658d68d3d2ba8d3cc09673b74a787ba927dce1bb3'
        '66860a795f8fd9a67234f27805b3755e14cadddd323d50143175d81e03c66712391b31b9f1912756944e82d4c4418dc65bf8d09b3c51fc0680ee37d5ae538a02')

prepare() {
  cd "${pkgname}-${pkgver}"

  patch --verbose --strip=1 --input="../${pkgname}_fix_tests.patch"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${pkgname}.gemspec" Gemfile

  # we need webmock
  sed --in-place \
    --expression '/group/d' \
    --expression '/end/d' \
    Gemfile

  sed --in-place --regexp-extended \
    --expression '/bump/d' \
    --expression '/simplecov/d' \
    Gemfile

  rm tasks/cut_release.rake
}

build() {
  cd "${pkgname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build "${pkgname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${pkgname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install/${_gemdir}/cache/" \
    "tmp_install/${_gemdir}/gems/${pkgname}-${pkgver}/vendor/" \
    "tmp_install/${_gemdir}/doc/${pkgname}-${pkgver}/ri/ext/"

  find "tmp_install/${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install/${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${pkgname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install/${_gemdir}" rake ascii_spec spec
}

package() {
  cd "${pkgname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
