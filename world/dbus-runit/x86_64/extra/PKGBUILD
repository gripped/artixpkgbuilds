# Maintainer: Muhammad Herdiansyah <koni@artixlinux.org>

_sed_args=(-e 's|/var/run|/run|g' -e 's|\(/usr\)\?/sbin|/usr/bin|g')

pkgname=dbus-runit
pkgver=20210427
pkgrel=1
pkgdesc="runit service scripts for dbus"
arch=('any')
url="https://artixlinux.org"
license=('BSD')
depends=('dbus' 'runit')
provides=('dbus-runscripts' 'init-dbus')
conflicts=('init-dbus')
replaces=('dbus-runscripts')
source=("dbus.run"
        "dbus.check")
sha256sums=('68b377243a3fcf3639ad7f2df1d97f1ef6f21bdf99567c6d34254f5e90428675'
            'b9472d398732e64e5044fd0c9b855185c0f7ab47ba8aea45212714e47455ae74')

_inst_sv(){
    for file in run finish check; do
        if test -f "$srcdir/$1.$file"; then
            install -Dm755 "$srcdir/$1.$file" "$pkgdir/etc/runit/sv/$1/$file"
            sed "${_sed_args[@]}" -i "$pkgdir/etc/runit/sv/$1/$file"
        fi
    done
}

package() {
    _inst_sv 'dbus'
    sed -e 's|dbus.pid|dbus/pid|g' -i "${pkgdir}/etc/runit/sv/dbus"/*

    install -d "${pkgdir}/etc/runit/runsvdir/default"
    ln -sf "/etc/runit/sv/dbus" "${pkgdir}/etc/runit/runsvdir/default/dbus"
}
