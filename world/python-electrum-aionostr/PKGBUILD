# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-electrum-aionostr
pkgver=0.1.0
pkgrel=2
pkgdesc="Asyncio nostr client for Python"
arch=('any')
url="https://github.com/spesmilo/electrum-aionostr"
license=('BSD-3-Clause')
depends=(
  'python'
  'python-aiohttp'
  'python-aiohttp-socks'
  'python-aiorpcx'
  'python-cryptography'
  'python-electrum-ecc'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'python-click'
  'python-pytest'
)
optdepends=('python-click: for command line interface')
source=("git+$url.git#tag=$pkgver?signed")
sha512sums=('4ad43fcb8766062d889f477697a9bc9c50e3aed6dbdac63a0eab49b6c545c7d66fa05e6576ab09b708f0b5e9279a833a01a272c5fa8e84fce8dce63831c0f433')
validpgpkeys=('4AD64339DFA05E20B3F6AD51E7B748CDAF5E5ED9') # SomberNight <somber.night@protonmail.com>

build() {
  cd ${pkgname#python-}
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest
}

package() {
  cd ${pkgname#python-}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
 
