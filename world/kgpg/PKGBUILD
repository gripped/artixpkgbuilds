# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kgpg
pkgver=24.08.2
pkgrel=1
pkgdesc='A GnuPG frontend'
url='https://apps.kde.org/kgpg/'
arch=(x86_64)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(akonadi-contacts
         gcc-libs
         glibc
         karchive
         kcodecs
         kconfig
         kconfigwidgets
         kcontacts
         kcoreaddons
         kcrash
         kdbusaddons
         ki18n
         kio
         kservice
         kstatusnotifieritem
         ktextwidgets
         kwidgetsaddons
         kxmlgui
         libakonadi
         qt6-base)
makedepends=(extra-cmake-modules
             kdoctools)
groups=(kde-applications
        kde-utilities)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('084b4043ff3db5f9675c3992a36b69ddb3c68e9ec6f07553fcccfee34898f30c'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
