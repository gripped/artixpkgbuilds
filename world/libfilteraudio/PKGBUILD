# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Carl Smedstad <carsme@archlinux.org>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>
# Contributor: Jiachen Yang <farseerfc@gmail.com>
# Contributor: HÃ¥vard Pettersson <mail@haavard.me>
# Contributor: Ivan Shapovalov <intelfx100 at gmail dot com>

_pkgname=filter_audio
pkgname=libfilteraudio
pkgver=0.0.1
pkgrel=7
pkgdesc="An easy to use audio filtering library made from webrtc code"
arch=('x86_64')
url="https://github.com/irungentoo/filter_audio"
license=('BSD-3-Clause')
depends=('glibc')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=(
  "$_pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
  "$_pkgname-$pkgver.tar.gz.sig::$url/releases/download/v$pkgver/v$pkgver.tar.gz.sig"
  'license-from-upstream'
)
sha512sums=('9ae4d77ddc0c9f604642a6a968ead684affd9dc957ab4ddc3cf9974da028b9c402155322a63dbbcdb7dc8e3eec63c2fcef267862580229cfc01bc496f990ed58'
            'SKIP'
            'ab7ea75c03fab3dfc5d452f04a38b42783f646c2e0acaf8494628bfdf6f83e4b04d470e019385de3e89dd57eb8ef6d02daa6256d4ff311cc66f3999b694ef143')
validpgpkeys=('FC0BC251E0BA54852E532C5B10349DC9BED89E98') # irungentoo <irungentoo@gmail.com>

build() {
  cd $_pkgname-$pkgver
  make
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" PREFIX="/usr" install
  install -vDm644 "$srcdir/license-from-upstream" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
