# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=atkmm-2.36
pkgname=(
  atkmm-2.36
  atkmm-2.36-docs
)
pkgver=2.36.4
pkgrel=1
pkgdesc="C++ bindings for ATK"
url="https://www.gtkmm.org/"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  atk
  glib2
  glibc
  glibmm-2.68
  libgcc
  libstdc++
)
makedepends=(
  git
  glibmm-2.68-docs
  meson
  mm-common
)
options=(!emptydirs)
source=("git+https://gitlab.gnome.org/GNOME/atkmm.git#tag=$pkgver")
b2sums=('2b9cfe54dbb33e1d50831448ec18576dc0a9658db4c71b2555d330496d1879247b2064ca8bd9735c8c3e5b88785aec3233f7496080c91b39661d7b1f5e614543')

prepare() {
  cd atkmm
}

build() {
  local meson_options=(
    -D maintainer-mode=true
  )

  artix-meson atkmm build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_atkmm-2.36() {
  depends+=(
    libglibmm-2.68.so
    libsigc-3.0.so
  )
  provides=(libatkmm-2.36.so)

  meson install -C build --destdir "$pkgdir"

  # Split -docs
  mkdir -p docs/usr/share
  mv -t docs/usr/share "$pkgdir"/usr/share/{devhelp,doc}
}

package_atkmm-2.36-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv -t "$pkgdir" docs/*
}

# vim:set sw=2 sts=-1 et:
