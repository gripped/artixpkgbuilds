# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=knights
pkgver=24.05.0
pkgrel=1
pkgdesc='Chess board by KDE with XBoard protocol support'
arch=(x86_64)
url='https://apps.kde.org/knights/'
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kcompletion
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         ki18n
         kplotting
         ksvg
         ktextwidgets
         kwallet
         kwidgetsaddons
         kxmlgui
         libkdegames
         libplasma
         qt6-base
         qt6-speech)
optdepends=('gnuchess: for playing against the computer')
makedepends=(extra-cmake-modules
             kdoctools)
groups=(kde-applications
        kde-games)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('ed90619f79e344ceb6100fd9d706f858bdab0b89d9aa8020ec95c9180d604af2'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF   
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
