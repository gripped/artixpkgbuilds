# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-mahjongg
pkgver=49.1.1
pkgrel=1
pkgdesc="Disassemble a pile of tiles by removing matching pairs"
url="https://apps.gnome.org/Mahjongg/"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  glib2
  glibc
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
  librsvg
)
makedepends=(
  appstream
  git
  gobject-introspection
  meson
  vala
  yelp-tools
)
groups=(gnome-extra)
source=("git+https://gitlab.gnome.org/GNOME/gnome-mahjongg.git?signed#tag=${pkgver/[a-z]/.&}")
b2sums=('04a0844e36f514642e100e35fd25fae5cba490ff3bdb487d771d47f17980dead92d792ffb26fc9bb03232edd6bf592c4a89a9d5862c1e77c31ff148bf20d5ed2')
validpgpkeys=(
  FAE80BEA85C476AF1BACC2A77B274B850A9296D4 # Mat <mail@mathias.is>
)

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
