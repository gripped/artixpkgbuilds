# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-mahjongg
pkgver=51.1
pkgrel=1
pkgdesc="Disassemble a pile of tiles by removing matching pairs"
url="https://apps.gnome.org/Mahjongg/"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  glib2
  glibc
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
  librsvg
)
makedepends=(
  appstream
  git
  gobject-introspection
  meson
  vala
  yelp-tools
)
groups=(gnome-circle)
source=("git+https://gitlab.gnome.org/GNOME/gnome-mahjongg.git?signed#tag=${pkgver/[a-z]/.&}")
b2sums=('6f7c1a9bc312049b742725720ba43486dc2df5921e17d74cc602dfb345d0afd99b381bc46419796b6bcb4ca16478a3f3dd1aedd1ebadb0b4334b9dfbf8573619')
validpgpkeys=(
  FAE80BEA85C476AF1BACC2A77B274B850A9296D4 # Mat <mail@mathias.is>
)

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
