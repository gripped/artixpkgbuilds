# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-mahjongg
pkgver=48.0
pkgrel=1
pkgdesc="Disassemble a pile of tiles by removing matching pairs"
url="https://wiki.gnome.org/Apps/Mahjongg"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  gdk-pixbuf2
  glib2
  glibc
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
  librsvg
)
makedepends=(
  appstream
  git
  gobject-introspection
  meson
  vala
  yelp-tools
)
groups=(gnome-extra)
source=("git+https://gitlab.gnome.org/GNOME/gnome-mahjongg.git?signed#tag=${pkgver/[a-z]/.&}")
b2sums=('dd47e96ddcfb018b772abc2ec38f4763f0014a9d78a9c9da767d6e6a5adc2b69e27c83a01b2aa0b6865fce9052bd6339f6fe5add901320f6a71e9285178fc835')
validpgpkeys=(
  FAE80BEA85C476AF1BACC2A77B274B850A9296D4 # Mat <mail@mathias.is>
)

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
