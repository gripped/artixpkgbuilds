# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-mahjongg
pkgver=49.0.1
pkgrel=1
pkgdesc="Disassemble a pile of tiles by removing matching pairs"
url="https://apps.gnome.org/Mahjongg/"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  glib2
  glibc
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
  librsvg
)
makedepends=(
  appstream
  git
  gobject-introspection
  meson
  vala
  yelp-tools
)
groups=(gnome-extra)
source=("git+https://gitlab.gnome.org/GNOME/gnome-mahjongg.git?signed#tag=${pkgver/[a-z]/.&}")
b2sums=('a0c88a364c0f1d46f446b356aa5f8329e2076d7ecfea6ab7827d01a1a2a13a47b12bbb10e5d6e661fd75d0b17fca9ec35888f0376901d8b786c7e82dfb5eb0c8')
validpgpkeys=(
  FAE80BEA85C476AF1BACC2A77B274B850A9296D4 # Mat <mail@mathias.is>
)

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
