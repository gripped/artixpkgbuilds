# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

_gemname=octokit
_archivename=octokit.rb
pkgname="ruby-${_gemname}"
pkgver=10.0.0
pkgrel=1
pkgdesc='Simple wrapper for the GitHub API'
arch=(any)
url="https://github.com/octokit/${_archivename}"
license=(MIT)
depends=(
  ruby
  ruby-faraday
  ruby-sawyer
)
makedepends=(git)
checkdepends=(
  ruby-bundler
  ruby-faraday-http-cache
  ruby-faraday-multipart
  ruby-faraday-retry
  ruby-jwt
  ruby-mime-types
  ruby-netrc
  ruby-rake
  ruby-rbnacl
  ruby-rspec
  ruby-rss
  ruby-simplecov
  ruby-test-queue
  ruby-timecop
  ruby-vcr
  ruby-webmock
)
options=(!emptydirs)
source=("git+https://github.com/octokit/${_archivename}.git#tag=v${pkgver}")
sha512sums=('27f4e2b5dc0786179f7021a4dea92e3067d14c15984c54f2ffd637519536bfcd5c9e74dcfe4960e7ec3ec17bb7f2bd1e90118bfb6c20a555dab2ff2ea70288c0')
b2sums=('acf2b999648305d83b24e05c1085f0b97805621e7839910f01499e36e9c3c6bbcd5c8eb34b87933ffece2211cbc4ed56ce6871b23c7776343799b9e8a1a5832a')

prepare() {
  cd "${_archivename}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"
}

build() {
  cd "${_archivename}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${_archivename}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rake test
}

package() {
  cd "${_archivename}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
