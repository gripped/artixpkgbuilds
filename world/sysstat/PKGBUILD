# Maintainer: nikolar <nikolar@artixlinux.org>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Martin Devera <devik@cdi.cz>

pkgname=sysstat
pkgver=12.7.9
pkgrel=1
pkgdesc="a collection of performance monitoring tools (iostat,isag,mpstat,pidstat,sadf,sar)"
arch=('x86_64')
url="https://sysstat.github.io/"
license=('GPL-2.0-only')
depends=(
  'glibc'
  'lm_sensors'
)
optdepends=(
  'tk: to use isag'
  'gnuplot: to use isag'
)
backup=(
  'etc/conf.d/sysstat'
  'etc/conf.d/sysstat.ioconf'
)
source=("https://github.com/sysstat/sysstat/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('aa72a1d48c885c4f1feff08d8a922d07cfd1d8b2e42f9b94bb8369674300c61b2ac2fc0962050bc7479f0852d44361ce250a84cb03f29d089d2d343647062555')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    conf_dir=/etc/conf.d \
    sa_lib_dir=/usr/lib/sa \
    --mandir=/usr/share/man \
    --enable-install-cron \
    --enable-copy-only \
    --disable-compress-manpg
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  # Artix stuff, don't delete â†“
  rm -rf "${pkgdir}"/usr/lib/systemd
}
