# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=mailcommon
pkgver=24.12.2
pkgrel=1
pkgdesc='KDE PIM library providing support for mail applications'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL-2.0-or-later)
depends=(akonadi-contacts
         akonadi-mime
         gcc-libs
         glibc
         gpgme
         karchive
         kcodecs
         kcolorscheme
         kcompletion
         kconfig
         kconfigwidgets
         kcontacts
         kcoreaddons
         ki18n
         kiconthemes
         kidentitymanagement
         kio
         kitemviews
         kmailtransport
         kmime
         ktextaddons
         kwidgetsaddons
         kxmlgui
         libakonadi
         libkdepim
         libkleo
         mailimporter
         messagelib
         phonon-qt6
         pimcommon
         qgpgme-qt6
         qt6-base
         syntax-highlighting)
makedepends=(doxygen
             extra-cmake-modules
             qt6-doc
             qt6-tools)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('c58a84b24204f2f768fc72e3cac3159de6be25b346a62cb58db0c1a26d2b4fbd'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
