# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=nsd
pkgver=4.12.0
pkgrel=1
pkgdesc="Authoritative only, high performance and simple DNS server"
arch=(x86_64)
url="https://www.nlnetlabs.nl/nsd/"
license=(BSD)
depends=(openssl libevent fstrm protobuf-c)
makedepends=(git)
validpgpkeys=(EDFAA3F2CA4E6EB05681AF8E9F6F1C2D7E045F8D  #Â W.C.A. Wijngaards <wouter@nlnetlabs.nl>
              C3E356788FAD0179D872D092BA811E62E7194568
              9E32AFD129E3AB1DC5AB34DE7DE08345DE34009F # Jeroen Koekkoek <jeroen@koekkoek.nl>
              DC34EE5DB2417BCC151E5100E5F8F8212F77A498) # Willem Toorop
source=("git+https://github.com/NLnetLabs/nsd.git#tag=NSD_${pkgver//./_}_REL"
        "git+https://github.com/NLnetLabs/simdzone.git"
        nsd.sysusers
        nsd.tmpfiles)
sha256sums=('374582fc74f5758c4abc8810a0ee85c7d181f0d45477f3b1a07048b9b6d69bc3'
            'SKIP'
            '12ce2a05fbb23bb38c31933530a7773f796e250691843b9da76f178f5e7c94f5'
            '07a31cecdc787c7ef44018dfc696115bd7b5d44b6e93f56c6c08ed0887d51579')
install=nsd.install

prepare() {
  cd ${pkgname}

  git submodule init
  git config submodule.simdzone.url "${srcdir}/simdzone"
  git -c protocol.file.allow=always submodule update

  autoreconf -vfi
}

build() {
  cd ${pkgname}
  ./configure \
    --prefix=/ \
    --sbindir=/usr/bin \
    --datarootdir=/usr/share \
    --with-pidfile=/run/nsd/nsd.pid \
    --with-user="" \
    --enable-ratelimit \
    --enable-relro-now \
    --enable-pie \
    --enable-dnstap \
    --disable-systemd \
    --enable-tcp-fastopen
  make
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install
  rmdir "${pkgdir}"/{tmp,run{/nsd,}}

  rm doc/differences.pdf
  install -d "${pkgdir}"/usr/share/{doc,licenses}/"${pkgname}"
  cp -r doc/* "${pkgdir}"/usr/share/doc/"${pkgname}"
  ln -s ../../doc/"${pkgname}"/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
  install -Dm644 ../nsd.sysusers "${pkgdir}"/usr/lib/sysusers.d/nsd.conf
  install -Dm644 ../nsd.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/nsd.conf
}
