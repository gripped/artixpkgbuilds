# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=kde-inotify-survey
pkgver=23.08.2
pkgrel=1
pkgdesc='Tooling for monitoring inotify limits and informing the user when they have been or about to be reached'
url='https://invent.kde.org/system/kde-inotify-survey'
arch=(x86_64)
license=(GPL LGPL)
groups=(kde-applications kde-system)
depends=(kauth5 knotifications5 ki18n5 kdbusaddons5)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('7f2edba2225b015cd3cb8283b5038c970d1f2e513c131710fd5c2f1109233dec'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  artix-cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
