# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Paul Mattal <paul@archlinux.org>

pkgname=ddrescue
pkgver=1.29
pkgrel=1
pkgdesc="GNU data recovery tool"
arch=(x86_64)
url="https://www.gnu.org/software/ddrescue/ddrescue.html"
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  glibc
)
source=(https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.lz{,.sig})
sha512sums=('b15ca0394a19f0fba577a12c19ffb1d14b0e66a4356bf0e1cdc27ba4edcb208fbce5ca214ddccaa88aa8e116ce76932ba26a14046ccd9c835fefc20557c36cf1'
            'SKIP')
b2sums=('88eab69f2296464859dc8720930f28f6bc32aa694649e15a99dcd9dfd63ed8ba845d7af347e8b1ee90e954f8317888ec0f2cd90ec98994a7d2b4d654a31adbfc'
        'SKIP')
validpgpkeys=('1D41C14B272A2219A739FA4F8FE99503132D7742') # Antonio Diaz Diaz

build() {
  # fake configure script reinvents autotools and requires custom parameters to
  # set CPPFLAGS, CXXFLAGS and LDFLAGS
  local configure_options=(
    --prefix=/usr
    CPPFLAGS="$CPPFLAGS"
    CXXFLAGS="$CXXFLAGS"
    LDFLAGS="$LDFLAGS"
  )

  cd $pkgname-$pkgver
  ./configure "${configure_options[@]}"
  make
}

package() {
  make DESTDIR="$pkgdir" install install-man -C "$pkgname-$pkgver"
  install -vDm 644  $pkgname-$pkgver/{AUTHORS,ChangeLog,NEWS,README} -t "$pkgdir/usr/share/doc/$pkgname"
}
