# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Orivej Desh <smpuj@bk.ru>

pkgname=vamp-plugin-sdk
pkgver=2.10
pkgrel=1
epoch=1
pkgdesc="The Vamp audio analysis plug-in system"
arch=(x86_64)
url="https://github.com/vamp-plugins/vamp-plugin-sdk"
license=(
  BSD-3-Clause
  MIT
)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
)
makedepends=(libsndfile)
optdepends=('libsndfile: for vamp-simple-host')
provides=(
  libvamp-hostsdk.so
  libvamp-sdk.so
)
source=($url/archive/refs/tags/$pkgname-v$pkgver.tar.gz)
sha512sums=('67a71e5396eab5ce9503e9111b4cfc16fc9755cf6ae2d8dfc99ed29fd91e75eaf0de9a9c55ce8f7751f04c235eb86430856eff18f02adde54f1850a87c917ef0')
b2sums=('ba206443d4a6f94f56c1e3b7e8f47b7c8f893d2df6601f9d26a613d56bf58fee7d0a4a996964aa0690ecf69fd1d686db28165e73a0d37880a7891c67ecaaae5f')
_source_dir=$pkgname-$pkgname-v$pkgver

prepare() {
  # extract license files
  sed -n '1,29p' $_source_dir/COPYING > MIT.txt
  sed -n '33,65p' $_source_dir/COPYING > BSD-3-Clause.txt
}

build() {
  cd $_source_dir
  ./configure --prefix=/usr
  make
}

package() {
  make DESTDIR="$pkgdir" install -C $_source_dir
  # license
  install -vDm 644 ./*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
  # docs
  install -vDm 644 $_source_dir/{CHANGELOG,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
