# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org> 

pkgname=qrca
pkgver=25.04.1
pkgrel=1
pkgdesc='QR code scanner'
arch=(x86_64)
url='https://apps.kde.org/qrca/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kconfig
         kcontacts
         kcoreaddons
         kcrash
         ki18n
         kio
         kirigami
         kirigami-addons
         knotifications
         kservice
         networkmanager-qt
         prison
         qt6-base
         qt6-declarative
         qt6-multimedia
         xdg-utils)
makedepends=(extra-cmake-modules)
groups=(kde-applications
        kde-utilities)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('3b24933c5e88d4dbde42c5f76dc681a058c824c4b22f0415f1be5a57e582a47e'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
