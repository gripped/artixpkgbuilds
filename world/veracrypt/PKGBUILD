# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sebastian Lau <archlinux _at_ slau _dot_ info>
# Contributor: Eric Ozwigh <ozwigh at gmail dot com>

pkgname=veracrypt
_pkgname=VeraCrypt
pkgver=1.26.20
pkgrel=1
pkgdesc='Disk encryption with strong security based on TrueCrypt'
url='https://www.veracrypt.fr/'
arch=(x86_64)
license=(LicenseRef-TrueCrypt)
depends=(
  device-mapper
  fuse2
  gcc-libs
  glibc
  libsm
  wxwidgets-common
  wxwidgets-gtk3
)
optdepends=(
  'sudo: mounting encrypted volumes as nonroot users'
)
makedepends=(
  pcsclite
  yasm
)
source=(https://launchpad.net/veracrypt/trunk/${pkgver}/+download/VeraCrypt_${pkgver}_Source.tar.bz2{,.sig}
        no-makeself.patch
        veracrypt.desktop
        wx-3.2-size-warnings.patch)
sha512sums=('6382d4a34aae537c87088429c17f7cb397d0f23bd3c8e5497f302799255b93d1eb2a40284222d23b44ac37934e9a2ab056199892aa9e6b826da57a07aa09eaa0'
            'SKIP'
            '40c269859bb97fbcceb443e5f457788bac650271ed118ec79d34f56fc340ad6e613114fe905ec5aba8c4d171c51c9a6865f97e9fa1ba01fa98ef18be4e97bbe1'
            'f689ca64bac7042030de7714aed8cc89f2c5f87b407444b1b121491c1d89c147babaaa454ddc2a93b70ae20d4da59f96ad64f01b04bea9017d658c377faeb75d'
            '5ff99e8d8569d36ff0e2e3b9fd85f59f5b3de98e3f5cda7ee85a6688a33a10d4527b0fed08e53812b28ea2b42ed88481455b5f1454870e35940b2e49bf59b5b2')
validpgpkeys=('5069A233D55A0EEB174A5FC3821ACD02680D16DE') # VeraCrypt Team <veracrypt@idrix.fr>

prepare() {
  cd src
  chmod -R u+w . # WAT award
  patch -Np1 < "${srcdir}/no-makeself.patch"
  patch -p1 < ../wx-3.2-size-warnings.patch # https://github.com/veracrypt/VeraCrypt/issues/952
}

build() {
  cd src
  make PKG_CONFIG_PATH=/usr/lib/pkgconfig \
    TC_EXTRA_LFLAGS+="-ldl ${LDFLAGS}" \
    TC_EXTRA_CXXFLAGS="${CXXFLAGS} ${CPPFLAGS}" \
    TC_EXTRA_CFLAGS="${CFLAGS} ${CPPFLAGS}"
}

package() {
  cd src
  install -Dm 755 Main/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 "${srcdir}/veracrypt.desktop" -t "${pkgdir}/usr/share/applications"
  install -Dm 644 Resources/Icons/VeraCrypt-256x256.xpm "${pkgdir}/usr/share/pixmaps/veracrypt.xpm"
  install -Dm 644 License.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
