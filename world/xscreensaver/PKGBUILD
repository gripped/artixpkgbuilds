# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=xscreensaver
pkgver=6.10.1
pkgrel=1
pkgdesc='Screen saver and locker for the X Window System'
url='https://www.jwz.org/xscreensaver/'
arch=('x86_64')
license=('BSD')
depends=(
  'gtk3' 'glu' 'xorg-appres' 'libglvnd' 'libjpeg-turbo' 'libjpeg.so'
  'libx11' 'libxcrypt' 'libcrypt.so' 'libxext' 'libxft' 'libxi'
  'libxinerama' 'libxmu' 'libxrandr' 'libxt' 'libxxf86vm' 'perl-libwww' 'pam'
  'libpam.so' 'glibc' 'glib2' 'gdk-pixbuf2'
)
makedepends=('bc' 'intltool' 'libxpm' 'gdm')
optdepends=('gdm: for login manager support')
backup=('etc/pam.d/xscreensaver')
source=(https://www.jwz.org/xscreensaver/${pkgname}-${pkgver}.tar.gz
        license-from-upstream)
sha512sums=('25219f670cdd18db34fc514dc68c628ac8721f88a15d5df2cecbef92ebdfaa916be40ddd171b79166fc20469c199573827dbcbeb7bfa258d48b7f1a9ca7ea07a'
            '863c699479b2ec2775a0d1cba22e615929194a14af164b3513e46a0c04229da6547255a4da8f7f1bbb40906898c124ed3c9ec2436b76b62affcb62385af9783e')
b2sums=('6bfeed1acbe0aee07ed64c31f8e503afd4930ef6063ca92f234ce7dc42b6b4e07d7d04b4e227f9172a63519d6376c6003c243336d139b831ebf0bb854f3657c5'
        'cacb6ba39d6ecb8703ef5f5a7dc74de0ca805cce73b43a8b9b6b4c255c909aa9b5e692de76c2fbd4da26ce6efb5f2a46138c43b1b37f53cee6d20fd6ed41f4a9')

build() {
  cd ${pkgname}-${pkgver%.*}
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib \
    --without-setuid-hacks \
    --with-pam \
    --with-login-manager \
    --with-gtk \
    --with-gl \
    --without-gle \
    --with-pixbuf \
    --with-jpeg \
    --enable-pam-check-account-type
  make
}

package() {
  cd ${pkgname}-${pkgver%.*}
  install -d "${pkgdir}/etc/pam.d"
  make install_prefix="${pkgdir}" install
  install -Dm 644 ../license-from-upstream -t "${pkgdir}/usr/share/licenses/${pkgname}"
  echo "NotShowIn=KDE;GNOME;" >> "${pkgdir}/usr/share/applications/xscreensaver-settings.desktop"
}

# vim: ts=2 sw=2 et:
