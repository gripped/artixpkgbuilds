# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Eric Berquist <eric dot berquist at gmail dot com>

pkgname=ruby-mixlib-cli
_pkgname="${pkgname#ruby-}"
pkgver=2.2.2
pkgrel=1
pkgdesc='A simple mixin for CLI interfaces, including option parsing'
arch=(any)
url='https://github.com/chef/mixlib-cli'
license=(Apache-2.0)
depends=(ruby)
makedepends=(git ruby-rake ruby-rdoc ruby-bundler)
options=(!emptydirs)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('d70ce00fe73a8d972fb31a617504ff8cbaad081d0dcea9b2d04827bd302fc6108b8e6ca6923a5d54e02f25dc5174ca635f552529cf26216b4991bde7e0c9c8cf')
b2sums=('ebe4047ea1922b2bc1e355daa051f5318099e29971863760f02995e1160937b47c75b18f215cba3b2a5ad9f57d46d134fbf074567aced218f04fabf83bdba39d')

build() {
  cd "$pkgname"

  rake build
}

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$pkgname/pkg/$_pkgname-$pkgver.gem"

  # delete cache
  rm -vrf "$pkgdir/$_gemdir/cache"

  # license
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  ln -sf "$_gemdir/gems/$_pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
