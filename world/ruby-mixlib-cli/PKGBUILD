# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Eric Berquist <eric dot berquist at gmail dot com>

pkgname=ruby-mixlib-cli
_pkgname="${pkgname#ruby-}"
pkgver=2.2.3
pkgrel=1
pkgdesc='A simple mixin for CLI interfaces, including option parsing'
arch=(any)
url='https://github.com/chef/mixlib-cli'
license=(Apache-2.0)
depends=(ruby)
makedepends=(git ruby-rake ruby-rdoc ruby-bundler)
options=(!emptydirs)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('81db4b40ad90e9419b48ef358e33fd1cb0cec9448a66c3f1e706672b66f9297cdb604c14c1cc99de064672910cfc24693e3044befdcbab3a1e703a7c17cfeee9')
b2sums=('2e67379567dda6676431eff4b614c6fe8ceed494c439c37ce2690444e1c99d22e277d14ad3bf03d0c59b251f3b2b1894dfc3a85abf54b83b64e549699993f041')

build() {
  cd "$pkgname"

  rake build
}

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$pkgname/pkg/$_pkgname-$pkgver.gem"

  # delete cache
  rm -vrf "$pkgdir/$_gemdir/cache"

  # license
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  ln -sf "$_gemdir/gems/$_pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
