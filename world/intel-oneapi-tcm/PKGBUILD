# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Torsten Ke√üler <tpkessler@archlinux.org>
pkgname=intel-oneapi-tcm
_pkgver=1.2
pkgver=1.2.0
_debpkgrel=589
pkgrel=3
pkgdesc="Thread Composability Manager"
arch=('x86_64')
url="https://www.intel.com/content/www/us/en/developer/tools/oneapi/overview.html"
license=('LicenseRef-Intel-Simplified')
depends=('intel-oneapi-common' 'glibc' 'gcc-libs' 'hwloc')
source=("https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb")
b2sums=('9469f2fa5e83f036cd7452678325bba282cc17d68b8d5c3deaefe6cdf638074434bd4da4ab74123cf66aef0d18560d726bea8e586673de004bf18831921b6d63')
noextract=("${pkgname}-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb")
conflicts=('intel-oneapi-basekit')

package() {
	ar x "${pkgname}-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
	tar xvf data.tar.xz -C "${pkgdir}"
	rm data.tar.xz

	# latest symlink
	local _prefix=/opt/intel/oneapi/tcm
	ln -s "${_prefix}/${_pkgver}" "${pkgdir}/${_prefix}/latest"

	install -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
	ln -s /usr/share/licenses/intel-oneapi "${pkgdir}"/usr/share/licenses/"${pkgname}"/oneapi
}
