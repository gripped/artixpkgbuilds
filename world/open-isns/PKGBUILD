# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=open-isns
pkgver=0.103
pkgrel=1
pkgdesc='iSNS server and client for Linux'
arch=('x86_64')
depends=('openssl')
makedepends=('meson')
url='https://github.com/gonzoleeman/open-isns'
license=('GPL')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/gonzoleeman/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('47499f3ce87a832840884dcf3eedbec8c039f472fe921a5378e3b206a3fc1a32')

build() {
  local _meson_options=(
    --default-library=shared
    -Dslp=disabled
    -Dlibdir=lib
  )

  artix-meson "${pkgname}-${pkgver}" build "${_meson_options[@]}"

  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}
