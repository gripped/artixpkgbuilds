# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Orhun Parmaksız <orhun@archlinux.org>
# Contributor: Daniele Basso <d dot bass05 at proton dot me>
# Contributor: T. Jameson Little <t.jameson.little at gmail dot com>
# Contributor: Usagi Ito <usagi@WonderRabbitProject.net>
# Contributor: siasia <http://pastebin.com/qsBEmNCw>
# Contributor: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Contributor: The one with the braid <info@braid.business>
# Contributor: Juan Cuevas <juanandrescuevas14@gmail.com>
# Contributor: Jacob Bang <julemand101@archlinux.dk>

pkgname=dart
pkgver=3.9.4
pkgrel=1
pkgdesc='The dart programming language SDK'
arch=('x86_64')
url='https://dart.dev/'
depends=('glibc')
license=('BSD-3-Clause')
makedepends=(
  'dart'
  'git'
  'gn'
  'ninja'
  'python'
)
# Pinned commit for depot_tools since it does not have release tags. See discussion here for further details:
# https://gitlab.archlinux.org/archlinux/packaging/packages/dart/-/merge_requests/14#note_335643
#
# Should be updated with latest commit on origin/main when new version of Dart are released.
_depotver='12b3e1f95e1377c0fe9cc3c84bc64193b234346b'
source=(
  "git+https://github.com/dart-lang/sdk.git#tag=$pkgver"
  "git+https://chromium.googlesource.com/chromium/tools/depot_tools.git#commit=$_depotver"
  "DEPS.patch"
  "0001-vm-Fix-gcc-build.patch"
)
sha256sums=('0b972e61b81d218b7dd85dd1be5a01713b91df412d2839e6f388927f7e72d8c3'
            '7de37e557d18f51ed87de52bd25b0b051ebcf2c6237468410d16939006ed2c66'
            'd68184503fa10ad95ad56b113225c178402e3f41dedbc8e89bb30373b609d76d'
            'b5c8ce3fa5d0d837b5c39b662ad7f070b27ab97941049fd43bcf63c14e40e880')

prepare() {
cat >.gclient <<EOF
solutions = [
  {
    "name": "sdk",
    "url": "file://${srcdir}/sdk",
    "deps_file": "DEPS",
    "managed": False,
    "custom_deps": {},
    "custom_vars": {},
  },
]
EOF

  export PATH+=":$PWD/depot_tools" DEPOT_TOOLS_UPDATE=0

  cd sdk

  patch -Np 1 --input="$srcdir/DEPS.patch"
  patch -Np 1 --input="$srcdir/0001-vm-Fix-gcc-build.patch"

  gclient sync -D \
      --nohooks \
      --no-history \
      --shallow \
      -r "${srcdir}/sdk@${_commit}"

  dart tools/generate_package_config.dart
  python tools/generate_sdk_version_file.py

  sed -i 's|prefix = "x86_64-linux-gnu-"|prefix = ""|g' build/toolchain/linux/BUILD.gn
}

build() {
  cd sdk

  # gn args --list out

  /usr/bin/gn gen -qv out --args='
                        target_cpu = "x64"
                        is_debug = false
                        is_release = true
                        is_clang = false
                        dart_platform_sdk = false
                        verify_sdk_hash = false'
  ninja create_sdk -v -C out
}

package() {
  # cd to directory
  cd sdk/out/

  # Create directories
  install -d "$pkgdir"{"/opt/$pkgname-sdk",/usr/{bin,"share/doc/$pkgname"}}

  # Package the files
  cp -a "$pkgname-sdk/"* "$pkgdir/opt/$pkgname-sdk/"

  # Set up symbolic links for the executables
  for f in dart dartaotruntime; do
    ln -s "/opt/$pkgname-sdk/bin/$f" "$pkgdir/usr/bin/$f"
  done

  # Package documentation
  install -Dm644 "$pkgdir/opt/$pkgname-sdk/README" -t "$pkgdir/usr/share/doc/$pkgname"

  # BSD License
  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
