# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Orhun Parmaksız <orhun@archlinux.org>
# Contributor: Daniele Basso <d dot bass05 at proton dot me>
# Contributor: T. Jameson Little <t.jameson.little at gmail dot com>
# Contributor: Usagi Ito <usagi@WonderRabbitProject.net>
# Contributor: siasia <http://pastebin.com/qsBEmNCw>
# Contributor: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Contributor: The one with the braid <info@braid.business>
# Contributor: Juan Cuevas <juanandrescuevas14@gmail.com>
# Contributor: Jacob Bang <julemand101@archlinux.dk>

pkgname=dart
pkgver=3.12.0
pkgrel=1
pkgdesc='The dart programming language SDK'
arch=('x86_64')
url='https://dart.dev/'
depends=('glibc')
license=('BSD-3-Clause')
makedepends=(
  'dart'
  'git'
  'gn'
  'ninja'
  'python'
)
# Pinned commit for depot_tools since it does not have release tags. See discussion here for further details:
# https://gitlab.archlinux.org/archlinux/packaging/packages/dart/-/merge_requests/14#note_335643
#
# Should be updated with latest commit on origin/main when new version of Dart are released.
_depotver='0cd0ed2e67f0507e0740da64926d80da9476aea9'  # As of 2026-05-19.
source=(
  "git+https://github.com/dart-lang/sdk.git#tag=$pkgver"
  "git+https://chromium.googlesource.com/chromium/tools/depot_tools.git#commit=$_depotver"
  "DEPS.patch"
  "0001-fix-gcc-related-build-settings.patch"
)

sha256sums=('301ef6a5b358dfb6bb8ca347b59c5f724052a2f00a8c1c10794b2e4c92c1e2ef'
            '69af2b84a9caa3d0a638d37f8a49932988080b8a98170bc49071e4b08dfeaffd'
            'a5b1901a606517ffad2dcc51c13da6d479837e9383dbc729710cabf5115b8d78'
            '56cff50bf180310fede033c7076a1169124cb36f07f518546add1ca9f429e4cd')

prepare() {
cat >.gclient <<EOF
solutions = [
  {
    "name": "sdk",
    "url": "file://${srcdir}/sdk",
    "deps_file": "DEPS",
    "managed": False,
    "custom_deps": {},
    "custom_vars": {},
  },
]
EOF

  export PATH+=":$PWD/depot_tools" DEPOT_TOOLS_UPDATE=0

  cd sdk

  patch -Np 1 --input="$srcdir/DEPS.patch"

  # Fix for https://github.com/dart-lang/sdk/issues/63406
  patch -Np 1 --input="$srcdir/0001-fix-gcc-related-build-settings.patch"

  gclient sync -D \
      --nohooks \
      --no-history \
      --shallow \
      -r "${srcdir}/sdk@${_commit}"

  dart tools/generate_package_config.dart
  python tools/generate_sdk_version_file.py

  sed -i 's|prefix = "x86_64-linux-gnu-"|prefix = ""|g' build/toolchain/linux/BUILD.gn
}

build() {
  cd sdk

  # gn args --list out

  /usr/bin/gn gen -qv out --args='
                        target_cpu = "x64"
                        is_debug = false
                        is_release = true
                        is_clang = false
                        dart_platform_sdk = false
                        verify_sdk_hash = false'
  ninja create_sdk -v -C out
}

package() {
  # cd to directory
  cd sdk/out/

  # Create directories
  install -d "$pkgdir"{"/opt/$pkgname-sdk",/usr/{bin,"share/doc/$pkgname"}}

  # Package the files
  cp -a "$pkgname-sdk/"* "$pkgdir/opt/$pkgname-sdk/"

  # Set up symbolic links for the executables
  for f in dart dartaotruntime; do
    ln -s "/opt/$pkgname-sdk/bin/$f" "$pkgdir/usr/bin/$f"
  done

  # Package documentation
  install -Dm644 "$pkgdir/opt/$pkgname-sdk/README" -t "$pkgdir/usr/share/doc/$pkgname"

  # BSD License
  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
