# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Ainola

pkgname=celluloid
pkgver=0.30
pkgrel=1
pkgdesc='Simple GTK+ frontend for mpv'
arch=('x86_64')
url='https://celluloid-player.github.io/'
license=('GPL3')
depends=('gtk4' 'mpv' 'libadwaita')
makedepends=('appstream-glib' 'meson' 'glib2-devel')
replaces=('gnome-mpv')
source=("https://github.com/celluloid-player/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz")
#validpgpkeys=('4BA23A4D2F798D926C7A0E0D0F9039A19BE2326C') # gnome-mpv
sha256sums=('7fef96431842c24e5ae78a9c42bc6523818a6c45213f23ceb146d037d6ec8559')

build() {
  artix-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
