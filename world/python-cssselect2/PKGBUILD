# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-cssselect2
pkgver=0.9.0
pkgrel=1
pkgdesc='CSS selectors for Python ElementTree'
arch=(any)
url='https://github.com/Kozea/cssselect2'
license=(BSD-3-Clause)
depends=(
  python
  python-tinycss2
  python-webencodings
)
makedepends=(
  git
  python-build
  python-installer
  python-flit-core
)
checkdepends=(python-pytest)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('6055421817c3d6a0a2e168c7f3d3e09aed3dd7d725cbf6288840c24448d05fbe707fd7b071eafa5e4ad3d0da72c6a452b2cfc414150deaf01c2ffb95af96d665')
b2sums=('656abe84c4e18024b56c3aa82933b35e0bbf63fc40f30c7282ceded1e2fba8fc99a8d969a343c4c2a146f6b95d7d6b152cff9b0082dd4fb1a6695161ee61aa65')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
