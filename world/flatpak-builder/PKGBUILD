# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=flatpak-builder
pkgver=1.4.4
pkgrel=2
pkgdesc="Tool to build flatpaks from source"
url="https://flatpak.org"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  appstream
  binutils
  breezy
  cpio
  curl
  debugedit
  elfutils
  flatpak
  fuse3
  gcc-libs
  git
  glib2
  glibc
  json-glib
  libelf
  libxml2
  libyaml
  ostree
  patch
  rpmextract
  tar
  unzip
)
makedepends=(
  docbook-xsl
  git
  gobject-introspection
  meson
  xmlto
)
replaces=('flatpak<0.9.10')
source=(
  "git+https://github.com/flatpak/flatpak-builder#tag=$pkgver"
  "git+https://gitlab.gnome.org/GNOME/libglnx.git"
  0001-Use-fusermount3.patch
)
b2sums=('04a6e52e225ebebeff17e3c7e712f09a926b341bea6051fb819c9d0bfdaecc0e789d0c7770c7ffd562acec8a22cb29f406c28af248f8d11d16b46a2690126b3b'
        'SKIP'
        '3b21365ce64effa5e67c28ebe96ca6105df5e4f54983737b9493c77814e50431e1670521023e75f02c5f8616410ee865747af392fc21683e5c40670f827d05fe')

prepare() {
  cd $pkgname

  # https://bugs.archlinux.org/task/75649
  git apply -3 ../0001-Use-fusermount3.patch

  git submodule init subprojects/libglnx
  git submodule set-url subprojects/libglnx "$srcdir/libglnx"
  git -c protocol.file.allow=always -c protocol.allow=never submodule update subprojects/libglnx
}

build() {
  local meson_options=(
    -D fuse=3

    # bubblewrap is broken in our build containers
    -D tests=false
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
