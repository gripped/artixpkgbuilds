# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=flatpak-builder
pkgver=1.2.3
pkgrel=2
pkgdesc="Tool to build flatpaks from source"
url="https://flatpak.org"
arch=(x86_64)
license=(LGPL)
depends=(
  appstream-glib
  binutils
  breezy
  cpio
  elfutils
  flatpak
  fuse3
  git
  patch
  rpmextract
  tar
  unzip
)
makedepends=(
  docbook-xsl
  git
  gobject-introspection
  xmlto
)
checkdepends=(valgrind)
replaces=('flatpak<0.9.10')
options=(debug)
_commit=d7dbc51267e83242a9fe0d5a7b5304db0ac72f4f  # tags/1.2.3
source=(
  "git+https://github.com/flatpak/flatpak-builder#commit=$_commit"
  "git+https://gitlab.gnome.org/GNOME/libglnx.git"
  "git+https://sourceware.org/git/debugedit.git"
  fusermount3.diff
)
b2sums=('SKIP'
        'SKIP'
        'SKIP'
        '8598941ac5ba4eab8e8e6adf1f6d43c4c161a4283335554ed03e968698c4f7326afc9b2c7d01cc3185ab05e889fc2220e459c156c10cd130edaae89bddf08979')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname

  # https://bugs.archlinux.org/task/75649
  git apply -3 ../fusermount3.diff

  git submodule init
  git config --local submodule.libglnx.url "$srcdir/libglnx"
  git config --local submodule.debugedit.url "$srcdir/debugedit"
  git -c protocol.file.allow=always submodule update

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
    --localstatedir=/var
    --sbindir=/usr/bin
    --libexecdir=/usr/lib
    --disable-static
  )

  cd $pkgname
  ./configure "${configure_options[@]}"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

# fails in jenkins; skip
#check() {
#  cd $pkgname
#  make -k check
#}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set sw=2 sts=-1 et:
