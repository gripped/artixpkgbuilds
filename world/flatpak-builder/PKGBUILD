# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=flatpak-builder
pkgver=1.4.7
pkgrel=1
pkgdesc="Tool to build flatpaks from source"
url="https://flatpak.org"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  7zip
  appstream
  binutils
  breezy
  cpio
  curl
  debugedit
  elfutils
  flatpak
  fuse3
  gcc-libs
  git
  glib2
  glibc
  json-glib
  libarchive
  libelf
  libxml2
  libyaml
  ostree
  patch
  rpmextract
  tar
)
makedepends=(
  docbook-xsl
  git
  gobject-introspection
  meson
  xmlto
)
replaces=('flatpak<0.9.10')
source=(
  "git+https://github.com/flatpak/flatpak-builder#tag=$pkgver"
  "git+https://gitlab.gnome.org/GNOME/libglnx.git"
  0001-Use-fusermount3.patch
)
b2sums=('089fa8a2e05f400f13c458cbda5f511622be1ef73938c68d49775059eda0bba588b59fe16fcb62e10802a81b083cdd1be7263eb162db263b2b308a5762e131f4'
        'SKIP'
        '43edf343ef4ab7a57562923549dc06020c57c197b96ecb521a8cea3bf00afe61ef04d0bb226b74d0bf082db486783c6c556e521707f648098cb422c4a0c8884c')

prepare() {
  cd $pkgname

  # https://bugs.archlinux.org/task/75649
  git apply -3 ../0001-Use-fusermount3.patch

  git submodule init subprojects/libglnx
  git submodule set-url subprojects/libglnx "$srcdir/libglnx"
  git -c protocol.file.allow=always -c protocol.allow=never submodule update subprojects/libglnx
}

build() {
  local meson_options=(
    -D fuse=3

    # bubblewrap is broken in our build containers
    -D tests=false
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
