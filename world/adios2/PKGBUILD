# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Bruno Pagani <archange@archlinux.org>
# Contributor: Jakub Klinkovský <lahwaacz at archlinux dot org>

pkgname=adios2
pkgver=2.12.0
pkgrel=1
pkgdesc="The Adaptable Input/Output System version 2"
arch=(x86_64)
url="https://adios2.readthedocs.io/en/latest/"
license=(Apache-2.0)
depends=(
  blosc2
  bzip2 libbz2.so
  curl libcurl.so
  glibc
  hdf5
  libfabric
  libgcc
  libgfortran
  libgomp
  libpng libpng16.so
  libsodium libsodium.so
  libstdc++
  mgard
  openmpi libmpi.so
  openssl libssl.so
  openucx libucp.so libucs.so
  paraview-catalyst
  pugixml
  python
  sqlite libsqlite3.so
  yaml-cpp libyaml-cpp.so
  zeromq libzmq.so
  zfp
  zlib libz.so
  zstd libzstd.so
)
makedepends=(
  cmake
  cuda
  gcc-fortran
  gtest
  ninja
  nlohmann-json
  python-mpi4py
  python-numpy
  sz
)
optdepends=(
  'python-numpy: for Python bindings'
  'python-cupy: for CuPy arrays'
  'python-pytorch: for PyTorch tensors'
)
source=(
  https://github.com/ornladios/ADIOS2/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
)
b2sums=('a08a4ccc55c67485d61f68c8954aa5a7a063672fe5b0b0af400578a0213f0ef3ce856dc21e5a20ffad81e0074532a07fa96018b9adbfd2ef4ec6a02fc1d623b3')

build() {
  # In general, we want to list all real archs (sm_XX) and the latest virtual arch (compute_XX) for future PTX compatibility.
  # Valid values can be discovered from nvcc --help
  local cuda_archs="75;80;86;87;88;89;90;100;103;110;120;121;121-virtual"

  local cmake_options=(
    -B build
    -S ${pkgname^^}-${pkgver}
    -G Ninja
    -W no-dev
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_CUDA_ARCHITECTURES="$cuda_archs"
    # Compile source code for supported GPU archs in parallel
    -DCMAKE_CUDA_FLAGS="--threads $(nproc)"
    -DADIOS2_USE_CUDA=ON
    -DADIOS2_USE_EXTERNAL_DEPENDENCIES=ON
    -DADIOS2_USE_EXTERNAL_PERFSTUBS=OFF
    # external nanobind does not work: https://gitlab.archlinux.org/archlinux/packaging/packages/nanobind/-/merge_requests/3
    -DADIOS2_USE_EXTERNAL_NANOBIND=OFF
    #-DADIOS2_HAVE_HDF5_VOL needs hdf5-openmpi
    -DADIOS2_HAVE_HDF5_VOL=OFF
    -DADIOS2_BUILD_EXAMPLES=OFF
    -DADIOS2_USE_Derived_Variable=ON
  )
  cmake "${cmake_options[@]}"
  cmake --build build
}

package() {
  CMAKE_PREFIX_PATH="$pkgdir"/usr/lib/cmake \
  DESTDIR="$pkgdir" cmake --install build
}
