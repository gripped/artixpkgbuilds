# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Yiyao Yu <yuydevel at protonmail dot com>
# Contributor: Moritz Lipp <mlq@pwmt.org>

pkgname=bear
pkgver=4.2.0
pkgrel=1
pkgdesc='A tool to generate compilation database for clang tooling'
arch=(x86_64)
url='https://github.com/rizsotto/Bear'
license=(GPL-3.0-or-later)
depends=(
  sh
  glibc
  libgcc
)
makedepends=(
  git
  rust
)
options=(!lto)
source=(
  "$pkgname::git+$url.git#tag=$pkgver"
  avoid-libexec.patch
)
sha512sums=('9b73dec36ca81987adac5aea736c8eee9fb9346fe979bb4b1f447a72e99bf758dcf6c63cecf2f9d462587d9c41beaf396f79533c63440f2b1c98ae58db948de3'
            '85348e77df755a0ff567f7b1a16773d7219b4f8411d3fd701c81bb8409ab05c974d675ba3b5efe5df676b4b910ba60b303e7e6f74045a5fc2f3bbbbc322c6050')
b2sums=('be06140ff3441c42db4c90a44aaee1694c36e6c8949a1c001255e73e23e7c83c443b9e6cffec84ca321c424d12ef2526ca16537d530a71920dd974ca8c08eb3d'
        '43de659d7545a56b8e2d1d2db126f63bcb654b09c50dcdea5e7e7610fde86faf8bbefac43032df847420e2727ce01f41d92776598f6a5568fe7c0086b09a2b0d')

prepare() {
  cd "$pkgname"

  # fix install script
  patch -p1 -i "$srcdir/avoid-libexec.patch"

  # download dependencies
  cargo fetch --locked --target host-tuple
}

build() {
  cd "$pkgname"

  cargo build --frozen --release

  # generate shell completions
  ./target/release/generate-completions target/release/completions
}

check() {
  cd "$pkgname"

  cargo build
  cargo test
}

package() {
  cd "$pkgname"

  DESTDIR="$pkgdir" PREFIX=/usr ./scripts/install.sh
}
