# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Yiyao Yu <yuydevel at protonmail dot com>
# Contributor: Moritz Lipp <mlq@pwmt.org>

pkgname=bear
pkgver=4.1.5
pkgrel=1
pkgdesc='A tool to generate compilation database for clang tooling'
arch=(x86_64)
url='https://github.com/rizsotto/Bear'
license=(GPL-3.0-or-later)
depends=(
  sh
  glibc
  libgcc
)
makedepends=(
  git
  rust
)
options=(!lto)
source=(
  "$pkgname::git+$url.git#tag=$pkgver"
  avoid-libexec.patch
)
sha512sums=('2489559b4adb70180d7e75944c74253e70556df28593e6b95e067b0308851e1da6cd6b7168aae758e964747c377679e5c2a2c4b9895c9f86d33c2a08c94e7e4c'
            '85348e77df755a0ff567f7b1a16773d7219b4f8411d3fd701c81bb8409ab05c974d675ba3b5efe5df676b4b910ba60b303e7e6f74045a5fc2f3bbbbc322c6050')
b2sums=('b8d81474a09c17ab2bb2d8fdada52fd5bd9662879274d03fd66554462f34e11887ce13e4c56fc4ef11beeb62ce97300216c4d60414f93e5fab78946050067571'
        '43de659d7545a56b8e2d1d2db126f63bcb654b09c50dcdea5e7e7610fde86faf8bbefac43032df847420e2727ce01f41d92776598f6a5568fe7c0086b09a2b0d')

prepare() {
  cd "$pkgname"

  # fix install script
  patch -p1 -i "$srcdir/avoid-libexec.patch"

  # download dependencies
  cargo fetch --locked --target host-tuple
}

build() {
  cd "$pkgname"

  cargo build --frozen --release

  # generate shell completions
  ./target/release/generate-completions target/release/completions
}

check() {
  cd "$pkgname"

  cargo build
  cargo test
}

package() {
  cd "$pkgname"

  DESTDIR="$pkgdir" PREFIX=/usr ./scripts/install.sh
}
