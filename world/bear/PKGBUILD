# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Yiyao Yu <yuydevel at protonmail dot com>
# Contributor: Moritz Lipp <mlq@pwmt.org>

pkgname=bear
pkgver=4.0.0
pkgrel=2
pkgdesc='A tool to generate compilation database for clang tooling'
arch=(x86_64)
url='https://github.com/rizsotto/Bear'
license=(GPL-3.0-or-later)
depends=(
  glibc
  gcc-libs
)
makedepends=(
  git
  rust
)
source=(
  "$pkgname::git+$url.git#tag=$pkgver"
  fix-wrapper-preload-path.patch
)
sha512sums=('5c84536095c04d640fe05e47cfa0aa586c6a0f539c4a3d0f5391fdd52da89b6955d6f5f7bde5cbcbe6053f929febf2716c4ae47a7373e9ea1d5118043ed1a433'
            '9445568e3865dafc2c562382f7c214b5189f2b2c9eb274210912b1ef42c0343f345c39e4a914739dc22bad36fec8e992fa0756ca988c50501ddbd6487b16d0cf')
b2sums=('f22505fb18cb8f271542fcd1262da9e5f9c71fa6836202ff32ec62966982f70410fa751b484d2622f0f97c0e7f7415d23446feb84ee1f49ead3ea779c2af9426'
        '4a1c08a0b56613985f81f2249afca0c30a319faed6c6f320c55f72fc97fc72aea8dec4faa698e84d887b21eb649a2bb1588da43c98cec84220efe9586479f28a')

prepare() {
  cd "$pkgname"

  # patch wrapper/preload path
  # https://github.com/rizsotto/Bear/issues/648
  patch -p1 -i "$srcdir/fix-wrapper-preload-path.patch"

  # download dependencies
  cargo fetch --locked --target $(rustc --print host-tuple)
}

build() {
  cd "$pkgname"

  local _bear_subprojects=(bear intercept-preload intercept-wrapper)
  cargo build --frozen --release ${_bear_subprojects[@]/#/--package }
}

check() {
  cd "$pkgname/bear"

  cargo test --frozen
}

package() {
  cd "$pkgname"

  # binaries
  install -vDm755 -t "$pkgdir/usr/bin" target/release/bear
  install -vDm755 -t "$pkgdir/usr/lib/bear" target/release/{wrapper,libexec.so}

  # man page
  install -vDm644 -t "$pkgdir/usr/share/man/man1" man/bear.1

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
}
