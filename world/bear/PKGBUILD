# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Yiyao Yu <yuydevel at protonmail dot com>
# Contributor: Moritz Lipp <mlq@pwmt.org>

pkgname=bear
pkgver=4.0.3
pkgrel=1
pkgdesc='A tool to generate compilation database for clang tooling'
arch=(x86_64)
url='https://github.com/rizsotto/Bear'
license=(GPL-3.0-or-later)
depends=(
  glibc
  gcc-libs
)
makedepends=(
  git
  rust
)
options=(!lto)
source=(
  "$pkgname::git+$url.git#tag=$pkgver"
  fix-wrapper-preload-path.patch
)
sha512sums=('5ee1f2dcf7eda0f38a588ede596fc2aa556d306094c2a50cb90dddf3c6a677e7f98408fa14d0d7f3d8d1339d20ae2b22784a2361c42286b6d67fc3ec4383d57d'
            '0f74102b49da4ca97f4c0c0618a6314a29dd391cda76417bac99eb222fee7cc2d1d3d0fbff6f8b82f9936bb9056f573fc6b036b9af4e6e4636e9eda2c3fb0cff')
b2sums=('33305fb679d5f05ef4feb7d7016ed935485af5d5aab2858b4a36e46dea71b3d18173dda2798db76f5377a208f5a27b3adf0fd578850f5eac4c782c7f27070f7f'
        '40962bdd175e26feb3497b412076347022ef3294ff74acf12d415c41e01e2e2e48582077e15c1f4cad05a3f5250613a44ee2683bfa297b634389a9081f49a9bc')

prepare() {
  cd "$pkgname"

  # patch wrapper/preload path
  # https://github.com/rizsotto/Bear/issues/648
  patch -p1 -i "$srcdir/fix-wrapper-preload-path.patch"

  # download dependencies
  cargo fetch --locked --target $(rustc --print host-tuple)
}

build() {
  cd "$pkgname"

  local _bear_subprojects=(bear intercept-preload intercept-wrapper)
  cargo build --frozen --release ${_bear_subprojects[@]/#/--package }
}

check() {
  cd "$pkgname/bear"

  cargo test --frozen
}

package() {
  cd "$pkgname"

  # binaries
  install -vDm755 -t "$pkgdir/usr/bin" target/release/bear
  install -vDm755 -t "$pkgdir/usr/lib/bear" target/release/{wrapper,libexec.so}

  # man page
  install -vDm644 -t "$pkgdir/usr/share/man/man1" man/bear.1

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
}
