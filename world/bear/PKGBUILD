# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Yiyao Yu <yuydevel at protonmail dot com>
# Contributor: Moritz Lipp <mlq@pwmt.org>

pkgname=bear
pkgver=4.1.1
pkgrel=1
pkgdesc='A tool to generate compilation database for clang tooling'
arch=(x86_64)
url='https://github.com/rizsotto/Bear'
license=(GPL-3.0-or-later)
depends=(
  sh
  glibc
  libgcc
)
makedepends=(
  git
  rust
)
options=(!lto)
source=("$pkgname::git+$url.git#tag=$pkgver")
sha512sums=('c60f917d57e1f3fea720f73b062759a932617b4c6a660de9f067567ef9d737ebdf8178712a7f13a906dc01a16bc5af553fc0f7b8168a7dd43b700fd003c40051')
b2sums=('85ea6b0a1e039b9c69e3585a55582ac999632d368f9254e401964d35227dc3f37208f4728848a8375a2944108e1e5d9a1af4696acac476813795183d4846845a')

prepare() {
  cd "$pkgname"

  # fix install script
  git checkout 5badd0912bbede48056fc3ba66d8a28a0b92ead2

  # download dependencies
  cargo fetch --locked --target host-tuple
}

build() {
  cd "$pkgname"

  cargo build --frozen --release

  # generate shell completions
  ./target/release/generate-completions target/release/completions
}

check() {
  cd "$pkgname/bear"

  cargo test --frozen
}

package() {
  cd "$pkgname"

  DESTDIR="$pkgdir" PREFIX=/usr ./scripts/install.sh
}
