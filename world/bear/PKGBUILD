# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Yiyao Yu <yuydevel at protonmail dot com>
# Contributor: Moritz Lipp <mlq@pwmt.org>

pkgname=bear
pkgver=4.0.1
pkgrel=1
pkgdesc='A tool to generate compilation database for clang tooling'
arch=(x86_64)
url='https://github.com/rizsotto/Bear'
license=(GPL-3.0-or-later)
depends=(
  glibc
  gcc-libs
)
makedepends=(
  git
  rust
)
source=(
  "$pkgname::git+$url.git#tag=$pkgver"
  fix-wrapper-preload-path.patch
)
sha512sums=('ae850f6607bac3a1a8e670af5e3914f2fd06b352892b087b3cb45687bb376ccb7e83678beef1ec13ac69970dfd8891434582e9636cb034ba566c61f03ae5a4ee'
            '489db7d869eb00676ccc500d8482680da2cb41903518d22064f4c6baa38995875312883a962117c993655c62ef2d299ede53dd7ef7ef7bd6e46b88459d879357')
b2sums=('3f076dd2e655238a290b68f2f64d6acf985e5b7fb7d543fd8eedaf96a0fe025a92927c91aad28d90ee148546e415c924f6d293251b09f54630841cb68f214b4b'
        '88888001effd42cce90cafc1dc7ee621594501f84da1cdab42768efa5aff97a934bcce4c851267a70eedd51307c73527dd7f8206c011790280ed91f308e76935')

prepare() {
  cd "$pkgname"

  # patch wrapper/preload path
  # https://github.com/rizsotto/Bear/issues/648
  patch -p1 -i "$srcdir/fix-wrapper-preload-path.patch"

  # download dependencies
  cargo fetch --locked --target $(rustc --print host-tuple)
}

build() {
  cd "$pkgname"

  local _bear_subprojects=(bear intercept-preload intercept-wrapper)
  cargo build --frozen --release ${_bear_subprojects[@]/#/--package }
}

check() {
  cd "$pkgname/bear"

  cargo test --frozen
}

package() {
  cd "$pkgname"

  # binaries
  install -vDm755 -t "$pkgdir/usr/bin" target/release/bear
  install -vDm755 -t "$pkgdir/usr/lib/bear" target/release/{wrapper,libexec.so}

  # man page
  install -vDm644 -t "$pkgdir/usr/share/man/man1" man/bear.1

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
}
