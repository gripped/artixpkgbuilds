# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Yiyao Yu <yuydevel at protonmail dot com>
# Contributor: Moritz Lipp <mlq@pwmt.org>

pkgname=bear
pkgver=4.1.2
pkgrel=1
pkgdesc='A tool to generate compilation database for clang tooling'
arch=(x86_64)
url='https://github.com/rizsotto/Bear'
license=(GPL-3.0-or-later)
depends=(
  sh
  glibc
  libgcc
)
makedepends=(
  git
  rust
)
options=(!lto)
source=(
  "$pkgname::git+$url.git#tag=$pkgver"
  avoid-libexec.patch
)
sha512sums=('ef16756caad9b9841aeb4d1486985171a61b1cff949cf2d7a1acc5fa053155ff4dee74536b6b5b1febb233230eb66772f4c21f81a9bceffa39a230ba919fbfa2'
            '1ce060b89a48502be9c688d4cdf5668649bf56f573d4c789004c9dc881b31073c38d8941131c360a1c26d306ae502cf037112bd31a9f74af01a7d2971346fb50')
b2sums=('c22cbc97e79e04cb62df81cf4bb1ac504307bc0af2f32b9a48e0442728637137bc2662f8379cf35f444d79d701f4a290c65b0273b69859b1d6fef16dee7dc8b0'
        'eb1a1d8b6b034d0141b7d6f030ffc825745e1f04bfed83950d860a28e6720df6bdc97b81f01986e27de39b6be188fc858d4e060d31d1f55ba3634696fe8c1dba')

prepare() {
  cd "$pkgname"

  # fix install script
  patch -p1 -i "$srcdir/avoid-libexec.patch"

  # download dependencies
  cargo fetch --locked --target host-tuple
}

build() {
  cd "$pkgname"

  cargo build --frozen --release

  # generate shell completions
  ./target/release/generate-completions target/release/completions
}

check() {
  cd "$pkgname/bear"

  cargo test --frozen
}

package() {
  cd "$pkgname"

  DESTDIR="$pkgdir" PREFIX=/usr ./scripts/install.sh
}
