# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Yiyao Yu <yuydevel at protonmail dot com>
# Contributor: Moritz Lipp <mlq@pwmt.org>

pkgname=bear
pkgver=3.1.4
pkgrel=7
pkgdesc='A tool to generate compilation database for clang tooling'
arch=('x86_64')
url='https://github.com/rizsotto/Bear'
license=('GPL-3.0-or-later')
depends=(
  'glibc'
  'gcc-libs'
  'grpc'
  'libgrpc++.so'
  'fmt'
  'spdlog'
  'nlohmann-json'
  'abseil-cpp'
  'protobuf'
  'openssl'
  'c-ares'
  're2'
)
makedepends=(
  'git'
  'cmake'
  'gtest'
  'python'
  'llvm'
)
source=(
  "$pkgname::git+$url.git#tag=$pkgver"
  "disable-lit-tests.patch"
  "fmt-v11.patch::https://github.com/rizsotto/Bear/commit/8afeafe61299c87449023d63336389f159b55808.patch"
)
b2sums=('7f20de8ff834455fc1086eb34adf3123fc7a1ec467ea6fa4f162d2bb8de7af7339c205a246a743fec3100937c91bc53c2a677ae1bad57a22199c8b071e511f59'
        '9bc603ac82c4b2c8bf1263e7d24205f7445b265da16c21935f58c651c5ed78fdb81d00565a3f2b81415831564b81384cd71c799f58c85bce0445ecdf67b7c3b8'
        '578da1c06ec36cef9706b9953a677eb26c96dbb4f67871d8a7617152f25310b413515510289e51b410a04c261c504f50711d1547618a377bdf8bc9d881afd26b')

# XXX if this is moved to build, tests fail.
# there must be some environment variables that
# are discarded inbetween functions.
prepare() {
  # temporary workaround for failing bear::func tests
  # lit errors out when failing to acquire semaphore locks in python multiprocessing module
  # probably related to chroot environment but unrelated to bear::func tests which are not even executed
  patch -d "$pkgname" -p1 -i ../disable-lit-tests.patch

  # Fix fmt v11 compatibility
  patch -d "$pkgname" -p1 -i ../fmt-v11.patch

  cmake \
    -B build \
    -S "$pkgname" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_LIBEXECDIR="lib/$pkgname"
}

build() {
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
