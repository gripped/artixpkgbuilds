# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Yiyao Yu <yuydevel at protonmail dot com>
# Contributor: Moritz Lipp <mlq@pwmt.org>

pkgname=bear
pkgver=4.2.1
pkgrel=1
pkgdesc='A tool to generate compilation database for clang tooling'
arch=(x86_64)
url='https://github.com/rizsotto/Bear'
license=(GPL-3.0-or-later)
depends=(
  sh
  glibc
  libgcc
)
makedepends=(
  git
  rust
)
options=(!lto)
source=(
  "$pkgname::git+$url.git#tag=$pkgver"
  avoid-libexec.patch
)
sha512sums=('a3c5ab386f4b4432e4548b6c322a21a0a9e7907df0c11b3951d8ec3997ec0c606655a9a1d069decfe857cd19f34237fd9dcd180baa24c5342bd6c170c89b2b09'
            '85348e77df755a0ff567f7b1a16773d7219b4f8411d3fd701c81bb8409ab05c974d675ba3b5efe5df676b4b910ba60b303e7e6f74045a5fc2f3bbbbc322c6050')
b2sums=('50d05e7bbccd9227df361d9b8014ef7dc809c387574d86393d7d778bac7671616fe1bbadf02658696f7b031003038d1ac7b94805e4ff4f7002b92034da994985'
        '43de659d7545a56b8e2d1d2db126f63bcb654b09c50dcdea5e7e7610fde86faf8bbefac43032df847420e2727ce01f41d92776598f6a5568fe7c0086b09a2b0d')

prepare() {
  cd "$pkgname"

  # fix install script
  patch -p1 -i "$srcdir/avoid-libexec.patch"

  # download dependencies
  cargo fetch --locked --target host-tuple
}

build() {
  cd "$pkgname"

  cargo build --frozen --release

  # generate shell completions
  ./target/release/generate-completions target/release/completions
}

check() {
  cd "$pkgname"

  cargo build
  cargo test
}

package() {
  cd "$pkgname"

  DESTDIR="$pkgdir" PREFIX=/usr ./scripts/install.sh
}
