# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Yiyao Yu <yuydevel at protonmail dot com>
# Contributor: Moritz Lipp <mlq@pwmt.org>

pkgname=bear
pkgver=4.0.0
pkgrel=1
pkgdesc='A tool to generate compilation database for clang tooling'
arch=(x86_64)
url='https://github.com/rizsotto/Bear'
license=(GPL-3.0-or-later)
depends=(
  glibc
  gcc-libs
)
makedepends=(
  git
  rust
)
source=("$pkgname::git+$url.git#tag=$pkgver")
sha512sums=('5c84536095c04d640fe05e47cfa0aa586c6a0f539c4a3d0f5391fdd52da89b6955d6f5f7bde5cbcbe6053f929febf2716c4ae47a7373e9ea1d5118043ed1a433')
b2sums=('f22505fb18cb8f271542fcd1262da9e5f9c71fa6836202ff32ec62966982f70410fa751b484d2622f0f97c0e7f7415d23446feb84ee1f49ead3ea779c2af9426')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target $(rustc --print host-tuple)
}

build() {
  cd "$pkgname"

  local _bear_subprojects=(bear intercept-preload intercept-wrapper)
  cargo build --frozen --release --all-features ${_bear_subprojects[@]/#/--package }
}

check() {
  cd "$pkgname/bear"

  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"

  # binaries
  install -vDm755 -t "$pkgdir/usr/bin" target/release/bear
  install -vDm755 -t "$pkgdir/usr/lib/bear" target/release/{wrapper,libexec.so}

  # man page
  install -vDm644 -t "$pkgdir/usr/share/man/man1" man/bear.1

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
}
