# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=(manifold python-manifold3d)
pkgver=3.3.2
pkgrel=2
pkgdesc="Geometry library for topological robustness"
arch=('x86_64')
license=('Apache-2.0')
url="https://github.com/elalish/manifold"
makedepends=('cmake' 'ninja' 'git' 'nanobind' 'pybind11' 'onetbb')
source=("https://github.com/elalish/manifold/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('efdae7cd75aabab20fa2673603a9ac263d5b48912559664dde37f5d9e85eced8')

build() {
  local cmake_options=(
    -B build
    -G Ninja
    -S "$pkgname"-$pkgver
    -W no-dev
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D MANIFOLD_PYBIND=ON
    -D MANIFOLD_PAR=ON
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

package_manifold() {
  depends=('glibc' 'gcc-libs' 'nanobind' 'pybind11' 'onetbb')

  DESTDIR="$pkgdir" cmake --install build
  rm -r "$pkgdir"/usr/lib/python*
}

package_python-manifold3d() {
  depends=('glibc' 'gcc-libs' 'manifold')

  DESTDIR="$pkgdir" cmake --install build --component bindings
}
