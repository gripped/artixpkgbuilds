# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bert Peters <bertptrs@archlinux.org>

pkgname=ruby-cucumber-messages
pkgver=34.2.0
pkgrel=1
pkgdesc="Protocol Buffer messages for Cucumber's inter-process communication"
arch=(any)
url='https://github.com/cucumber/messages/tree/main/ruby'
license=(MIT)
depends=(
  ruby
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-bundler
  ruby-rake
  ruby-rspec
)
options=(!emptydirs)
source=(git+https://github.com/cucumber/messages.git#tag=v$pkgver)
sha512sums=('73ef6a026bcd3063d5d5e1fa835eeb0d0c0cc86c158b6832b2fa7246621229dd26869e765cee023af1b039816f70784d4b088ea4a03bd14de80b6b60545729c2')
b2sums=('10d3445b893873a438cd3409dc211cac813778e26edf6723eb7a706ec8e14cc53fe565f01c0ded2ad4f89a9500395fb95cb89926361152a70ede32ae3e943ed7')

build() {
  local _gemdir="$(gem env gemdir)"
  cd messages/ruby
  gem build cucumber-messages.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    cucumber-messages-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd messages/ruby
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd messages/ruby
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 ../LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
