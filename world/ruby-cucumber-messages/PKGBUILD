# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bert Peters <bertptrs@archlinux.org>

pkgname=ruby-cucumber-messages
pkgver=31.0.1
pkgrel=1
pkgdesc="Protocol Buffer messages for Cucumber's inter-process communication"
arch=(any)
url='https://github.com/cucumber/messages/tree/main/ruby'
license=(MIT)
depends=(
  ruby
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bundler
  ruby-rake
  ruby-rspec
)
options=(!emptydirs)
source=(https://github.com/cucumber/messages/archive/v$pkgver/cucumber-messages-$pkgver.tar.gz)
sha512sums=('1ee553f6a0fd0a0740990e896520e6b6c06ddbc9006981ce52253aee18ed202175bbc945bc7674843be91c6010dca8384956663e014df4fc7e61fb79afe21d3c')
b2sums=('f8d1b95cc0e2c5415af162c45b255a9997d93283e358b5f64cc96fab9866d0ad0a2e0d4258dd6c6dc89d4d31430639dca6569be486099467917aad13d26dad58')

build() {
  local _gemdir="$(gem env gemdir)"
  cd messages-$pkgver/ruby
  gem build cucumber-messages.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    cucumber-messages-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd messages-$pkgver/ruby
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd messages-$pkgver/ruby
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 ../LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
