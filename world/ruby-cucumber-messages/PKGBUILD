# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bert Peters <bertptrs@archlinux.org>

pkgname=ruby-cucumber-messages
pkgver=32.3.1
pkgrel=1
pkgdesc="Protocol Buffer messages for Cucumber's inter-process communication"
arch=(any)
url='https://github.com/cucumber/messages/tree/main/ruby'
license=(MIT)
depends=(
  ruby
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-bundler
  ruby-rake
  ruby-rspec
)
options=(!emptydirs)
source=(git+https://github.com/cucumber/messages.git#tag=v$pkgver)
sha512sums=('3b6e15a28b025c59dbcc880586bb3250758d8811b5771656237229e02ee238aded5689477d9d3e441e6d03ad09fc6827862f1e72f6c31840be42d46a981f1775')
b2sums=('0c5889a2ad13bf258dda8996bd2e6e361f17e9592be0d30b152572f5815ed451f7baef4bffc6ffad3dc5babc90164f74f7fceca9876ceff975fec4a0e063a5a9')

build() {
  local _gemdir="$(gem env gemdir)"
  cd messages/ruby
  gem build cucumber-messages.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    cucumber-messages-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd messages/ruby
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd messages/ruby
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 ../LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
