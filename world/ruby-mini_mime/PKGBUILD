# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Bert Peters <bertptrs@archlinux.org>
_name=mini_mime
pkgname=ruby-mini_mime
pkgver=1.1.5
pkgrel=3
pkgdesc='A lightweight mime type lookup toy'
arch=('any')
url='https://github.com/discourse/mini_mime'
depends=('ruby')
checkdepends=('ruby-bundler' 'ruby-minitest' 'ruby-rake')
license=('MIT')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/discourse/$_name/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('920dcec03f599d654bed03a465801d85a3c4d9691c97fd5dc67ace013c1ad60e')

prepare() {
  cd "${_name}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_name}.gemspec"

  # we don't build from a git checkout
  sed --in-place --regexp-extended 's|git ls-files -z|find . -type f -print0|' "${_name}.gemspec"
}

build() {
  cd "${_name}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build "${_name}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --build-root "tmp_install" \
    "${_name}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install/${_gemdir}/cache/" \
    "tmp_install/${_gemdir}/gems/${_name}-${pkgver}/vendor/" \
    "tmp_install/${_gemdir}/doc/${_name}-${pkgver}/ri/ext/"

  find "tmp_install/${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install/${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${_name}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install/${_gemdir}" rake test
}

package() {
  cd "${_name}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE.txt --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
