# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=path_expander
pkgname=ruby-${_gemname}
pkgver=1.1.2
pkgrel=1
pkgdesc='Helps pre-process command-line arguments expanding directories into their constituent file'
url='https://github.com/seattlerb/path_expander'
arch=('any')
license=('MIT')
depends=('ruby')
makedepends=('ruby-rdoc' 'ruby-rake' 'ruby-hoe')
checkdepends=('ruby-minitest')
options=('!emptydirs')
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('e5c8a31112a34f1d047f20af70ff260a2a258db33b1b5ea38d4a0170126e8b4ad7cc3957b1eb30497f46d40c4f5eb77d14f4254197661ea70e29c78f52f136c0')
b2sums=('e1000124e5c211f1cb1e9fef898c45b68fa142810d1c31f477875eb8df40329365abee8ba9c1080953ae13f760885f1febb246378eeb3d52afa0ba47a48ff538')

build() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i Rakefile
  rake gem
}

check() {
  cd ${_gemname}-${pkgver}
  rake test
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" \
    -n "${pkgdir}/usr/bin" pkg/${_gemname}-${pkgver}.gem

  install -Dm 644 README.rdoc History.rdoc -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 README.rdoc -t "${pkgdir}/usr/share/licenses/${pkgname}"

  rm -r "${pkgdir}/${_gemdir}/"{cache,gems/${_gemname}-${pkgver}/test}
}

# vim: ts=2 sw=2 et:
