# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ktouch
pkgver=25.04.3
pkgrel=1
pkgdesc='Touch Typing Tutor'
url='https://apps.kde.org/ktouch/'
arch=(x86_64)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kcmutils5
         kcompletion5
         kconfig5
         kconfigwidgets5
         kcoreaddons5
         ki18n5
         kitemviews5
         kqtquickcharts
         ktextwidgets5
         kwidgetsaddons5
         kxmlgui5
         libx11
         libxcb
         qt5-base
         qt5-declarative
         qt5-graphicaleffects
         qt5-quickcontrols2
         qt5-x11extras
         qt5-xmlpatterns)
makedepends=(extra-cmake-modules
             kdoctools5
             libxkbfile)
groups=(kde-applications
        kde-education)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('acc1cf7f43526a8fb569f96e63aca39ec3e6e2ac6807d27005650f870af59e90'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
