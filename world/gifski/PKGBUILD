# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Filipe Laíns (FFY00) <lains@archlinux.org>
# Contributor: Orhun Parmaksız <orhun@archlinux.org>
# Contributor: Philipp Wolfer <ph.wolfer@gmail.com>

pkgname=gifski
pkgver=1.32.0
pkgrel=4
pkgdesc='GIF encoder based on libimagequant (pngquant). Squeezes maximum possible quality from the awful GIF format.'
arch=('x86_64')
url='https://gif.ski'
license=('AGPL-3.0-or-later')
depends=('ffmpeg')
makedepends=('cargo' 'clang')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ImageOptim/$pkgname/archive/$pkgver.tar.gz"
         ffmpeg-7.patch
         cargo-update.patch)
sha512sums=('0088917b5080c31efe98c76193b119069c1eb8ac9d50e98b207371ed2e22a3a4203f6f2cfee4b885d99d7d85d1f0d247831002997aac4058e8eb70b647f82988'
            'f368215a7d3ac209af37d567ba62fa621eb623691eabaf4c989ff34c03e42c18416d2b9f5a4b2b87ba47bb11eee961dff4a75aa28512789e86d720ed9e359e2f'
            'fd4ba70a82ea8f736e3910c027431ed89d979272248946bc3f856063ab156956ed33b74e4f55eaa7a902477de001f6e4ad2cd7fb7f6b710f988de4e76d7c2299')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../ffmpeg-7.patch # Fix build with ffmpeg 7
  patch -p1 -i ../cargo-update.patch # Fix build with x265-4.0

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}


build() {
  cd $pkgname-$pkgver

  cargo build --frozen --release --features=video
}

check() {
  cd $pkgname-$pkgver

  cargo test --frozen
}

package() {
  cd $pkgname-$pkgver

  install -Dm 755 target/release/gifski "$pkgdir"/usr/bin/gifski
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
