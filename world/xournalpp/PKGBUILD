# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Justin Kromlinger <hashworks@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>

pkgname=xournalpp
pkgver=1.2.4
pkgrel=1
pkgdesc='Handwriting notetaking software with PDF annotation support'
url='https://github.com/xournalpp/xournalpp'
license=('GPL-2.0-or-later')
arch=('x86_64')
optdepends=('texlive-latexextra: LaTeX package')
makedepends=('git' 'cmake' 'doxygen' 'graphviz')
depends=('gtk3' 'poppler-glib' 'libxml2' 'portaudio' 'libsndfile' 'lua'
         'libzip')
source=("git+https://github.com/xournalpp/xournalpp.git#tag=v${pkgver}")
sha256sums=('5d668948a6d456c5e736b27c2d8043fe93beb73ebc432071c0c66046f54364e1')

replaces=('xournal')

build() {
	cd "${srcdir}/${pkgname}"
	cmake -DCMAKE_INSTALL_PREFIX=/usr
	make translations
	make
}

package() {
	cd "${srcdir}/${pkgname}"
	make DESTDIR="${pkgdir}" install
}
