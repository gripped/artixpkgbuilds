# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Torsten Ke√üler <tpkessler@archlinux.org>
# Contributor: HurricanePootis <hurricanepootis@protonmail.com>
pkgname=hiprt
pkgver=3.1.0.cb09c56
# For the calculation of the HIPRT_VERSION, see
# https://github.com/GPUOpen-LibrariesAndSDKs/HIPRT/blob/8602b8c475255fb922c2792654aae0a6bcdeb0af/CMakeLists.txt#L262
# For zero padding, see
# https://github.com/GPUOpen-LibrariesAndSDKs/HIPRT/blob/8602b8c475255fb922c2792654aae0a6bcdeb0af/CMakeLists.txt#L265-L266
_libver=$(awk -F "." '{printf "%05d\n", $1 * 1000 + $2}' <<< $pkgver)
pkgrel=2
pkgdesc="Ray Tracing Library for HIP"
arch=('x86_64')
url="https://gpuopen.com/hiprt/"
license=('MIT')
depends=('rocm-core' 'hip-runtime-amd' 'glibc' 'gcc-libs')
makedepends=('git' 'python' 'cmake')
source=("$pkgname-$pkgver::git+https://github.com/GPUOpen-LibrariesAndSDKs/HIPRT#tag=$pkgver")
sha256sums=('6c60046f8d9406807063a1c5accc6ce85b03b694d818cc56f8d79f3b700bc27a')

build() {
	local cmake_args=(
		-Wno-dev
		-S "$pkgname-$pkgver"
		-B build
		-D CMAKE_BUILD_TYPE=Release
		-D HIP_PATH=/opt/rocm
		-D BAKE_KERNEL=OFF
		-D BAKE_COMPILED_KERNEL=OFF
		-D BITCODE=ON
		-D PRECOMPILE=ON
		-D NO_UNITTEST=ON
		-D FORCE_DISABLE_CUDA=ON
		-D CMAKE_INSTALL_PREFIX=/opt/rocm
	)
	cmake "${cmake_args[@]}"
	cmake --build build
}

package() {
	DESTDIR="$pkgdir" cmake --install build

	# GPU files are generated at build time by python scripts and put into the
	# source folder. Move them to the package folder
	local file
	for file in "$pkgname-$pkgver"/scripts/bitcodes/*.{bc,hipfb}; do
		install -v -Dm644 "$file" "$pkgdir"/opt/rocm/lib/"$(basename "$file")"
	done

	# HIPRT library is installed to the bin folder,
	# so we have to move it manually.
	install -dm755 "$pkgdir"/opt/rocm/lib
	mv -v "$pkgdir"/opt/rocm/bin/* "$pkgdir"/opt/rocm/lib
	rmdir "$pkgdir"/opt/rocm/bin
	ln -vs /opt/rocm/lib/libhiprt${_libver}64.so "${pkgdir}"/opt/rocm/lib/libhiprt64.so

	install -Dm644 "$pkgname-$pkgver"/license.txt "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
