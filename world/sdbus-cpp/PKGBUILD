# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Cirk2 <privat+aur at cirk2 dot de>

pkgbase=sdbus-cpp
pkgname=($pkgbase $pkgbase-doc)
pkgver=2.2.1
pkgrel=1
pkgdesc='a high-level C++ D-Bus library designed to provide expressive, easy-to-use API'
url="https://github.com/Kistler-Group/$pkgbase"
arch=(x86_64)
license=(LGPL-2.1-only LicenseRef-sdbus-c++-LGPL-Exception-1.0)
depends=(expat
         libelogind)
makedepends=(cmake
             doxygen
             elogind)
_archive="$pkgbase-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('da69a0104beb6e51415a59f1571a47beb1eacc65cc6027b250eb1cf13ff4f802')

build() {
	cmake -B build -S "$_archive" \
		-D CMAKE_INSTALL_PREFIX=/ \
		-D CMAKE_BUILD_TYPE=Release \
		-D SDBUSCPP_BUILD_CODEGEN=ON \
		-D SDBUSCPP_BUILD_DOCS=ON
	cmake --build build

	# Install so we can split the packaging up later
	DESTDIR="fakeinstall" cmake --install build

	# Remove references to $srcdir
	find fakeinstall/usr/share/doc \
		-name \*.html \
		-print \
		-exec sed \
		  -e "s|$srcdir/sdbus-cpp/include|/usr/include|g" \
		  -e "s|$srcdir/sdbus-cpp||" \
		  -i {} \;
}

_package_dir() {
	install -dm755 "$pkgdir/usr/$1"
	cp -dr --no-preserve=owner "$srcdir/fakeinstall/usr/$1/"* "$pkgdir/usr/$1"
}

package_sdbus-cpp() {
	provides=(libsdbus-c++.so)
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgbase/" "$_archive/COPYING"*
	for dir in lib include bin; do
		_package_dir $dir
	done
}

package_sdbus-cpp-doc() {
	_package_dir share/doc
}
