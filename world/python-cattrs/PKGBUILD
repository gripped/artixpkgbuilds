# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-cattrs
pkgver=26.2.1
pkgrel=1
pkgdesc='Complex custom class converters for attrs'
arch=(any)
url='https://catt.rs'
_url='https://github.com/python-attrs/cattrs'
license=(MIT)
depends=(
  python
  python-attrs
  python-typing_extensions
)
makedepends=(
  git
  python-build
  python-hatch-vcs
  python-hatchling
  python-installer
  python-wheel
)
checkdepends=(
  python-hypothesis
  python-msgspec
  python-orjson
  python-pytest
  python-pytest-benchmark
  python-yaml
)
optdepends=(
  'python-msgspec: msgspec converter'
  'python-orjson: orjson converter'
  'python-yaml: YAML converter'
)
source=("git+$_url#tag=v$pkgver")
b2sums=('79ff686ee7155b3fb0e88e4e05c4852922d1819182c3ebee0323a17eed93494c7af8ea3b0121fdb811cb6f6e9ae462b8a95a137d520847c066872a71c957c96a')

build() {
  cd "${pkgname#python-}"
  python -m build --wheel --no-isolation
}

check() {
  cd "${pkgname#python-}"
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  # Deselected tests depend on currently unpackaged python-immutables,
  # python-bson, python-msgspec.
  test-env/bin/python -m pytest --override-ini="addopts=" \
    --ignore=tests/preconf/test_msgspec_cpython.py \
    --ignore=tests/preconf/test_pyyaml.py \
    --ignore=tests/test_cols.py \
    --ignore=tests/test_preconf.py \
    --ignore=tests/test_unstructure_collections.py
}

package() {
  cd "${pkgname#python-}"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
