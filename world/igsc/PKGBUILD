# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgname=igsc
pkgver=0.8.13
pkgrel=1
pkgdesc="Intel graphics system controller firmware update library"
arch=(x86_64)
url="https://github.com/intel/igsc/"
license=(Apache)
depends=(intel-metee libudev)
makedepends=(cmake udev)
source=(${url}/archive/V${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('c429b09cc5b593570bbc02a3515b4aa8d65d12a678fb02ab509a830b48e8f2f4')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -G 'Unix Makefiles' \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_PREFIX_PATH=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DENABLE_WERROR=OFF \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
