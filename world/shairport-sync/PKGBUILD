# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Mike Lei <mikelei@duck.com>

pkgname=shairport-sync
pkgver=5.0.4
pkgrel=2
pkgdesc='AirPlay and AirPlay 2 audio player'
url='https://github.com/mikebrady/shairport-sync'
arch=(x86_64)
license=(GPL-2.0-only)
backup=(etc/shairport-sync.conf)
depends=(openssl avahi libsoxr popt alsa-lib libconfig libpipewire libpulse jack mosquitto ffmpeg nqptp libplist)
makedepends=(glib2-devel xmltoman vim) # vim for xxd tool
source=(shairport-sync-$pkgver.tar.gz::https://github.com/mikebrady/shairport-sync/archive/refs/tags/$pkgver.tar.gz
        shairport-sync.sysusers
	remove_useradd.patch)
sha256sums=('b89d4af74cffadd83d1be6eaf4e967180aa5a6aed32f561c937ae1d787909c25'
            'bc2d92254910996e837d1c4c7dd81eddfb96a9f5f0cb2faad9fcb0414ea79a1d'
            'b10bcba9dbe0988868ceabe1b6e1d6d9dbce385661a76a139f0c365966db45f2')

prepare() {
  cd shairport-sync-$pkgver

  patch -p1 < ../remove_useradd.patch
}

build() {
  cd shairport-sync-$pkgver

  autoreconf -i -f
  configure_args=(
    --prefix=/usr
    --sysconfdir=/etc
    --with-airplay-2
    # Audio Output
    --with-alsa
    --with-pipewire
    --with-pulseaudio
    --with-jack
    --with-stdout
    --with-pipe
    # Audio Options
    --with-soxr
    --with-convolution
    # Metadata
    --with-metadata
    # IPC
    --with-mqtt-client
    --with-dbus-interface
    --with-mpris-interface
    # Zeroconf/Bonjour
    --with-avahi
    --with-dns_sd
    # Misc
    --without-systemd-startup
    --with-ssl=openssl
    --with-configfiles
    --with-pkg-config
  )
  ./configure "${configure_args[@]}"
  make
}

package() {
  cd shairport-sync-$pkgver
  make DESTDIR="$pkgdir" install
  install -D -m644 "$srcdir"/shairport-sync.sysusers "$pkgdir"/usr/lib/sysusers.d/shairport-sync.conf
  install -D -m664 LICENSES "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm "$pkgdir"/etc/shairport-sync.conf.sample
}
