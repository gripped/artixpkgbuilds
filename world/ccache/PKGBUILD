# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=ccache
pkgver=4.13.1
pkgrel=1
pkgdesc='Compiler cache that speeds up recompilation by caching previous compilations'
url='https://ccache.dev/'
arch=('x86_64')
license=('GPL-3.0-or-later')
depends=(
  'fmt'
  'glibc'
  'hiredis'
  'libblake3'
  'libgcc'
  'libstdc++'
  'libxxhash.so'
  'libzstd.so'
  'xxhash'
  'zstd'
)
makedepends=(
  'asciidoctor'
  'cmake'
  'perl'
  'tl-expected'
)
checkdepends=('doctest')
source=("https://github.com/ccache/ccache/releases/download/v$pkgver/ccache-$pkgver.tar.xz"{,.minisig})
sha512sums=('f621f2617c922ba82ee7e7a84613db1f7d9d73d097246cf171cb3ac0e9da79d42ce6dfab23937081ffadd3bc11fdd6df2f74a671873ba3836b96788441b13cca'
            '205cb30a20ef1c0b2ed3e4df946e71581425a805be3e04cff172257bcc5b9968d720018093176e3466b41464a16f46ef401381a5c9ea13f2546b1aabdd0c1b9f')
b2sums=('627e266bbeab8496322dcfde07c6880fc5e3ccf9f87edce972a6c5f54eba647a8bf4eca256adcef819afe315499b57cc9b3b02c2b6717af0b4c1432abdccf3f4'
        'eb47644b7c5ae50e59ea33129e9e92a79082c44b5817385ee531da0472498830ba94d2d6e3aff35321691d56e3064ffc4a851f33566aa03eb22a541474f3a985')
validpgpkeys=('5A939A71A46792CF57866A51996DDA075594ADB8') # Joel Rosdahl <joel@rosdahl.net>

build() {
  cd $pkgname-$pkgver
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -Wno-dev \
    -B build \
    -S .
  cmake --build build
  cmake --build build --target doc
}

check() {
  cd $pkgname-$pkgver
  ctest --test-dir build --output-on-failure
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install -C build
  make DESTDIR="$pkgdir" install -C build/doc

  install -vDm 644 -t "$pkgdir/usr/share/doc/$pkgname" doc/*.md doc/*.adoc

  install -vdm 755 "$pkgdir/usr/lib/ccache/bin"
  local prog
  for prog in gcc g++ c++; do
    ln -vs /usr/bin/ccache "$pkgdir/usr/lib/ccache/bin/$prog"
    ln -vs /usr/bin/ccache "$pkgdir/usr/lib/ccache/bin/$CHOST-$prog"
  done
  for prog in cc clang clang++; do
    ln -vs /usr/bin/ccache "$pkgdir/usr/lib/ccache/bin/$prog"
  done
}

