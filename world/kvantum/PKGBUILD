# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgbase=kvantum
pkgname=(kvantum
         kvantum-qt5)
pkgver=1.1.4
pkgrel=1
pkgdesc='SVG-based theme engine for Qt6 (including config tool and extra themes)'
arch=(x86_64)
url='https://github.com/tsujan/Kvantum'
license=(GPL-3.0-or-later)
depends=(gcc-libs
         glibc
         libx11)
makedepends=(cmake
             git
             qt5-svg
             qt5-tools
             qt5-x11extras
             kwindowsystem
             qt6-svg
             qt6-tools)
source=(git+https://github.com/tsujan/Kvantum#tag=V$pkgver?signed)
sha256sums=('01f1b38773daa8880a6f2a65bd7ac668da04d8e97e582b1d248a139e8ecaa532')
validpgpkeys=(19DFDF3A579BD509DBB572D8BE793007AD22DF7E) # Pedram Pourang <tsujan2000@gmail.com>

build() {
  cmake -B build5 -S Kvantum/Kvantum \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_QT5=ON
  make -C build5

  cmake -B build6 -S Kvantum/Kvantum \
    -DCMAKE_INSTALL_PREFIX=/usr
  make -C build6
}

package_kvantum() {
  depends+=(kwindowsystem
            qt6-base
            qt6-svg)
  optdepends=('kvantum-qt5: Qt5 style')

  DESTDIR="$pkgdir" cmake --install build6
}

package_kvantum-qt5() {
  pkgdesc='SVG-based theme engine for Qt5'
  depends+=(kvantum
            qt5-base
            qt5-svg
            qt5-x11extras)

  DESTDIR="$pkgdir" cmake --install build5/style
}
