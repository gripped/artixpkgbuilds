# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgbase=kvantum
pkgname=(kvantum
         kvantum-qt5)
pkgver=1.1.0
pkgrel=1
pkgdesc='SVG-based theme engine for Qt6 (including config tool and extra themes)'
arch=(x86_64)
url='https://github.com/tsujan/Kvantum'
license=(GPL-3.0-or-later)
depends=(gcc-libs
         glibc
         libx11)
makedepends=(cmake
             kwindowsystem5
             qt5-svg
             qt5-tools
             qt5-x11extras
             kwindowsystem
             qt6-svg
             qt6-tools)
source=(https://github.com/tsujan/Kvantum/releases/download/V$pkgver/Kvantum-$pkgver.tar.xz{,.asc})
sha256sums=('bfc5d97630c87e5b21ccab86efbc3511105c61829a75798923ec2274b7d5cd32'
            'SKIP')
validpgpkeys=(19DFDF3A579BD509DBB572D8BE793007AD22DF7E) # Pedram Pourang <tsujan2000@gmail.com>

build() {
  cmake -B build5 -S Kvantum-$pkgver/Kvantum \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_QT5=ON
  make -C build5

  cmake -B build6 -S Kvantum-$pkgver/Kvantum \
    -DCMAKE_INSTALL_PREFIX=/usr
  make -C build6
}

package_kvantum() {
  depends+=(kwindowsystem
            qt6-base
            qt6-svg)
  optdepends=('kvantum-qt5: Qt5 style')

  DESTDIR="$pkgdir" cmake --install build6
}

package_kvantum-qt5() {
  pkgdesc='SVG-based theme engine for Qt5'
  depends+=(kvantum
            kwindowsystem5
            qt5-base
            qt5-svg
            qt5-x11extras)

  DESTDIR="$pkgdir" cmake --install build5/style
}
