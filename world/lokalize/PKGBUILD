# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=lokalize
pkgver=25.12.1
pkgrel=1
pkgdesc='Computer-Aided Translation System'
url='https://apps.kde.org/lokalize/'
arch=(x86_64)
license=(GPL-2.0-or-later
         LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         hunspell
         kcolorscheme
         kcompletion
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         kdbusaddons
         ki18n
         kiconthemes
         kio
         kitemviews
         knotifications
         ktextwidgets
         kwidgetsaddons
         kxmlgui
         qt6-base
         sonnet)
makedepends=(extra-cmake-modules
             kdoctools)
optdepends=('languagetool: spell and grammar checking'
            'sh: for some scripts')
groups=(kde-applications
        kde-sdk)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('5d4521dca1442276b78261b3c9d82245d61840b0c20bdac1e3896ddb5fa8facd'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
