# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-qed
pkgver=2.9.2
pkgrel=1
pkgdesc='Quality Ensured Documentation'
arch=(any)
url='https://github.com/rubyworks/qed'
license=(BSD-2-Clause)
depends=(
  ruby
  ruby-ansi
  ruby-brass
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-ae
)
options=(!emptydirs)
_commit=ff8cf7fd8572d675c7e4a1c1db3e0a9727283d60
source=(
  # Upstream did not tag 2.9.2: https://github.com/rubyworks/qed/issues/27
  git+https://github.com/rubyworks/qed.git#commit=$_commit
)
sha256sums=('a87772cf2f6972118b1a1ea2e475f116733e3b64336672b0c7818c6053e7abf4')

build() {
  local _gemdir="$(gem env gemdir)"
  cd qed
  gem build .gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    qed-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd qed
  PATH="$PWD/tmp_install/usr/bin:$PATH" GEM_HOME="tmp_install/$_gemdir" qed
}

package() {
  cd qed
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
