# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=akonadi-import-wizard
pkgver=26.04.3
pkgrel=1
pkgdesc='Import data from other mail clients to KMail'
arch=(x86_64)
url='https://kontact.kde.org'
license=(GPL-2.0-or-later)
depends=(akonadi
         glibc
         kconfig
         kconfigwidgets
         kcontacts
         kcoreaddons
         kcrash
         kdbusaddons
         ki18n
         kiconthemes
         kidentitymanagement
         kmailtransport
         kwidgetsaddons
         kxmlgui
         libgcc
         libstdc++
         mailcommon
         mailimporter
         messagelib
         pimcommon
         qt6-base
         qtkeychain-qt6)
makedepends=(extra-cmake-modules
             kdoctools)
optdepends=('kdepim-addons: additional import plugins')
groups=(kde-applications
        kde-pim)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('834f6dcb0b1c06e26d812fa4f35c399d7847d8aa7ae74bfba6064fc8dd06c91b'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
