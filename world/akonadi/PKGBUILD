# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=akonadi
pkgname=(akonadi libakonadi)
pkgver=25.08.3
pkgrel=2
pkgdesc='PIM layer, which provides an asynchronous API to access all kind of PIM data'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kconfig
         kcoreaddons
         ki18n
         libaccounts-qt
         qt6-base)
makedepends=(extra-cmake-modules
             postgresql
             kaccounts-integration
             kconfigwidgets
             kiconthemes
             kitemmodels
             kxmlgui
             qt6-tools)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/pim/akonadi/-/commit/54c01bab.patch)
sha256sums=('f867fd1e16d9d637a92dab334dc4170f20f58be13367392094ec539e3eee017a'
            'SKIP'
            'dbfd341f7bdbd7b31369301c064c4c9b4deb6c972020f65ee424b9713779e96e')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < 54c01bab.patch # Fix SQL syntax error with MariaDB 12.1
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package_libakonadi() {
  pkgdesc='Libraries used by applications based on Akonadi'
  depends+=(kaccounts-integration
            kcolorscheme
            kconfigwidgets
            kcrash
            kiconthemes
            kitemmodels
            kwidgetsaddons
            kxmlgui
            libxml2
            xz)
  optdepends=('kirigami-addons: QML bindings')

  DESTDIR="$pkgdir" cmake --install build
  rm -r "$pkgdir"/usr/bin # Provided by akonadi
}

package_akonadi() {
  depends+=(kcrash
            kwidgetsaddons
            libakonadi
            mariadb)
  optdepends=('postgresql: PostgreSQL backend')

  DESTDIR="$pkgdir" cmake --install build
  rm -r "$pkgdir"/{etc,usr/{include,lib,share}} # Provided by libakonadi
}
