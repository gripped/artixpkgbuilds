# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=tokodon
pkgver=25.12.0
pkgrel=1
pkgdesc='A Mastodon client for Plasma'
url='https://apps.kde.org/tokodon/'
arch=(x86_64)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kcolorscheme
         kconfig
         kcoreaddons
         kdbusaddons
         kdeclarative
         ki18n
         kio
         kirigami
         kirigami-addons
         kitemmodels
         knotifications
         kservice
         kunifiedpush
         kwindowsystem
         prison
         purpose
         qqc2-desktop-style
         qt6-base
         qt6-declarative
         qt6-multimedia
         qt6-websockets
         qt6-webview
         qtkeychain-qt6)
makedepends=(extra-cmake-modules
             qcoro)
groups=(kde-applications 
        kde-network)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('7223cd3957e84755f0fb73871510c083454bc049522fbacc8850d61bbd755c6f'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
