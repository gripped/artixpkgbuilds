# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Gabriel Martinez < reitaka at gmail dot com >

pkgname=libplist
pkgver=2.5.0
pkgrel=2
pkgdesc='Library to handle Apple Property List files'
arch=('x86_64')
url='https://libimobiledevice.org/'
license=('LGPL-2.1-or-later')
depends=('gcc-libs' 'glibc')
makedepends=('cython' 'git' 'python' 'python-setuptools')
optdepends=('python: Python bindings')
source=("git+https://github.com/libimobiledevice/libplist.git#tag=$pkgver"
        'libplist-fix-api-break.patch')
b2sums=('6e0219ede2b230d2242975d7efa6a5461d11d6b086e5378600d83ce885d6cb99b75b7e498fcbafd47b33c75d8e985414a6d27c509ea97ddaa780d118858d1929'
        'c5243f38bc27680962bc12d30ec3c015bf22194c8696134b7f8ec5550fea9ea6c7eaf54333a59556809ceea119e76d07321c9b2a83377d02b8ef76d652d2c2cc')

prepare() {
  cd $pkgname

  # restore compatibility with older API after breakage by libplist-2.5.0
  # https://github.com/libimobiledevice/libplist/pull/261
  patch -Np1 -i ../libplist-fix-api-break.patch

  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {    
  cd $pkgname
  make check  
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
