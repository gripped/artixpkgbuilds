# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=towncrier
pkgver=26.9.0
pkgrel=1
pkgdesc="Utility to produce useful, summarised news files for your project"
arch=('any')
url="https://github.com/twisted/towncrier"
license=('MIT')
depends=(
  'python'
  'python-click'
  'python-jinja'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-hatchling'
)
checkdepends=(
  'python-twisted'
  'mercurial'
)
source=("git+https://github.com/twisted/towncrier.git#tag=$pkgver")
sha512sums=('f95e7ae5453c25aa5805f04b7a7e42639897f146c8426c9ec6f164a73a43650189008e73f94aaed944708825f97c3212629556db972726988cf5e1328c45a5ec')

build() {
  cd towncrier
  python -m build --wheel --no-isolation
}

check() {
  cd towncrier
  python -m installer --destdir=tmp_install dist/*.whl
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="$PWD/tmp_install/usr/lib/python${python_version}/site-packages" PATH="$PWD/tmp_install/usr/bin:$PATH" trial towncrier
}

package() {
  cd towncrier
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
