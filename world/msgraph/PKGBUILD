# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=msgraph
pkgname=(
  msgraph
  msgraph-docs
)
pkgver=0.3.4
pkgrel=1
pkgdesc="A shared library for accessing MS Graph API"
url="https://gitlab.gnome.org/GNOME/msgraph"
arch=(x86_64)
license=(LGPL-3.0-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  json-glib
  libgoa
  libsoup3
)
makedepends=(
  gi-docgen
  git
  gobject-introspection
  meson
  uhttpmock
)
source=("git+https://gitlab.gnome.org/GNOME/msgraph.git#tag=$pkgver")
b2sums=('679092f34701b0f44a38f17fad629cd9e7efad98dec8c888d058759f066fae06cb49cd2c39f205e5b1e2fb81f3b274dd158462a22d80cc2acd0c0f91e6c89b16')

prepare() {
  cd msgraph
}

build() {
  artix-meson msgraph build
  meson compile -C build
}

check() {
  meson test -vC build --print-errorlogs
}

package_msgraph() {
  meson install -C build --destdir "$pkgdir"
  provides=('libmsgraph-1.so')

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_msgraph-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
