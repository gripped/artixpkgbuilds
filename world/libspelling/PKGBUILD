# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=libspelling
pkgname=(
  libspelling
  libspelling-docs
)
pkgver=0.4.5
pkgrel=1
pkgdesc="Spellcheck library for GTK 4"
url="https://gitlab.gnome.org/GNOME/libspelling"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  enchant
  gcc-libs
  glib2
  glibc
  gtk4
  gtksourceview5
  icu
  pango
)
makedepends=(
  gi-docgen
  git
  gobject-introspection
  meson
  vala
)
checkdepends=(
  aspell
  aspell-en
  enchant
  hspell
  hunspell
  nuspell
  libvoikko
)
source=("git+https://gitlab.gnome.org/GNOME/libspelling.git#tag=$pkgver")
b2sums=('24c2ad242c3efc88c6abb47c2c5bad7a44650d66b8982deeb294afc818bb3eb72808d3f07df73d35d3e5fc74640ceccc1289ae7bfb299fb1cd0b327ef8ff97db')

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_libspelling() {
  provides=(libspelling-1.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_libspelling-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
