# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=plasmatube
pkgver=25.12.0
pkgrel=1
pkgdesc='Kirigami YouTube video player based on QtMultimedia and youtube-dl'
url='https://apps.kde.org/plasmatube/'
arch=(x86_64)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kconfig
         kcoreaddons
         kdbusaddons
         kdeclarative # for kquickcontrolsaddons
         ki18n
         kirigami
         kirigami-addons
         kitemmodels
         knotifications
         kwindowsystem
         mpvqt
         purpose
         qqc2-desktop-style
         qt6-base
         qt6-declarative
         qtkeychain-qt6)
makedepends=(extra-cmake-modules)
groups=(kde-applications
        kde-multimedia)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('49a15f9b38404a8cd24b03b2200117a2983e44f830c3853dbfa8e3afa40fd2b2'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
