# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=plasmatube
pkgver=23.08.5
pkgrel=1
pkgdesc='Kirigami YouTube video player based on QtMultimedia and youtube-dl'
url='https://apps.kde.org/plasmatube/'
arch=(x86_64)
license=(GPL LGPL)
groups=(kde-applications kde-multimedia)
depends=(kirigami-addons5 qt5-svg kcoreaddons5 kconfig5 ki18n5 mpv yt-dlp)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('66ca4fdc2059d72651d6be5264860ba66af90e0be10af80c2ea30859727c1b9d'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  artix-cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
