# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=tinycompress
pkgver=1.2.11
pkgrel=3
pkgdesc="ALSA compressed device interface"
url="https://github.com/alsa-project/tinycompress"
arch=(x86_64)
license=("BSD-3-Clause OR LGPL-2.1-only")
depends=(glibc)
makedepends=(
  ffmpeg
  git
)
optdepends=("ffmpeg: fcplay utility")
provides=(libtinycompress.so)
options=(debug)
_commit=e98e500873c9c4fdd752d1c85b6116da426a8a70  # tags/v1.2.11^0
source=(
  "git+$url#commit=$_commit"
  0001-Fix-build-with-FFmpeg-7.patch
)
b2sums=('e23df6e28228fc059613f182bf6d5c5edbd95932ccc6d36e7aefadaa7474f59a55dccb841d24c25453d416cbd31ffb3a1d651061b3441c21d813e7127fd451db'
        '29ccd63a6c70397239ac7f9661c3b27256ae044b2de6458e55595886867300cd3c004bb9ba827a1304ba410dd3f5a869e8045755f7e9a04eebd34034bee2cd9a')

pkgver() {
  cd tinycompress
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  mkdir -p build

  cd tinycompress
  git apply -3 ../0001-Fix-build-with-FFmpeg-7.patch
  ./autogen.sh
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
    --localstatedir=/var
    --enable-fcplay
  )

  cd build
  ../tinycompress/configure "${configure_options[@]}"
  make
}

check() {
  make -C build check
}

package() {
  make -C build install DESTDIR="$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 tinycompress/COPYING
}

# vim:set sw=2 sts=-1 et:
