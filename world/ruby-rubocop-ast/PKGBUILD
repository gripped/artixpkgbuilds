# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>
# Contributor: Mario Finelli <mario at finel dot li>

_gemname='rubocop-ast'
pkgname="ruby-${_gemname}"
pkgver=1.39.0
pkgrel=1.1
pkgdesc="RuboCop's AST extensions and NodePattern functionality"
arch=('any')
url="https://github.com/rubocop/${_gemname}"
license=('MIT')
depends=(
  ruby
  ruby-parser
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bundler
  ruby-oedipus_lex
  ruby-rake
  ruby-rspec
)
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('e4321c230f72ddbbdd7a637214ff32adecfdc9ef753434d077c5c7ba403a74c2a766cacdcc86f95e500980e71ddb7c2dbc25ba06dc6abfd3328b73fb3bb0d22b')
b2sums=('d057eaf0fe4a133e366596058ff876c6145f5f3cece142f77dd1b14b3dd0cbc8f3fe49dcbac877ebd1a0a71c6066cb86017fb0f69152976b2b1f5ac59bb8a578')

prepare() {
  cd "${_gemname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec" Gemfile

  # Remove dependency on bump and simplecov
  sed --in-place --regexp-extended '/bump|simplecov/d' Gemfile
  rm tasks/cut_release.rake

  # we don't build from a git checkout
  sed --in-place --regexp-extended 's|git ls-files|find|' "${_gemname}.gemspec"

  sed --in-place --regexp-extended '/knows all current node types/,/end/d' spec/rubocop/ast/traversal_spec.rb
}

build() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  RUBOCOP_VERSION=none rake generate
  gem build "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install/${_gemdir}/cache/" \
    "tmp_install/${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install/${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install/${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install/${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install/${_gemdir}" RUBOCOP_VERSION=none rake spec
}

package() {
  cd "${_gemname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}
