# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Chris Severance aur.severach AatT spamgourmet.com
# Contributor: Chris Fordham <chris [at] fordham-nagy [dot] id [dot] au> aka flaccid
# Package Source: https://github.com/flaccid/archlinux-packages/blob/master/python-jmespath/PKGBUILD

pkgname=python-jmespath
pkgver=1.0.1
pkgrel=7
pkgdesc='A query language for JSON'
arch=(any)
url='https://github.com/jmespath/jmespath'
license=(Apache-2.0)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-hypothesis
  python-pytest
)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('53d8c0d1678d8bcd31440e3d2041c92c7121c4876bf1a6e8de357ecda5e7d20e3aab2a91fb3f7bb73a42fc20ed9df2d90971e8164da781e0a93fad762347e53b')
b2sums=('318b8a6170bc4de0168184b42d319276f39098ca93315adc44b3a915795eb108e7d11ea4732f0ed8b6e39ff5272f28267cce699a8e9e55425c521fa05398d077')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  # Their tox.ini is misleading...
  pytest -v tests/
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
 
