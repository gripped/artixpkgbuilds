# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=glycin
pkgver=1.2.2
pkgrel=1
pkgdesc="Sandboxed and extendable image decoding"
arch=(x86_64)
url="https://gitlab.gnome.org/sophie-h/glycin"
license=('MPL-2.0 OR LGPL-2.1-or-later')
depends=(
  bubblewrap
  cairo
  fontconfig
  gcc-libs
  glib2
  glibc
  gtk4
  lcms2
  libjxl
  librsvg
  libseccomp
)
makedepends=(
  clang
  git
  gobject-introspection
  libheif
  meson
  rust
  vala
)
optdepends=(
  'libheif: heif/heic file support'
)
checkdepends=(
  dav1d
  ffmpeg
  rav1e
  svt-av1
)
provides=(
  libglycin-1.so
  libglycin-gtk4-1.so
)
source=(
  "git+https://gitlab.gnome.org/sophie-h/glycin.git#tag=${pkgver/[a-z]/.&}"
  "git+https://gitlab.gnome.org/sophie-h/test-images.git"
)
b2sums=('5e60eeb3145f70590c1c4a510dd683249478ac49ef5d68ad94d66d14911687005f81a5c985012dea9f044b025250702f9eddbdfac8b16943ba3e419c3c367b91'
        'SKIP')

# Use debug
export CARGO_PROFILE_RELEASE_DEBUG=2 CARGO_PROFILE_RELEASE_STRIP=false

# Use LTO
export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

prepare() {
  cd $pkgname

  git submodule init
  git submodule set-url tests/test-images "$srcdir/test-images"
  git -c protocol.file.allow=always -c protocol.allow=never submodule update

  # Match cargo_home in meson.build
  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  local meson_options=(
    -D test_skip_install=false
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  RUST_BACKTRACE=full meson test -v -C build --print-errorlogs --no-rebuild ||:
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}

# vim:set sw=2 sts=-1 et:
