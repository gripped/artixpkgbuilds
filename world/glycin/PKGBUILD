# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=glycin
pkgver=1.1.4
pkgrel=1
pkgdesc="Sandboxed and extendable image decoding"
arch=(x86_64)
url="https://gitlab.gnome.org/sophie-h/glycin"
license=('MPL-2.0 OR LGPL-2.1-or-later')
depends=(
  bubblewrap
  cairo
  fontconfig
  gcc-libs
  glib2
  glibc
  gtk4
  lcms2
  libjxl
  librsvg
  libseccomp
)
makedepends=(
  clang
  git
  gobject-introspection
  libheif
  meson
  rust
  vala
)
optdepends=(
  'libheif: heif/heic file support'
)
checkdepends=(
  dav1d
  ffmpeg
  rav1e
  svt-av1
)
provides=(
  libglycin-1.so
  libglycin-gtk4-1.so
)
source=(
  "git+https://gitlab.gnome.org/sophie-h/glycin.git#tag=${pkgver/[a-z]/.&}"
  "git+https://gitlab.gnome.org/sophie-h/test-images.git"
)
b2sums=('44cecb3b94037fb197ccc539e59da3f1086c9bd86a72f9e345f6df11e857aed9433ea13175d410ffd591f6c377c80d88df21af528085055bdfb8b32802e9ab3c'
        'SKIP')

# Use LTO
export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

# Use debug
export CARGO_PROFILE_RELEASE_DEBUG=2

prepare() {
  cd $pkgname

  git submodule init
  git submodule set-url tests/test-images "$srcdir/test-images"
  git -c protocol.file.allow=always -c protocol.allow=never submodule update

  # Match cargo_home in meson.build
  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  local meson_options=(
    -D test_skip_install=false
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  RUST_BACKTRACE=full meson test -v -C build --print-errorlogs --no-rebuild ||:
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}

# vim:set sw=2 sts=-1 et:
