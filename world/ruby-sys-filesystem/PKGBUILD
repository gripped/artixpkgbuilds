# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Tim Meusel <tim@bastelfreak.de>
# Contributor: loqs

_gemname=sys-filesystem
pkgname=ruby-${_gemname}
pkgver=1.5.0
pkgrel=4
pkgdesc='An interface for getting file system information.'
arch=(any)
url="https://github.com/djberg96/${_gemname}"
license=(Apache-2.0)
depends=(ruby-ffi)
makedepends=(ruby-mkmf-lite)
checkdepends=(
  ruby-rake
  ruby-rspec
)
options=(!emptydirs)
source=($url/archive/$_gemname-$pkgver.tar.gz)
sha256sums=('1ce10bb6dd0d214587b46d08c8f639df936ac589203229676ff46435c7feea70')

prepare() {
  cd ${_gemname}-${_gemname}-${pkgver}

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' $_gemname.gemspec

  sed --in-place --regexp-extended '/add_development_dependency/d' $_gemname.gemspec
  sed --in-place --regexp-extended '/RuboCop::RakeTask.new/,+10d;/rubocop/Id' Rakefile
}

build() {
  cd ${_gemname}-${_gemname}-${pkgver}

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd ${_gemname}-${_gemname}-${pkgver}

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rake
}

package() {
  cd ${_gemname}-${_gemname}-${pkgver}

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}
