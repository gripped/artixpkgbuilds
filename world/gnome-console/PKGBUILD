# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Igor Dyatlov <dyatlov.igor@gmail.com>

pkgname=gnome-console
pkgver=48.0.1
pkgrel=1
pkgdesc="A simple user-friendly terminal emulator for the GNOME desktop"
url="https://apps.gnome.org/Console/"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  dconf
  gcc-libs
  gtk4
  glib2
  glibc
  hicolor-icon-theme
  libadwaita
  libgtop
  pango
  vte4
)
makedepends=(
  appstream
  git
  glib2-devel
  meson
)
groups=(gnome)
source=("$pkgname::git+https://gitlab.gnome.org/GNOME/console.git#tag=${pkgver/[a-z]/.&}")
b2sums=('0a6feb9f8423da33d5bc50df8dbc55cb446f008d951fb24fc995b6369ee109ec0e80054cc41bada01b7ded3bdec21d0a3e4f6046c1c6be913c11a77070e3237c')

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D tests=true
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
