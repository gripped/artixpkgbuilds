# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Igor Dyatlov <dyatlov.igor@gmail.com>

pkgname=gnome-console
pkgver=51.0
pkgrel=1
pkgdesc="A simple user-friendly terminal emulator for the GNOME desktop"
url="https://apps.gnome.org/Console/"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  dconf
  gtk4
  glib2
  glibc
  hicolor-icon-theme
  libadwaita
  libgcc
  libgtop
  pango
  vte4
)
makedepends=(
  appstream
  git
  glib2-devel
  meson
)
checkdepends=(
  dbus
  mutter
  vulkan-swrast
)
groups=(gnome)
source=("$pkgname::git+https://gitlab.gnome.org/GNOME/console.git#tag=${pkgver/[a-z]/.&}")
b2sums=('673bae633b2a113126c11cbb903cb14487c01f382901a8f78744bbacb93a4545610467d48a9a85172bc66cb8c303913ac06821787a83b7e01973f6fc5fcfe0e4')

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D tests=true
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() (
  export XDG_RUNTIME_DIR="$PWD/runtime-dir"
  mkdir -p -m 700 "$XDG_RUNTIME_DIR"

  dbus-run-session -- \
  mutter \
    --headless \
    --wayland \
    --no-x11 \
    --virtual-monitor 1024x768 \
    -- \
      meson test -C build --print-errorlogs
)

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
