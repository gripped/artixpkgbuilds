# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Igor Dyatlov <dyatlov.igor@gmail.com>

pkgname=gnome-console
pkgver=49.2
pkgrel=1
pkgdesc="A simple user-friendly terminal emulator for the GNOME desktop"
url="https://apps.gnome.org/Console/"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  dconf
  gcc-libs
  gtk4
  glib2
  glibc
  hicolor-icon-theme
  libadwaita
  libgtop
  pango
  vte4
)
makedepends=(
  appstream
  git
  glib2-devel
  meson
)
checkdepends=(
  dbus
  mutter
  vulkan-swrast
)
groups=(gnome)
source=("$pkgname::git+https://gitlab.gnome.org/GNOME/console.git#tag=${pkgver/[a-z]/.&}")
b2sums=('b079240dfc1f47f974899ee64eb46faf4dd47d41e719001dc7098bd3955433df842f1655396757f7097f843b4d4565c8c2d59a26f5497e7bb19a67969fb8efea')

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D tests=true
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() (
  export XDG_RUNTIME_DIR="$PWD/runtime-dir"
  mkdir -p -m 700 "$XDG_RUNTIME_DIR"

  dbus-run-session -- \
  mutter \
    --headless \
    --wayland \
    --no-x11 \
    --virtual-monitor 1024x768 \
    -- \
      meson test -C build --print-errorlogs
)

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
