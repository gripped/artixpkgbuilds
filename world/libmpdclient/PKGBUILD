# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libmpdclient
pkgver=2.23
pkgrel=1
pkgdesc="C library to implement a MPD client"
arch=(x86_64)
url="https://www.musicpd.org/libs/libmpdclient/"
license=(BSD-2-Clause BSD-3-Clause)
depends=(glibc)
makedepends=(git meson)
provides=(libmpdclient.so)
source=(git+https://github.com/MusicPlayerDaemon/libmpdclient.git?signed#tag=v${pkgver})
sha512sums=('6cf0997c45ac75a0d4a9427dbf30975a0899dbc36ec4436843861160cf9c664af0e2aae4dfd6586d3fe71e81ca1405c4c3239349a8a9d239c8781a24050aa5ce')
b2sums=('0e20a14d7be586d1407fc07da02c87bc8f671f4a00030ecb91c232eebe0fa88a7c7fdc12f9d6cfb218402ca898c46fbb966e46c6f1c8167b118c0af6763bbd35')
validpgpkeys=('0392335A78083894A4301C43236E8A58C6DB4512') # Max Kellermann <max@blarg.de>

build() {
  artix-meson build $pkgname
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -vdm 755 "$pkgdir/usr/share/licenses/$pkgname"
  ln -sv /usr/share/doc/$pkgname/BSD-2-Clause.txt "$pkgdir/usr/share/licenses/$pkgname/"
  ln -sv /usr/share/doc/$pkgname/BSD-3-Clause.txt "$pkgdir/usr/share/licenses/$pkgname/"
}
