# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libmpdclient
pkgver=2.26
pkgrel=1
pkgdesc="C library to implement a MPD client"
arch=(x86_64)
url="https://www.musicpd.org/libs/libmpdclient/"
license=(BSD-2-Clause BSD-3-Clause)
depends=(glibc)
makedepends=(git meson)
provides=(libmpdclient.so)
source=(git+https://github.com/MusicPlayerDaemon/libmpdclient.git?signed#tag=v${pkgver})
sha512sums=('f5aa136d0e1ec5cffb9c25608e9e0022058f82ad8a21349b6d0d6ea9716b655ce3922c83aee7975908273b03208af4f0d3feeaafbeea2aecdcfa085b43cb466b')
b2sums=('8999ec3964cea27564c6c1d2c01243dc59bf5cee7cd3fd6ecb194c2849954080f301bd8ab88d8fd37e0bf3f9489ee0d945de1bcb922c2c8b3eb4dbb519b30218')
validpgpkeys=('0392335A78083894A4301C43236E8A58C6DB4512') # Max Kellermann <max@blarg.de>

build() {
  artix-meson build $pkgname
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -vdm 755 "$pkgdir/usr/share/licenses/$pkgname"
  ln -sv /usr/share/doc/$pkgname/BSD-2-Clause.txt "$pkgdir/usr/share/licenses/$pkgname/"
  ln -sv /usr/share/doc/$pkgname/BSD-3-Clause.txt "$pkgdir/usr/share/licenses/$pkgname/"
}
