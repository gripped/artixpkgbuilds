# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgbase=libshumate
pkgname=(
  libshumate
  libshumate-docs
)
pkgver=1.5.3
pkgrel=1
pkgdesc="Map widget for GTK 4"
url="https://wiki.gnome.org/Projects/libshumate"
license=(LGPL-2.1-or-later)
arch=(x86_64)
depends=(
  cairo
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  graphene
  gtk4
  json-glib
  libsoup3
  libsysprof-capture
  pango
  protobuf-c
  sqlite
)
makedepends=(
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  gperf
  meson
  vala
)
checkdepends=(xorg-server-xvfb)
source=(
  "git+https://gitlab.gnome.org/GNOME/libshumate.git?signed#tag=${pkgver/[a-z]/.&}"
)
b2sums=('07966c90f0472fc5ec5960102e18672e036f4141470b7943e348cf484cf4f79aa5eac983197ce0e6f43c5e98e33a314cd39657ff034793b32f1e5e7b34ec546c')
validpgpkeys=(
  7448F128A4B18AB2EF87F092FA6624580A420D5D # Marcus Lundblad <ml@update.uu.se>
)

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    --buildtype debugoptimized  # Required for tests
    -D demos=true
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  GTK_A11Y=none dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package_libshumate() {
  provides=(libshumate-1.0.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_libshumate-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
