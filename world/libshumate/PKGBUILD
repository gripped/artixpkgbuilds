# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgbase=libshumate
pkgname=(
  libshumate
  libshumate-docs
)
pkgver=1.6.0
pkgrel=1
pkgdesc="Map widget for GTK 4"
url="https://gitlab.gnome.org/GNOME/libshumate"
license=(LGPL-2.1-or-later)
arch=(x86_64)
depends=(
  cairo
  gdk-pixbuf2
  glib2
  glibc
  graphene
  gtk4
  json-glib
  libgcc
  libsoup3
  libsysprof-capture
  pango
  protobuf-c
  sqlite
)
makedepends=(
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  gperf
  meson
  vala
)
checkdepends=(xorg-server-xvfb)
source=(
  "git+https://gitlab.gnome.org/GNOME/libshumate.git?signed#tag=${pkgver/[a-z]/.&}"
)
b2sums=('2376b8a3b5fc067a61518fd84b59c637ce7f5548ddcb1844cbd671a9cf60a2c13cd727b38e40289236ce19194b30ae4f5f1eea6626b64944bab6d4fb1f858c48')
validpgpkeys=(
  7448F128A4B18AB2EF87F092FA6624580A420D5D # Marcus Lundblad <ml@update.uu.se>
)

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    --buildtype debugoptimized  # Required for tests
    -D demos=true
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  GTK_A11Y=none dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package_libshumate() {
  provides=(libshumate-1.0.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_libshumate-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
