# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Greg Sutcliffe <puppet-aur (at) emeraldreverie (dot) org>
# Contributor: Hyacinthe Cartiaux <hyacinthe (dot) cartiaux (at) free (dot) fr>
# Contributor: Thomas S Hatch <thatch45 (at) Gmail (dot) com>
# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>
# Contributor: Miah Johnson <miah (at) chia-pet dot org>
# Contributor: Dave Simons <miouhpi (at) Gmail (dot) com>
# Contributor: Niels Abspoel <aboe76 (at) Gmail (dot) com>

pkgname=puppet
pkgver=7.30.0
pkgrel=0
pkgdesc='Server automation framework and application'
arch=('any')
url='https://www.puppet.com/community/open-source'
license=('Apache-2.0')
depends=(ruby facter ruby-semantic_puppet ruby-augeas ruby-shadow ruby-concurrent ruby-multi_json ruby-puppet-resource_api ruby-deep_merge ruby-fast_gettext hiera ruby-locale ruby-scanf ruby-msgpack)
makedepends=('git' 'rake' 'ruby-rdoc')
checkdepends=('ruby-rspec' 'ruby-yard' 'ruby-rspec-its' 'ruby-webmock')
backup=('etc/puppetlabs/puppet/puppet.conf' 'etc/puppetlabs/puppet/hiera.yaml')
install="$pkgname.install"

_augeas_commit=245508727cdf301ea1efa5bc9214f55b90fb2b30 # v1.5.0
_cron_commit=19eb78319c9ea3389b3601fef69faafcc250ab43 # v1.3.0
_host_commit=d1ab552a1dd84e7c50821878a34f4110f9f67dac # v1.3.0
_mount_commit=5f82312986e5e026c046d9f7c7f08c553553ae4a # v1.3.0
_selinux_commit=4bdb318acb4a85521f2e5b2b9ffa9ca238e6885c # v1.4.0
_sshkeys_commit=c7d5955e4d3cc2437195b2c9888ffe0fa7b6b02d # v2.5.0
_yumrepo_commit=394ca00538994e4cedb479767527ce50a8d1a4ca # v2.1.0
_zfs_commit=393b74c2817597015d4b9a0803f1df84dd0136aa # v1.5.0
_zone_commit=1f0be5d14778af89b1adcdfbecda2be51f5aa01a # v1.2.0

source=("https://github.com/puppetlabs/puppet/archive/${pkgver}/puppet-${pkgver}.tar.gz"
        "augeas_core::git+https://github.com/puppetlabs/puppetlabs-augeas_core#commit=${_augeas_commit}"
        "cron_core::git+https://github.com/puppetlabs/puppetlabs-cron_core#commit=${_cron_commit}"
        "host_core::git+https://github.com/puppetlabs/puppetlabs-host_core#commit=${_host_commit}"
        "mount_core::git+https://github.com/puppetlabs/puppetlabs-mount_core#commit=${_mount_commit}"
        "selinux_core::git+https://github.com/puppetlabs/puppetlabs-selinux_core#commit=${_selinux_commit}"
        "sshkeys_core::git+https://github.com/puppetlabs/puppetlabs-sshkeys_core#commit=${_sshkeys_commit}"
        "yumrepo_core::git+https://github.com/puppetlabs/puppetlabs-yumrepo_core#commit=${_yumrepo_commit}"
        "zfs_core::git+https://github.com/puppetlabs/puppetlabs-zfs_core#commit=${_zfs_commit}"
        "zone_core::git+https://github.com/puppetlabs/puppetlabs-zone_core#commit=${_zone_commit}"
        "artix.patch::https://github.com/puppetlabs/puppet/commit/b31d40b687c05ef6f6350b29f727144285b7aaab.patch"
        )
sha512sums=('44dc5c790dafcbbf29a1f07845215f601247307b6134217ef90abd4e14a7ee917b6f554f1db7b327c3a68e04449f5d87d37ae72497d00c24560bc41219b2eb93'
            '6ebc4603db2e702623070f1703d3e82a25c689b8c149c3328660ab43d74bfa49c5853aa14d267b48c9f91d12bd4a96579bcf3184f8881a57748763484892bf90'
            '584093f64216becfeeb45514c9308c7df6819b39b93489790656f6e206c52abb7ed7077a9630c3f0aa2a6b9a6e57502f84cb1a862c03df325f2d97772773c4ce'
            '2bcb93ea1d9d6312205aa1975b1e233414d7bd9c713fb2e59a04f2f3ca436b89dde5b3070b3899fc0a3b5259866c6f3039c06517041d56d73dba08926e7afbec'
            '46e23741f1fcc141fcea45b07da95588eed14762b7f96794447d7f00f8f5e83750f6d23f9d9a80797e711783d30f79375578d91f4ea146d12ca2dee396073593'
            'f3a2b4f1b40ab78ff8c8772be1b94b4a41465dfdad7e44c4cc4158d5852cca732ea7ca08c8d795c97eabc40c43e8145a509ea3aeced99485e6066b2f64555d68'
            '9d458354a95206b998a148bf9b459c8a792051ddaf03672f0ea4c161721ac405c00523ecc065e6acfa610cb9b61e7940a1611b08bb2395c8d3d5a620c6c3662a'
            '6203781a13142747827ee658db3e428ed97732abf356f88224a7f780bfa046ddafcdd69f1e551b3dbe1ad5b00d9a78248a1ba7e97c58fbac636500a95c5e3b7e'
            '60ba6875d868b5bc2b0cc1f5bd1980508e8f6aa9dfed0053d75d78bcc45857e8801afcd608e7a1c13a56d86a2397f6c71feada3d878a7f34bbbde33ad3a19a3b'
            '47fafc95cc9aba258cfd8919707366072a50f8a99b59f540c2531e1a367e8c9230070f8b9e7ae5c9ed4f536e05116234ed02ea431b6e48f61c82ce6dd92ebd9b'
            '2e12f6155acb3e065372cde456add7abf8335341a70d89ac3c3e28c370a4eecb1a062f7751cb4f05bf49225b31e70c882deb8fa3b7e31bd5d669a7e471242276')
conflicts=('puppet5' 'puppet6')
provides=('puppet')

prepare() {
  cd "${pkgname}-${pkgver}"

  patch --strip=1 --input=../artix.patch
}

check() {
  cd "${pkgname}-${pkgver}"
  # This breaks right now due to missing dependencies
  #rake --tasks             # show all tasks
  #rake commits             # requires a git repo
  #rake warnings            # requires a git repo
  #rake spec                # run tests single threaded
  #rake parallel:spec[8]
}

package() {
  cd "${pkgname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  # install the gemspec file
  # this allows other gems to load puppet as a library (for example puppet-strings)
  install -Dm644 .gemspec "${pkgdir}/${_gemdir}/specifications/puppet-${pkgver}.gemspec"

  # generate manpages | this breaks right now..
  #rake gen_manpages

  ruby install.rb --destdir="$pkgdir" --sitelibdir="$( ruby -e 'puts RbConfig::CONFIG["vendorlibdir"]' )"

  install -d "$pkgdir"/usr/lib/{sysusers.d,tmpfiles.d}

  echo 'u puppet 52 "Puppet" /var/lib/puppet' > "$pkgdir"/usr/lib/sysusers.d/$pkgname.conf
  echo "d /run/puppetlabs 0755 $pkgname $pkgname -" > "$pkgdir"/usr/lib/tmpfiles.d/$pkgname.conf

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # Match permissions that puppet sets when running
  install -dm775 "$pkgdir"/var/lib/puppet
  chmod 750 "$pkgdir"/var/log/puppetlabs/puppet

  rm -r "$pkgdir"/var/run

  cd ..
  install --directory "${pkgdir}/opt/puppetlabs/puppet/vendor_modules/"
  mv *_core "${pkgdir}/opt/puppetlabs/puppet/vendor_modules/"
  # Search for unwanted directories and files and delete them
  find "${pkgdir}/opt/puppetlabs/puppet/vendor_modules/" -type d \( -name .git -o -name spec -o -name rakelib \) -exec rm -rf {} +
  find "${pkgdir}/opt/puppetlabs/puppet/vendor_modules/" -name '.*' -exec rm -rf {} +
  find "${pkgdir}/opt/puppetlabs/puppet/vendor_modules/" -type f \( -name pdk.yaml -o -name appveyor.yml -o -name Rakefile -o -name Gemfile \) -delete
}
