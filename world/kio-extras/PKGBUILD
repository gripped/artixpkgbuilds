# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio-extras
pkgver=24.08.1
pkgrel=1
pkgdesc='Additional components to increase the functionality of KIO'
arch=(x86_64)
url='https://www.kde.org/'
license=(LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         karchive
         kcmutils
         kconfig
         kcoreaddons
         kdbusaddons
         kdnssd
         kdsoap-qt6
         kdsoap-ws-discovery-client
         ki18n
         kio
         kservice
         ktextwidgets
         kwidgetsaddons
         libimobiledevice
         libkexiv2
         libmtp
         libplist
         libssh
         libxcursor
         phonon-qt6
         plasma-activities
         qt6-5compat
         qt6-base
         qt6-svg
         smbclient
         solid
         syntax-highlighting)
makedepends=(extra-cmake-modules
             gperf
             kdoctools
             libappimage
             openexr
             plasma-activities-stats
             qcoro-qt6
             taglib)
optdepends=('icoutils: Windows executable thumbnails'
            'kimageformats: thumbnails for additional image formats'
            'libappimage: AppImage thumbnails'
            'openexr: EXR format thumbnails'
            'perl: info kioslave'
            'plasma-activities-stats: recently used kioslave'
            'qt6-imageformats: thumbnails for additional image formats'
            'taglib: audio file thumbnails')
groups=(kde-applications
        kde-network)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/network/kio-extras/-/commit/0b589bd8.patch)
sha256sums=('64051dda2f17a3901426c0a9b5ca65b64429f00b6820e7796e605084b0cfceda'
            'SKIP'
            '414bc89c5a2112a09ffaa2b61311891560d37da0bf95a21dc9f130c0676c8962')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < 0b589bd8.patch # Fix SFTP file corruption
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DLIBAPPIMAGE_LIBRARIES=libappimage.so \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
