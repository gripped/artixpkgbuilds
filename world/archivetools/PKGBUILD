# Maintainer: Sébastien Luttringer

pkgname=archivetools
pkgver=5
pkgrel=1
pkgdesc='Arch Linux Archive Tools'
arch=('any')
url='https://gitlab.archlinux.org/archlinux/archivetools'
license=('GPL-2.0-or-later')
depends=('rsync' 'hardlink' 'xz' 'util-linux' 'bash')
makedepends=('git')
backup=('etc/archive.conf')
source=("git+https://gitlab.archlinux.org/archlinux/archivetools.git#tag=v$pkgver")
sha256sums=('68f69c46995b3b68fab7a04f6168b5f18cdf570c030dbbf06bd2ae8b239b9bd5')

package() {
  cd $pkgname
  install -Dm644 archive.conf "$pkgdir/etc/archive.conf"
  install -Dm755 archive.sh "$pkgdir/usr/bin/archive"
  install -Dm755 archive-cleaner -t "$pkgdir/usr/bin"
  install -Dm644 archive.sysusers "$pkgdir/usr/lib/sysusers.d/archive.conf"
  install -Dm644 archive.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/archive.conf"
}

# vim:set ts=2 sw=2 et:
