# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=pimcommon
pkgver=25.12.0
pkgrel=1
pkgdesc='Common libraries for KDE PIM'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL-2.0-or-later)
depends=(akonadi-contacts
         akonadi-search
         gcc-libs
         glibc
         kcmutils
         kcodecs
         kcolorscheme
         kcompletion
         kconfig
         kcontacts
         kcoreaddons
         ki18n
         kimap
         kio
         kitemmodels
         kitemviews
         kldap
         knewstuff
         ktextaddons
         kwidgetsaddons
         kxmlgui
         libakonadi
         libkdepim
         plasma-activities
         purpose
         qt6-base)
makedepends=(extra-cmake-modules
             qt6-tools)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('11e0faea093a8634bbccad788e31dbbefaf028751e5d131384ef480cdf0b0bb2'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
