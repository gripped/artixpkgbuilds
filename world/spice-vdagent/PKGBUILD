# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: mutantmonkey <aur@mutantmonkey.in>

pkgname=spice-vdagent
pkgver=0.23.0
pkgrel=1
pkgdesc='Spice agent for Linux guests'
arch=(x86_64)
url='https://www.spice-space.org/'
license=(GPL-3.0-or-later)
depends=(
  alsa-lib
  dbus
  glib2
  glibc
  gtk3
  libdrm
  libpciaccess
  libx11
  libxinerama
  libxrandr
)
makedepends=(
  git
  spice-protocol
)
checkdepends=(procps-ng)
backup=(etc/conf.d/spice-vdagentd)
source=("git+https://gitlab.freedesktop.org/spice/linux/vd_agent.git?signed#tag=$pkgname-$pkgver")
b2sums=(63a98f6d07afe920a024e4e2990452ef28271c9b95fc79f2af4cc5ddc49e10b8b9d9b051d78c72ccdef265096530576d122fc5d4da99bfc0a063a6281302bbea)
validpgpkeys=(
  206D3B352F566F3B0E6572E997D9123DE37A484F # Victor Toso de Carvalho <me@victortoso.com>
  87A9BD933F87C606D276F62DDAE8E10975969CE5 # Marc-André Lureau <marcandre.lureau@redhat.com>
)

prepare() {
  cd vd_agent

  # Skip broken test
  sed -i '/check_PROGRAMS += tests\/test-session-info/d' Makefile.am

  # Set proper paths
  sed -i 's|/etc/sysconfig/spice-vdagentd|/etc/conf.d/spice-vdagentd|
          s|/usr/sbin/spice-vdagentd|/usr/bin/spice-vdagentd|' data/spice-vdagentd.service
  sed -i 's|/etc/sysconfig/spice-vdagentd|/etc/conf.d/spice-vdagentd|' data/spice-vdagentd.1.in

  autoreconf -fi
}

build() {
  cd vd_agent
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
  make
}

check() {
  cd vd_agent
  make check
}

package() {
  cd vd_agent
  make DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/etc/conf.d/"
  echo 'SPICE_VDAGENTD_EXTRA_ARGS=""' > "$pkgdir/etc/conf.d/spice-vdagentd"
}
