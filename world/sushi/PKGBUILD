# Maintainer: Fabian Bornschein <fabiscafe-at-mailbox-dot-org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=sushi
pkgver=50rc.1
pkgrel=1
pkgdesc="A quick previewer for Nautilus"
url="https://gitlab.gnome.org/GNOME/sushi"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  evince
  freetype2
  gdk-pixbuf2
  gjs
  glib2
  glibc
  gst-plugin-gtk
  gst-plugins-base-libs
  gstreamer
  gtk3
  gtksourceview4
  harfbuzz
  libepoxy
  libgcc
  libsoup3
  pango
)
makedepends=(
  git
  gobject-introspection
  meson
)
optdepends=(
  'libreoffice: OpenDocument formats'
  'webkit2gtk-4.1: Render HTML files'
)
groups=(gnome)
source=(
  "git+$url.git?signed#tag=${pkgver/[a-z]/.&}"
  0001-sushi-media-bin-Fix-llvmpipe-detection.patch
)
b2sums=('db457838b473a074bef4ba38fe5ca715255cc8bba92470b56169e6d97e5136761ddc0d71d1ff3a7372ddfbf73829746d9581b5b268bd58e3ae36e07e955a5a3c'
        '1c0df7c8674960bfeb480a0f63d7ab33fae5e042e20a20dec53a5791dd1ceee98cc974e439e0c8e7b49d69c3e96431d2857c551f858c41ff709485daae64476c')
validpgpkeys=(
  550660707A6F40376B9B9F8D504A78811E6160CC # Corey Berla <corey@berla.me>
)

prepare() {
  cd sushi

  # Fix llvmpipe detection
  # https://gitlab.gnome.org/GNOME/sushi/-/merge_requests/56
  git apply -3 ../0001-sushi-media-bin-Fix-llvmpipe-detection.patch
}

build() {
  artix-meson sushi build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
