# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=sushi
pkgver=51.1
pkgrel=1
pkgdesc="A quick previewer for Nautilus"
url="https://gitlab.gnome.org/GNOME/sushi"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  freetype2
  fribidi
  gjs
  glib2
  glibc
  glycin-gtk4
  gst-plugins-base-libs
  gstreamer
  gtk4
  harfbuzz
  libgcc
  libx11
  pango
  papers
)
makedepends=(
  blueprint-compiler
  git
  gobject-introspection
  meson
  webkitgtk-6.0
)
optdepends=(
  'libreoffice: OpenDocument formats'
  'webkitgtk-6.0: Render HTML files'
)
groups=(gnome)
# sushi tags use SSH signatures which makepkg doesn't understand
source=(
  "git+$url.git#tag=${pkgver/[a-z]/.&}"
)
b2sums=('ef9b3314343bf04a0cefdeeeb46b096da963718d300ddda01a4191275ab8084b5b3c23a74e31ca99f5d43d6971b0c3f635be446da5387ff96e46dd2ab2cf54dd')

prepare() {
  cd sushi
}

build() {
  artix-meson sushi build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
