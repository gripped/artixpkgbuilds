# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=dconf
pkgver=51.0
pkgrel=1
pkgdesc="Configuration database system"
url="https://gitlab.gnome.org/GNOME/dconf"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  bash
  glib2
  glibc
  libgcc
)
makedepends=(
  bash-completion
  dbus
  git
  glib2-devel
  gtk-doc
  meson
  python
  vala
)
provides=(libdconf.so)
install=dconf.install
source=(
  "git+https://gitlab.gnome.org/GNOME/dconf.git#tag=${pkgver/[a-z]/.&}"
  dconf-update.hook
  dconf-update.script
)
b2sums=('063aee2b0b9a2a3d9cef9633cea6fa204ab780a55913df3526a13e8a9cccb15d7017921801e447f6946706b850166e62cd6484b768da6a341441626ad357f027'
        '5c8da042897f79d17751b02761b8b7d6f9ee3e3a8252c8cd4ca48e5ac7718131385f0a585078465e556f09ca94444d5357e44f9d07dfcb67c0bda063ff7875c3'
        '3d6ea18385db710a0ce669abdc38861cab5ffd5f412789621ded3ade0b8d8c75834137f6d4e18955d13200d972ec985ab2be83447fe7a0a7d26b73218a5a84a4')

prepare() {
  cd dconf
}

build() {
  local meson_options=(
    -D gtk_doc=true
  )

  artix-meson dconf build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"

  install -Dm644 dconf-update.hook -t "$pkgdir/usr/share/libalpm/hooks"
  install -D dconf-update.script "$pkgdir/usr/share/libalpm/scripts/dconf-update"

  # Prevent removal of /etc/dconf/db/ when other
  # packages installing files there get uninstalled
  install -Dm644 /dev/null "$pkgdir/etc/dconf/db/.placeholder"

  rm -r $pkgdir/usr/lib/systemd
}

# vim:set sw=2 sts=-1 et:
