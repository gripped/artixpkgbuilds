# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>

pkgname=kismet
pkgver=2025_09_R1
_realver="${pkgver//_/-}"
pkgrel=3
pkgdesc="802.11 layer2 wireless network detector, sniffer, and intrusion detection system"
url="https://www.kismetwireless.net/"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  bash
  bluez-libs
  gcc-libs
  glib2
  glibc
  libcap
  libcap-ng
  libelf
  libnl
  libnm
  libnm.so
  libpcap
  libpcap.so
  libsensors.so
  libusb
  libusb-1.0.so
  libwebsockets
  libwebsockets.so
  lm_sensors
  mosquitto
  openssl
  pcre2
  rtl-sdr
  sqlite
 
  zlib
)
optdepends=(
  'gpsd: log coordinates of detected networks'
  'wireshark-cli: provide OUI files used to determine device manufacturer'
  'wireshark-cli: mergecap, to merge multiple capture files'
  'sox: provide the default kismet sound playback binary'
  'festival: text-to-speech support'
  'flite:  alternative/lightweight text-to-speech support')
backup=(
  etc/kismet/kismet.conf
  etc/kismet/kismet_80211.conf
  etc/kismet/kismet_alerts.conf
  etc/kismet/kismet_filter.conf
  etc/kismet/kismet_httpd.conf
  etc/kismet/kismet_logging.conf
  etc/kismet/kismet_memory.conf
  etc/kismet/kismet_uav.conf)
install=kismet.install
source=(
  https://www.kismetwireless.net/code/${pkgname}-${_realver}.tar.xz
  kismet.sysusers
)
sha512sums=('218f6bd2ab1f1c99d3d18a8ff2da542a76965ad6251703c06c0d62ed16e084a5135c715288f99a7f0ebb54e924a42762a0fb768f0ef5868afb62fb15daff3dff'
            '452b728c30c9932bdb91741af425267da87a66962ea9b736751948881f5477dee0bff8e2eeca2befdea2920da6d7d9e198bde69cef8c48bb49a0093a7f0ef897')
validpgpkeys=('354689DF3C9DED803381A661D7B28822738BBDB1') # Michael Kershaw

prepare() {
  cd ${pkgname}-${_realver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${_realver}
  ./configure \
    --prefix=/usr \
    --localstatedir=/var \
    --sysconfdir=/etc/kismet \
    --with-suidgroup=315
  make all plugins
}

package() {
  cd ${pkgname}-${_realver}
  make DESTDIR="${pkgdir}" install

  # Makepkg strip bug #43600
  chmod u+w "${pkgdir}"/usr/bin/kismet*
  chmod o-x "${pkgdir}"/usr/bin/kismet_cap*
  chown 0:315 "${pkgdir}"/usr/bin/kismet_cap*

  install -Dm 644 ../kismet.sysusers "${pkgdir}/usr/lib/sysusers.d/kismet.conf"
}

# vim: ts=2 sw=2 et:
