# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Kristian Mosegaard <kristian@mosen.me>
# Contributor: Max Liebkies <mail@maxliebkies.de>
# Contributor: Krzysztof Bogacki <krzysztof.bogacki@leancode.pl>

pkgbase=dotnet-core-8.0
pkgname=(
 dotnet-runtime-8.0
 aspnet-runtime-8.0
 dotnet-sdk-8.0
 dotnet-targeting-pack-8.0
 aspnet-targeting-pack-8.0
 dotnet-source-built-artifacts-8.0
)
pkgver=8.0.29.sdk129
pkgrel=1
arch=(x86_64)
url=https://dotnet.microsoft.com
license=(MIT)
makedepends=(
  bash
  clang20
  cmake
  dotnet-sdk-8.0
  dotnet-source-built-artifacts-8.0
  git
  icu
  krb5
  libgit2
  libunwind
  libxml2
  lldb
  llvm20
  lttng-ust2.12
  nodejs
  openssl
  udev
  zlib
)
optdepends=('bash-completion: Bash completion support')
options=(
  !buildflags
  !lto
  staticlibs
)
_tag=56fc147a7ac76b53fb65b82756da001129b26cc4
source=(git+https://github.com/dotnet/dotnet.git#tag=v${pkgver/.*.sdk/.0.})
b2sums=('c418274bc3d2a72e93244866cd8678eb14119ff1ef3e833f7d86936e8c8b9c5f9b61504b6e39e1af21e9e0c703803f5a667c91ee664590f59bb1c1454ec6ffe3')

prepare() {
  cd dotnet

  # fix bootstrap
  git remote set-url origin https://github.com/dotnet/dotnet.git

  local _bootstrapver=$(xmllint --xpath "//*[local-name()='PrivateSourceBuiltArtifactsPackageVersion']/text()" src/installer/eng/Versions.props)
  local _previousver=$(pacman -Q dotnet-source-built-artifacts-8.0 | sed -r 's/.*([0-9]+\.[0-9]+)\.[0-9]+\.sdk([0-9]+)-.*/\1.\2/')

  if [[ $_bootstrapver == $_previousver ]]; then
    cp -r /usr/share/dotnet .dotnet
    ln -sf /usr/share/dotnet/source-built-artifacts/Private.SourceBuilt.Artifacts.*.tar.gz prereqs/packages/archive/
    ln -sf /usr/share/dotnet/source-built-artifacts/Private.SourceBuilt.Prebuilts.*.tar.gz prereqs/packages/archive/
  fi
  ./prep.sh
}

case ${CARCH} in
  aarch64) _ARCH=arm64;;
  x86_64*) _ARCH=x64;;
esac

build() {
  export DOTNET_CLI_TELEMETRY_OPTOUT=1
  cd dotnet

  export COMPlus_LTTng=0
  export VERBOSE=1
  export OPENSSL_ENABLE_SHA1_SIGNATURES=1

  # .NET crashes when compiled with clang 21.1 on Fedora 43 #119706
  # https://github.com/dotnet/runtime/issues/119706
  export PATH="/usr/lib/llvm20/bin:$PATH"

  # this uses malloc_usable_size, which is incompatible with fortification level 3
  CFLAGS="${CFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"
  CXXFLAGS="${CXXFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"

  CFLAGS=$(echo $CFLAGS  | sed -e 's/-fstack-clash-protection//' )
  CXXFLAGS=$(echo $CXXFLAGS  | sed -e 's/-fstack-clash-protection//' )
  export EXTRA_CFLAGS="$CFLAGS"
  export EXTRA_CXXFLAGS="$CXXFLAGS"
  export EXTRA_LDFLAGS="$LDFLAGS"
  unset CFLAGS
  unset CXXFLAGS
  unset LDFLAGS

  ./build.sh --clean-while-building --online
}

package_dotnet-runtime-8.0() {
  pkgdesc='The .NET Core runtime'
  depends=(
    dotnet-host
    glibc
    icu
    krb5
    libgcc
    libstdc++
    libunwind
    zlib
    openssl
  )
  optdepends=('lttng-ust2.12: CoreCLR tracing')
  provides=(dotnet-runtime=${pkgver%.*.sdk*})

  install -dm 755 "${pkgdir}"/usr/share/{dotnet,licenses}
  bsdtar -xf dotnet/artifacts/${_ARCH}/Release/dotnet-sdk-${pkgver%.*.sdk*}.${pkgver#*sdk}-artix-${_ARCH}.tar.gz -C "${pkgdir}"/usr/share/dotnet/ --no-same-owner shared/Microsoft.NETCore.App
  ln -s dotnet-host "${pkgdir}"/usr/share/licenses/dotnet-runtime-8.0
}

package_aspnet-runtime-8.0() {
  pkgdesc='The ASP.NET Core runtime'
  depends=(dotnet-runtime-8.0)
  provides=(aspnet-runtime=${pkgver%.*.sdk*})

  install -dm 755 "${pkgdir}"/usr/share/{dotnet,licenses}
  bsdtar -xf dotnet/artifacts/${_ARCH}/Release/dotnet-sdk-${pkgver%.*.sdk*}.${pkgver#*sdk}-artix-${_ARCH}.tar.gz -C "${pkgdir}"/usr/share/dotnet/ --no-same-owner shared/Microsoft.AspNetCore.App
  ln -s dotnet-host "${pkgdir}"/usr/share/licenses/aspnet-runtime-8.0
}

package_dotnet-sdk-8.0() {
  pkgdesc='The .NET Core SDK'
  depends=(
    dotnet-runtime-8.0
    dotnet-targeting-pack-8.0
    glibc
    libgcc
    libstdc++
    netstandard-targeting-pack
  )
  optdepends=('aspnet-targeting-pack: Build ASP.NET Core applications')
  provides=(dotnet-sdk=${pkgver%.*.sdk*})

  install -dm 755 "${pkgdir}"/usr/share/{dotnet,licenses}
  bsdtar -xf dotnet/artifacts/${_ARCH}/Release/dotnet-sdk-${pkgver%.*.sdk*}.${pkgver#*sdk}-artix-${_ARCH}.tar.gz -C "${pkgdir}"/usr/share/dotnet/ --no-same-owner sdk sdk-manifests templates
  install -dm 755 "${pkgdir}"/usr/share/dotnet/metadata/workloads/${pkgver%.*.sdk*}.${pkgver#*.sdk}
  touch "${pkgdir}"/usr/share/dotnet/metadata/workloads/${pkgver%.*.sdk*}.${pkgver#*.sdk}/userlocal
  ln -s dotnet-host "${pkgdir}"/usr/share/licenses/dotnet-sdk-8.0
}

package_dotnet-targeting-pack-8.0() {
  pkgdesc='The .NET Core targeting pack'
  depends=(netstandard-targeting-pack)
  provides=(dotnet-targeting-pack=${pkgver%.*.sdk*})

  install -dm 755 "${pkgdir}"/usr/share/{dotnet,licenses}
  bsdtar -xf dotnet/artifacts/${_ARCH}/Release/dotnet-sdk-${pkgver%.*.sdk*}.${pkgver#*sdk}-artix-${_ARCH}.tar.gz -C "${pkgdir}"/usr/share/dotnet/ --no-same-owner packs/Microsoft.NETCore.App.{Host.artix-${_ARCH},Ref}
  ln -s dotnet-host "${pkgdir}"/usr/share/licenses/dotnet-targeting-pack-8.0
}

package_aspnet-targeting-pack-8.0() {
  pkgdesc='The ASP.NET Core targeting pack'
  depends=(dotnet-targeting-pack-8.0)
  provides=(aspnet-targeting-pack=${pkgver%.*.sdk*})

  install -dm 755 "${pkgdir}"/usr/share/{dotnet,licenses}
  bsdtar -xf dotnet/artifacts/${_ARCH}/Release/dotnet-sdk-${pkgver%.*.sdk*}.${pkgver#*sdk}-artix-${_ARCH}.tar.gz -C "${pkgdir}"/usr/share/dotnet/ --no-same-owner packs/Microsoft.AspNetCore.App.Ref
  ln -s dotnet-host "${pkgdir}"/usr/share/licenses/aspnet-targeting-pack-8.0
}

package_dotnet-source-built-artifacts-8.0() {
  pkgdesc='Internal package for building the .NET Core SDK'
  provides=(dotnet-source-built-artifacts=${pkgver%.*.sdk*})

  install -Dm 644 dotnet/artifacts/${_ARCH}/Release/Private.SourceBuilt.Artifacts.*.tar.gz -t "${pkgdir}"/usr/share/dotnet/source-built-artifacts/
  install -Dm 644 dotnet/artifacts/${_ARCH}/Release/Private.SourceBuilt.Prebuilts.*.tar.gz -t "${pkgdir}"/usr/share/dotnet/source-built-artifacts/
}
