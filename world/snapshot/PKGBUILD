# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=snapshot
pkgver=47.0.1
pkgrel=1
pkgdesc="Take pictures and videos"
arch=(x86_64)
url="https://apps.gnome.org/Snapshot"
license=(GPL-3.0-or-later)
groups=(gnome)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  glycin
  graphene
  gst-plugin-pipewire
  gst-plugins-bad-libs
  gst-plugins-base
  gst-plugins-base-libs
  gst-plugins-good
  gstreamer
  gtk4
  hicolor-icon-theme
  lcms2
  libadwaita
  libseccomp
)
makedepends=(
  git
  meson
  rust
)
source=("git+https://gitlab.gnome.org/GNOME/snapshot.git#tag=${pkgver/[a-z]/.&}")
b2sums=('71b67638307ce5fc82566fe151218bf65fffba281de3dd4f7901410767ddb7b852c5da4a58729cb45aa7b35e3ca411fb34c42ef49faeb2aa4b13d91fa2446215')
validpgpkeys=(
  3475CBA8D3483594C889B470D64A8D747F6FE706 # Maximiliano Sandoval <msandova@gnome.org>
  D25626D42D675B9C5EAF57DF7F3B4AADE28427AE # Jamie Murphy <hello@itsjamie.dev>
)

prepare() {
  cd snapshot

  export CARGO_HOME="$srcdir/build/cargo-home"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  artix-meson snapshot build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
