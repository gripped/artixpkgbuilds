# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=snapshot
pkgver=46.2
pkgrel=1
pkgdesc="Take pictures and videos"
arch=(x86_64)
url="https://apps.gnome.org/Snapshot"
license=(GPL-3.0-or-later)
groups=(gnome)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  graphene
  gst-plugin-pipewire
  gst-plugins-bad-libs
  gst-plugins-base
  gst-plugins-base-libs
  gst-plugins-good
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  git
  meson
  rust
)
source=("git+https://gitlab.gnome.org/GNOME/snapshot.git?signed#tag=${pkgver/[a-z]/.&}")
b2sums=('d863cacf400b452842a78e874c49f1f866591169e379b74044372fa7c853901f772ec1c41d4b1c7a036081f5412e93aae5dc11e83f132634dd4997da27264a55')
validpgpkeys=(
  3475CBA8D3483594C889B470D64A8D747F6FE706 # Maximiliano Sandoval <msandova@gnome.org>
)

prepare() {
  cd ${pkgname}
}

build() {
  artix-meson ${pkgname} build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
