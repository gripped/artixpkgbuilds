# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=snapshot
pkgver=46.1
pkgrel=1
pkgdesc="Take pictures and videos"
arch=(x86_64)
url="https://apps.gnome.org/Snapshot"
license=(GPL-3.0-or-later)
groups=(gnome)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  graphene
  gst-plugin-pipewire
  gst-plugins-bad-libs
  gst-plugins-base
  gst-plugins-base-libs
  gst-plugins-good
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  git
  meson
  rust
)
_commit=9b8b37b057c0dc15887d7ef4ddb299faf17f331c  # tags/46.1^0
source=("git+https://gitlab.gnome.org/GNOME/snapshot.git#commit=$_commit")
b2sums=('0e23942ea9c5fb4af290255f925c520255c2dc6799fb8a9f0fd5add1e2a27af7f74f172a50f18cc1cf771b0856dd59b4b2abf89ef6004064393d18b744d495d5')

prepare() {
  cd ${pkgname}
}

pkgver() {
  cd ${pkgname}
  git describe --tags | sed -r 's/\.([a-z])/\1/;s/([a-z])\./\1/;s/[^-]*-g/r&/;s/-/+/g'
}

build() {
  artix-meson ${pkgname} build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
