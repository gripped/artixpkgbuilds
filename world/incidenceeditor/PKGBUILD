# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=incidenceeditor
pkgver=26.04.2
pkgrel=2
pkgdesc='KDE PIM incidence editor'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL-2.0-or-later)
depends=(akonadi
         akonadi-calendar
         akonadi-contacts
         akonadi-mime
         calendarsupport
         eventviews
         glibc
         kcalendarcore
         kcalutils
         kcodecs
         kcompletion
         kconfig
         kcontacts
         kcoreaddons
         kdiagram
         kguiaddons
         ki18n
         kidentitymanagement
         kio
         kitemmodels
         kldap
         kmime
         kpimtextedit
         ktextwidgets
         kwidgetsaddons
         kxmlgui
         libgcc
         libkdepim
         libstdc++
         pimcommon
         qt6-base)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('ad3da4dd53a47f072c16e277fe3b841ad0e3dddf29906cf9492fef32ee7eb664'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
