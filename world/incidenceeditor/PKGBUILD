# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=incidenceeditor
pkgver=25.08.1
pkgrel=1
pkgdesc='KDE PIM incidence editor'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL-2.0-or-later)
depends=(akonadi-calendar
         akonadi-contacts
         akonadi-mime
         calendarsupport
         eventviews
         gcc-libs
         glibc
         kcalendarcore
         kcalutils
         kcodecs
         kcompletion
         kconfig
         kcontacts
         kcoreaddons
         kdiagram
         kguiaddons
         ki18n
         kidentitymanagement
         kio
         kitemmodels
         kldap
         kmime
         kpimtextedit
         ktextwidgets
         kwidgetsaddons
         kxmlgui
         libakonadi
         libkdepim
         pimcommon
         qt6-base)
makedepends=(doxygen
             extra-cmake-modules
             qt6-doc
             qt6-tools)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('e15675399975b73617af51b7ad5ca07f105908d2abeda8627e7ec1bdc3e40bd5'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
