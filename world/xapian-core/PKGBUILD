# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgbase=xapian-core
pkgname=(xapian-core{,-docs})
pkgver=2.0.0
pkgrel=2
epoch=1
pkgdesc='Open source search engine library'
arch=('x86_64')
url="https://www.xapian.org/"
license=('GPL-2.0-or-later')
makedepends=('util-linux' 'zlib')
source=("https://oligarchy.co.uk/xapian/${pkgver}/${pkgname}-${pkgver}.tar.xz"{,.asc}
         https://github.com/xapian/xapian/commit/832e642d.patch)
sha512sums=('b9205997ca05748b3dae1b669f428bc04c6a5bda41dcae1443869741e1bd6c245e0e433c8c4afef021299a699fc4e4ed6ae82aae8541db613aa570a003afea43'
            'SKIP'
            '947d729470eb25207d37632afbb3082efd852b36aca51b302407be762e6cd77f5ca2cac2115064b2f03a2135df476d206afeaaf9308d174632f90fff89dce5c1')
b2sums=('2032ae047312bdc03b1002cbc2a4f01a2e5d37c0859fd5a6013758d456d7642c07ca0d28141daf0eeb2ba33db41206df088ba2666efcd83d545d123d51e8cb6c'
        'SKIP'
        'ef6f0db017e3777bf0e7a8cc7217e98bd6abbe176a49f0f6371ff2165067bcf26566040e722cd667c0f073390b156686c81ac95dfb721feb06e0d0db4f2c21a4')
validpgpkeys=('08E2400FF7FE8FEDE3ACB52818147B073BAD2B07') # Olly Betts <olly@debian.org>

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -p2 --ignore-whitespace -i ../832e642d.patch # Fix segfault in libzim tests
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"

  # workaround for test failure in libzim
  #   23/27 search_iterator  FAIL  0.47s  killed by signal 6 SIGABRT
  CXXFLAGS=${CXXFLAGS/-Wp,-D_GLIBCXX_ASSERTIONS}

  ./configure --prefix=/usr
  make
}

check() {
  make check -C $pkgname-$pkgver
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_xapian-core() {
  depends=(
    'bash'
    'libgcc'
    'libstdc++'
    'glibc'
    'util-linux-libs' 'libuuid.so'
    'zlib' 'libz.so'
  )
  optdepends=(
    'xapian-core-docs: for documentation'
  )
  provides=('libxapian.so')
  # xapian-config requires libxapian.la
  options=('libtool')

  make DESTDIR="${pkgdir}" install -C $pkgname-$pkgver

  (
    cd "$pkgdir"
    _pick $pkgbase-docs usr/share/doc
  )

  install -vDm 644 $pkgname-$pkgver/{AUTHORS,ChangeLog,HACKING,NEWS,README} -t "${pkgdir}/usr/share/doc/${pkgname}"
}

package_xapian-core-docs() {
  pkgdesc+=' - documentation'

  mv -v $pkgname/* "$pkgdir"
}
