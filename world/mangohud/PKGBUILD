# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Campbell Jones <arch at serebit dot com>
# Co-Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Ranieri Althoff <ranisalt+aur at gmail dot com>
# Contributor: Mark Wagie <mark dot wagie at proton dot me>
_pkgname=MangoHud
pkgname=mangohud
pkgver=0.8.3
pkgrel=2
pkgdesc="A Vulkan overlay layer for monitoring FPS, temperatures, CPU/GPU load and more."
arch=('x86_64')
url="https://github.com/flightlessmango/MangoHud"
license=('MIT')
makedepends=('appstream' 'cmocka' 'git' 'glslang' 'libxnvctrl' 'libxrandr' 'meson' 'python-mako' 'vulkan-headers')
depends=('dbus' 'fmt' 'glfw' 'hicolor-icon-theme' 'libglvnd' 'libx11' 'libxkbcommon' 'python' 'python-matplotlib' 'python-numpy' 'vulkan-icd-loader' 'wayland')
optdepends=('gamescope: Use MangoApp as an overlay within gamescope'
            'libxnvctrl: NVIDIA GPU stats by XNVCtrl')
replaces=("$pkgname-common" "$pkgname-common-wayland" "$pkgname-common-x11" "mangoapp" "$pkgname-x11" "$pkgname-wayland")
conflicts=("$pkgname-common" "$pkgname-common-wayland" "$pkgname-common-x11" "mangoapp" "$pkgname-x11" "$pkgname-wayland")
source=("$pkgname-$pkgver.tar.xz::https://github.com/flightlessmango/MangoHud/releases/download/v$pkgver/$_pkgname-v$pkgver-Source.tar.xz")
b2sums=('38b375b6d338fd3d4cd4b2177e43af1fe200db8376df018f988b3eb0f43c2544a082731266c5508bc053c6a9846b1dd95999b11e6aec40632dca5bbbb3e440e5')

prepare() {
    cd "$_pkgname-v$pkgver"

    # Use system cmocka instead of subproject
    sed --in-place "s/  cmocka = subproject('cmocka')//g" meson.build
    sed --in-place "s/cmocka_dep = cmocka.get_variable('cmocka_dep')/cmocka_dep = dependency('cmocka')/g" meson.build
}

build() {
    artix-meson "$_pkgname-v$pkgver" build \
        -Dmangoapp=true \
        -Dmangohudctl=true

    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"

    install -Dm 0644 "$srcdir/$_pkgname-v$pkgver/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"
    rm --force "$pkgdir/usr/lib/libimgui.a"
}

check() {
    meson test -C build --print-errorlogs || :
}
