# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: gem2arch 0.1
# Contributor: theapodan

pkgname=ruby-pkg-config
_pkgname="${pkgname#ruby-}"
pkgver=1.6.4
pkgrel=1
pkgdesc='Implementation of pkg-config in Ruby'
arch=(any)
url='https://github.com/ruby-gnome/pkg-config'
license=(LGPL-2.1-only)
depends=(ruby)
makedepends=(
  git
  ruby-rdoc
)
provides=(ruby-pkgconfig)
replaces=(ruby-pkgconfig)
conflicts=(ruby-pkgconfig)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('70deac5185a008bc7f60dfe4fbbc3bc37d2f19ec6bf4d8a19bf61492af51e441c9d087fbac6ca73ddfa14f81e05a3f9b763add80f1e9faae8b417ff7d0c1d915')
b2sums=('9e7508016782f65f28309a83d7a8fca718ce7d2ccfafab961a72e3801499693c1dfc6fb9bda7b128ded5903b17671a8fb1bd96de70dbab2f85c3b8ecd921b6f8')

build() {
  cd "$pkgname"

  gem build "$_pkgname.gemspec"
}

package() {
  cd "$pkgname"

  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$_pkgname-$pkgver.gem"

  # delete cache
  rm -rf "$pkgdir/$_gemdir/cache"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LGPL-2.1
}
