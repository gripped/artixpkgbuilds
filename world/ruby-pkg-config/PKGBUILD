# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: gem2arch 0.1
# Contributor: theapodan

pkgname=ruby-pkg-config
_pkgname="${pkgname#ruby-}"
pkgver=1.6.3
pkgrel=1
pkgdesc='Implementation of pkg-config in Ruby'
arch=(any)
url='https://github.com/ruby-gnome/pkg-config'
license=(LGPL-2.1-only)
depends=(ruby)
makedepends=(
  git
  ruby-rdoc
)
provides=(ruby-pkgconfig)
replaces=(ruby-pkgconfig)
conflicts=(ruby-pkgconfig)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('6b6b3be336c8cb747efd44216e0a035ba4b8c5099d42ee617a75ea1954bcdf0de00c561c8efdbe14436363386c31d11ffd7c1576c45b979eb105c39e39a53c8c')
b2sums=('e38b51eece728d27e3926ca8016ca2651a50c2137f92dab5619661099d2642fd9d82aa5f9955b92729236a687d9167565b2b35f56db8177201311d19cc5a7555')

build() {
  cd "$pkgname"

  gem build "$_pkgname.gemspec"
}

package() {
  cd "$pkgname"

  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$_pkgname-$pkgver.gem"

  # delete cache
  rm -rf "$pkgdir/$_gemdir/cache"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LGPL-2.1
}
