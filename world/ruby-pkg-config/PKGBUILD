# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: gem2arch 0.1
# Contributor: theapodan

pkgname=ruby-pkg-config
_pkgname="${pkgname#ruby-}"
pkgver=1.5.7
pkgrel=4
pkgdesc='Implementation of pkg-config in Ruby'
arch=('any')
url='https://github.com/ruby-gnome/pkg-config'
license=('LGPL-2.1-only')
depends=(
  ruby
)
makedepends=(
  git
  ruby-rdoc
)
provides=('ruby-pkgconfig')
replaces=('ruby-pkgconfig')
conflicts=('ruby-pkgconfig')
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('3fc3cb0c286b5be7ea92bf2edd472cca88c93b24c65e0b4e67befc49653a03159ca3c878549bdb22869192ae5aedce8dba27be148084403788ec14d2fb4657c7')
b2sums=('47342399581cce2a3a8bbb1da8fc6a783bcefe0f87fe6ef33cd163a60ac47103f606b569f50fd534c06ac2af6a26563c8f3c8c46fc3c11936db41ab9d555b049')

build() {
  cd "$pkgname"

  gem build "$_pkgname.gemspec"
}

package() {
  cd "$pkgname"

  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$_pkgname-$pkgver.gem"

  # delete cache
  rm -rf "$pkgdir/$_gemdir/cache"

  # license
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  ln -sf "$_gemdir/gems/$_pkgname-$pkgver/LGPL-2.1" "$pkgdir/usr/share/licenses/$pkgname"
}
