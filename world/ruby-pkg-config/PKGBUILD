# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: gem2arch 0.1
# Contributor: theapodan

pkgname=ruby-pkg-config
_pkgname="${pkgname#ruby-}"
pkgver=1.6.2
pkgrel=1
pkgdesc='Implementation of pkg-config in Ruby'
arch=('any')
url='https://github.com/ruby-gnome/pkg-config'
license=('LGPL-2.1-only')
depends=(
  ruby
)
makedepends=(
  git
  ruby-rdoc
)
provides=('ruby-pkgconfig')
replaces=('ruby-pkgconfig')
conflicts=('ruby-pkgconfig')
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('1a4d42f93fe4d539c26d0c241f48af4fca777e156485a152d040cbcc0d07109ccbcfff08c7a980f1e648366c86383a53eda7879da7a4765704cbed72a2ec00e8')
b2sums=('63d282e31d0d8afd9eb466fe3a761877cb191a1c0662e0068fa1d7d73cb774663b2a685bd03841c3b47a2f237534a4bbbdc6e2b458a80fc5d94fcf161684a727')

build() {
  cd "$pkgname"

  gem build "$_pkgname.gemspec"
}

package() {
  cd "$pkgname"

  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$_pkgname-$pkgver.gem"

  # delete cache
  rm -rf "$pkgdir/$_gemdir/cache"

  # license
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  ln -sf "$_gemdir/gems/$_pkgname-$pkgver/LGPL-2.1" "$pkgdir/usr/share/licenses/$pkgname"
}
