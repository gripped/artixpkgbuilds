# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: gem2arch 0.1
# Contributor: theapodan

pkgname=ruby-pkg-config
_pkgname="${pkgname#ruby-}"
pkgver=1.6.5
pkgrel=1
pkgdesc='Implementation of pkg-config in Ruby'
arch=(any)
url='https://github.com/ruby-gnome/pkg-config'
license=(LGPL-2.1-only)
depends=(ruby)
makedepends=(
  git
  ruby-rdoc
)
provides=(ruby-pkgconfig)
replaces=(ruby-pkgconfig)
conflicts=(ruby-pkgconfig)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('e06456aafbc20978005a80682aef728c278c479d4d1310270ac154c5d3d99daca5c12d1cf4543e7f2e40cad4b9af1b329241ff59fcbc7cc4bd3a41e525183fa2')
b2sums=('2ad142c1f3d451a4e9d6debf87a0c5866d888d838f97422c261fb9a33863c25142b4d41d49e083f08363d1dd4075e5997c92091ac7cd16d50b250c6aa8cb69d4')

build() {
  cd "$pkgname"

  gem build "$_pkgname.gemspec"
}

package() {
  cd "$pkgname"

  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$_pkgname-$pkgver.gem"

  # delete cache
  rm -rf "$pkgdir/$_gemdir/cache"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LGPL-2.1
}
