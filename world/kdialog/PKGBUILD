# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kdialog
pkgver=26.08.1
pkgrel=1
pkgdesc='A utility for displaying dialog boxes from shell scripts'
arch=(x86_64)
url='https://kde.org/'
license=(LGPL-2.0-or-later)
depends=(glibc
         kconfig
         kcoreaddons
         kdbusaddons
         kguiaddons
         ki18n
         kiconthemes
         kio
         ktextwidgets
         kwidgetsaddons
         kwindowsystem
         libstdc++
         libx11
         qt6-base)
makedepends=(extra-cmake-modules)
groups=(kde-applications
        kde-utilities)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('e7142c812d2b4b202abd12bea054f8368fdba9bb0552a9f46d3a7d7283339c72'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
