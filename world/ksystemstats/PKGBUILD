# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=ksystemstats
pkgver=6.5.4
_dirver=$(echo $pkgver | cut -d. -f1-3)
pkgrel=2
pkgdesc='A plugin based system monitoring daemon'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kcoreaddons
         kcrash
         ki18n
         kio
         libksysguard
         lm_sensors
         qt6-base
         solid)
makedepends=(extra-cmake-modules
             libnl
             networkmanager-qt)
optdepends=('libnl: network usage monitor'
            'networkmanager-qt: network usage monitor'
            'libudev: GPU monitor')
groups=(plasma)
source=(https://download.kde.org/stable/plasma/$_dirver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('a53038232221b773c695312c6cd3f11a903be1aa186fd4c274ddded8c1ac31fb'
            'SKIP')
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D'  # Jonathan Esk-Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

build() {
  cmake -B build  -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_LIBEXECDIR=lib
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  setcap CAP_PERFMON=+ep "$pkgdir"/usr/lib/ksystemstats_intel_helper

  rm -r $pkgdir/usr/lib/systemd
}
