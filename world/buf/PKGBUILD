# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=buf
pkgver=1.56.0
pkgrel=1
pkgdesc='A tool for working with Protocol Buffers'
arch=('x86_64')
url='https://buf.build'
license=('Apache-2.0')
depends=('glibc')
makedepends=('git' 'go')
options=('!lto')
source=("$pkgname::git+https://github.com/bufbuild/buf#tag=v$pkgver")
sha512sums=('f5fefc2e58a0e1a913208c8fe934682f2b39df7a02e4f5b4301ee83f9dbc98da2be45fd9bb335e87c89a8db483663ff1044fd1c4682669921c76bd093c98dac3')
b2sums=('959f3c9dbc9a36985136a50d1065cdc35885d1f8a6b07d3ac1761474393e615868121cef4007d4b34216fbfe4cb44cb97af277eb46179489a8ae2bb3640246b5')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}'" \
    -o build \
    ./cmd/...

  # generate shell completions
  for shell in bash fish zsh; do
    ./build/buf completion "$shell" > "build/completion.$shell"
  done

  # generate manpages
  mkdir -p build/manpages
  ./build/buf manpages build/manpages

  # ensure manpages are reproducible
  local _commit_date=$(git show --no-patch --format=%cd --date=format:%Y-%m-%d)
  sed \
  -e "s/^\(\.TH.* \"\)\w\{3\} [0-9]\{4\}/\1${_commit_date}/" \
  -e "s/.*\( Auto generated by spf13\/cobra\)$/${_commit_date}\1/" \
  -i build/manpages/*
}

package() {
  cd "$pkgname"

  # binaries
  install -vDm755 -t "$pkgdir/usr/bin" build/{buf,protoc-gen-buf-lint,protoc-gen-buf-breaking}

  # shell completions
  install -vDm644 build/completion.bash "$pkgdir/usr/share/bash-completion/completions/buf"
  install -vDm644 build/completion.fish "$pkgdir/usr/share/fish/vendor_completions.d/buf.fish"
  install -vDm644 build/completion.zsh "$pkgdir/usr/share/zsh/site-functions/_buf"

  # man pages
  install -vDm644 build/manpages/* -t "$pkgdir/usr/share/man/man1"
}
