# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Jonas Witschel <diabonas@archlinux.org>
# Contributor: Philipp A. <flying-sheep@web.de>

# Check if new updates break python-engineio

pkgname=python-aiohttp
pkgver=3.12.11
pkgrel=1.1
pkgdesc='HTTP client/server for asyncio'
arch=(x86_64)
url=https://aiohttp.readthedocs.io
license=(Apache-2.0)
depends=(
  glibc
  llhttp
  python
  python-aiohappyeyeballs
  python-aiosignal
  python-attrs
  python-frozenlist
  python-multidict
  python-propcache
  python-yarl
)
makedepends=(
  cython
  git
  npm
  python-build
  python-installer
  python-pkgconfig
  python-setuptools
  python-wheel
)
checkdepends=(
  gunicorn
  python-aiodns
  python-blockbuster
  python-brotli
  python-freezegun
  python-isal
  python-proxy.py
  python-pytest
  python-pytest-codspeed
  python-pytest-mock
  python-pytest-xdist
  python-re-assert
  python-time-machine
  python-trustme
  python-uvloop
  python-zlib-ng
)
optdepends=(
  'gunicorn: to deploy using Gunicorn'
  'python-aiodns: for fast DNS resolving'
  'python-brotli: for Brotli transfer-encodings support'
)
source=("$pkgname::git+https://github.com/aio-libs/aiohttp#tag=v$pkgver")
b2sums=('28309057dd629971798e7b7ead5b20a082ca12e5a331cdda10a7d3aabacc5370f2de7473bfc8f83bd9bf98871d770a9daf88616f9e7a7aa81dd9b8ee8aa1bb53')

prepare() {
  cd $pkgname
  sed 's|.install-cython ||' -i Makefile
}

build() {
  cd $pkgname
  export AIOHTTP_USE_SYSTEM_DEPS=1
  make cythonize
  python -m build --wheel --no-isolation
}

check() {
  local pytest_args=(
    -vvv
    --override-ini="addopts="

    # Fails, not sure why.
    --deselect=tests/test_http_parser.py::test_http_response_parser_bad_chunked_strict_c
    --deselect=tests/test_http_parser.py::test_http_response_parser_bad_chunked_strict_py
    --deselect=tests/test_http_parser.py::test_http_response_parser_strict_headers
    --deselect=tests/test_http_parser.py::test_http_response_parser_strict_obs_line_folding
    --deselect=tests/test_proxy_functional.py::test_uvloop_secure_https_proxy

    # Fails, calls the Python interpreter without -P, deselect to enable
    # venv-based testing.
    --deselect=tests/test_circular_imports.py::test_no_warnings

    --ignore=tests/autobahn/test_autobahn.py # Docker tests
    --ignore=tests/test_benchmarks_client.py
    --ignore=tests/test_benchmarks_client_request.py
    --ignore=tests/test_benchmarks_client_ws.py
    --ignore=tests/test_benchmarks_cookiejar.py
    --ignore=tests/test_benchmarks_http_websocket.py
    --ignore=tests/test_benchmarks_http_writer.py

    # Artix CI
    --deselect='tests/test_connector.py::test_available_connections_no_limits'
    --deselect='tests/test_connector.py::test_available_connections_with_limit_per_host'
    --deselect='tests/test_connector.py::test_available_connections_without_limit_per_host'
    --deselect='tests/test_connector.py::test_tcp_connector_resolve_host[pyloop]'
    --deselect='tests/test_connector.py::test_tcp_connector_socket_factory[pyloop]'
    --deselect='tests/test_cookiejar.py::test_constructor'
    --deselect='tests/test_cookiejar.py::test_save_load'
    --deselect='tests/test_cookiejar.py::test_update_cookie_with_unicode_domain'
    --deselect='tests/test_cookiejar.py::test_filter_cookie_with_unicode_domain'
    --deselect='tests/test_proxy_functional.py::test_proxy_http_connection_error[pyloop]'
    --deselect='tests/test_proxy_functional.py::test_proxy_https_connection_error[pyloop]'

    # Tests freeze and timeout when running serialized, no idea why
    -n auto
  )

  cd $pkgname
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -Pm pytest "${pytest_args[@]}"
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim: ts=2 sw=2 et:
