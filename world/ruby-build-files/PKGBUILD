# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-build-files
pkgver=1.9.1
pkgrel=2
pkgdesc='Abstractions for handling and mapping paths'
arch=(any)
url='https://github.com/ioquatix/build-files'
license=(MIT)
depends=(
  ruby
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-decode
  ruby-sus
)
options=(!emptydirs)
source=(https://github.com/ioquatix/build-files/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('118da8815a1d40d23f1009d1b469996a2a640c6e190dceb888c0778f37147eefb42e83b6a47cda484089338b4e7eb51717e5bf4c8304e55d0d95617f087fbe39')
b2sums=('df8017f27fa12cb075403fe51083e7e689cce6f8a196beac71cc270890eca0713eb11102633623afea01c54867a341da11124b609a21816c4adef3726464445b')

prepare() {
  cd build-files-$pkgver
  sed -e 's|~>|>=|' -e '/signing_key/d' -i build-files.gemspec

  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd build-files-$pkgver
  gem build build-files.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    build-files-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd build-files-$pkgver
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd build-files-$pkgver
  cp -a tmp_install/* "$pkgdir"/
}
