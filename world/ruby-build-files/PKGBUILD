# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-build-files
pkgver=1.10.0
pkgrel=1
pkgdesc='Abstractions for handling and mapping paths'
arch=(any)
url='https://github.com/ioquatix/build-files'
license=(MIT)
depends=(
  ruby
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-decode
  ruby-sus
)
options=(!emptydirs)
source=(git+https://github.com/ioquatix/build-files.git#tag=v$pkgver)
sha512sums=('0ba4c31b795d5f22f59596bcecd807aa1072d86e89984cdfee2f4c6fb6d46d77286c60c675f342ca515065cef5edcc63def0d9da39fd7fca2ffec6ffc603a634')
b2sums=('3a7836ec856228b1ee64e5e40847166bb532b01d05bb533fa416a22e4d9e8fb1cd6104024d17f8308c035c1b275c0e234226dcc3a5e86ede1abad53185471fdd')

prepare() {
  cd build-files
  sed -e 's|~>|>=|' -e '/signing_key/d' -i build-files.gemspec

  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd build-files
  gem build build-files.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    build-files-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd build-files
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd build-files
  cp -a tmp_install/* "$pkgdir"/
}
