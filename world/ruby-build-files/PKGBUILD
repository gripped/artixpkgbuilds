# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-build-files
pkgver=1.10.1
pkgrel=1
pkgdesc='Abstractions for handling and mapping paths'
arch=(any)
url='https://github.com/ioquatix/build-files'
license=(MIT)
depends=(
  ruby
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-decode
  ruby-sus
)
options=(!emptydirs)
source=(git+https://github.com/ioquatix/build-files.git#tag=v$pkgver)
sha512sums=('4b7a23c69721fda2c3f3f2b59de535d7e4449e395242bc6842d9fff5d3e7bd361e4e1bb766f46bc2d12eb620804fa14c61746d7a75ac4d2caadd0465260ea41f')
b2sums=('487cd9212eaacd71ea651c8a333da65602c9144c663ed6ae2aabdaae2f71dc9c2bc2939835a99b0f2a34001e7e2ca2d4d87da77da4f068b006ae1badffd621ae')

prepare() {
  cd build-files
  sed -e 's|~>|>=|' -e '/signing_key/d' -i build-files.gemspec

  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd build-files
  gem build build-files.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    build-files-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd build-files
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd build-files
  cp -a tmp_install/* "$pkgdir"/
}
