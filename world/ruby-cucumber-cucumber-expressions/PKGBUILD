# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bert Peters <bertptrs@archlinux.org>

pkgname=ruby-cucumber-cucumber-expressions
pkgver=18.1.0
_commit=33e9d62439e760b2bc0d122039d84a6d7b103084
pkgrel=1
pkgdesc="Cucumber Expressions - a simpler alternative to Regular Expressions"
arch=(any)
url='https://github.com/cucumber/cucumber-expressions/tree/main/ruby'
license=(MIT)
depends=(
  ruby
  ruby-bigdecimal
)
makedepends=(
  git
  ruby-rake
  ruby-rspec
)
checkdepends=(
  ruby-bundler
)
options=(!emptydirs)
source=(git+https://github.com/cucumber/cucumber-expressions.git#commit=$_commit)
sha256sums=('d58d5ed0e073656568e400adb8da7b310e0c09efb8d493923a44d5ee77de943a')

prepare() {
  cd cucumber-expressions/ruby

  # The last tag appears to have forgotten to update this file
  echo "$pkgver" > VERSION
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd cucumber-expressions/ruby
  gem build cucumber-cucumber-expressions.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    cucumber-cucumber-expressions-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd cucumber-expressions/ruby
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd cucumber-expressions/ruby
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 ../LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
