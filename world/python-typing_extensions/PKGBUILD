# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Michael Yeatts <mwyeatts@gmail.com>

pkgname=python-typing_extensions
_name=${pkgname#python-}
pkgver=4.12.0
pkgrel=1
pkgdesc='Backported and Experimental Type Hints for Python 3.8+'
arch=(any)
url=https://github.com/python/typing_extensions
license=(Python-2.0.1)
depends=(python)
makedepends=(git python-build python-flit-core python-installer)
checkdepends=(python-tests)
source=("git+${url}.git#tag=${pkgver}")
b2sums=('13be98b2492a73dc54f147024efe7de91a0f10d0957b0506090b2e57b42854c7c63fa377b038e790938d019d16a55aa2008782e9130b2c975a04d5c53eb3f720')

build() {
  cd "${_name}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "${_name}"

  PYTHONPATH="${PWD}/src" python -m unittest discover src
}

package() {
  cd "${_name}"

  python -m installer --destdir="${pkgdir}" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "${pkgdir}"/usr/share/licenses/${pkgname}
  ln -s "${site_packages}"/"${_name}"-${pkgver}.dist-info/LICENSE \
    "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

# vim: ts=2 sw=2 et:
