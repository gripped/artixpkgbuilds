# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

_name=dask
pkgname=python-$_name
pkgver=2025.1.0
pkgrel=2
pkgdesc="Parallel computing with task scheduling"
arch=(any)
url="https://dask.org"
license=(BSD-3-Clause)
depends=(
  python
  python-click
  python-cloudpickle
  python-fsspec
  python-importlib-metadata
  python-packaging
  python-partd
  python-yaml
  python-toolz
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-versioneer
  python-wheel
)
optdepends=(
#  'python-bokeh: visualizing dask diagnostics'
#  'python-cityhash: faster hashing of arrays'
  'python-distributed: distributed computing'
#  'python-fastparquet: storing and reading data from parquet files'
#  'python-gcsfs: file-system interface to Google Cloud Storage'
#  'python-jinja: diagnostics'
#  'python-murmurhash: faster hashing of arrays'
  'python-numpy: arrays and dataframes support'
  'python-pandas: dataframes support'
  'python-psutil: more accurate CPU count'
  'python-pyarrow: Apache Arrow, for parquet support'
#  'python-s3fs: Amazon S3 support'
  'python-sqlalchemy: writing and reading from SQL databases'
  'python-xxhash: faster hashing of arrays'
)
checkdepends=(
  ipython
  python-pytest
  python-pytest-mock
  python-pytest-rerunfailures
  python-aiohttp
#  python-bokeh
  python-boto3
  python-botocore
  python-cytoolz
#  python-cityhash
  python-distributed
#  python-fastparquet
#  python-gcsfs
  python-graphviz
  python-h5py
  python-httpretty
  python-jinja
  python-jsonschema
  python-lz4
  python-matplotlib
  python-moto
#  python-murmurhash
#  python-numba
  python-numexpr
  python-numpy
  python-pandas
  python-psutil
  python-pyarrow
  python-pytables
  python-requests
#  python-s3fs
  python-scikit-learn
  python-scipy
  python-snappy
#  python-sparse
  python-sqlalchemy
  python-xarray
  python-xxhash
)
source=(
  https://github.com/dask/dask/archive/$pkgver/$pkgname-$pkgver.tar.gz
)
b2sums=('f3e16895564bba801830052b3cac46b360b2a988a17791f7a54a07be3800a243e6396d9a471569dfa0a16cf9ce08ba1f301780ae072dc188947353d11f1470dc')

prepare() {
  cd $_name-$pkgver
  sed -e 's|versioneer\[toml\].*\"|versioneer[toml]\"|g' -i pyproject.toml
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    --override-ini="addopts="
    --deselect dask/dataframe/dask_expr/tests/test_shuffle.py::test_set_index_head_nlargest_string
    -k 'not test_RandomState_only_funcs'
    -m 'not network and not slow and not gpu'
    -W ignore::DeprecationWarning
  )

  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest "${pytest_options[@]}"
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
