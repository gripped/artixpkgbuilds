# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-extras
pkgver=1.0.0
pkgrel=15
pkgdesc="Useful extra bits for Python - things that should be in the standard library"
arch=('any')
license=('MIT')
url="https://github.com/testing-cabal/extras"
depends=('python')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-testtools')
source=("git+https://github.com/testing-cabal/extras.git#tag=$pkgver")
sha512sums=('bb877ea833216c90532ef6a2ff87faff4c92743765ba5658e59c386cf246484df2957c1d3e89a1c3515cca6e81567cd4fa71b29a46357c764b81bd569bae1358')

prepare() {
  cd extras
  sed -i 's/test.assertEquals/test.assertEqual/' extras/tests/test_extras.py
}

build() {
  cd extras
  python -m build --wheel --no-isolation
}

check() {
  cd extras
  python -m testtools.run extras.tests.test_suite
}

package() {
  cd extras
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
