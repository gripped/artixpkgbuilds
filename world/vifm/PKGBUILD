# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Carl Smedstad <carsme@archlinux.org>

pkgname=vifm
pkgver=0.13
pkgrel=3
pkgdesc="A file manager with curses interface, which provides Vi[m]-like environment"
arch=('x86_64')
url="https://vifm.info/"
license=('GPL-2.0-or-later')
depends=(
  'file'
  'glibc'
  'ncurses'
)
makedepends=('perl')
changelog=$pkgname.changelog
source=(
  "https://github.com/vifm/vifm/releases/download/v$pkgver/$pkgname-$pkgver.tar.bz2"{,.asc}
  "fix-bash-completion.patch::https://github.com/vifm/vifm/commit/efbf106a095c1163eafdc144be1f1c8fecb91e72.patch"
)
sha256sums=('0d9293749a794076ade967ecdc47d141d85e450370594765391bdf1a9bd45075'
            'SKIP'
            '57dd10a82e87f446d45b09ccd875006c268b33fff5a1583de3a2670eddbd2b71')
validpgpkeys=('22349CDBE5227D2108A5667799DC5E4DB05F6BE2') # xaizek <xaizek@posteo.net>

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i "$srcdir/fix-bash-completion.patch"
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
