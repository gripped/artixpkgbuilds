# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Carl Smedstad <carsme@archlinux.org>

pkgname=vifm
pkgver=0.14
pkgrel=1
pkgdesc="A file manager with curses interface, which provides Vi[m]-like environment"
arch=('x86_64')
url="https://vifm.info/"
license=('GPL-2.0-or-later')
depends=(
  'file'
  'glib2'
  'glibc'
  'ncurses'
)
makedepends=('perl')
changelog=$pkgname.changelog
source=("https://github.com/vifm/vifm/releases/download/v$pkgver/$pkgname-$pkgver.tar.bz2"{,.asc})
sha256sums=('2714dd4cef4e53e7a8980ae8445e88279104f815d47f417fa0b8adfe2f3d1bed'
            'SKIP')
validpgpkeys=('22349CDBE5227D2108A5667799DC5E4DB05F6BE2') # xaizek <xaizek@posteo.net>

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
