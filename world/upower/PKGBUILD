# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=upower
pkgver=1.91.5
pkgrel=1
pkgdesc="Abstraction for enumerating power devices, listening to device events and querying history and statistics"
url="https://upower.freedesktop.org"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  glib2
  glibc
  libgcc
  libgudev
  libimobiledevice
  libplist
  polkit
)
makedepends=(
  docbook-xsl
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  meson
  python
  udev
  usbmuxd
)
optdepends=('usbmuxd: Read charge status of iOS devices')
checkdepends=(
  python-dbus
  python-dbusmock
  python-gobject
  python-packaging
  umockdev
)
backup=(etc/UPower/UPower.conf)
source=("git+https://gitlab.freedesktop.org/upower/upower.git#tag=v$pkgver")
b2sums=('db7dc8e9e42f6806a905eb271a4c3cff59e73d24617ca7012b10a71a3b047052d518dc4dae91438058ad5f688323e2e264aa97f2cb88246c113fa79e59f234df')

prepare() {
  cd upower
}

build() {
  local meson_options=(
    -D installed_tests=false
  )

  artix-meson -D systemdsystemunitdir=no upower build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(libg{lib,object,io}-2.0.so)
  provides+=(libupower-glib.so)

  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
