# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=upower
pkgver=1.90.8
pkgrel=1
pkgdesc="Abstraction for enumerating power devices, listening to device events and querying history and statistics"
url="https://upower.freedesktop.org"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  libgudev
  libimobiledevice
  libplist
  polkit
)
makedepends=(
  docbook-xsl
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  meson
  python
  udev
  usbmuxd
)
optdepends=('usbmuxd: Read charge status of iOS devices')
checkdepends=(
  python-dbus
  python-dbusmock
  python-gobject
  python-packaging
  umockdev
)
backup=(etc/UPower/UPower.conf)
source=("git+https://gitlab.freedesktop.org/upower/upower.git#tag=v$pkgver")
b2sums=('39d6a6c66ba3dcb3ed826e793399748ef86af98ad4b723b09feb2c4ef92fb8d6100ed11de0d37c6b6a4daf45e650386c57f95a071e29d63f849146e25f611a13')

prepare() {
  cd upower
}

build() {
  artix-meson -D systemdsystemunitdir=no upower build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(libg{lib,object,io}-2.0.so)
  provides+=(libupower-glib.so)

  meson install -C build --destdir "$pkgdir"

  # Remove installed-tests
  rm -rv "$pkgdir"/usr/{lib/upower,share/installed-tests}
}

# vim:set sw=2 sts=-1 et:
