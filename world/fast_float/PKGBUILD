# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Carl Smedstad <carsme@archlinux.org>
# Contributor: éclairevoyant
# Contributor: endlesseden <eden at rose dot place>

pkgname=fast_float
pkgver=8.3.1
pkgrel=1
pkgdesc='Fast and exact implementation of the C++ from_chars functions for float and double types'
arch=('any')
url="https://github.com/fastfloat/fast_float"
license=('Apache-2.0 OR BSL-1.0 OR MIT')
makedepends=(
  'cmake'
  'doctest'
  'git'
)
source=("git+$url.git#tag=v$pkgver")
b2sums=('ab9dcd558c2f998e2407b67243de45dfbe2deb05a32f86fc9f9557b20ce08ba4438c5e1b2a034aff5e12d465794dd0496239a54b73b870dde17527bc4c95fc0d')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev \
    -DFASTFLOAT_TEST=ON \
    -DSYSTEM_DOCTEST=ON
  cmake --build build
}

check() {
  cmake --build build --target test
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" $pkgname/LICENSE-MIT
}
