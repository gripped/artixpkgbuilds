# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

pkgname=python-dask-expr
pkgver=1.1.20
pkgrel=1
pkgdesc="High Level Expressions for Dask"
arch=(any)
url="https://dask.org"
license=(BSD-3-Clause)
depends=(python python-dask python-pyarrow python-pandas)
makedepends=(python-build python-installer python-wheel python-versioneer)
checkdepends=(python-pytest)
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/dask/dask-expr/archive/refs/tags/v${pkgver}.tar.gz)
sha256sums=('118ee4100c97fa2b1fb487a7ed5e47939b3203862597b1cdacdfc5cb59dbf438')

prepare() {
  cd dask-expr-${pkgver}
  sed -i 's/versioneer\[toml\]==0.28/versioneer/' pyproject.toml
}

build() {
  cd dask-expr-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd dask-expr-${pkgver}
  local pytest_options=(
    --deselect dask_expr/array/tests/test_array.py::test_array_function
    --deselect dask_expr/array/tests/test_array.py::test_reduction_functions
    --deselect dask_expr/array/tests/test_array.py::test_reductions
    --deselect dask_expr/array/tests/test_array.py::test_transpose
    --deselect dask_expr/tests/test_format.py::test_df_to_html
  )
  pytest "${pytest_options[@]}"
}

package() {
  cd dask-expr-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
}
