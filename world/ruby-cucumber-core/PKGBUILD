# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bert Peters <bertptrs@archlinux.org>

pkgname=ruby-cucumber-core
pkgver=15.2.0
pkgrel=1
pkgdesc="Core library for the Cucumber BDD app"
arch=(any)
url='https://github.com/cucumber/cucumber-ruby-core'
license=(MIT)
depends=(
  ruby
  ruby-cucumber-gherkin
  ruby-cucumber-messages
  ruby-cucumber-tag-expressions
)
makedepends=(
  ruby-rake
  ruby-rdoc
  ruby-rspec
  ruby-unindent
)
checkdepends=(
  ruby-bundler
  ruby-simplecov
)
options=(!emptydirs)
source=("https://github.com/cucumber/cucumber-ruby-core/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('df924521edf16f78dbcfebaa0e01953a09aea3bcfd61777bfaf6a9e86934c06579f39483e53d1b9d38b4c798424dbd70cd666623accbafaf54aa7cd7380c0ffd')
b2sums=('c212981485baa5481bd4d61a8e8557c19c583958e914601e7addfcf575cedb33956b30cd34be1f8ce20ecafeaafa48ddf73504414f9e969f9edca80561487c8b')

prepare() {
  cd cucumber-ruby-core-$pkgver

  sed -i -r -e 's|~>|>=|g' cucumber-core.gemspec
  sed -i -e "s/, '< 28'//" -e "s/, '< 23'//" -e "s/, '< 7'//" cucumber-core.gemspec
  sed -i '/[rR]ubo[cC]op/d' cucumber-core.gemspec Rakefile
  sed -i \
    -e "s/require 'cucumber\/messages\/time_conversion'/require 'cucumber\/messages\/helpers\/time_conversion'/" \
    -e "s/Cucumber::Messages::TimeConversion/Cucumber::Messages::Helpers::TimeConversion/" \
    -e "s/Cucumber::Messages::IdGenerator::UUID/Cucumber::Messages::Helpers::IdGenerator::UUID/" \
    lib/cucumber/core/compiler.rb \
    lib/cucumber/core/test/result.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd cucumber-ruby-core-$pkgver
  gem build cucumber-core.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    cucumber-core-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd cucumber-ruby-core-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rake spec spec:slow
}

package() {
  cd cucumber-ruby-core-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
