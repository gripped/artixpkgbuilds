# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bert Peters <bertptrs@archlinux.org>

pkgname=ruby-cucumber-core
pkgver=17.0.0
pkgrel=1
pkgdesc="Core library for the Cucumber BDD app"
arch=(any)
url='https://github.com/cucumber/cucumber-ruby-core'
license=(MIT)
depends=(
  ruby
  ruby-cucumber-gherkin
  ruby-cucumber-messages
  ruby-cucumber-tag-expressions
)
makedepends=(
  git
  ruby-rake
  ruby-rdoc
  ruby-rspec
  ruby-unindent
)
checkdepends=(
  ruby-bundler
  ruby-simplecov
)
options=(!emptydirs)
source=("git+https://github.com/cucumber/cucumber-ruby-core.git#tag=v$pkgver")
sha512sums=('d1e350097b51450ba38290433a08d3ce7c570d3d6192c94858903588e3832c2c67492bf0aa3319eff67403bac2c2d8307dd2fb660fc73c9324da2ce626fa08bc')
b2sums=('0eb3039d8fec6c8e3b763f5e75f4946b7c7669590b0d0128d678514368166f4bfad58512752013dc02e80329b3c03507601d7736f59240a34a453d8704686d8c')

prepare() {
  cd cucumber-ruby-core

  sed -i -r -e 's|~>|>=|g' -e "s/, '< [0-9]+'//" cucumber-core.gemspec
  sed -i '/[rR]ubo[cC]op/d' cucumber-core.gemspec Rakefile
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd cucumber-ruby-core
  gem build cucumber-core.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    cucumber-core-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd cucumber-ruby-core
  GEM_HOME="tmp_install/$_gemdir" rake spec spec:slow
}

package() {
  cd cucumber-ruby-core
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
