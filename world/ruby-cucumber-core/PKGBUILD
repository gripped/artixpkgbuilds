# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bert Peters <bertptrs@archlinux.org>

pkgname=ruby-cucumber-core
pkgver=15.2.1
pkgrel=1
pkgdesc="Core library for the Cucumber BDD app"
arch=(any)
url='https://github.com/cucumber/cucumber-ruby-core'
license=(MIT)
depends=(
  ruby
  ruby-cucumber-gherkin
  ruby-cucumber-messages
  ruby-cucumber-tag-expressions
)
makedepends=(
  git
  ruby-rake
  ruby-rdoc
  ruby-rspec
  ruby-unindent
)
checkdepends=(
  ruby-bundler
  ruby-simplecov
)
options=(!emptydirs)
source=("git+https://github.com/cucumber/cucumber-ruby-core.git#tag=v$pkgver")
sha512sums=('749b0ae4cb901ca4e7e94bf6426d45bbbbf158373cd4dc323f1f796fec64b089aaac0f666615b997ac593882209c2317471734db5326051c97befc5b0ccd0f1b')
b2sums=('bfb0f520ac089ef9851c785a8240b9ca2cd6f1f42e0d191bc77d0f2a32b509fcb4e18218b4ec60b8452a0fb0f8e3d45fd4075fbf161723f49db67e5b1a98c402')

prepare() {
  cd cucumber-ruby-core

  sed -i -r -e 's|~>|>=|g' cucumber-core.gemspec
  sed -i '/[rR]ubo[cC]op/d' cucumber-core.gemspec Rakefile
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd cucumber-ruby-core
  gem build cucumber-core.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    cucumber-core-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd cucumber-ruby-core
  GEM_HOME="tmp_install/$_gemdir" rake spec spec:slow
}

package() {
  cd cucumber-ruby-core
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
