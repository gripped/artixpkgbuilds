# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bert Peters <bertptrs@archlinux.org>

pkgname=ruby-cucumber-core
pkgver=15.1.0
pkgrel=1
pkgdesc="Core library for the Cucumber BDD app"
arch=(any)
url='https://github.com/cucumber/cucumber-ruby-core'
license=(MIT)
depends=(
  ruby
  ruby-cucumber-gherkin
  ruby-cucumber-messages
  ruby-cucumber-tag-expressions
)
makedepends=(
  ruby-rake
  ruby-rdoc
  ruby-rspec
  ruby-unindent
)
checkdepends=(
  ruby-bundler
  ruby-simplecov
)
options=(!emptydirs)
source=("https://github.com/cucumber/cucumber-ruby-core/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('0cfdc2f3dfd5ac231a77e222c033315d1f299ccc7e01643b299eec7d0fcde8f2e6bd7ee887361ce189a3a6d1f685191b6817c787f7db8a1c86e29d0fdaf61554')
b2sums=('d6623aefc1b8ad6d00357c41e87dfd2c30c87ba7b1bb79b0f4e863fa0a3a8bf795e03000f007c60bb7a753bd771283cae39cd7d423566d22eb31d38337727c95')

prepare() {
  cd cucumber-ruby-core-$pkgver

  sed -i -r -e 's|~>|>=|g' cucumber-core.gemspec
  sed -i -e "s/, '< 28'//" -e "s/, '< 23'//" -e "s/, '< 7'//" cucumber-core.gemspec
  sed -i '/[rR]ubo[cC]op/d' cucumber-core.gemspec Rakefile
  sed -i \
    -e "s/require 'cucumber\/messages\/time_conversion'/require 'cucumber\/messages\/helpers\/time_conversion'/" \
    -e "s/Cucumber::Messages::TimeConversion/Cucumber::Messages::Helpers::TimeConversion/" \
    -e "s/Cucumber::Messages::IdGenerator::UUID/Cucumber::Messages::Helpers::IdGenerator::UUID/" \
    lib/cucumber/core/compiler.rb \
    lib/cucumber/core/test/result.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd cucumber-ruby-core-$pkgver
  gem build cucumber-core.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    cucumber-core-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd cucumber-ruby-core-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rake spec spec:slow
}

package() {
  cd cucumber-ruby-core-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
