# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=plasma5support
pkgver=6.5.1
_dirver=$(echo $pkgver | cut -d. -f1-3)
pkgrel=1
pkgdesc='Support components for porting from KF5/Qt5 to KF6/Qt6'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kconfig
         kcoreaddons
         kguiaddons
         kholidays
         ki18n
         kidletime
         kio
         kjobwidgets
         knotifications
         kservice
         kunitconversion
         libksysguard
         libx11
         libxfixes
         qt6-base
         qt6-declarative
         solid)
makedepends=(doxygen
             extra-cmake-modules
             gpsd
             networkmanager-qt
             plasma-activities
             qt6-tools)
optdepends=('gpsd: GPS-based geolocation dataengine'
            'networkmanager-qt: IP-based geolocation dataengine'
            'plasma-activities: activities dataengine')
groups=(plasma)
source=(https://download.kde.org/stable/plasma/$_dirver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('70b9f96122f19cefa4ca5efb6990b9689e289e986af6c9e3eca4e74cf559d279'
            'SKIP')
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D'  # Jonathan Esk-Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

build() {
  cmake -B build  -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
