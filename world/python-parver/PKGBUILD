# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-parver
pkgver=1.0.1.post0
pkgrel=1
pkgdesc="Parse and manipulate version numbers"
url="https://github.com/RazerM/parver"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('git' 'python-build' 'python-hatchling' 'python-installer')
checkdepends=('python-pytest' 'python-hypothesis' 'python-pretend' 'python-pytest-xdist')
source=("git+https://github.com/RazerM/parver.git#tag=$pkgver")
sha512sums=('d994a97c8dc8605b234380d4d71155c9c4d9f061565f862e411fd4b51ac48a3d4a8fdaa9d04fcd4b4e979daeb7a83757c40a9fb847773b006037cab770584122')

build() {
  cd parver
  python -m build -nw
}

check() {
  cd parver
  PYTHONPATH=src pytest
}

package() {
  cd parver
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
