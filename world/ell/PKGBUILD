# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>

pkgname=ell
pkgver=0.78
pkgrel=1
pkgdesc="Embedded Linux library"
arch=(x86_64)
url="https://git.kernel.org/pub/scm/libs/ell/ell.git/"
license=('LGPL-2.1-or-later')
depends=('gcc-libs' 'glibc')
checkdepends=('dbus' 'procps-ng' 'udev')
# https://mirrors.edge.kernel.org/pub/linux/libs/ell/sha256sums.asc
source=(https://mirrors.edge.kernel.org/pub/linux/libs/$pkgname/$pkgname-$pkgver.tar.{xz,sign})
sha256sums=('28ee1a586b4891c96e68667a424684e64ad35120f345e89d6705bd4023d2d40a'
            'SKIP')
validpgpkeys=('E932D120BC2AEC444E558F0106CA9F5D1DCF2659') # "Marcel Holtmann <marcel@holtmann.org>"

build() {
  cd "$pkgname-$pkgver"
 ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  make check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
