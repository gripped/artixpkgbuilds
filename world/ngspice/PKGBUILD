# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Abhishek Dasgupta <abhidg@gmail.com>
# Contributor: Jason Taylor <jftaylor21@gmail.com>
# Contributor: Luis Henrique <lmello.009@gmail.com>

pkgname=ngspice
pkgver=47
_manual_pkgver=46 # manual seems to lag a bit behind
pkgrel=1
pkgdesc='Mixed-level/Mixed-signal circuit simulator based on Spice3f5, Ciber1b1, and Xspice'
arch=(x86_64)
url='https://ngspice.sourceforge.net'
license=(BSD-3-Clause)
depends=(
  glibc
  libxt
  libx11
  ncurses
  libgcc
  libstdc++
  libgomp
  readline
  libxaw
)
source=(
  "$pkgname-$pkgver.tar.gz::https://downloads.sourceforge.net/project/$pkgname/ng-spice-rework/$pkgver/$pkgname-$pkgver.tar.gz"
  "https://sourceforge.net/projects/ngspice/files/ng-spice-rework/$pkgver/ngspice-$_manual_pkgver-manual.pdf"
)
sha512sums=('0eec72a8450f6b654ea13ac62ea74c09a1f74093c8b3b88d098100bd0193df24f07ffe723dfe174dd576f6488f95484b2898934a20dee0865c3491b560dc6583'
            '50a24fba5b84677b30477ed60d50c9e39852ecd3f766e4cf58ed35ce3108a25817b02aba3c887b93ce5e0c5cb2c5d72a7a8019dd9fdb32fa8c849cc30b741991')
b2sums=('3dbca11560d017f4f784c5d14f55742b2837de43b2a280acc6d8fda9f2e1738148a4f1f5805af70b1093bdffd855ef5e1c86fa2238f156089a08898c45efa2fb'
        '071cf9e8311266b58fd0569a1c17e6ea7fb17891cd39c86f069382c3591766c5f2e835ebd13aa9064dfed8ee97ebb995c28adae836ff3ae5e461f75ae88c5f85')

build() {
  cd "$pkgname-$pkgver"

  local _common_flags=(
    --prefix=/usr
    --mandir=/usr/share/man
    --infodir=/usr/share/info
    --with-readline=yes
    --enable-xspice
    --enable-cider
    --enable-openmp
    --disable-debug
  )

  # FS#45230, create so lib
  # shared lib sets flags and modifies headers, needs dedicated pass
  ./configure \
    "${_common_flags[@]}" \
    --with-ngshared
  make

  ./configure "${_common_flags[@]}"
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" COPYING

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" "$srcdir/ngspice-$_manual_pkgver-manual.pdf"

  # shared library
  local _sover="0.0.16"
  install -vDm755 "src/.libs/libngspice.so.$_sover" "$pkgdir/usr/lib/libngspice.so.$_sover"
  ln -s "libngspice.so.$_sover" "$pkgdir/usr/lib/libngspice.so.0"
  ln -s "libngspice.so.$_sover" "$pkgdir/usr/lib/libngspice.so"

  # FS#70563, probably remove on v35
  install -Dm644 ngspice.pc "$pkgdir/usr/lib/pkgconfig/ngspice.pc"
  install -Dm644 src/include/ngspice/sharedspice.h "$pkgdir/usr/include/ngspice/sharedspice.h"
}
