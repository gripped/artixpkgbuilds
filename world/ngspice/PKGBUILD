# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Abhishek Dasgupta <abhidg@gmail.com>
# Contributor: Jason Taylor <jftaylor21@gmail.com>
# Contributor: Luis Henrique <lmello.009@gmail.com>

pkgname=ngspice
pkgver=45
_manual_pkgver=45 # manual seems to lag a bit behind
pkgrel=1
pkgdesc='Mixed-level/Mixed-signal circuit simulator based on Spice3f5, Ciber1b1, and Xspice'
arch=(x86_64)
url='https://ngspice.sourceforge.net'
license=(BSD-3-Clause)
depends=(
  glibc
  libxt
  libx11
  ncurses
  gcc-libs
  readline
  libxaw
)
source=(
  "$pkgname-$pkgver.tar.gz::https://downloads.sourceforge.net/project/$pkgname/ng-spice-rework/$pkgver/$pkgname-$pkgver.tar.gz"
  "https://sourceforge.net/projects/ngspice/files/ng-spice-rework/$pkgver/ngspice-$_manual_pkgver-manual.pdf"
)
sha512sums=('a23f41e84d6662aeb33e025693ebf5d77fc430ef39a9c8221fabb825b9163a48815edc5de7aa22ec7a0222d00ba34da1e827a2ba74e88329df6d0bb192a57d17'
            '425d9320a01d409c7baab83d111f92643cae7df62a109e06d7e4fd366262441f4fdd62532db63fb4d4af4cb98e478b4f9fca5bd6c63fb89507dba685220f34ab')
b2sums=('3865a0da33c0aa245cf32c7e96157e80736d068a03711d1fb61ce0c869b59fdf0931e91de6640bc4cfd139188fcb9d4893ab0095b54d5fb87b77e5cdf7aff961'
        'd5163652c6e7e088d3d3a95494a088f394f0c8efd71e80f15ac0f1d3dd238f17b7104be45390de39bf9aa2d41f1677ea7979685471cf9deaafc934c2a531c67a')

build() {
  cd "$pkgname-$pkgver"

  local _common_flags=(
    --prefix=/usr
    --mandir=/usr/share/man
    --infodir=/usr/share/info
    --with-readline=yes
    --enable-xspice
    --enable-cider
    --enable-openmp
    --disable-debug
  )

  # FS#45230, create so lib
  # shared lib sets flags and modifies headers, needs dedicated pass
  ./configure \
    "${_common_flags[@]}" \
    --with-ngshared
  make

  ./configure "${_common_flags[@]}"
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" COPYING

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" "$srcdir/ngspice-$_manual_pkgver-manual.pdf"

  # shared library
  local _sover="0.0.13"
  install -vDm755 "src/.libs/libngspice.so.$_sover" "$pkgdir/usr/lib/libngspice.so.$_sover"
  ln -s "libngspice.so.$_sover" "$pkgdir/usr/lib/libngspice.so.0"
  ln -s "libngspice.so.$_sover" "$pkgdir/usr/lib/libngspice.so"

  # FS#70563, probably remove on v35
  install -Dm644 ngspice.pc "$pkgdir/usr/lib/pkgconfig/ngspice.pc"
  install -Dm644 src/include/ngspice/sharedspice.h "$pkgdir/usr/include/ngspice/sharedspice.h"
}
