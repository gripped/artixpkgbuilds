# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-font-viewer
pkgver=49.0
pkgrel=1
pkgdesc="A font viewer utility for GNOME"
url="https://apps.gnome.org/FontViewer/"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  fontconfig
  freetype2
  fribidi
  gcc-libs
  glib2
  glibc
  graphene
  gtk4
  harfbuzz
  hicolor-icon-theme
  libadwaita
  pango
)
makedepends=(
  git
  meson
)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/gnome-font-viewer.git#tag=${pkgver/[a-z]/.&}")
b2sums=('1652a62ea63a0a7abd68cdc8c04e74d1b75885c43154e7e59b926b15f18455e6a14b0797c5dbbdf4d3e1f2a24d6431a6a15b30fc970305b26067a815566bc012')

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
