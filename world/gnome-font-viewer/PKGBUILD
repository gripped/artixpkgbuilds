# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-font-viewer
pkgver=48.0
pkgrel=1
pkgdesc="A font viewer utility for GNOME"
url="https://apps.gnome.org/FontViewer/"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  fontconfig
  freetype2
  fribidi
  gcc-libs
  glib2
  glibc
  graphene
  gtk4
  harfbuzz
  hicolor-icon-theme
  libadwaita
  pango
)
makedepends=(
  git
  meson
)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/gnome-font-viewer.git#tag=${pkgver/[a-z]/.&}")
b2sums=('de2e41320d99b30652f2899e2c0861a5aff820d40faab5e9f2bdb21c2b35980422b5af20f15b1cca44137dd27384f45f5e2994e985535d959770e46312ae63c5')

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
