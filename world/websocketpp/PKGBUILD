# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>
# Contributor: Daichi Shinozaki <dsdseg@gmail.com>
# Contributor: skydrome <skydrome@i2pmail.org>
# Contributor: MTsoul

pkgname=websocketpp
pkgver=0.8.2
pkgrel=4
pkgdesc='C++/Boost Asio based websocket client/server library'
url='https://www.zaphoyd.com/websocketpp/'
arch=('any')
license=('BSD')
optdepends=(
  'openssl: TLS socket component support'
  'asio: alternative asio transport support'
  'boost: non C++11 environments support'
  'boost-libs: non C++11 environments support'
)
makedepends=('cmake' 'boost' 'boost-libs' 'boost' 'zlib' 'openssl')
source=(https://github.com/zaphoyd/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/zaphoyd/websocketpp/commit/deb0a334471362608958ce59a6b0bcd3e5b73c24.patch
        websocketpp-0.8.2-boost-1.87-compat.patch)
sha512sums=('b2afc63edb69ce81a3a6c06b3d857b3e8820f0e22300ac32bb20ab30ff07bd58bd5ada3e526ed8ab52de934e0e3a26cad2118b0e68ecf3e5e9e8d7101348fd06'
            '7fe74681074df6504b9cab795e4f68cb1a3094903f3cef755291408f3a70e0de1831d082b15965d71612a905630da5d0bb9f40440c4c8f6031101fe07d9d71a4'
            '6b64fed053690f1dfd17ba5b33f9e3ba1c0e51a5eb40dc33a4e32ec23a76a33ea3e9ad792d2c4f1b478c1873fd9461db9893475ffc060190836347021f6d1294')
b2sums=('dacee33832f493d465afe208f9edea1393414a22c8db8f8c86b8f913521d0d8d68b95673a2e82b7479acfbab1ac541eda2d713a55d5de387b3879461d5884df7'
        '1dfb56533aceeb79191b1c3b4c6e9db80f4ef5e8e4c3c7afb27381d33722aa5aba4909fc18c6fd3067dd0ead67c26a0efa23b30576ac5adfe637faa138f84920'
        'cd472b448382b21dd312c4ee132622c291c50eae05888858e5bdb3e64cf7d27a393514ff4ff780320b76c15ffb70b62b78369df92ed2d82f277378d0f04bf795')

prepare() {
  cd ${pkgname}-${pkgver}

  # Backport of https://github.com/zaphoyd/websocketpp/pull/1164/ to 0.8.2
  # Fixes compatibility with boost 1.87
  # https://gitlab.archlinux.org/archlinux/packaging/packages/websocketpp/-/issues/2
  patch -Np1 -i ../websocketpp-0.8.2-boost-1.87-compat.patch

  # Backport of https://github.com/zaphoyd/websocketpp/pull/1168 (CMake compatibility fix)
  patch -Np1 -i ../deb0a334471362608958ce59a6b0bcd3e5b73c24.patch

  sed 's|"${WEBSOCKETPP_BOOST_LIBS}"|${WEBSOCKETPP_BOOST_LIBS}|' -i CMakeLists.txt
}

build() {
  cd ${pkgname}-${pkgver}
  cmake -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_CPP11=ON \
    -DBUILD_TESTS=ON
  make -C build
}

check() {
  cd ${pkgname}-${pkgver}
  make -C build test
}

package() {
  cd ${pkgname}-${pkgver}
  make -C build DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 readme.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
