# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux,org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=konqueror
pkgver=26.04.2
pkgrel=1
pkgdesc='KDE File Manager & Web Browser'
arch=(x86_64)
url='https://apps.kde.org/konqueror/'
license=(LGPL-2.0-or-later)
depends=(glibc
         karchive
         kbookmarks
         kcmutils
         kcodecs
         kcolorscheme
         kcompletion
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         kdbusaddons
         kguiaddons
         ki18n
         kiconthemes
         kio
         kitemviews
         kjobwidgets
         kparts
         kservice
         ktextwidgets
         kwallet
         kwidgetsaddons
         kwindowsystem
         kxmlgui
         libstdc++
         plasma-activities
         qt6-base
         qt6-webengine
         solid
         sonnet
         zlib)
makedepends=(extra-cmake-modules
             kdesu
             kdoctools)
optdepends=('kdesu: shell command plugin')
groups=(kde-applications
        kde-network)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('aa9a35f81fa1f30aac1d77b9008d074fb041b2a7c12319d1f8c9b194137e57de'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DUSE_SYSTEM_DICTIONARIES=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
