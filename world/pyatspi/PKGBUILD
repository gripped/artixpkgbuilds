# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=pyatspi
pkgname=(python-atspi)
pkgver=2.58.1
pkgrel=1
pkgdesc="Python bindings for D-Bus AT-SPI"
url="https://gitlab.gnome.org/GNOME/pyatspi2"
arch=(any)
license=(LGPL-2.0-only)
depends=(
  at-spi2-core
  glib2
  python
  python-gobject
)
makedepends=(
  git
  meson
  python-dbus
)
source=("git+$url.git#tag=$pkgver")
b2sums=('1c94d019ff74f7e6bb855449f53471fde1383a100114e38056c8d43c7e0f8668262d4255f8460a7d8ef9b264fb3627c800bd3f0bc5b3d99ea0994c0d1486cf39')

prepare() {
  cd pyatspi2
}

build() {
  local meson_options=(
    -D enable_tests=true
  )

  artix-meson pyatspi2 build "${meson_options[@]}"
  meson compile -C build
}

check() {
  dbus-run-session meson test -C build --print-errorlogs
}

package_python-atspi() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
