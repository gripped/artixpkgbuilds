# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=pyatspi
pkgname=(python-atspi)
pkgver=2.58.0
pkgrel=2
pkgdesc="Python bindings for D-Bus AT-SPI"
url="https://gitlab.gnome.org/GNOME/pyatspi2"
arch=(any)
license=(LGPL-2.0-only)
depends=(
  at-spi2-core
  glib2
  python
  python-gobject
)
makedepends=(
  git
  meson
  python-dbus
)
source=("git+$url.git#tag=$pkgver")
b2sums=('8eea640ae1fdc8a06a652def26e05842cf0e927e0334d40d75e98c72fda11476663230feb20bec55d1e34d291b3ba6214a62a62da415ab1f37cad5c28bb5a997')

prepare() {
  cd pyatspi2
  git cherry-pick -n 76ca7bae2d3c69a770d93cf42a351b7069332348 # Fix tests
}

build() {
  local meson_options=(
    -D enable_tests=true
  )

  artix-meson pyatspi2 build "${meson_options[@]}"
  meson compile -C build
}

check() {
  dbus-run-session meson test -C build --print-errorlogs
}

package_python-atspi() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
