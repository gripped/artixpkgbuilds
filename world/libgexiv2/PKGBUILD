# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=libgexiv2
pkgver=0.14.5
pkgrel=1
pkgdesc='GObject-based wrapper around the Exiv2 library'
url='https://gitlab.gnome.org/GNOME/gexiv2'
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(exiv2 gcc-libs glib2 glibc)
makedepends=(git glib2-devel gobject-introspection meson python-gobject vala)
source=("git+$url.git?signed#tag=gexiv2-$pkgver")
b2sums=('ef665a3a8cbcfc00f1d0ece6c01d4a036d3cd6d673ed8aa527c08d85f873d6ecff4b7d89d22cd1a5d2b45ccdae30f1da24f06cd31a6685c7b78d6f44cfcd826c')
validpgpkeys=(AC9CD4E32D7C7F6357BA8ADD10F6E970175D29E1) # Jens Georg <mail@jensge.org>

build() {
  artix-meson gexiv2 build -D gtk_doc=true -D tests=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(
    libg{lib,object,io}-2.0.so
    libexiv2.so
  )
  provides+=(libgexiv2.so)

  meson install -C build --destdir "$pkgdir"
}
