# Maintainer: Peter Jung <ptr1337@cachyos.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=libpfm
pkgver=4.13.0+r83+g91970fe
pkgrel=1
pkgdesc='Library to encode performance events for use by perf tool'
arch=('x86_64')
url='https://perfmon2.sourceforge.net'
license=('MIT')
depends=(
  glibc
)
makedepends=(
  git
)
_commit='91970fe6eb4e80b63f77fb54a9592e28a207050c'
source=("$pkgname::git+https://git.code.sf.net/p/perfmon2/libpfm4#commit=$_commit")
b2sums=('1fbc4c8143e9f948ea714af22687fb31a07ee2c688c863153c9b276671d8ee179ce0883e012e0ec5b46fbf39fb853989845b1ba3359aceb68909ffcd18b5cfd0')

pkgver() {
  cd "$pkgname"

    git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

build() {
  cd "$pkgname"

  make PREFIX=/usr
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" PREFIX=/usr install

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" COPYING
}

