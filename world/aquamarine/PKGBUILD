# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=aquamarine
pkgver=0.7.1
pkgrel=2
pkgdesc='a very light linux rendering backend library'
arch=(x86_64 aarch64)
url="https://github.com/hyprwm/$pkgname"
license=(BSD-3-Clause)
depends=(gcc-libs # libstdc++.so libgcc_s.so
         glibc # libc.so libm.so
         hyprutils libhyprutils.so
         hyprwayland-scanner
         libdisplay-info libdisplay-info.so
         libdrm # libdrm.so
         libglvnd libEGL.so libGLESv2.so
         libinput # libinput.so
         mesa # libgbm.so
         opengl-driver
         pixman
         pugixml
         seatd libseat.so
         libudev libudev.so
         wayland libwayland-client.so
         wayland-protocols)
makedepends=(cmake)
provides=("lib$pkgname.so")
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('e61453345e1df9c37cedd10d4c48df0b2d3a53d8e535fc71c3dd06f88f1d31f3')

build() {
	cd "$_archive"
	cmake -B build \
		-D CMAKE_INSTALL_PREFIX=/usr \
		-D CMAKE_BUILD_TYPE=Release
	cmake --build build
}

package() {
	cd "$_archive"
	DESTDIR="$pkgdir" cmake --install build
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
