# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='lsp'
pkgname="ruby-${_gemname}"
pkgver=0.26.9
pkgrel=1
pkgdesc='An opinionated language server for Ruby'
arch=('any')
url="https://github.com/Shopify/ruby-${_gemname}"
license=('MIT')
depends=(
  ruby
  ruby-language_server-protocol
  ruby-rbs
  ruby-sorbet-runtime
)
makedepends=(
  ruby-rdoc
)
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/ruby-${pkgname}-${pkgver}.tar.gz")
sha512sums=('8f75140310e8a8dd14a7eb832c96d3dfee14a4d501c0201bf59c7681f36934d58dcbb8e7523ee879388b8ee372d05f9240bf937919ad26dc6f316b1241b17e62')
b2sums=('95a7b84463c9854c2527c4b25a477ccb05ac4b5307c0849978784fbbb26ec5a282282a1d2e4a1a0727d42488416cb8ae7e25dbe17c507b61bcf147cc679b17d0')

prepare() {
  cd "${pkgname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${pkgname}.gemspec"
}

build() {
  cd "${pkgname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${pkgname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${pkgname}-${pkgver}.gem"

  # remove unreproducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${pkgname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${pkgname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

package() {
  cd "${pkgname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
