# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-regexp_property_values
pkgver=1.5.2
pkgrel=3
pkgdesc="Inspect property values supported by Ruby's regex engine"
arch=(x86_64)
url='https://github.com/jaynetics/regexp_property_values'
license=(MIT)
depends=(ruby)
makedepends=(git)
checkdepends=(ruby-character_set ruby-rake ruby-rake-compiler ruby-range_compressor ruby-rspec)
options=(!emptydirs)
source=("git+https://github.com/jaynetics/regexp_property_values.git#tag=v${pkgver}")
sha256sums=('2811f544b68d7b175b4235bdb1acbac44e31215f8753cf7ae985dcc9945f6aeb')

prepare() {
  cd regexp_property_values
  sed -i -r 's|~>|>=|' Gemfile
  # Don't recompile
  sed -i '/compile/d' Rakefile
  sed -i '/bundler/d' spec/spec_helper.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd regexp_property_values
  gem build --verbose regexp_property_values.gemspec

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    regexp_property_values-$pkgver.gem

  # remove unreproducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd regexp_property_values
  GEM_HOME="tmp_install/$_gemdir" rspec
}

package() {
  cd regexp_property_values
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
