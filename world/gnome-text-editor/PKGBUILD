# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-text-editor
pkgver=49.1
pkgrel=1
pkgdesc="A simple text editor for the GNOME desktop"
url="https://apps.gnome.org/TextEditor/"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  dconf
  editorconfig-core-c
  gcc-libs
  glib2
  glibc
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  libspelling
  pango
)
makedepends=(
  appstream
  git
  glib2-devel
  meson
  yelp-tools
)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/gnome-text-editor.git#tag=${pkgver/[a-z]/.&}")
b2sums=('86701f4ce868ac5f756afbb028c1414aac42454369bf2ec3e850272eedaac7891ab8c33d131c2f47eca3c6136c890087c9d57e610d4cf0d17f449af8564d2fd9')

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D bugreport_url="https://gitlab.archlinux.org/archlinux/packaging/packages/gnome-text-editor/-/issues"
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
