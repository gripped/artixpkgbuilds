# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-text-editor
pkgver=51.0
pkgrel=1
pkgdesc="A simple text editor for the GNOME desktop"
url="https://apps.gnome.org/TextEditor/"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  dconf
  editorconfig-core-c
  glib2
  glibc
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  libgcc
  libspelling
  pango
)
makedepends=(
  appstream
  git
  glib2-devel
  meson
  yelp-tools
)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/gnome-text-editor.git#tag=${pkgver/[a-z]/.&}")
b2sums=('162f610d07385f0b1e0470f1489aad96f370061087a784001786aea95dc3b58560da4cdd051f69bb130f64e7a5f3c8be4c99e898e64c82bd3917a14ccbff4b6a')

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D bugreport_url="https://gitlab.archlinux.org/archlinux/packaging/packages/gnome-text-editor/-/issues"
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
