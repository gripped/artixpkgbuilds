# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-text-editor
pkgver=47.2
pkgrel=1
pkgdesc="A simple text editor for the GNOME desktop"
url="https://gitlab.gnome.org/GNOME/gnome-text-editor"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  editorconfig-core-c
  gcc-libs
  glib2
  glibc
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  libspelling
  pango
)
makedepends=(
  appstream-glib
  git
  glib2-devel
  meson
  yelp-tools
)
groups=(gnome)
source=("git+$url.git#tag=${pkgver/[a-z]/.&}")
b2sums=('a70ceafcca9f8932aebef4c3439fb443de89d0144d7e4788979a09b9c8e17557752e01e94ed0b8f35b9fbc4333d61f4cab64fe28710899ffc95927641164cdb9')

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D bugreport_url="https://gitlab.archlinux.org/archlinux/packaging/packages/gnome-text-editor/-/issues"
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
