# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-text-editor
pkgver=48.2
pkgrel=3
pkgdesc="A simple text editor for the GNOME desktop"
url="https://apps.gnome.org/TextEditor/"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  editorconfig-core-c
  gcc-libs
  glib2
  glibc
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  libspelling
  pango
)
makedepends=(
  appstream
  git
  glib2-devel
  meson
  yelp-tools
)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/gnome-text-editor.git#tag=${pkgver/[a-z]/.&}")
b2sums=('ba1d0ac98358664093a5c3e7e1d53882da6592aafbb812002c325d32e0d3e0b9efca46a7bd1cb39e33a29c6a34b8df968cf5b4c0e3deeedbf5371fec5c7c3a2a')

prepare() {
  cd $pkgname

  # Activate the application if no window present
  # https://gitlab.gnome.org/GNOME/gnome-text-editor/-/merge_requests/219
  git cherry-pick -n 926ff29b2eac70639e909e4fc8146f2d8a997e75
  # https://gitlab.gnome.org/GNOME/gnome-text-editor/-/merge_requests/221
  git cherry-pick -n 6baf599d0cf4d9daf395c5db326679151f711688
  # https://gitlab.gnome.org/GNOME/gnome-text-editor/-/merge_requests/223
  git cherry-pick -n 8175fc9b74d4ac0e1bbb51203b9f63d14c1bfcc6
}

build() {
  local meson_options=(
    -D bugreport_url="https://gitlab.archlinux.org/archlinux/packaging/packages/gnome-text-editor/-/issues"
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
