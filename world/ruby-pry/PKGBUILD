# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-pry
pkgver=0.15.2
pkgrel=2
pkgdesc='A runtime developer console and IRB alternative with powerful introspection capabilities'
arch=(any)
url='https://pry.github.io/'
license=(MIT)
depends=(
  ruby
  ruby-coderay
  ruby-method_source
)
makedepends=(
  git
)
checkdepends=(
  ruby-bundler
  ruby-irb
  ruby-rake
  ruby-rspec
  ruby-yard
)
options=(!emptydirs)
source=(git+https://github.com/pry/pry.git#tag=v$pkgver)
sha512sums=('98ce5ba3e6ee720d5d38c95af8f4c211ec6cf97c48383993e1acb9339e1577f8591a4b464a6a46a9425e2fa5aa11a6e8bd28b453786d6d2f8519a885cc0b975a')
b2sums=('85d84e5fb9feb99b0cf41c5b58846f4d3c4b883a29c57df0896a5688c35d3b17fe20171f878c0792f85193ae18d8ac75a32d17fdff6c7c702b4e7dc3650b3f2e')

prepare() {
  cd pry
  sed -i -r -e 's|~>|>=|' pry.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd pry
  gem build pry.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    pry-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd pry
  EDITOR=vim TERM=screen-256color GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd pry
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
