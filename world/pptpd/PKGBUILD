# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=pptpd
pkgver=1.5.0
pkgrel=1
pkgdesc="Poptop server"
arch=(x86_64)
url="http://poptop.sourceforge.net/"
license=('GPL-2.0-only')
depends=('ppp')
backup=(etc/pptpd.conf)
validpgpkeys=('A602F7C9A42CB3B54634A8826E6470BFAE2466C0'
	      'DAA83B717ACAB6663BCD98C49C866442E9C65C67')
source=(https://downloads.sourceforge.net/sourceforge/poptop/pptpd-$pkgver.tar.gz{,.asc})
sha256sums=('6a724284b1ce00ea23f7d7608d081843a10c8a8d87d951cb2ea86e70aa1b4e77'
            'SKIP')

build() {
  local _pppver=`pacman -Q ppp`
  _pppver=${_pppver%-*}
  _pppver=${_pppver#ppp }

  cd "$srcdir"/$pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib \
    --sbindir=/usr/bin \
    --exec-prefix=/usr \
    --mandir=/usr/share/man
  make
  sed -i 's|?=|=|g' plugins/Makefile
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir"/etc/
  echo -e "# Read man pptpd.conf, see samples in /usr/share/doc/pptpd\n# and write your pptpd configuration here" >"$pkgdir"/etc/pptpd.conf
  mkdir -p "$pkgdir"/usr/share/doc/pptpd
  cp -a samples "$pkgdir"/usr/share/doc/pptpd/
}
