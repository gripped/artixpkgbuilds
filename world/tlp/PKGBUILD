# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Marc Schulte <bomba@nerdstube.de>

pkgbase=tlp
pkgname=(
  tlp
  tlp-rdw
)
pkgver=1.8.0
pkgrel=1
arch=(any)
url=https://linrunner.de/en/tlp/tlp.html
license=(GPL2)
makedepends=(git)
_tag=f6e2628bbfaa378d9a408e0a63bc58c01ad81384
source=(git+https://github.com/linrunner/TLP.git#tag=${_tag})
sha256sums=('8e92195fb9ed8efea685625c246f74cab44c4417157bf9cda43d843654c0f35c')
install=tlp.install

pkgver() {
  cd TLP
  git describe --tags
}

package_tlp() {
  pkgdesc='Linux Advanced Power Management'
  depends=(
    hdparm
    iw
    pciutils
    perl
    rfkill
    usbutils
    util-linux
  )
  optdepends=(
    'bash-completion: Bash completion'
    'ethtool: Disable Wake On Lan'
    'smartmontools: Display S.M.A.R.T. data in tlp-stat'
    'tp_smapi: Older ThinkPad battery functions (before Sandy Bridge)'
  )
  conflicts=(
    laptop-mode-tools
    pm-utils
    power-profiles-daemon
  )
  backup=(etc/tlp.conf)

  export TLP_NO_INIT=1
  export TLP_SBIN=/usr/bin
  export TLP_ULIB=/usr/lib/udev
  export TLP_ELOD=/usr/lib/elogind/system-sleep
  export TLP_WITH_ELOGIND=1
  export TLP_WITH_SYSTEMD=0

  make DESTDIR="${pkgdir}" -C TLP install-tlp install-man-tlp
}

package_tlp-rdw() {
  pkgdesc='Linux Advanced Power Management - Radio Device Wizard'
  depends=(
    networkmanager
    tlp
  )

  make DESTDIR="${pkgdir}" -C TLP install-rdw install-man-rdw
}

# vim: ts=2 sw=2 et:
