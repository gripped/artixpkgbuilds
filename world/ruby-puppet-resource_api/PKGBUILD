# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Tim Meusel <tim@bastelfreak.de>
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>

_gemname='puppet-resource_api'
pkgname="ruby-${_gemname}"
pkgver=1.9.1
pkgrel=1
pkgdesc='This library provides a simple way to write new native resources for puppet.'
arch=('any')
url='https://github.com/puppetlabs/puppet-resource_api'
license=('Apache-2.0')
depends=('ruby' 'ruby-hocon')
makedepends=('ruby-rdoc')
checkdepends=('ruby-rake' 'ruby-rspec' 'ruby-bundler')
source=("${url}/archive/v${pkgver}/${_gemname}-${pkgver}.tar.gz")
options=("!emptydirs")
sha512sums=('bd98cd61bd0d6d402210c42d0c991774a52c3e76712ee3d6fa799dfc3991f7161529f79df5ec855506ff7f21815874b7061a59fc6f5a62d6ba752eead46e8e35')

build() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  gem build "${_gemname}.gemspec"
}

check() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  # tests currently don't work because of outdated and partially dependencies
  #rake test
}

package() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  install -Dm 644 NOTICE LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -Dm 644 CHANGELOG.md CONTRIBUTING.md HISTORY.md README.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm -r "${pkgdir}/${_gemdir}/cache"
}

# vim: ts=2 sw=2 et:
