# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Filip Dvorak <fila@pruda.com>

pkgname=enca
pkgver=1.21
pkgrel=1
pkgdesc="Charset analyser and converter"
arch=('x86_64')
url="https://cihar.com/software/enca/"
license=('GPL-2.0-only')
depends=(
  'glibc'
  'recode'
)
makedepends=(
  'git'
  'libiconv'
)
source=("git+https://github.com/nijel/enca#tag=$pkgver")
b2sums=('6a6dccb2fbb71329b7460d560a483cc83b3d4b588a5978a66b6d10810d0038d98fd2d73d3b061fc943edc21feab8611a1889723adfe0ae0411ee0a7827b597f3')

prepare() {
  cd $pkgname
  autoreconf -fiv -I /usr/share/gettext/m4
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --mandir=/usr/share/man \
    --with-libiconv=/usr \
    --with-librecode=/usr \
    --enable-external
  make
}

check() {
  cd $pkgname
  make test
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
