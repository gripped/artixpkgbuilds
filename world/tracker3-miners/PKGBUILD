# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=tracker3-miners
pkgver=3.7.2
pkgrel=1
pkgdesc="Filesystem indexer and metadata extractor"
url="https://tracker.gnome.org/"
arch=(x86_64)
license=(
  LGPL-2.1-or-later
  GPL-2.0-or-later
)
depends=(
  dconf
  exempi
  gcc-libs
  giflib
  glib2
  glibc
  gstreamer
  gst-plugins-base-libs
  icu
  libcue
  libexif
  libgexiv2
  libgrss
  libgsf
  libgxps
  libiptcdata
  libjpeg-turbo
  libnm
  libosinfo
  libpng
  libseccomp
  libtiff
  poppler-glib
  totem-plparser
  tracker3
  upower
  util-linux-libs
  libxml2
)
makedepends=(
  asciidoc
  git
  meson
  python-packaging
)
groups=(gnome)
source=(
  "git+https://gitlab.gnome.org/GNOME/tracker-miners.git#tag=$pkgver"
)
b2sums=('3ec95f9f30fb265c4f1aac359e26c7d1fced4d8b8b26ffa72efdb4cdce220572fb4861ea0eeea8baf905d463df02e65f8533841f71dcf44f37ffd77bb098cbb8')

prepare() {
  cd tracker-miners
}

build() {
  local meson_options=(
    -D systemd_user_services=false
    -D tests_tap_protocol=true
    -D landlock=enabled
  )

  artix-meson tracker-miners build "${meson_options[@]}"
  meson compile -C build
}

package() {
  depends+=(libtracker-sparql-3.0.so)

  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
