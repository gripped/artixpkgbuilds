# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=tracker3-miners
pkgver=3.6.0
pkgrel=1
pkgdesc="Filesystem indexer and metadata extractor"
url="https://tracker.gnome.org/"
arch=(x86_64)
license=(GPL)
depends=(
  exempi
  giflib
  gst-plugins-base-libs
  libcue
  libexif
  libgexiv2
  libgrss
  libgsf
  libgxps
  libiptcdata
  libnm
  libosinfo
  libseccomp
  poppler-glib
  totem-plparser
  tracker3
  upower
)
makedepends=(
  asciidoc
  git
  meson
)
checkdepends=(
  gst-libav
  gst-plugins-base
  gst-plugins-good
  python-gobject
  python-tappy
)
groups=(gnome)
_commit=2fe320aa5ab337c5eb94b3eddbd721f4a95d6e27  # tags/3.6.0^0
source=(
  "git+https://gitlab.gnome.org/GNOME/tracker-miners.git#commit=$_commit"
)
b2sums=('SKIP')

pkgver() {
  cd tracker-miners
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd tracker-miners
}

build() {
  local meson_options=(
    -D tests_tap_protocol=true
    -D systemd_user_services=false
  )

  artix-meson tracker-miners build "${meson_options[@]}"
  meson compile -C build
}

check() {
  # Intermittent test failures: https://gitlab.gnome.org/GNOME/tracker-miners/-/issues/79
  dbus-run-session meson test -C build --print-errorlogs -t 3 || :
}

package() {
  depends+=(libtracker-sparql-3.0.so)

  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
