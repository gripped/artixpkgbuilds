# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=tracker3-miners
pkgver=3.7.1
pkgrel=1
pkgdesc="Filesystem indexer and metadata extractor"
url="https://tracker.gnome.org/"
arch=(x86_64)
license=(
  LGPL-2.1-or-later
  GPL-2.0-or-later
)
depends=(
  dconf
  exempi
  gcc-libs
  giflib
  glib2
  glibc
  gstreamer
  gst-plugins-base-libs
  icu
  libcue
  libexif
  libgexiv2
  libgrss
  libgsf
  libgxps
  libiptcdata
  libjpeg-turbo
  libnm
  libosinfo
  libpng
  libseccomp
  libtiff
  poppler-glib
  totem-plparser
  tracker3
  upower
  util-linux-libs
  libxml2
)
makedepends=(
  asciidoc
  git
  meson
  python-packaging
)
groups=(gnome)
_commit=eae431ef23ddbf0910bc716f36b5f7512c783752  # tags/3.7.1^0
source=(
  "git+https://gitlab.gnome.org/GNOME/tracker-miners.git#commit=$_commit"
)
b2sums=('ada10b31b18dcfcbaffff8d2aa9b9cafdeb715ab5741dc8afee67011873f661abfb80c12fa6c38b63ee4866a259a964cad6bd9b6241965b96c3433268da9e62e')

pkgver() {
  cd tracker-miners
  git describe --tags | sed -r 's/\.([a-z])/\1/;s/([a-z])\./\1/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd tracker-miners
}

build() {
  local meson_options=(
    -D systemd_user_services=false
    -D tests_tap_protocol=true
    -D landlock=enabled
  )

  artix-meson tracker-miners build "${meson_options[@]}"
  meson compile -C build
}

package() {
  depends+=(libtracker-sparql-3.0.so)

  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
