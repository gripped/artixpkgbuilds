# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-ffi-compiler
pkgver=1.2.2
pkgrel=1
pkgdesc='Ruby library for automating compilation of native libraries for use with ffi'
arch=(any)
url='https://github.com/ffi/ffi-compiler'
license=(Apache-2.0)
depends=(ruby-ffi ruby-rake)
makedepends=(git ruby-rspec ruby-rubygems-tasks ruby-rdoc)
options=(!emptydirs)
source=(git+https://github.com/ffi/ffi-compiler.git#tag=$pkgver)
sha256sums=('a9d2e41d19e5685b5a9e4ff0db37c7d76416f890859f6ad94ff2705b5b4b92a0')

build() {
  local _gemdir="$(gem env gemdir)"
  cd ffi-compiler
  rake build
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    pkg/ffi-compiler-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd ffi-compiler
  # Should output: Example.example=deadbeef
  # TODO: Disabled because example needs to be built first.
  # GEM_HOME="tmp_install/$_gemdir" ruby <<< 'require "example/example"; puts "Example.example=#{Example.example.to_s(16)}"'
}

package() {
  cd ffi-compiler
  cp -a tmp_install/* "$pkgdir"/
}
