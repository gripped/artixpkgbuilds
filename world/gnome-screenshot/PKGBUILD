# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-screenshot
pkgver=41.0+r39+ga362197
pkgrel=3
pkgdesc="Take pictures of your screen"
url="https://gitlab.gnome.org/GNOME/gnome-screenshot"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  libhandy
  libx11
  libxext
)
makedepends=(
  git
  meson
)
_commit=a36219781bd08201d0454bc6101ff9502430aea3  # master
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-screenshot.git#commit=$_commit"
  0001-Fix-various-fallback-paths.patch
)
b2sums=('83f2327f72e839f1fa8c50787c1709407150bcceb67418ad719ba48fbf91a09dfb9040961fe10406459ab1138a28ea1a0f5f91523e029a0cd3e7b25f1c88ff78'
        '543ba200a41eb5948c40dadf0ffa2c2c484f6e83f10ea0d9a4c3bb8c91a7b27e0ea14d9f4cee8c85355f981a7fa87b3ba04dad16251d396c65011967a53da9aa')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname

  # Fix various fallback paths
  # https://gitlab.gnome.org/GNOME/gnome-screenshot/-/merge_requests/72
  patch -Np1 -i ../0001-Fix-various-fallback-paths.patch
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
