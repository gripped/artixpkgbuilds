# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org> 

pkgname=isoimagewriter
pkgver=25.12.0
pkgrel=1
pkgdesc='Program to write hybrid ISO files onto USB disks'
arch=(x86_64)
url='https://apps.kde.org/isoimagewriter/'
license=(GPL-3.0-or-later)
depends=(gcc-libs
         glibc
         gpgmepp
         karchive
         kcoreaddons
         kcrash
         ki18n
         kiconthemes
         kwidgetsaddons
         qgpgme
         qt6-base
         solid)
makedepends=(extra-cmake-modules)
groups=(kde-applications
        kde-utilities)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('baaa8c619747cbf7a65b6e83ea450921a137fe3e374289a80b7cc9da8e75f3b9'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() { 
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
