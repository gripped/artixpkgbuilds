# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-console
pkgver=1.27.0
pkgrel=1
pkgdesc='Beautiful logging for Ruby'
arch=(any)
url='https://github.com/socketry/console'
license=(MIT)
depends=(
  ruby
  ruby-fiber-annotation
  ruby-fiber-local
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-bake
  ruby-bake-modernize
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-decode
  ruby-sus
)
options=(!emptydirs)
source=(
  "git+${url}.git#tag=v$pkgver"
  "${url}/commit/039d7f01a1264897255b02b59941857103891088.patch"
)
sha512sums=('791c40c0d570b6e8ed0dfdc5c531f5cc8566463d2434e47ed1487f90987c76dc35d5a3272f5604e2212e090798050ee8172e5062f1c21c6e2d0fd19794c6b40a'
            '6e493031b804d9f0adf29dff78552277ccd2ee6509482a28353fcea61fb57d82a1bbfade77569a984af7b7145bfb292c5fb83af1aebba9f32827275933b943e3')
b2sums=('b2502f648b50f237de88224f92bb0436bd84d02120afc2bc482543fcc56915bed2ace2a87e0b051754f3d25a761f61423c1f7f50519a9e042f752408a6cf9ccc'
        '8b7c6a1cd3782127db6c3b843f47327e0eb96212ebbc0ac047e084542b6fe59411587ce09b1347ddb98537a4fb33a93d010b23deb970fba36df577324e702d00')

prepare() {
  cd console

  patch --verbose --strip=1 --input="../039d7f01a1264897255b02b59941857103891088.patch"

  sed -r -e 's|~>|>=|g' -e '/signing_key/d' -i console.gemspec
  sed -i \
    -e '/group :maintenance/,/end/d' \
    -e '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd console
  gem build console.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    console-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd console
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd console
  cp -a tmp_install/* "$pkgdir"/
}
