# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-console
pkgver=1.36.0
pkgrel=1
pkgdesc='Beautiful logging for Ruby'
arch=(any)
url='https://github.com/socketry/console'
license=(MIT)
depends=(
  ruby
  ruby-fiber-annotation
  ruby-fiber-local
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-sus
  ruby-sus-fixtures-console
)
options=(!emptydirs)
source=("git+${url}.git#tag=v$pkgver")
sha512sums=('402b64818a814cee7e6c494f878dc72797f689c2636ab915b942656f2483c8b9e5b7c1907235305bbc303d4958d018aa26b478d9753e9c60cf743c2bf146f850')
b2sums=('76749109d0da154f28f137bf8553bbc1a7019683abcdd64298bf2bdee02947be03d0c00161d9031f4ffb6099cb5e8e595ff247aa0281935ee7ecce41b0b04f54')

prepare() {
  cd console

  sed -r -e 's|~>|>=|g' -e '/signing_key/d' -i console.gemspec
  sed -i \
    -e '/group :maintenance/,/end/d' \
    -e '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd console
  gem build console.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    console-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd console
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd console
  cp -a tmp_install/* "$pkgdir"/
}
