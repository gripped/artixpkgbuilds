# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=ruby-unicode-display_width
_pkgname="${pkgname#ruby-}"
pkgver=3.1.2
pkgrel=2
pkgdesc='Determines the monospace display width of a string in Ruby'
arch=('any')
url='https://github.com/janlelis/unicode-display_width'
license=('MIT')
depends=('ruby' 'ruby-unicode-emoji')
makedepends=('git' 'ruby-rdoc')
checkdepends=('ruby-rake' 'ruby-rspec')
options=('!emptydirs')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('2bdd522acd82d78eea0f5f69f7e07602231b7902c6294cf75d9cb7a40e6d2358408c4d9c6a22523291e396aa42ba7c8e93b6db450a97e7e7fa4e985846c32f30')
b2sums=('0ca3fc354bc3d7a0cbb79f503c563e5328ef796d0451b24f7f0176727fdb1a7ed1b908a0d6083c8442121f6faaff9ec4492a8f3011456d83a17a680db71e024d')

build() {
  cd "$pkgname"

  gem build "$_pkgname.gemspec"
}

check() {
  cd "$pkgname"

  rake test
}

package() {
  cd "$pkgname"

  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$_pkgname-$pkgver.gem"

  # delete cache
  rm -vrf "$pkgdir/$_gemdir/cache"

  # license
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  ln -sf "$_gemdir/gems/$_pkgname-$pkgver/MIT-LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname"
}
