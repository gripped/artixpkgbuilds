# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=ruby-unicode-display_width
_pkgname="${pkgname#ruby-}"
pkgver=3.1.5
pkgrel=1
pkgdesc='Determines the monospace display width of a string in Ruby'
arch=('any')
url='https://github.com/janlelis/unicode-display_width'
license=('MIT')
depends=('ruby' 'ruby-unicode-emoji')
makedepends=('git' 'ruby-rdoc')
checkdepends=('ruby-rake' 'ruby-rspec')
options=('!emptydirs')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('2593d3203c6ca185d7c8ce6b1a249649ba86a147f1a0e752ddd84c0f008bd9f6f6c6910e410ea3321673f5db6b464c429b01ffed162b7ddce888328e39ece11d')
b2sums=('f1292c7390bdeee30dee1eaa1e496507050edd62d9adc7cd5f4a2c260c097d642cc133ad3d2016f0ef3c543c0565a126b883f6fb836329d74be93a2127172e05')

build() {
  cd "$pkgname"

  gem build "$_pkgname.gemspec"
}

check() {
  cd "$pkgname"

  rake test
}

package() {
  cd "$pkgname"

  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$_pkgname-$pkgver.gem"

  # delete cache
  rm -vrf "$pkgdir/$_gemdir/cache"

  # license
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  ln -sf "$_gemdir/gems/$_pkgname-$pkgver/MIT-LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname"
}
