# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=ruby-unicode-display_width
_pkgname="${pkgname#ruby-}"
pkgver=3.2.0
pkgrel=1
pkgdesc='Determines the monospace display width of a string in Ruby'
arch=(any)
url='https://github.com/janlelis/unicode-display_width'
license=(MIT)
depends=(ruby ruby-unicode-emoji)
makedepends=(git ruby-rdoc)
checkdepends=(ruby-rake ruby-rspec)
options=(!emptydirs)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('aa20615a92c90dd7d7f99655b052da4a7855a2bdeb9b20f4f62c73940081412c2399e16a7646906552eb7a17c8b6e6413051772fe268d2dc72d9b8c5701aff36')
b2sums=('32b3c989e6dc44ef88828e44701e2ab202066b857bf063396544ce24483deb6779c14a552685606829f544a8292424471bd4b715f8f62f937e65b81bdc8f8674')

build() {
  cd "$pkgname"

  gem build "$_pkgname.gemspec"
}

check() {
  cd "$pkgname"

  rake test
}

package() {
  cd "$pkgname"

  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$_pkgname-$pkgver.gem"

  # delete cache
  rm -vrf "$pkgdir/$_gemdir/cache"

  # license
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  ln -sf "$_gemdir/gems/$_pkgname-$pkgver/MIT-LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname"
}
