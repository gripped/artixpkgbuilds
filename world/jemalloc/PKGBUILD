# Maintainer:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Kovivchak Evgen <oneonfire@gmail.com>

pkgname=jemalloc
epoch=1
pkgver=5.4.0
pkgrel=1
pkgdesc='General-purpose scalable concurrent malloc implementation'
arch=('x86_64')
license=('BSD-2-Clause')
url='https://jemalloc.net/'
depends=('glibc' 'libgcc' 'libgcc_s.so' 'libstdc++' 'libstdc++.so')
optdepends=('perl: for jeprof')
makedepends=('git')
provides=('libjemalloc.so')
source=("git+https://github.com/jemalloc/jemalloc.git#tag=${pkgver}")
sha256sums=('1013d73abb1820cd7c7a24b2270414c09f663474bdeb1c86c36a200f4f950ad6')

prepare() {
  cd "${pkgname}"

  autoreconf -fi
}

build() {
  cd "${pkgname}"

  ./configure \
    --enable-prof \
    --enable-autogen \
    --disable-static \
    --prefix=/usr
  make
}

check() {
  make -C "${pkgname}" check
}

package() {
  cd "${pkgname}"

  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
