# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwalletmanager
pkgver=24.08.0
pkgrel=2
pkgdesc='Wallet management tool'
arch=(x86_64)
url='https://apps.kde.org/kwalletmanager5/'
license=(LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         karchive
         kauth
         kcmutils
         kcodecs
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         kdbusaddons
         ki18n
         kio
         kitemviews
         kservice
         kstatusnotifieritem
         kwallet
         kwidgetsaddons
         kwindowsystem
         kxmlgui
         qt6-base)
makedepends=(extra-cmake-modules
             kdoctools)
groups=(kde-applications
        kde-utilities)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/utilities/kwalletmanager/-/commit/9b25d58f.patch)
sha256sums=('08f900f9ef770b2bc9208781e5dcdb7e0c43f0c7a47a1515b1888f24756542f7'
            'SKIP'
            '461d8d224d0983e521c324e867894c663b0d9ee321eecf2c25304d1ec1770895')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < 9b25d58f.patch # Fix starting application
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
