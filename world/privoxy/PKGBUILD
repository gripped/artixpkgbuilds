# Maintainer: nikolar <nikolar@artixlinux.org>
# Maintainer: Nathan <ndowens@artixlinux.org>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@hoetzel.info>
# Contributor: basilburn (basilburn), Paul Bredbury (brebs)
# Contributor: lolilolicon

pkgname=privoxy
pkgver=4.0.0
pkgrel=1
pkgdesc='A web proxy with advanced filtering capabilities.'
arch=('x86_64')
url='https://www.privoxy.org'
license=('GPL2')
depends=('pcre2' 'zlib')
makedepends=('git')
backup=('etc/privoxy/'{config,trust,match-all.action,{default,user}.{action,filter}}
        'etc/logrotate.d/privoxy')
source=("git+https://www.privoxy.org/git/privoxy.git#tag=v_${pkgver//./_}"
        'privoxy.logrotate.d'
		'privoxy.sysusers')
sha256sums=('f2621a95a004f2869ed57053e11c0875ce930f152049fd0aed008a13c5b326fc'
            '769740ea3c15228f24b559192e7b3f45b95dcf9040e2b25f15bfdfae8af86ce3'
			'6681231552f431962dda2ac49187df833b2b57544eac97ca94d1f4207b27b04c')

prepare() {
  cd privoxy
  autoheader
  autoconf
}

build() {
  cd privoxy
  ./configure --prefix=/usr --sysconfdir=/etc/privoxy --enable-compression
  make
  sed -i '
    s+^confdir \.+confdir /etc/privoxy+
    s+^logdir \.+logdir /var/log/privoxy+
    s+^#\?user-manual .*+user-manual /usr/share/doc/privoxy/user-manual/+' config
}

package() {
  cd privoxy

  install -Dm644 "$srcdir/privoxy.sysusers" "$pkgdir/usr/lib/sysusers.d/privoxy.conf"
  install -Dm644 "$srcdir/privoxy.logrotate.d" "$pkgdir/etc/logrotate.d/privoxy"

  install -Dm755 {,"$pkgdir"/usr/bin/}privoxy
  install -Dm644 {,"$pkgdir"/usr/share/man/man8/}privoxy.8
  install -d -o42 -g42 "$pkgdir"/var/log/privoxy

  install -d "$pkgdir"/etc/privoxy/
  install -m644 config trust *.{action,filter} "$pkgdir"/etc/privoxy/ # -m0660 upstream
  find templates -type f -exec install -Dm644 '{}' "$pkgdir"/etc/privoxy/'{}' \;

  (d="$pkgdir"/usr/share/doc/privoxy
  cd doc/webserver
  install -Dm644 {privoxy-,"$d"/}index.html
  install -m644 p_doc.css ../../{AUTHORS,README,ChangeLog} "$d"/
  install -Dm644 {,"$d"/user-manual/}p_doc.css
  find user-manual developer-manual faq man-page \( -name '*.html' -o -name '*.jpg' \) \
    -exec install -Dm644 '{}' "$d"/'{}' \;)
}
