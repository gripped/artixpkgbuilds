# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
# Contributor: Butui Hu <hot123tea123@gmail.com>
# Contributor: Aaron Liu <aaronliu0130 at gmail dot com

pkgname=python-pyvips
_pkgname=pyvips
pkgver=3.1.1
pkgrel=1
pkgdesc='Python binding for libvips using cffi'
arch=('x86_64')
url='https://github.com/libvips/pyvips'
license=(MIT)
depends=(
  glib2
  glibc
  libvips
  python-cffi
  python-numpy
  python-pkgconfig
)
makedepends=(
  imagemagick
  libheif
  libjxl
  openslide
  poppler-glib
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/libvips/pyvips/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('1ab3c901fb9ae2221eef4b2258cddbaf52af30fc6d6023d610ebbe7cd94d3aff')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 "LICENSE.txt" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
