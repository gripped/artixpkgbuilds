# Maintainer: Christian Hesse <mail@eworm.de>
# Maintainer: Christian Heusel <gromit@archlinux.org>
# Contributor: Daniel Isenmann <daniel@archlinux.org>

pkgname=babl
pkgver=0.1.118
pkgrel=1
pkgdesc='Dynamic, any to any, pixel format conversion library'
arch=('x86_64')
url='https://gegl.org/babl/'
license=('LGPL-3.0-or-later')
depends=('glibc' 'lcms2')
makedepends=(
    'git'
    'meson'
    'gobject-introspection'
    'vala'
    'gi-docgen'
)
source=("git+https://gitlab.gnome.org/GNOME/babl.git#tag=BABL_${pkgver//./_}")
sha256sums=('b74368ff8e9d5a5358f440f7fcbb8a8c8a600d631d59ad499edc015d279d7db9')

build() {
  artix-meson "${pkgname}" build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}
