# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Eduardo Romero <eduardo@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgbase=libgphoto2
pkgname=(
  libgphoto2
  libgphoto2-docs
)
pkgver=2.5.32
pkgrel=4
pkgdesc="Digital camera access library"
url="http://www.gphoto.org/"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  bash
  curl
  gd
  glibc
  libexif
  libjpeg-turbo
  libltdl
  libusb
  libxml2
)
makedepends=(
  doxygen
  git
  graphviz
  meson
)
source=(
  "git+https://github.com/gphoto/libgphoto2#tag=v$pkgver"
  0001-meson-check-more-functions-that-weren-t-checked.patch
)
b2sums=('de9380961e1731b7510d5555b0f8ec4a691461aa269bcde98e457cf06867f3cd841244f9f5a267b8998e9ab983ac398a49241d7fbc1c6dd7795b87c899926b04'
        '560929852da8449f48eb9b42db3d98e7b126da32718223de694b93775a2351a8bfacfa9a3b9885bf6c211734efb30d6fd0733ce773e477f830498894144f764e')
validpgpkeys=(
  7C4AFD61D8AAE7570796A5172209D6902F969C95 # Marcus Meissner <marcus@jet.franken.de>
)

prepare() {
  cd $pkgbase

  # Crash fix
  # https://github.com/gphoto/libgphoto2/issues/1136
  git cherry-pick -n 19b9d65289938caba06ef2848ca3917aa5e832b3

  # Meson fixes
  # https://github.com/gphoto/libgphoto2/pull/1134
  git cherry-pick -n 53a0d5e8ed57ac2582c54a5fc0c425455e9eafc1 \
                     fd294cdf2d905472519e937b3383cd6928230322
  # https://github.com/gphoto/libgphoto2/pull/1139
  git cherry-pick -n 2eaa91be3c24835667e99cc5214ecf8429b5aecf
  # https://github.com/gphoto/libgphoto2/pull/1163
  git apply -3 ../0001-meson-check-more-functions-that-weren-t-checked.patch
  sed -i 's/2\.5\.32\.1/2.5.32/' meson.build
}

build() {
  local meson_options=(
    -D docs=true
  )

  artix-meson $pkgbase build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-suite no-ci
}

package_libgphoto2() {
  provides=(libgphoto2{,_port}.so)

  meson install -C build --destdir "$pkgdir"

  local camlibdir="$pkgdir/usr/lib/libgphoto2/$pkgver"
  test -d "$camlibdir"

  # Remove unused udev helper
  rm -v "$pkgdir/usr/lib/udev/check-ptp-camera"

  (
    export LD_LIBRARY_PATH="$pkgdir/usr/lib"
    export CAMLIBS="$camlibdir"

    build/packaging/generic/print-camera-list hwdb \
      | install -Dm644 /dev/stdin "$pkgdir/usr/lib/udev/hwdb.d/20-gphoto.hwdb"
    build/packaging/generic/print-camera-list udev-rules version 201 \
      | install -Dm644 /dev/stdin "$pkgdir/usr/lib/udev/rules.d/40-gphoto.rules"
  )

  # Split docs
  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_libgphoto2-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
