# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Torsten Ke√üler <tpkessler at archlinux dot org>
# Contributor: Jingbei Li <i@jingbei.li>

pkgname=intel-oneapi-dev-utilities
_pkgver=2025.0
_debpkgrel=599
pkgver=2025.0.0
pkgrel=1
pkgdesc="Intel oneAPI Dev Utilities"
arch=('any')
url='https://software.intel.com/content/www/us/en/develop/tools/oneapi.html'
license=("LicenseRef-Intel-EULA-Developer-Tools")
source=("https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-${pkgver}-${_debpkgrel}_amd64.deb"
        "https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-eclipse-cfg-${_pkgver}-${pkgver}-${_debpkgrel}_all.deb")
b2sums=('6e61a2432c5eca830e5bcee8c835aeb1e9ccf366864eddd42637a94996509940d8482f8996c5e7a7d392442a409cdc7c2837e413b1447a707161c3d3888a7e49'
        '121d10c1ec4882837957c7bf93f0e4c239282b81a5c4696c3c2e4c1fee463f1bec1bf72bba556d98eea12e9a8f959099e53768407436172aa5fbbbda4050f24f')
depends=('intel-oneapi-common')
provides=("${pkgname}-eclipse-cfg")
conflicts=('intel-oneapi-basekit')

noextract=(
	"${pkgname}-${pkgver}-${_debpkgrel}_amd64.deb"
	"${pkgname}-eclipse-cfg-${_pkgver}-${pkgver}-${_debpkgrel}_all.deb"
)

package() {
  cd "${srcdir}"
  ar x ${pkgname}-${pkgver}-${_debpkgrel}_amd64.deb
  tar xvf data.tar.xz -C "${pkgdir}"
  rm data.tar.xz

  ar x ${pkgname}-eclipse-cfg-${_pkgver}-${pkgver}-${_debpkgrel}_all.deb
  tar xvf data.tar.xz -C "${pkgdir}"
  rm data.tar.xz

  local _dev_path='/opt/intel/oneapi/dev-utilities'
  ln -s "${_dev_path}/${_pkgver}" "${pkgdir}/${_dev_path}/latest"

  install -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  ln -s /usr/share/licenses/intel-oneapi "${pkgdir}"/usr/share/licenses/"${pkgname}"/oneapi
}
