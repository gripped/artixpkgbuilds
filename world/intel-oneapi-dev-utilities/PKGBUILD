# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Torsten Keßler <tpkessler at archlinux dot org>
# Contributor: Jingbei Li <i@jingbei.li>

pkgname=intel-oneapi-dev-utilities
pkgver=2026.0.0_230
_pkgver=$(echo $pkgver | sed 's/_/-/')
_majmin=$(echo $pkgver | cut -d. -f1,2)
pkgrel=1
pkgdesc="Intel oneAPI Dev Utilities"
arch=('any')
url='https://software.intel.com/content/www/us/en/develop/tools/oneapi.html'
license=("LicenseRef-Intel-EULA-Developer-Tools")
source=("https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-${_pkgver}_amd64.deb"
        "https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-eclipse-cfg-${_majmin}-${_pkgver}_all.deb")
b2sums=('ab8d4f5fabb834dd84679111291352c483e7d9c7425241e45be390e09ebc8dbcacabb0929bcfd2f488bc16ca39e2f81ab7a1dad401460ebb73430c6e7928c959'
        '4bb7afb5c330bfbb0da97d5e559e0bfac2edb09ead69fb9054b6e938a9423e9e5165a085640ef1ba9d840d56d12432127fcfc59489049e5e9b77dae20405184f')
depends=('intel-oneapi-common')
provides=("${pkgname}-eclipse-cfg")
conflicts=('intel-oneapi-basekit')

noextract=(
	"${pkgname}-${_pkgver}_amd64.deb"
	"${pkgname}-eclipse-cfg-${_majmin}-${_pkgver}_all.deb"
)

package() {
  cd "${srcdir}"
  ar x ${pkgname}-${_pkgver}_amd64.deb
  tar xvf data.tar.xz -C "${pkgdir}"
  rm data.tar.xz

  ar x ${pkgname}-eclipse-cfg-${_majmin}-${_pkgver}_all.deb
  tar xvf data.tar.xz -C "${pkgdir}"
  rm data.tar.xz

  local _dev_path='/opt/intel/oneapi/dev-utilities'
  ln -s "${_dev_path}/${_majmin}" "${pkgdir}/${_dev_path}/latest"

  install -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  ln -s /usr/share/licenses/intel-oneapi "${pkgdir}"/usr/share/licenses/"${pkgname}"/oneapi
}
