# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Michel Brabants <michel.linux@tiscali.be>

pkgname=seahorse
pkgver=47.0.1
pkgrel=1
epoch=1
pkgdesc="GNOME application for managing PGP keys."
url="https://wiki.gnome.org/Apps/Seahorse"
arch=(x86_64)
license=('GPL-2.0-or-later AND LGPL-2.1-or-later AND CC-BY-SA-3.0')
depends=(
  dconf
  gcc-libs
  gcr
  gdk-pixbuf2
  glib2
  glibc
  gpgme
  gtk3
  hicolor-icon-theme
  libhandy
  libldap
  libpwquality
  libsecret
  libsoup3
  org.freedesktop.secrets
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  meson
  vala
  yelp-tools
)
provides=(x11-ssh-askpass)
groups=(gnome-extra)
source=(
  "git+https://gitlab.gnome.org/GNOME/seahorse.git?signed#tag=${pkgver/[a-z]/.&}"
)
b2sums=('e8d8fae487e872c180ee1e8743a95a58445f89b41471067c065745c052703f57b003b49364f350a4fe76fdcdf0ce38b7c8b16baab4a86120dcc90906b5aab390')
validpgpkeys=('A7C626E13F9AD776776BD9CA1D8A57CF2E8D36A3') # Niels De Graef (nielsdg) <nielsdegraef@gmail.com>

prepare() {
  cd seahorse
}

build() {
  artix-meson seahorse build -D manpage=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
