# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Michel Brabants <michel.linux@tiscali.be>

pkgname=seahorse
pkgver=47.0.1
pkgrel=6
epoch=1
pkgdesc="GNOME application for managing PGP keys"
url="https://wiki.gnome.org/Apps/Seahorse"
arch=(x86_64)
license=('GPL-2.0-or-later AND LGPL-2.1-or-later AND CC-BY-SA-3.0')
depends=(
  dconf
  gcc-libs
  gcr
  gdk-pixbuf2
  glib2
  glibc
  gpgme
  gtk3
  hicolor-icon-theme
  libhandy
  libldap
  libpwquality
  libsecret
  libsoup3
  org.freedesktop.secrets
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  meson
  vala
  yelp-tools
)
provides=(x11-ssh-askpass)
source=(
  "git+https://gitlab.gnome.org/GNOME/seahorse.git?signed#tag=${pkgver/[a-z]/.&}"
  seahorse-fix-search-provider.patch
  gpgme-2.0.patch
)
b2sums=('e8d8fae487e872c180ee1e8743a95a58445f89b41471067c065745c052703f57b003b49364f350a4fe76fdcdf0ce38b7c8b16baab4a86120dcc90906b5aab390'
        'c5df3ec69f030d422a4688d9f167b3b7ab8001f65912a815b5ec9999a2d7e109ce995da0844c6b7228d772e758c1e79405fe7b90ad61a02e2532f49bf037ab17'
        '38a4de15a485d12328bcc8645f6c08f3080664e1b699e2acaf21bf06af85af91a734454f542b00b06f6391112d30d1c9eb79544cbc1cf7ece33dd10fd0a83654')
validpgpkeys=('A7C626E13F9AD776776BD9CA1D8A57CF2E8D36A3') # Niels De Graef (nielsdg) <nielsdegraef@gmail.com>

prepare() {
  cd seahorse

  # Fix symbolic icon name
  # https://gitlab.gnome.org/GNOME/seahorse/-/merge_requests/230
  git cherry-pick -n c6fcd381425b638c16eeab7e5858bddf7de76b9f

  # Fix search provider
  # https://gitlab.gnome.org/GNOME/seahorse/-/merge_requests/250
  git apply -3 ../seahorse-fix-search-provider.patch

  # Fix build with gpgme 2.0
  patch -p1 -i ../gpgme-2.0.patch
}

build() {
  artix-meson seahorse build -D manpage=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
