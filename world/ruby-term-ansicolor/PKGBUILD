# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Bert Peters <bertptrs@archlinux.org>
# Contributor: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Hyacinthe Cartiaux <hyacinthe.cartiaux@free.fr>
# Contributor: Hilton Medeiros <medeiros.hilton@gmail.com>

pkgname=ruby-term-ansicolor
pkgver=1.11.2
pkgrel=3
pkgdesc='A ruby library that colors strings using ANSI escape sequences'
arch=('any')
url='https://rubygems.org/gems/term-ansicolor'
license=('GPL2')
depends=('ruby-tins')
options=('!emptydirs')
source=("https://rubygems.org/downloads/${pkgname#*-}-${pkgver}.gem")
noextract=("${pkgname#*-}-${pkgver}.gem")
sha256sums=('4b00c63f21d496055045240907da30de077e8c6502b2ba7e8638509d689e5083')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install --ignore-dependencies --no-user-install -N -i "${pkgdir}"/${_gemdir} -n "${pkgdir}"/usr/bin ${pkgname#*-}-${pkgver}.gem
  find "${pkgdir}" -type f -name *.gem -delete

  install -dm 755 "${pkgdir}"/usr/share/licenses/${pkgname}
  ln -s ${_gemdir}/gems/${pkgname#*-}-${pkgver}/COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/
}

# vim: ts=2 sw=2 et:
