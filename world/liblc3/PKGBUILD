# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: OSAMC <https://github.com/osam-cologne/archlinux-proaudio>
# Contributor: Florian HÃ¼lsmann <fh@cbix.de>

pkgname=liblc3
pkgver=1.1.2
pkgrel=1
pkgdesc="Low Complexity Communication Codec library and tools"
url="https://github.com/google/liblc3"
arch=(x86_64)
license=(Apache-2.0)
depends=(glibc)
makedepends=(
  git
  meson
  python
)
optdepends=('python: Python bindings')
provides=(liblc3.so)
source=("git+$url#tag=v$pkgver")
b2sums=('4cab5ee8e0b01b7ad05ee0f933c48bb1de8619644405a03e77d243b5fc5a9f81d4df1da4a3c189a37d17afcc066c14c85016fa2d358055558c1f59d5058268d1')

prepare() {
  cd liblc3
}

build() {
  local meson_options=(
    -D python=true
    -D tools=true
  )

  artix-meson liblc3 build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/doc/$pkgname" -m644 liblc3/README.md
}

# vim:set sw=2 sts=-1 et:
