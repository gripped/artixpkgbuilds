# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-tour
pkgver=47.0
pkgrel=2
pkgdesc="Guided tour and greeter for GNOME"
url="https://apps.gnome.org/Tour/"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  appstream
  git
  meson
  rust
)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/gnome-tour.git#tag=${pkgver/[a-z]/.&}")
b2sums=('7553c065d4a74e2587be2d6e11256f8dd779c11e6bb663570b98b551fd013da99ad23e767e2373189139d4a348177fcd07c1c4836a654ed0ea1e75b378741350')

# Use LTO
export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

# Use debug
export CARGO_PROFILE_RELEASE_DEBUG=2

prepare() {
  cd $pkgname

  # Match CARGO_HOME in src/meson.build
  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

  # Fix crash on second activation
  # https://gitlab.gnome.org/GNOME/gnome-tour/-/merge_requests/79
  git cherry-pick -n 6e4e8a7d90c76cd1aaa8efa8eb676f68e53c84b9
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}

# vim:set sw=2 sts=-1 et:
