# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-pry-byebug
pkgver=3.11.0
pkgrel=2
pkgdesc="Combine 'pry' with 'byebug'. Adds 'step', 'next', 'finish', 'continue' and 'break' commands to control execution."
arch=(any)
url='https://github.com/deivid-rodriguez/pry-byebug'
license=(MIT)
depends=(
  ruby
  ruby-byebug
  ruby-pry
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  markdownlint
  ruby-bundler
  ruby-irb
  ruby-minitest
  ruby-rake
)
options=(!emptydirs)
source=(
  "${url}/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
  "${pkgname}_fix_tests.patch"
)
sha256sums=('2389d5c95eddd687f743ce791a1f9f04db8bc19564c44e9835a81132b7d057d2'
            '5dabd6fad133ba87e778f5a51a4e9122be83c8ab0959058191f3c964d04669fe')

prepare() {
  cd pry-byebug-$pkgver

  patch --verbose --strip=1 --input="../${pkgname}_fix_tests.patch"

  sed -e 's|~>|>=|' -e '/chandler/d;/[rR]ubo[cC]op/d;/minitest-bisect/d' -i Gemfile Rakefile
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd pry-byebug-$pkgver
  gem build pry-byebug.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    pry-byebug-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd pry-byebug-$pkgver
  MT_COMPAT="true" GEM_HOME="tmp_install/$_gemdir" rake test
}

package() {
  cd pry-byebug-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
