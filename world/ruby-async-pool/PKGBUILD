# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-async-pool
pkgver=0.8.1
pkgrel=1
pkgdesc='A singleplex and multiplex resource pool for implementing robust clients'
arch=(any)
url='https://github.com/socketry/async-pool'
license=(MIT)
depends=(
  ruby
  ruby-async
  ruby-metrics
  ruby-traces
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-decode
  ruby-io-event
  ruby-sus
  ruby-sus-fixtures-async
)
options=(!emptydirs)
source=(https://github.com/socketry/async-pool/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('0364c3de1b34b87bdac759e7852ae23d94a12ddcbcb2262685c6810b63b1940f')

prepare() {
  cd async-pool-$pkgver

  sed -r -e 's|~>|>=|g' -e '/signing_key/d' -i async-pool.gemspec gems.rb

  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd async-pool-$pkgver
  gem build async-pool.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    async-pool-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd async-pool-$pkgver
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd async-pool-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 license.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
