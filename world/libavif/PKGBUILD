# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=libavif
pkgver=1.0.0
pkgrel=1
pkgdesc="Library for encoding and decoding .avif files"
arch=(x86_64)
url="https://github.com/AOMediaCodec/libavif"
license=(BSD)
depends=(glibc aom dav1d rav1e svt-av1 zlib libpng libjpeg libyuv)
makedepends=(cmake nasm pkgconf gdk-pixbuf2)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/AOMediaCodec/libavif/commit/26224fe4.patch)
sha256sums=('410f85cf0d13f403b41197c0774da469f5d73b89aa06d40fc726165377f215a0'
            'e94f3473b7b18a9192c8a0edc32757657e0583a5044a3bca6534d9ec2ace88e1')

prepare() {
  patch -d ${pkgname}-${pkgver} -p1 < 26224fe4.patch # Fix build
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DAVIF_BUILD_APPS=ON \
    -DAVIF_CODEC_AOM=ON \
    -DAVIF_CODEC_DAV1D=ON \
    -DAVIF_CODEC_RAV1E=ON \
    -DAVIF_CODEC_SVT=ON \
    -DAVIF_BUILD_GDK_PIXBUF=ON
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${pkgname}-${pkgver}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
