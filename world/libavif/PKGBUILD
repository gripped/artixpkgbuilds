# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libavif
pkgver=1.2.1
pkgrel=1
pkgdesc="Library for encoding and decoding .avif files"
arch=(x86_64)
url="https://github.com/AOMediaCodec/libavif"
license=(LicenseRef-libavif)
depends=(aom
         dav1d
         gcc-libs
         glibc
         libjpeg-turbo
         libpng
         libwebp
         libyuv
         rav1e
         svt-av1)
makedepends=(cmake
             gdk-pixbuf2
             git
             nasm
             pandoc-cli)
source=(git+https://github.com/AOMediaCodec/libavif#tag=v$pkgver)
sha256sums=('f51d3c255d40dae0a44ec44f711fbdeed79b2bc0fb5b0b241fc64abbed202a7c')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DAVIF_BUILD_APPS=ON \
    -DAVIF_BUILD_MAN_PAGES=ON \
    -DAVIF_CODEC_AOM=SYSTEM \
    -DAVIF_CODEC_DAV1D=SYSTEM \
    -DAVIF_CODEC_RAV1E=SYSTEM \
    -DAVIF_CODEC_SVT=SYSTEM \
    -DAVIF_LIBSHARPYUV=SYSTEM \
    -DAVIF_BUILD_GDK_PIXBUF=ON
  make -C build
}

package() {
  make -C build DESTDIR="$pkgdir" install
  install -Dm644 $pkgname/LICENSE -t "${pkgdir}"/usr/share/licenses/$pkgname
}
