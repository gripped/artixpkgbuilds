# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: ObserverOfTime <chronobserver@disroot.org>

pkgname=tree-sitter-python
pkgver=0.25.0
pkgrel=1
pkgdesc='Python grammar for tree-sitter'
arch=(x86_64)
url=https://github.com/tree-sitter/tree-sitter-python
license=(MIT)
groups=(tree-sitter-grammars)
makedepends=(
  git
  openssh
  tree-sitter-cli
)
optdepends=('tree-sitter: core library')
provides=("lib$pkgname.so")
source=(
  "git+$url.git#tag=v$pkgver"
  ssh_allowed_signers
)
b2sums=('6377c9a85a31e7d0cf179e0327841a2178acb915481c8180694c97ee545a52d233493cc30d8105d3a20568d84d9bead40358d448dc88562dc573b1b2cf9df9ff'
        '6bca8254d6dcec4a7b137da4b50ff5be6fb03175586af6f0e40b3cd384a5ecdeeda26565dd47742b29c363e344300d55cb67a4a7570cdd40fafa52a05e258afe')

prepare() {
  # XXX: move to verify() when devtools supports it
  # https://gitlab.archlinux.org/archlinux/devtools/-/issues/224
  git -C $pkgname -c gpg.ssh.allowedSignersFile="$PWD"/ssh_allowed_signers verify-tag v$pkgver

  cd $pkgname
  tree-sitter generate src/grammar.json
}

build() {
  cd $pkgname
  make PREFIX=/usr PARSER_URL=$url
}

package() {
  install -d "$pkgdir"/usr/lib/tree_sitter
  ln -s /usr/lib/lib$pkgname.so \
    "$pkgdir"/usr/lib/tree_sitter/${pkgname#tree-sitter-}.so

  cd $pkgname
  make DESTDIR="$pkgdir" PREFIX=/usr install
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
