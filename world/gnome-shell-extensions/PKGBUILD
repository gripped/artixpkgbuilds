# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=gnome-shell-extensions
pkgver=49.0
pkgrel=1
pkgdesc="Extensions for GNOME shell, including classic mode"
url="https://gitlab.gnome.org/GNOME/gnome-shell-extensions"
arch=(any)
license=(GPL-2.0-or-later)
depends=(
  dconf
  gnome-shell
)
makedepends=(
  git
  meson
)
optdepends=(
  'gnome-menus: Apps Menu extension'
  'libgtop: System Monitor extension'
)
source=(
  # GSE tags use SSH signatures which makepkg doesn't understand
  "git+https://gitlab.gnome.org/GNOME/gnome-shell-extensions.git#tag=${pkgver/[a-z]/.&}"
)
b2sums=('a2cbfa40370e5e661233bdf5b5940ab277cf8a4250cb3e8a541f80e250ee8b0df8cc7a1995010a1038bb12da119b6025d7fae31976d36dc6c9d3dd2d003c8342')

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D extension_set=all
    -D classic_mode=true
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
