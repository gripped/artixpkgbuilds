# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=gnome-shell-extensions
pkgver=47.4
pkgrel=1
pkgdesc="Extensions for GNOME shell, including classic mode"
url="https://gitlab.gnome.org/GNOME/gnome-shell-extensions"
arch=(any)
license=(GPL-2.0-or-later)
depends=(
  dconf
  gnome-shell
)
makedepends=(
  git
  meson
)
optdepends=(
  'gnome-menus: Apps Menu extension'
  'libgtop: System Monitor extension'
)
groups=(gnome)
source=(
  # GSE tags use SSH signatures which makepkg doesn't understand
  "git+https://gitlab.gnome.org/GNOME/gnome-shell-extensions.git#tag=${pkgver/[a-z]/.&}"
)
b2sums=('258b7da32bb0294e552a8f7e900221535101a1bcf316c843e4c7a494c5936665c3c4e34d12aed0166f2497b53311093f810d8517249b4842c7a0362265b511b6')

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D extension_set=all
    -D classic_mode=true
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
