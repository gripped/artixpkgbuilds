# Maintainer: Fabian Bornschein <fabiscafe-at-mailbox-dot-org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-shell-extensions
pkgver=45.1
pkgrel=2
pkgdesc="Extensions for GNOME shell, including classic mode"
url="https://wiki.gnome.org/Projects/GnomeShell/Extensions"
arch=(any)
license=(GPL)
depends=(gnome-shell)
makedepends=(
  git
  meson
)
optdepends=('gnome-menus: applications menu extension')
groups=(gnome)
_commit=f27b239f5566ad587e09e8607d8efc0ac5ba532b  # tags/45.1^0
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-shell-extensions.git#commit=$_commit"
)
b2sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname

  # Revert https://gitlab.gnome.org/GNOME/gnome-shell-extensions/-/merge_requests/280
  # for potentially causing https://bugs.archlinux.org/task/80132
  git revert -n f576f1b1b67cf78be8d63353ee795f68cfa67446 \
                b9d2a445b2b9ff3dea771acd557b7f7c55eced7c
}

build() {
  local meson_options=(
    -D extension_set=all
    -D classic_mode=true
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
