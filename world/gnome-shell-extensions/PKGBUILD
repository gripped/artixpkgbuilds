# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=gnome-shell-extensions
pkgver=48.3
pkgrel=1
pkgdesc="Extensions for GNOME shell, including classic mode"
url="https://gitlab.gnome.org/GNOME/gnome-shell-extensions"
arch=(any)
license=(GPL-2.0-or-later)
depends=(
  dconf
  gnome-shell
)
makedepends=(
  git
  meson
)
optdepends=(
  'gnome-menus: Apps Menu extension'
  'libgtop: System Monitor extension'
)
groups=(gnome)
source=(
  # GSE tags use SSH signatures which makepkg doesn't understand
  "git+https://gitlab.gnome.org/GNOME/gnome-shell-extensions.git#tag=${pkgver/[a-z]/.&}"
)
b2sums=('25ef95ddc90b536b51f3185cde6b10ed8b61af52c272eec81be2d1a5d0036323291e5eb6b18d900744300d9f8c8369d4f96cb8c4f2de854fb8b0c9f3fdf7ed37')

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D extension_set=all
    -D classic_mode=true
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
