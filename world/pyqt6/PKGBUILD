# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=pyqt6
pkgname=python-pyqt6
pkgdesc='A set of Python bindings for the Qt6 toolkit'
pkgver=6.8.0
pkgrel=3
arch=(x86_64)
url='https://riverbankcomputing.com/software/pyqt/intro'
license=(GPL)
groups=(pyqt6)
depends=(python-pyqt6-sip
         qt6-base)
optdepends=('qt6-tools: QtHelp, QtDesigner bindings'
            'qt6-svg: QtSvg bindings'
            'qt6-declarative: QtQml bindings, qmlplugin'
            'qt6-quick3d: QtQuick3D bindings'
            'qt6-connectivity: QtBluetooth, QtNfc bindings'
            'qt6-multimedia: QtMultimedia, QtSpatialAudio bindings'
            'qt6-positioning: QtPositioning bindings'
            'qt6-remoteobjects: QtRemoteObjects bindings'
            'qt6-sensors: QtSensors bindings'
            'qt6-serialport: QtSerialPort bindings'
            'qt6-speech: QtTextToSpeech bindings'
            'qt6-webchannel: QtWebChannel bindings'
            'qt6-webengine: QtPdf bindings'
            'qt6-websockets: QtWebSockets bindings'
            'dbus-python: for python-dbus mainloop support')
makedepends=(dbus-python
             pyqt-builder
             python-opengl
             qt6-connectivity
             qt6-declarative
             qt6-multimedia
             qt6-positioning
             qt6-quick3d
             qt6-remoteobjects
             qt6-sensors
             qt6-serialport
             qt6-shadertools
             qt6-speech
             qt6-svg
             qt6-tools
             qt6-webchannel
             qt6-webengine
             qt6-websockets
             sip)
provides=(qt6-python-bindings)
source=(https://pypi.python.org/packages/source/P/PyQt6/PyQt6-$pkgver.tar.gz
        qt-6.8.2.patch)
sha256sums=('6d8628de4c2a050f0b74462e4c9cb97f839bf6ffabbca91711722ffb281570d9'
            'c7dfae13566128bdf9c0503960d754109b35ca93d3f6af1aa56a554b5dc99a63')

prepare() {
  patch -d PyQt6-$pkgver -p1 < qt-6.8.2.patch
}

build() {
  cd PyQt6-$pkgver
  sip-build --scripts-dir=/usr/bin \
    --confirm-license \
    --no-make \
    --qmake=/usr/bin/qmake6 \
    --api-dir /usr/share/qt6/qsci/api/python \
    --pep484-pyi
  cd build
  make
}

package_python-pyqt6(){
  cd PyQt6-$pkgver/build
  make INSTALL_ROOT="$pkgdir" install

  # compile Python bytecode
  python -m compileall -d / "$pkgdir"/usr/lib
  python -O -m compileall -d / "$pkgdir"/usr/lib
}
