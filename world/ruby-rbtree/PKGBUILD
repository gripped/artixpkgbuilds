# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=ruby-rbtree
_pkgname="${pkgname#ruby-}"
pkgver=0.4.6
pkgrel=7
pkgdesc='A sorted associative collection that is implemented with a Red-Black Tree'
arch=('x86_64')
url='https://github.com/mame/rbtree'
license=('MIT')
depends=('ruby')
makedepends=('git')
options=('!emptydirs')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('3c99b3e3b111f437308ae65edfbd6c156e16ad989f380317093d56852a9c1daa31c870ad9d3645ec350e1330895b0b7d61e74fe3c48edf1bbb649baf741ebffa')
b2sums=('0c7b9f1efeffe89c1a395599b4e33dc12f57d9e38d50b689c257bbe02a737b591a916cfd6cba57c5a20e2aaec419edf478704a3207b5eb0b84578ec1bc0cf418')

build() {
  cd "$pkgname"

  gem build "$_pkgname.gemspec"
}

package() {
  cd "$pkgname"

  local _gemdir="$(gem env gemdir)"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$_pkgname-$pkgver.gem"

  # delete unnecessary files/folders
  rm -rf "$pkgdir/$_gemdir/cache"
  find "$pkgdir" \
    -type f \
    -name '*.o' -delete -o \
    -name 'gem_make.out' -delete -o \
    -name 'mkmf.log' -delete
  rm -rf "$pkgdir/$_gemdir/gems/$_pkgname-$pkgver/$_pkgname.so"

  # license
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  ln -sf "$_gemdir/gems/$_pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
