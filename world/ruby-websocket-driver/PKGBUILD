# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='websocket-driver'
pkgname="ruby-${_gemname}"
pkgver=0.7.6
pkgrel=4
pkgdesc='WebSocket protocol handler with pluggable I/O'
arch=('x86_64')
url='https://github.com/faye/websocket-driver-ruby'
license=('Apache')
options=(!emptydirs)
depends=(
  ruby
  ruby-base64
  ruby-websocket-extensions
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bundler
  ruby-eventmachine
  ruby-permessage_deflate
  ruby-rake
  ruby-rake-compiler
  ruby-rspec
)
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('5444a0748821ab651e9e5ed40fd737c9af50eca1f8046b3110ab436bb8c0c56250ee8a8e762ae1668249d9ebfa2712b52802950529a949aba2574ffc66c1c58a')
b2sums=('e8da76846b519603f7eef9cef53afdff3d48176cba21de1ce6b17e3718dd33b8309aaca721d4d11480e34e04b1ed2bfbb8892af507fbc72c1ff846c800f46499')

prepare() {
  cd "${_gemname}-ruby-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"
}

build() {
  cd "${_gemname}-ruby-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unreproducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${_gemname}-ruby-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rspec
}

package() {
  cd "${_gemname}-ruby-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
