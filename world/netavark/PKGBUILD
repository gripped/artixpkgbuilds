# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Morten Linderud <foxboron@archlinux.org>

pkgname=netavark
pkgver=2.1.0
pkgrel=3
pkgdesc="Container network stack"
arch=(x86_64)
url="https://github.com/containers/netavark"
license=(Apache-2.0)
depends=(
  aardvark-dns
  glibc
  libgcc
  nftables
)
makedepends=(
  cargo
  git
  go-md2man
  protobuf
)
provides=(container-network-stack=2)
source=(git+$url#tag=v$pkgver)
sha512sums=('938d3537705ff8d805bcbbc1528f2d1f1bad1b60400b09e6b09244296c4a5ddaa6c8f92fdd0cec4a5fab64ebc017c600ff250b0058d935ad6e1380f254d546e2')
b2sums=('8b7df6e838b5c4661f796a9082432f4b1613684f83113dd6c387d186f21d0238cf65b0b34f208bd37ec97f3bd6c7dc7c7f8f889851022f01b2623d41ed210856')

prepare() {
  cd $pkgname
  cargo fetch --locked --target host-tuple
}

build() {
  cd $pkgname
  make -C docs
  cargo build --frozen --release
}

check() {
  cd $pkgname
  cargo test --frozen
}

package() {
  cd $pkgname
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/lib/podman/"
  install -vDm 644 docs/*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 docs/*.7 -t "$pkgdir/usr/share/man/man7/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
