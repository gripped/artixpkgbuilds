# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=netavark
pkgver=1.17.1
pkgrel=1
pkgdesc="Container network stack"
arch=(x86_64)
url="https://github.com/containers/netavark"
license=(Apache-2.0)
depends=(
  aardvark-dns
  gcc-libs
  glibc
)
makedepends=(
  cargo
  git
  go-md2man
  protobuf
)
provides=(container-network-stack=2)
source=(git+$url#tag=v$pkgver)
sha512sums=('1afbb2a336318eb27a2d64b4d147474062d7ae68dc949582349752ff780294b988ae6518d752ef02f06cb21d0befa70665a63c738e71da8cdba0fb4da6e919b8')
b2sums=('e239b18efb5006130a575d16f6226f8cdbebd46825835e9166cda52e004650de20992386c377b32d6525b5af2545531e84c7e3907b65395d2278bdfad9166f8f')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  make -C docs
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname
  cargo test --frozen --all-features
}

package() {
  cd $pkgname
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/lib/podman/"
  install -vDm 644 docs/*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 docs/*.7 -t "$pkgdir/usr/share/man/man7/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
