# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=netavark
pkgver=1.16.0
pkgrel=2
pkgdesc="Container network stack"
arch=(x86_64)
url="https://github.com/containers/netavark"
license=(Apache-2.0)
depends=(
  aardvark-dns
  gcc-libs
  glibc
)
makedepends=(
  cargo
  git
  mandown
  protobuf
)
provides=(container-network-stack=2)
source=(git+$url#tag=v$pkgver)
sha512sums=('5dff9539ffe1c7966c80c3b0e996b736c038e5d49f74e7819b54a6e037085f78d1e2d3f70d8aa8f9aec3fda29a874e0fb9ad4e16ac6cee8a17dee0460ca0f3b5')
b2sums=('437a3c9ac7c9be711966853406c711859f4b06f3896619a98f4c113c9a83f62b96b505dd2ceec33454691883e94f4f04f458af0e88c83ef7b3e41e112474a586')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  # generate man page directly as docs target is broken: https://github.com/containers/netavark/issues/524
  mandown docs/$pkgname.1.md > $pkgname.1
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname
  cargo test --frozen --all-features
}

package() {
  cd $pkgname
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/lib/podman/"
  install -vDm 644 $pkgname.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
