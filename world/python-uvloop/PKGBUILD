# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-uvloop
pkgver=0.23.0
pkgrel=1
pkgdesc='Ultra fast asyncio event loop'
arch=(x86_64)
url=https://github.com/MagicStack/uvloop
license=(
  Apache-2.0
  MIT
)
depends=(
  glibc
  python
  libuv
)
makedepends=(
  cython
  git
  python-build
  python-installer
  python-packaging
  python-setuptools
  python-wheel
)
source=(git+https://github.com/MagicStack/uvloop.git#tag=v${pkgver})
b2sums=('a98e93ad7c2beeaf2cb5c31ae699dc8ef11c5deba1999222085877a813cf032e201b5d495a5f5cd3f853050a66f08623b5e18d9816b774909102598020eeb002')

prepare() {
  sed 's/self.use_system_libuv = False/self.use_system_libuv = True/' -i uvloop/setup.py
  sed -e 's|>=0.29.36,<0.30.0|>=0.29.36|g' -i uvloop/pyproject.toml
}

build() {
  cd uvloop
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" uvloop/dist/*.whl
  install -Dm 644 uvloop/LICENSE-APACHE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-APACHE
  install -Dm 644 uvloop/LICENSE-MIT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-MIT
}
