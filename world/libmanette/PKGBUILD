# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=libmanette
pkgver=0.2.9
pkgrel=1
pkgdesc='Simple GObject game controller library'
arch=(x86_64)
url='https://gnome.pages.gitlab.gnome.org/libmanette/'
license=(LGPL-2.1-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  libevdev
  libgudev
)
makedepends=(
  git
  gobject-introspection
  gtk-doc
  meson
  vala
)
source=("git+https://gitlab.gnome.org/GNOME/$pkgname.git#tag=$pkgver")
b2sums=(906f0f37c21037e1e6d7b11f264e38e141575a3decc1c7a126a96efdf02e455a2742918081ad9b0143564ba3960ea1c74a3c3bcfe51eeee07b37133cf0cb308c)

build() {
  artix-meson $pkgname build \
    -D doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
