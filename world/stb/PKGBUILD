# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Noa-Emil Nissinen <aur.satella@spamgourmet.org>


pkgname=stb
pkgver=r2216.f1c79c0
pkgrel=1
pkgdesc='Single-file public domain (or MIT licensed) libraries for C/C++'
arch=(any)
url=https://github.com/nothings/stb
license=(MIT)
makedepends=(git)
_commit=f1c79c02822848a9bed4315b12c8c8f3761e1296
source=(git+https://github.com/nothings/stb.git#commit=${_commit})
b2sums=('92d4c1abc694aa3883847a556dc1dad86db3f7d66d092c32c37a568c75896527fd180ce2b976e1692805fa0053d7ff412dc971b97acfec10afef61be8afe2081')

pkgver() {
  cd stb
  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

package() {
  install -Dm 644 stb/*.{c,h} -t "${pkgdir}"/usr/include/stb/
  install -Dm 644 stb/LICENSE -t "${pkgdir}"/usr/share/licenses/stb/
}

# vim: ts=2 sw=2 et:
