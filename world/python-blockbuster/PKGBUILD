# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-blockbuster
pkgver=1.5.26
pkgrel=1
pkgdesc='Utility to detect blocking calls in the async event loop'
arch=(any)
url=https://github.com/cbornet/blockbuster
license=(Apache-2.0)
depends=(
  python
  python-forbiddenfruit
)
makedepends=(
  git
  python-build
  python-hatchling
  python-installer
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytest-asyncio
  python-requests
)
source=("git+$url.git#tag=v$pkgver")
sha256sums=('d58d4de1299b0864c3f3b7ad77caea9a8f8e4eb45af81db5cf70dd7fbb0f114d')

build() {
  cd ${pkgname#python-}
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}
  pytest \
    --deselect tests/test_blockbuster.py::test_socket_send \
    --deselect tests/test_blockbuster.py::test_socket_send_non_blocking
  # The above tests fail in artix CI
}

package() {
  cd ${pkgname#python-}
  python -m installer --destdir="$pkgdir" dist/*.whl
}
