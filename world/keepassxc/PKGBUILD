# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=keepassxc
pkgver=2.7.10
pkgrel=3
pkgdesc="Cross-platform community-driven port of Keepass password manager"
url="https://keepassxc.org/"
arch=(x86_64)
license=('GPL-2.0-only OR GPL-3.0-only OR LGPL-2.1-only')
depends=('argon2' 'botan' 'hicolor-icon-theme' 'libxtst' 'minizip' 'pcsclite' 'qrencode' 'qt5-svg' 'qt5-x11extras' 'libusb')
makedepends=('asciidoctor' 'cmake' 'git' 'qt5-tools')
checkdepends=('xclip' 'xorg-server-xvfb' 'xclip')
optdepends=('xclip: keepassxc-cli clipboard support under X server'
            'wl-clipboard: keepassxc-cli clipboard support under Wayland')
provides=('org.freedesktop.secrets')
source=("git+https://github.com/keepassxreboot/keepassxc.git#tag=${pkgver}?signed"
        'TestEntryModel.patch')
sha256sums=('789edc3cbd55783b10a3b64863ea34e2588b6b52de85ffe11898fa5ca3022b10'
            '1448fb616c4eaf7b0f9019ee10f813199d7105442e3f61aea31e901139b87f43')
validpgpkeys=(3489040C1F0C3EEAB8D556A5440FC65F2E0C6E01  # Jonathan White <support@dmapps.us>
              BF5A669F2272CF4324C1FDA8CFB4C2166397D0D2  # KeePassXC Release <release@keepassxc.org>
              71D4673D73C7F83C17DAE6A2D8538E98A26FD9C4  # KeePassXC Release <release@keepassxc.org>
              AF0AEA44ABAC8F1047733EA7AFF235EEFB5A2517  # KeePassXC Release <release@keepassxc.org>
              C1E4CBA3AD78D3AFD894F9E0B7A66F03B59076A8) # KeePassXC Release <release@keepassxc.org>


prepare() {
	cd "${pkgname}"

	# Work around some test failures with the TestEntryModel test
	# See https://gitlab.alpinelinux.org/alpine/aports/-/blob/df401d633cb8812c52c2a33f699b9bdaadff27a1/community/keepassxc/tests.patch
	patch -Np1 -i "${srcdir}/TestEntryModel.patch"
}

build() {
	cmake -S "${pkgname}" -B build \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DKEEPASSXC_BUILD_TYPE=Release \
		-DWITH_XC_ALL=ON \
		-DWITH_GUI_TESTS=ON \
		-DWITH_XC_UPDATECHECK=OFF
	cmake --build build
}

check() {
	xvfb-run --auto-servernum cmake --build build --target test -j1
}

package() {
	cmake --build build --target install -- DESTDIR="${pkgdir}"
}
