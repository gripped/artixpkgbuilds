# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=libpeas-2
pkgname=(
  libpeas-2
  libpeas-2-docs
)
pkgver=2.2.0
pkgrel=2
pkgdesc="GObject Plugin System"
url="https://wiki.gnome.org/Projects/Libpeas"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  gcc-libs
  glib2
  glibc
)
makedepends=(
  gi-docgen
  git
  gjs
  glade
  glib2-devel
  gobject-introspection
  js140
  lua51
  lua51-lgi
  luajit
  meson
  python
  python-gobject
  python-setuptools
  vala
)
checkdepends=(xorg-server-xvfb)
source=("git+https://gitlab.gnome.org/GNOME/libpeas.git#tag=$pkgver")
b2sums=('7c2fc9055498b72233d46d61d102ff03e42ab1207d291ee8be2589cbff85c11b6fc2c85920d1fdf49a1c5f793b6d4ae83747dd2182378e40e3ae5ac6658738ec')

prepare() {
  cd libpeas
}

build() {
  local meson_options=(
    -D gtk_doc=true
    -D vapi=true
  )

  artix-meson libpeas build "${meson_options[@]}"
  meson compile -C build
}

check() {
  xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_libpeas-2() {
  optdepends=(
    'gjs: GJS loader'
    'lua51-lgi: Lua loader'
    'python-gobject: Python loader'
  )
  provides=(libpeas-2.so)

  meson install -C build --destdir "$pkgdir"

  cd "$pkgdir"

  _pick docs usr/share/doc
}

package_libpeas-2-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  mv docs/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
