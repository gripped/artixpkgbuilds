# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Thayne McCombs <astrothayne@gmail.com>

pkgname=dart-sass
pkgver=1.82.0
pkgrel=1
pkgdesc='Sass makes CSS fun again'
arch=('x86_64')
url='http://sass-lang.com/'
license=('MIT')
depends=('glibc')
makedepends=('git' 'dart' 'buf')
options=('!strip')
provides=('sass')
conflicts=('ruby-sass')
_sass_version=3.1.0
source=(
  "$pkgname::git+https://github.com/sass/dart-sass.git#tag=$pkgver"
  "github.com-sass-sass::git+https://github.com/sass/sass.git#tag=embedded-protocol-$_sass_version"
)
sha512sums=('84db3a70adfa828bd5d10cc11f0046950856829b9f9620d20d40b9adf5b19cc2d8376b1912bd447ca29d31a89be796b37e19b0c5862ddc61d537eb088545da16'
            'b787f37bdc894b9f009b7c0d819cea5620fff7e121fd5dfbed4627e59251a958eb9cd59987962a0a1ce10fb46c3289a601372e852148522272d10f06892d30dc')
b2sums=('32a9c71a1936d9cd1c32c730aed17df1001df4953fc66a0f8e38b7f8227df8df0678833b9202eb84a73c0911ae48d1e78f1d8ba8171964a01a54ff8a7a9f093e'
        '540be3fdf37c12294b9a181753981f6ac5f0d62d19177f5ffabab9997531c8961be725ff6448f8bedee7eb385a43bf4d1c9eee84d6d3067711d6194beb2d2d50')

prepare() {
  cd "$pkgname"

  mkdir -p build
  ln -sf "$srcdir/github.com-sass-sass" build/language

  # disable analytics
  dart --disable-analytics

  # download dependencies
  dart pub get
}

build() {
  cd "$pkgname"

  UPDATE_SASS_PROTOCOL=false dart run grinder protobuf
  dart compile exe \
    -Dversion=$pkgver \
    -Dprotocol-version=$(cat build/language/spec/EMBEDDED_PROTOCOL_VERSION) \
    -o sass \
    bin/sass.dart
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" sass

  # embedded-protocol protobuf file
  install -vDm644 -t "$pkgdir/usr/share/$pkgname" build/language/spec/embedded_sass.proto

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
