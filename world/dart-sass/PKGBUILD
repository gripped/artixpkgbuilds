# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Thayne McCombs <astrothayne@gmail.com>

pkgname=dart-sass
pkgver=1.83.4
pkgrel=1
pkgdesc='Sass makes CSS fun again'
arch=('x86_64')
url='http://sass-lang.com/'
license=('MIT')
depends=('glibc')
makedepends=('git' 'dart' 'buf')
options=('!strip')
provides=('sass')
conflicts=('ruby-sass')
_sass_version=3.1.0
source=(
  "$pkgname::git+https://github.com/sass/dart-sass.git#tag=$pkgver"
  "github.com-sass-sass::git+https://github.com/sass/sass.git#tag=embedded-protocol-$_sass_version"
)
sha512sums=('f16dee7c2dc5510f0cfe0cb23b62c4582ac1e63128fe8a46cf731102046c0a69119fb2e1021606bd90327a643c6dfb486fd58db13595ba052c5335a79db780fd'
            'b787f37bdc894b9f009b7c0d819cea5620fff7e121fd5dfbed4627e59251a958eb9cd59987962a0a1ce10fb46c3289a601372e852148522272d10f06892d30dc')
b2sums=('2660331feef35778cbfdebdab356b1c4fb994e99dc528746cae3806fff8eb9fb1e04f5c09bc69124100e3acc101bce164660082de587931ffa4b854fabc3b017'
        '540be3fdf37c12294b9a181753981f6ac5f0d62d19177f5ffabab9997531c8961be725ff6448f8bedee7eb385a43bf4d1c9eee84d6d3067711d6194beb2d2d50')

prepare() {
  cd "$pkgname"

  mkdir -p build
  ln -sf "$srcdir/github.com-sass-sass" build/language

  # disable analytics
  dart --disable-analytics

  # download dependencies
  dart pub get
}

build() {
  cd "$pkgname"

  UPDATE_SASS_PROTOCOL=false dart run grinder protobuf
  dart compile exe \
    -Dversion=$pkgver \
    -Dprotocol-version=$(cat build/language/spec/EMBEDDED_PROTOCOL_VERSION) \
    -o sass \
    bin/sass.dart
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" sass

  # embedded-protocol protobuf file
  install -vDm644 -t "$pkgdir/usr/share/$pkgname" build/language/spec/embedded_sass.proto

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
