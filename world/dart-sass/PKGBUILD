# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Thayne McCombs <astrothayne@gmail.com>

pkgname=dart-sass
pkgver=1.105.1
pkgrel=1
pkgdesc='Sass makes CSS fun again'
arch=(x86_64)
url='http://sass-lang.com/'
license=(MIT)
depends=(glibc)
makedepends=(git dart buf)
options=(!strip)
provides=(sass)
conflicts=(ruby-sass)
_sass_version=3.3.0
source=(
  "$pkgname::git+https://github.com/sass/dart-sass.git#tag=$pkgver"
  "github.com-sass-sass::git+https://github.com/sass/sass.git#tag=embedded-protocol-$_sass_version"
)
sha512sums=('91266e141e43200a9c2971a57fe7108594dcbb203e1752231e7383172bddb6335a16469d19597a43a64c29d4ffe1d4776c8457e4cc31c06a3fcd7db8b709057f'
            '82a2ada59c1ca0513dd61d2c12c6048d3c8544c81061792e92886928fb6ef237b76bd39dbc2935d7338f0af8f54a53d991acd5c0cc7b409202b44566d448b7e8')
b2sums=('0b0218975e0ba53eb819d347dcbc277c1d983370075133d3b664baf46686b477c64fc357b4b977ed6dd3f65872c3b21acf47a471581dbee6d5c6a8d512651788'
        'a4ee88650f6a2075a20c0dcf5496e63c123654f36a48bb04e2fa2a4602dc96c36fca0f140081dda20da7d015aa0241bf8f4dd0d15abce421d8c878f8f87c0202')

prepare() {
  cd "$pkgname"

  mkdir -p build
  ln -sf "$srcdir/github.com-sass-sass" build/language

  # disable analytics
  dart --disable-analytics

  # download dependencies
  dart pub get
}

build() {
  cd "$pkgname"

  UPDATE_SASS_PROTOCOL=false dart run grinder protobuf
  dart compile exe \
    -Dversion=$pkgver \
    -Dprotocol-version=$(cat build/language/spec/EMBEDDED_PROTOCOL_VERSION) \
    -o sass \
    bin/sass.dart
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" sass

  # embedded-protocol protobuf file
  install -vDm644 -t "$pkgdir/usr/share/$pkgname" build/language/spec/embedded_sass.proto

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
