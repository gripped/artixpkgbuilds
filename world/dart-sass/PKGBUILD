# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Thayne McCombs <astrothayne@gmail.com>

pkgname=dart-sass
pkgver=1.86.3
pkgrel=1
pkgdesc='Sass makes CSS fun again'
arch=('x86_64')
url='http://sass-lang.com/'
license=('MIT')
depends=('glibc')
makedepends=('git' 'dart' 'buf')
options=('!strip')
provides=('sass')
conflicts=('ruby-sass')
_sass_version=3.1.0
source=(
  "$pkgname::git+https://github.com/sass/dart-sass.git#tag=$pkgver"
  "github.com-sass-sass::git+https://github.com/sass/sass.git#tag=embedded-protocol-$_sass_version"
)
sha512sums=('4972684c07d78fd52bc628b56e809642c21dae161d265b4a7de0147f8a9f6b303543d06f1150caee4fd949a8f4a2eee51b7c356d96e0fc401cf29f9ee670974a'
            'b787f37bdc894b9f009b7c0d819cea5620fff7e121fd5dfbed4627e59251a958eb9cd59987962a0a1ce10fb46c3289a601372e852148522272d10f06892d30dc')
b2sums=('a0a5fc6083f4cd77338691fa3e3adf8453e70533998b94bef576afb539076557160ae7d93e6ac0440067dd954ba5b308ff5dd4ca100d6a6c6a01fd3a29bf127e'
        '540be3fdf37c12294b9a181753981f6ac5f0d62d19177f5ffabab9997531c8961be725ff6448f8bedee7eb385a43bf4d1c9eee84d6d3067711d6194beb2d2d50')

prepare() {
  cd "$pkgname"

  mkdir -p build
  ln -sf "$srcdir/github.com-sass-sass" build/language

  # disable analytics
  dart --disable-analytics

  # download dependencies
  dart pub get
}

build() {
  cd "$pkgname"

  UPDATE_SASS_PROTOCOL=false dart run grinder protobuf
  dart compile exe \
    -Dversion=$pkgver \
    -Dprotocol-version=$(cat build/language/spec/EMBEDDED_PROTOCOL_VERSION) \
    -o sass \
    bin/sass.dart
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" sass

  # embedded-protocol protobuf file
  install -vDm644 -t "$pkgdir/usr/share/$pkgname" build/language/spec/embedded_sass.proto

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
