# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Thayne McCombs <astrothayne@gmail.com>

pkgname=dart-sass
pkgver=1.79.4
pkgrel=1
pkgdesc='Sass makes CSS fun again'
arch=('x86_64')
url='http://sass-lang.com/'
license=('MIT')
depends=('glibc')
makedepends=('git' 'dart' 'buf')
options=('!strip')
provides=('sass')
conflicts=('ruby-sass')
_sass_version=3.0.0
source=(
  "$pkgname::git+https://github.com/sass/dart-sass.git#tag=$pkgver"
  "github.com-sass-sass::git+https://github.com/sass/sass.git#tag=embedded-protocol-$_sass_version"
)
sha512sums=('46f9ec55baac4c957e4308f63df09dccd3a296a14333d1bb4ec217d3302d3a53e73d6b0eeee311c345d76039dd09e52fda4d6eb6b5e633d541ab551c9a6c529e'
            'aa038ee2b6a5ef95512d06e83a2dd3edf53af94e8f08bf597cdddf5946ad04d58ecd1fa287496600949191942c44aebffb848fabae40e612cdae3dfd1f43dba6')
b2sums=('c12f782ce274f0f57de5c010ae0bd7a11203a3cc948789b85c9b410dd0e1dd5aa06312c941e59c076e79b58b4ebb7d100b741607e2b6bfc705a620983e63c525'
        '243b1112937f3aa7b23655285ff72d96e161b50e130c71112c23d0e0bb7d8f7eb392ad9291744268cb58c9f3c28cea806cb90f418f8f062c18dcb380a0b182c4')

prepare() {
  cd "$pkgname"

  mkdir -p build
  ln -sf "$srcdir/github.com-sass-sass" build/language

  # disable analytics
  dart --disable-analytics

  # download dependencies
  dart pub get
}

build() {
  cd "$pkgname"

  UPDATE_SASS_PROTOCOL=false dart run grinder protobuf
  dart compile exe \
    -Dversion=$pkgver \
    -Dprotocol-version=$(cat build/language/spec/EMBEDDED_PROTOCOL_VERSION) \
    -o sass \
    bin/sass.dart
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" sass

  # embedded-protocol protobuf file
  install -vDm644 -t "$pkgdir/usr/share/$pkgname" build/language/spec/embedded_sass.proto

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
