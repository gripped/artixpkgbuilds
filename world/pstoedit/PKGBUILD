# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Caleb Maclennan <caleb@alerque.com>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Tobias Powalowski <t.powa@gmx.de>

pkgname=pstoedit
pkgver=4.3
pkgrel=1
pkgdesc='Translates PostScript and PDF graphics into other vector formats'
arch=(x86_64)
url='https://www.pstoedit.com'
_url="https://github.com/woglu/$pkgname"
license=(GPL-2.0-only)
depends=(gcc-libs
         gd
         imagemagick
         plotutils)
makedepends=(ghostscript
             texlive-{basic,bin,latex{,recommended}}) # all that for a man page
_archive="$pkgname-$pkgver"
source=("$_url/archive/refs/tags/v$pkgver/$_archive.tar.gz")
sha256sums=('36dfdc79c750930dd57e2c4a4dee2a6ab1a1fe65cd8fc4dc047dbfb6f2cfa15b')

prepare() {
	cd "$_archive"
	autoreconf -vi
}

build() {
	cd "$_archive"
	./configure --prefix=/usr
	make
}

package() {
	cd "$_archive"
	make DESTDIR="$pkgdir" install
}
