# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-panel
pkgver=3.50.0
pkgrel=3
pkgdesc='Panel of GNOME Flashback'
arch=('x86_64')
url='https://wiki.gnome.org/Projects/GnomePanel'
license=('GPL-2.0-or-later AND LGPL-2.1-or-later')
depends=('at-spi2-core' 'cairo' 'dconf' 'evolution-data-server' 'gdk-pixbuf2' 'geocode-glib-2'
         'glib2' 'glibc' 'gnome-desktop' 'gnome-menus' 'gsettings-desktop-schemas' 'gtk3'
         'hicolor-icon-theme' 'libgdm' 'libgweather-4' 'libical' 'libwnck3' 'libx11' 'libxrandr'
         'nautilus' 'pango' 'polkit')
makedepends=('itstool')
optdepends=('alacarte: Main menu editor'
            'gnome-applets: Extra applets for the panel')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz"
        '0001-build-remove-systemd-dependency-and-code.patch'
        'gnome-panel-git-fixes.patch'
        'gnome-panel-60.patch'
        'gnome-panel-64.patch'
        'gnome-panel-65.patch'
        'gnome-panel-66.patch')
sha256sums=('025ec889d04262d41c95bee85be6421ba3a6050e269b9ce99f692cad49a2eb13'
            '39c82de2bfebe7904590b1fd584f7845f9dd45c4391f86ef1864545a6050d2d6'
            '73795d7b1dc0412c061b4d26b49fba4fe82f5ffa44fe5987d808b6d2cb872451'
            '3ea598905185d8f19381c93766734ae0f99491329548a816740ff6bc2449c848'
            '83a951c262707e4e8baf218e3da05abb041560268bb0afd93f61bd95f27b7c8b'
            'f0a9ac2d5cf69e276d9446925df79dc633b4f58b7cd6757e4313f4dffa7403f7'
            '8d53c1689c8ebf3ab78fb02704997637c4126582fd9a87420d26939b934a81eb')

prepare() {
  cd $pkgname-$pkgver

  # Remove systemd dependency/code
  patch -Np1 -i ../0001-build-remove-systemd-dependency-and-code.patch

  # Apply fixes from git master
  patch -Np1 -i ../gnome-panel-git-fixes.patch

  # https://gitlab.gnome.org/GNOME/gnome-panel/-/merge_requests/60
  patch -Np1 -i ../gnome-panel-60.patch

  # https://gitlab.gnome.org/GNOME/gnome-panel/-/merge_requests/64
  patch -Np1 -i ../gnome-panel-64.patch

  # https://gitlab.gnome.org/GNOME/gnome-panel/-/merge_requests/65
  patch -Np1 -i ../gnome-panel-65.patch

  # https://gitlab.gnome.org/GNOME/gnome-panel/-/merge_requests/66
  patch -Np1 -i ../gnome-panel-66.patch

  # Regenerate resources file
  rm gnome-panel/panel-resources.{c,h}
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
