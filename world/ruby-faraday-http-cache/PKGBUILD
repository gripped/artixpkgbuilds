# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname='ruby-faraday-http-cache'
pkgver=2.6.1
pkgrel=1
pkgdesc='Middleware to handle HTTP caching'
arch=('any')
url='https://github.com/sourcelevel/faraday-http-cache'
license=('Apache-2.0')
depends=(
  ruby
  ruby-faraday
)
makedepends=(
  git
)
checkdepends=(
  ruby-activesupport
  ruby-bundler
  ruby-rake
  ruby-rackup
  ruby-rspec
  ruby-sinatra
  ruby-webrick
)
options=('!emptydirs')
source=("git+${url}.git#tag=v${pkgver}")
sha512sums=('b9b9e9719231f9824f4e0e2a0b6d5f1efebc556a53230014f459b5f624a83e07c29bc0fe4078c7112fe0f66f2bc0197b0b3d4ec42f8ba49a2be0e506a4880faf')
b2sums=('ab2007b48b2fb18957c712d57e461d70236c30631c933885f5cf487f9714aaa4127c99de1e170e793b7ca6e682a4139ba971588e242bed90f2a3a5fec90ad48c')

prepare() {
  cd faraday-http-cache

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' faraday-http-cache.gemspec
}

build() {
  cd faraday-http-cache

  local _gemdir="$(gem env gemdir)"

  gem build --verbose faraday-http-cache.gemspec

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "faraday-http-cache-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/faraday-http-cache-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/faraday-http-cache-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd faraday-http-cache

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rake spec
}

package() {
  cd faraday-http-cache

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
