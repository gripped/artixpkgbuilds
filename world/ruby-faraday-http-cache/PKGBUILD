# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname='ruby-faraday-http-cache'
pkgver=2.7.0
pkgrel=1
pkgdesc='Middleware to handle HTTP caching'
arch=('any')
url='https://github.com/sourcelevel/faraday-http-cache'
license=('Apache-2.0')
depends=(
  ruby
  ruby-faraday
)
makedepends=(
  git
)
checkdepends=(
  ruby-activesupport
  ruby-bundler
  ruby-rake
  ruby-rackup
  ruby-rspec
  ruby-sinatra
  ruby-webrick
)
options=('!emptydirs')
source=("git+${url}.git#tag=v${pkgver}")
sha512sums=('b015169a72dcd978f338dc15138269b9ac2aa0a449cb02da61c129bd8d88d214efd224c7c2f1715d0f9067a9595793e903fcb27417474cb616715c8f27535d1d')
b2sums=('19be84221cee4b477b74f7528c104d00fe678c7c2f14838c100781c9ad1eea58c277c945f7ce7256c43c981b5d010645101557cb58ed5611c9bafc148d4db5c6')

prepare() {
  cd faraday-http-cache

  # v2.7.0 was tagged with the old version in this file.
  # Fixed upstream by 4fd5dd3400740b8ca8bfbf040cd570171b18bc8b.
  sed --in-place "s|VERSION = '[^']*'|VERSION = '${pkgver}'|" lib/faraday/http_cache/version.rb
}

build() {
  cd faraday-http-cache

  local _gemdir="$(gem env gemdir)"

  gem build --verbose faraday-http-cache.gemspec

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "faraday-http-cache-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/faraday-http-cache-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/faraday-http-cache-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd faraday-http-cache

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rake spec
}

package() {
  cd faraday-http-cache

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
