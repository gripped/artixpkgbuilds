# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-regexp_parser
pkgver=2.10.0
pkgrel=2
pkgdesc='A library for tokenizing, lexing, and parsing Ruby regular expressions'
arch=(any)
url='https://github.com/ammar/regexp_parser'
license=(MIT)
depends=(
  ruby
)
makedepends=(
  ragel
  ruby-bundler
  ruby-rake
  ruby-rspec
)
checkdepends=(
  ruby-benchmark-ips
  ruby-ice_nine
  ruby-leto
  ruby-regexp_property_values
)
options=(!emptydirs)
source=(https://github.com/ammar/regexp_parser/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('2052d83d84c621b68f64685ff74bdc3babf0aa741d8cb5f36967700e34eabbb6')

prepare() {
  cd regexp_parser-$pkgver
  sed -i -e '/gouteur/d' -e '/rubocop/d' Gemfile
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd regexp_parser-$pkgver
  CI="true" rake build
  gem build regexp_parser.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    regexp_parser-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd regexp_parser-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rake spec
}

package() {
  cd regexp_parser-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
