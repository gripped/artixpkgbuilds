# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-async-http
pkgver=0.94.0
pkgrel=1
pkgdesc='A HTTP client and server library'
arch=(any)
url='https://github.com/socketry/async-http'
license=(MIT)
depends=(
  ruby
  ruby-async
  ruby-async-pool
  ruby-io-endpoint
  ruby-io-stream
  ruby-metrics
  ruby-protocol-http
  ruby-protocol-http1
  ruby-protocol-http2
  ruby-protocol-url
  ruby-traces
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-async-container
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-decode
  ruby-localhost
  ruby-rack-test
  ruby-sus
  ruby-sus-fixtures-async
  ruby-sus-fixtures-async-http
  ruby-sus-fixtures-benchmark
  ruby-sus-fixtures-openssl
)
options=(!emptydirs)
source=(https://github.com/socketry/async-http/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('dd66efc028b6d7c062cdbabc14af4ac6854e55752d5f3f1511d41f103195ba87487ec60e701ca977cb86de70c0cc4577897b43004f4d23c09e787c5534273d06')
b2sums=('24de7dd77f87b253d25a5ce316f6ecb63c5d04f343ed9e06c7aa8727e441fd6f606c18e42699c5bff3d5fc22ee4bdf496b24c0acb7831c4b3f80ddbc235b93aa')

prepare() {
  cd async-http-$pkgver

  sed -r \
    -e 's|~>|>=|g' \
    -e '/signing_key/d' \
    -i async-http.gemspec

  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd async-http-$pkgver
  gem build async-http.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    async-http-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd async-http-$pkgver
  GEM_HOME="tmp_install/$_gemdir" bake test
  GEM_HOME="tmp_install/$_gemdir" sus
}

package() {
  cd async-http-$pkgver
  cp -a tmp_install/* "$pkgdir"/
}
