# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Bruno Pagani <archange@archlinux.org>
# Contributor: Baptiste Jonglez <baptiste--aur at jonglez dot org>

pkgname=jami-daemon
pkgver=20250929
pkgrel=2
pkgdesc="Free and universal communication platform which preserves the usersâ€™ privacy and freedoms (daemon component)"
arch=(x86_64)
url="https://jami.net"
license=(GPL-3.0-or-later)
groups=(jami)
depends=(glibc opendht gnutls nettle libgit2 libsecp256k1 libsecp256k1.so ffmpeg speexdsp
         fmt yaml-cpp jsoncpp zlib alsa-lib libpulse jack libupnp libnatpmp
         openssl webrtc-audio-processing-0.3 libudev libarchive)
# portaudio needs a not-yet-upstream patch https://git.jami.net/savoirfairelinux/jami-daemon/-/issues/650
makedepends=(git cmake perl asio msgpack-c msgpack-cxx restinio udev meson)
checkdepends=(cppunit)
_commit=afe2446133eb3c9279e42b0d1dcfdd9a3c76a35f
_pjprojectver=93dc96918bb6ba74e1e1d00c40c80402e856f2ac
_dhtnetver=7861b4620b4cec5fa34c5d1bb2b304912730f638
source=(git+https://git.jami.net/savoirfairelinux/${pkgname}.git#commit=${_commit}
        https://github.com/savoirfairelinux/pjproject/archive/${_pjprojectver}/pjproject-${_pjprojectver}.tar.gz
        dhtnet-$_dhtnetver.tar.gz::https://git.jami.net/savoirfairelinux/dhtnet/-/archive/${_dhtnetver}/dhtnet-${_dhtnetver}.tar.gz
        ffmpeg-7.patch
        ffmpeg-8.patch
        fmt-12.patch)
noextract=(pjproject-${_pjprojectver}.tar.gz
           dhtnet-${_dhtnetver}.tar.gz)
sha512sums=('1748c2ebbdfbce597f0c3f409963a7898f097b85c2a2ac56e097cb3abe4ff070d5c559ffa5224dc37af26d4c0f8a0713b4ef96c6716c709f6946bfdaba9de114'
            'a6e95c93a12e66f86020d506916dfe9f028bc8fd5fb7308839358734b19de73b33696493a3958ac3797295d68eb285cfa3077c9c1f7eae3a571236d400dd2870'
            'SKIP'
            '720c60053779e72273d7abf339008284f5473b4f9a101e21eae352e9663559c7591f8e3481d9b479d113f306da9d0b3ffa53d32091c8d795b3252afd7e39afec'
            '933e223f4bcaacaaf052de906daa10ed85020677a20158df96b78b9648edf27c601d932434315d16563dadc61338a7124e5cc8e2495281ed1f83b1f30234b496'
            'b645545120608e70f70ee664d43de03fa07ebc5b2af9a6515a40220aabfe057d4f69034b15781a21c5e8ff6f2fbb353b7dea69d840cb299f9dbf58eeaf082a40')

pkgver() {
  cd ${pkgname}
  TZ=UTC git show -s --pretty=%cd --date=format-local:%Y%m%d HEAD
}

prepare() {
  cd ${pkgname}
  mkdir -p contrib/tarballs
  cp ../pjproject-${_pjprojectver}.tar.gz contrib/tarballs/
  cp ../dhtnet-$_dhtnetver.tar.gz contrib/tarballs/
  mkdir -p contrib/native

# Fix build with fmt 12
  cd contrib/tarballs
  mkdir -p dhtnet
  tar -xzf dhtnet-$_dhtnetver.tar.gz -C dhtnet --strip-components=1
  cd dhtnet
  patch -p1 -i "$srcdir"/fmt-12.patch
  tar -czf ../dhtnet-$_dhtnetver.tar.gz *
  cd ..
  rm -fr dhtnet
  cd ../..

  patch -p1 -i ../ffmpeg-7.patch # Fix build with ffmpeg 7+
  patch -p1 -i ../ffmpeg-8.patch # Fix build with ffmpeg 8
}

build() {
  cd ${pkgname}/contrib/native
  ../bootstrap \
      --disable-downloads \
      --disable-all \
      --enable-pjproject \
      --enable-dhtnet

  # fmt v11 compatibility
  make dhtnet
  sed -io 's/fmt::ptr(pimpl_)/fmt::ptr(pimpl_.get())/g' dhtnet/src/ice_transport.cpp

  make DEPS_pjproject= DEPS_dhtnet=pjproject

  cd "${srcdir}"
  artix-meson ${pkgname} build -D interfaces=library -D opensl=disabled -D portaudio=disabled \
                              $( ((CHECKFUNC)) && echo -D tests=true) \
                              -D pkg_config_path="${PWD}/${pkgname}/contrib/$(cc -dumpmachine)/lib/pkgconfig"
  meson compile -C build
}

#check() {
# https://git.jami.net/savoirfairelinux/jami-daemon/-/issues/649
#  meson test -C build --print-errorlogs
#}

package() {
  meson install -C build --destdir "${pkgdir}"
}
