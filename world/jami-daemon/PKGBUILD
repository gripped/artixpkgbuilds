# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Bruno Pagani <archange@archlinux.org>
# Contributor: Baptiste Jonglez <baptiste--aur at jonglez dot org>

pkgname=jami-daemon
pkgver=20260706
pkgrel=2
pkgdesc="Free and universal communication platform which preserves the users’ privacy and freedoms (daemon component)"
arch=(x86_64)
url="https://jami.net"
license=(GPL-3.0-or-later)
groups=(jami)
depends=(
  alsa-lib
  ffmpeg
  fmt
  glibc
  gnutls
  jack
  jsoncpp
  libarchive
  libgcc
  libgit2
  libnatpmp
  libpulse
  libsecp256k1
  libupnp
  libstdc++
  nettle
  opendht
  openssl
  simdutf
  speexdsp
  libudev
  util-linux-libs
  webrtc-audio-processing-0.3
  yaml-cpp
  zlib
)
# portaudio needs a not-yet-upstream patch https://git.jami.net/savoirfairelinux/jami-daemon/-/issues/650
makedepends=(
  asio
  cmake
  git
  meson
  msgpack-c
  msgpack-cxx
  perl
  restinio
  udev
)
checkdepends=(cppunit)
_commit=12948b655fb38ff2d441a67871d0feab3573adbc
_pjprojectver=08d2e8aef47d2f73546cc9e95514db454e57821e
_dhtnetver=ac94420150d43f3748541c1827f30fa97295e713
source=(git+https://git.jami.net/savoirfairelinux/${pkgname}.git#commit=${_commit}
        https://github.com/savoirfairelinux/pjproject/archive/${_pjprojectver}/pjproject-${_pjprojectver}.tar.gz
        dhtnet-$_dhtnetver.tar.gz::https://git.jami.net/savoirfairelinux/dhtnet/-/archive/$_dhtnetver/dhtnet-$_dhtnetver.tar.gz
        ffmpeg-7.patch
        ffmpeg-8.patch
        fix-missing-header.patch)
noextract=(pjproject-${_pjprojectver}.tar.gz
           dhtnet-${_dhtnetver}.tar.gz)
sha512sums=('77cdfb10cacde3a619c64b14ac1cab966c26dd78103f652ea0ed33c78b436193b94dd712bfa888a46c705db9e94d6b07ddd2bdb9e003d4dadc13afca39f3aeb0'
            '9f464b7fb70bac99f346f9254dee2a75fd4f606f4fc253d03c5197283b739979753958626fd9296d47e99b156149be70cb67dec451a08d8c5b75482083959557'
            '1c2498486caf791928d6e662e16257f3a521df589374d32be8d81adf700698241dbd2e170a2661a1c7aa16d0e8dde1beec77847753e86041d21d555c4ebc0de3'
            '39b7181fb66f72d96b63777920a26dfc1fa5377e81d22dce622c6ef4a3d9d2e5002457908a89fc14bdf9af175b7e781114c2d2fd0c572bbea344254495bed563'
            '8fcfe52808d00b8535dc6d181af0233dc0f3a51e8da69728a04e7f6edc82c54fb11e62ffad05344d9eeb6d203dc323ffa03181c615c7c6f84682f70a02ee4319'
            '6025357ffcfe2f8c3088d60b780ea1b63d1d512b80c5ec29f616b5bc7336cc5a7dacb02c1f9e88f9255545c1cbd2446f69e711b3b6244eeea4f7a68291e39465')

pkgver() {
  cd ${pkgname}
  TZ=UTC git show -s --pretty=%cd --date=format-local:%Y%m%d HEAD
}

prepare() {
  cd ${pkgname}
  mkdir -p contrib/tarballs
  cp ../pjproject-${_pjprojectver}.tar.gz contrib/tarballs/
  cp ../dhtnet-$_dhtnetver.tar.gz contrib/tarballs/
  mkdir -p contrib/native

# Fix build with fmt 12
  pushd contrib/tarballs
  tar -xvzf dhtnet-$_dhtnetver.tar.gz
  sed -e 's|fmt/core.h|fmt/format.h|' -i dhtnet-$_dhtnetver/include/pj_init_lock.h -i dhtnet-$_dhtnetver/src/string_utils.cpp
  tar -cvzf dhtnet-$_dhtnetver.tar.gz dhtnet-$_dhtnetver
  popd
  git cherry-pick -n bc80496d4bf7f3975e2e6e20cf9c9299458298de
  
  patch -p1 -i ../ffmpeg-7.patch # Fix build with ffmpeg 7+
  patch -p1 -i ../ffmpeg-8.patch # Fix build with ffmpeg 8
  patch -p1 -i ../fix-missing-header.patch
}

build() {
#  export CXXFLAGS+=" -std=c++17"
  cd ${pkgname}/contrib/native
  ../bootstrap \
      --disable-downloads \
      --disable-all \
      --enable-pjproject \
      --enable-dhtnet

  make DEPS_pjproject= DEPS_dhtnet=pjproject

  cd "${srcdir}"
  artix-meson ${pkgname} build -D interfaces=library -D aaudio=disabled -D portaudio=disabled \
                              $( ((CHECKFUNC)) && echo -D tests=true) \
                              -D pkg_config_path="${PWD}/${pkgname}/contrib/$(cc -dumpmachine)/lib/pkgconfig"
  meson compile -C build
}

#check() {
# https://git.jami.net/savoirfairelinux/jami-daemon/-/issues/649
#  meson test -C build --print-errorlogs
#}

package() {
  meson install -C build --destdir "${pkgdir}"
}
