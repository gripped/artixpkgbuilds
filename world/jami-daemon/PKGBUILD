# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Bruno Pagani <archange@archlinux.org>
# Contributor: Baptiste Jonglez <baptiste--aur at jonglez dot org>

pkgname=jami-daemon
pkgver=20240812
pkgrel=3
pkgdesc="Free and universal communication platform which preserves the usersâ€™ privacy and freedoms (daemon component)"
arch=(x86_64)
url="https://jami.net"
license=(GPL-3.0-or-later)
groups=(jami)
depends=(glibc opendht gnutls nettle libgit2 libsecp256k1 libsecp256k1.so ffmpeg speexdsp
         fmt yaml-cpp jsoncpp zlib alsa-lib libpulse jack libupnp libnatpmp
         openssl webrtc-audio-processing libudev libarchive)
# portaudio needs a not-yet-upstream patch https://git.jami.net/savoirfairelinux/jami-daemon/-/issues/650
makedepends=(git cmake perl asio msgpack-c msgpack-cxx restinio udev meson)
checkdepends=(cppunit)
_commit=cbf8f0af6d49d1e95cf7fc85487c9c876d49878c
_pjprojectver=8fc165b833eea6e3c88d67a541385424b129fd3f
_dhtnetver=cfe512b0632eea046f683b22e42d01eeb943d751
source=(git+https://git.jami.net/savoirfairelinux/${pkgname}.git#commit=${_commit}
        https://github.com/savoirfairelinux/pjproject/archive/${_pjprojectver}/pjproject-${_pjprojectver}.tar.gz
        dhtnet-$_dhtnetver.tar.gz::https://review.jami.net/plugins/gitiles/dhtnet/+archive/$_dhtnetver.tar.gz
        ffmpeg-7.patch
        fmt-v11.patch)
noextract=(pjproject-${_pjprojectver}.tar.gz
           dhtnet-${_dhtnetver}.tar.gz)
sha512sums=('b2dae76c8d77dc641f60cb9e93ec405c7b654d3419f494bc672771f1161669b55184a73c8d6a5d3365d9a6f7b949a5063369c6d87dfc75e332c1d1efbc75f453'
            '62eda88458411fb846582c11008f4908f4674cbd5ebf042853c802b2efa52b0c242256178475535e94845117330190d8234a1b698df21aad7ee758f531ec1c10'
            'SKIP'
            '2a895a82e8fad356445ed1a8adf860154e268f89c0812b3bfac8bb37fd45198ed8b076e81b2cc2cdd635d27cb0496a8d88ae982dcdc46fa42d7799b913695731'
            'f6a720ba4c6a119b638a159b8825af6e1427c55c394f01b7b74b5568bc74b6d3e79068fcde7e57cf8e97dfc926432f227ab65c04d128e7428e991dd73d920a8b')

pkgver() {
  cd ${pkgname}
  TZ=UTC git show -s --pretty=%cd --date=format-local:%Y%m%d HEAD
}

prepare() {
  cd ${pkgname}
  mkdir -p contrib/tarballs
  cp ../pjproject-${_pjprojectver}.tar.gz contrib/tarballs/
  cp ../dhtnet-$_dhtnetver.tar.gz contrib/tarballs/
  mkdir -p contrib/native

  patch -p1 -i ../ffmpeg-7.patch # Fix build with ffmpeg 7
  patch -p1 -i ../fmt-v11.patch # Fix build with fmt v11

  git cherry-pick -n 58758918d31b440671b78fbb1e8aa80260bf587a
}

build() {
  cd ${pkgname}/contrib/native
  ../bootstrap \
      --disable-downloads \
      --disable-all \
      --enable-pjproject \
      --enable-dhtnet

  # fmt v11 compatibility
  make dhtnet
  sed -io 's/fmt::ptr(pimpl_)/fmt::ptr(pimpl_.get())/g' dhtnet/src/ice_transport.cpp

  make DEPS_pjproject= DEPS_dhtnet=pjproject

  cd "${srcdir}"
  artix-meson ${pkgname} build -D interfaces=library -D opensl=disabled -D portaudio=disabled \
                              $( ((CHECKFUNC)) && echo -D tests=true) \
                              -D pkg_config_path="${PWD}/${pkgname}/contrib/$(cc -dumpmachine)/lib/pkgconfig"
  meson compile -C build
}

#check() {
# https://git.jami.net/savoirfairelinux/jami-daemon/-/issues/649
#  meson test -C build --print-errorlogs
#}

package() {
  meson install -C build --destdir "${pkgdir}"
}
