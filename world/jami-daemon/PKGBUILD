# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Bruno Pagani <archange@archlinux.org>
# Contributor: Baptiste Jonglez <baptiste--aur at jonglez dot org>

pkgname=jami-daemon
pkgver=20250912
pkgrel=1
pkgdesc="Free and universal communication platform which preserves the usersâ€™ privacy and freedoms (daemon component)"
arch=(x86_64)
url="https://jami.net"
license=(GPL-3.0-or-later)
groups=(jami)
depends=(glibc opendht gnutls nettle libgit2 libsecp256k1 libsecp256k1.so ffmpeg speexdsp
         fmt yaml-cpp jsoncpp zlib alsa-lib libpulse jack libupnp libnatpmp
         openssl webrtc-audio-processing-0.3 libudev libarchive)
# portaudio needs a not-yet-upstream patch https://git.jami.net/savoirfairelinux/jami-daemon/-/issues/650
makedepends=(git cmake perl asio msgpack-c msgpack-cxx restinio udev meson)
checkdepends=(cppunit)
_commit=ce82bdbbf38e7f7ac9d675bfc959e4d7f7ac387
_pjprojectver=37130c943d59f25a71935803ea2d84515074a237
_dhtnetver=6c5ee3a21556d668d047cdedb5c4b746c3c6bdb2
source=(git+https://git.jami.net/savoirfairelinux/${pkgname}.git#commit=${_commit}
        https://github.com/savoirfairelinux/pjproject/archive/${_pjprojectver}/pjproject-${_pjprojectver}.tar.gz
        dhtnet-$_dhtnetver.tar.gz::https://review.jami.net/plugins/gitiles/dhtnet/+archive/$_dhtnetver.tar.gz
        ffmpeg-7.patch)
noextract=(pjproject-${_pjprojectver}.tar.gz
           dhtnet-${_dhtnetver}.tar.gz)
sha512sums=('91d9ff98033f4fd889d1c68fcc1a7b59923627178576a831dde2ddcd5f588c74cab0fec8c3e4c6893e7ceedf66275dd7c50acef28fa8e63602895f689e34ced8'
            'SKIP'
            'SKIP'
            '720c60053779e72273d7abf339008284f5473b4f9a101e21eae352e9663559c7591f8e3481d9b479d113f306da9d0b3ffa53d32091c8d795b3252afd7e39afec')

pkgver() {
  cd ${pkgname}
  TZ=UTC git show -s --pretty=%cd --date=format-local:%Y%m%d HEAD
}

prepare() {
  cd ${pkgname}
  mkdir -p contrib/tarballs
  cp ../pjproject-${_pjprojectver}.tar.gz contrib/tarballs/
  cp ../dhtnet-$_dhtnetver.tar.gz contrib/tarballs/
  mkdir -p contrib/native

  patch -p1 -i ../ffmpeg-7.patch # Fix build with ffmpeg 7
}

build() {
  cd ${pkgname}/contrib/native
  ../bootstrap \
      --disable-downloads \
      --disable-all \
      --enable-pjproject \
      --enable-dhtnet

  # fmt v11 compatibility
  make dhtnet
  sed -io 's/fmt::ptr(pimpl_)/fmt::ptr(pimpl_.get())/g' dhtnet/src/ice_transport.cpp

  make DEPS_pjproject= DEPS_dhtnet=pjproject

  cd "${srcdir}"
  artix-meson ${pkgname} build -D interfaces=library -D opensl=disabled -D portaudio=disabled \
                              $( ((CHECKFUNC)) && echo -D tests=true) \
                              -D pkg_config_path="${PWD}/${pkgname}/contrib/$(cc -dumpmachine)/lib/pkgconfig"
  meson compile -C build
}

#check() {
# https://git.jami.net/savoirfairelinux/jami-daemon/-/issues/649
#  meson test -C build --print-errorlogs
#}

package() {
  meson install -C build --destdir "${pkgdir}"
}
