# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-jose
pkgver=3.4.0
pkgrel=1
pkgdesc="A JOSE implementation in Python"
arch=('any')
license=('MIT')
url="https://github.com/mpdavis/python-jose"
depends=('python-ecdsa' 'python-pyasn1' 'python-rsa')
optdepends=('python-cryptography: one option for better performance'
            'python-pycryptodome: one option for better performance')
makedepends=('git' 'python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-cryptography' 'python-pycryptodome' 'python-pytest')
source=("git+https://github.com/mpdavis/python-jose.git#tag=$pkgver")
sha512sums=('2646acf38f450a267191518225135765855bfdc97cc297f3a5e1fea9fd76e24738cc0956ef3c638f84f8e9b4e5205e9e349e3549aeb7d0f92be2fa3efb8f916b')

prepare() {
  cd python-jose
  # https://github.com/mpdavis/python-jose/pull/338
  git cherry-pick -n 574e3a340b53e70e2c42212d18bb2fe1c35d7d68
}

build() {
  cd python-jose
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd python-jose
  pytest
}

package() {
  cd python-jose
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
