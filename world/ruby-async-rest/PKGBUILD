# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-async-rest
pkgver=0.19.1
pkgrel=1
pkgdesc='A library for RESTful clients (and hopefully servers)'
arch=(any)
url='https://github.com/socketry/async-rest'
license=(MIT)
depends=(
  ruby
  ruby-async-http
  ruby-protocol-http
)
checkdepends=(
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-decode
  ruby-sus
  ruby-sus-fixtures-async-http
)
options=(!emptydirs)
source=(https://github.com/socketry/async-rest/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('31bce6f344370f38fd096765567ea729dd6f0d9cc32a462d431bf17d9a7a4c407b26dc3220e6568ac5c991079155af8e6ed84f5e854afc20b7266e00bcaa5206')
b2sums=('40938b67f3f7c0b4c6c175736b2d98858c276472cfd342fca86545db2a1d72e107fde5a0236c566b00ad90d10171ee4773eb492e723bc47f1b572ea73b071b69')

prepare() {
  cd async-rest-$pkgver
  sed -r -i \
    -e 's|~>|>=|g' \
    -e '/signing_key/d' \
    async-rest.gemspec
  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd async-rest-$pkgver
  gem build async-rest.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    async-rest-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd async-rest-$pkgver
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd async-rest-$pkgver
  cp -a tmp_install/* "$pkgdir"/
}
