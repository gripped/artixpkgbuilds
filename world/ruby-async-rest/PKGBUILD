# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-async-rest
pkgver=0.18.0
pkgrel=3
pkgdesc='A library for RESTful clients (and hopefully servers)'
arch=(any)
url='https://github.com/socketry/async-rest'
license=(MIT)
depends=(
  ruby
  ruby-async-http
  ruby-protocol-http
)
checkdepends=(
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-decode
  ruby-sus
  ruby-sus-fixtures-async-http
)
options=(!emptydirs)
source=(https://github.com/socketry/async-rest/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('ee2550f37e99cb65456cb6d4f6530aff2315e3fe23122a5f256d1b7810c911701d1fae8645e3832842940416bcb4cba4e0a4ccb3b064e498aaec83e6bb8ed0db')
b2sums=('893c1fb04e43a5b2a023146a727d323b76ff3003fb5d7d8e0840db1b21903fd1c47ad861d708a52b68565010b966b22171d01d687b83e2e9c87dac866b532a2c')

prepare() {
  cd async-rest-$pkgver
  sed -r -i \
    -e 's|~>|>=|g' \
    -e '/signing_key/d' \
    async-rest.gemspec
  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd async-rest-$pkgver
  gem build async-rest.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    async-rest-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd async-rest-$pkgver
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd async-rest-$pkgver
  cp -a tmp_install/* "$pkgdir"/
}
