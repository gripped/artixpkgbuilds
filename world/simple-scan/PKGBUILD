# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>

pkgname=simple-scan
pkgver=48.1
pkgrel=1
pkgdesc="Simple scanning utility"
url="https://apps.gnome.org/SimpleScan/"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  cairo
  colord-sane
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  libcolord
  libgusb
  libwebp
  sane
  zlib
)
makedepends=(
  git
  meson
  vala
  yelp-tools
)
groups=(gnome)
source=(
  "git+https://gitlab.gnome.org/GNOME/simple-scan.git#tag=${pkgver/[a-z]/.&}"
  0001-Fix-application-activate.patch
  0002-Rename-desktop-file-to-match-with-the-application-ID.patch
)
b2sums=('3fdcb48949a8a3d3b230e02bc8647ab04fe56eb4ca36e4f05089f920a7f5e1018c687750aa9b9987baf7fd3349b492503c926f5ebc3834ebd3768d588d502707'
        'ed6449989fc2afc5260c1453e29051294f184638ded10eb5b30ef7c171de947dbec857e979a519497a52353a0823f281e6a6fe9b056f989c72c67afa55fab765'
        '261d0af6487e31ab9d86403b714cfa2e45b018b5503df33d96eb37fe73ca418ca77531b0f0b1e32dfd6b84d0455fed1f9eaa60489f56ffa6b5ebe98d57de243f')
validpgpkeys=(
  4D0BE12F0E4776D8AACE9696E66C775AEBFE6C7D # Jeremy Bicha <jeremy.bicha@canonical.com>
)

prepare() {
  cd $pkgname

  # Fix application activate
  # https://gitlab.gnome.org/GNOME/simple-scan/-/merge_requests/284
  git apply -3 ../0001-Fix-application-activate.patch

  # Rename desktop file to match with the application ID
  # https://gitlab.gnome.org/GNOME/simple-scan/-/merge_requests/285
  git apply -3 ../0002-Rename-desktop-file-to-match-with-the-application-ID.patch
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
