# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>

pkgname=simple-scan
pkgver=49.0
pkgrel=1
pkgdesc="Simple scanning utility"
url="https://apps.gnome.org/SimpleScan/"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  cairo
  colord-sane
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  libcolord
  libgusb
  libwebp
  sane
  zlib
)
makedepends=(
  git
  meson
  vala
  yelp-tools
)
groups=(gnome)
source=(
  "git+https://gitlab.gnome.org/GNOME/simple-scan.git#tag=${pkgver/[a-z]/.&}"
)
b2sums=('3bb82d99fd15b4a6e0a88eeeeee6970aed56c8518cf3ff80030460ef778b7e7e8e8b202ebb3a518f090e8e07624eec096d9b9fb0ebbc067cd04b31031a5da6c8')
validpgpkeys=(
  4D0BE12F0E4776D8AACE9696E66C775AEBFE6C7D # Jeremy Bicha <jeremy.bicha@canonical.com>
)

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
