# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgbase=marble
pkgname=(marble
         marble-behaim
         marble-common
         marble-maps
         marble-qt)
pkgver=24.12.0
pkgrel=3
pkgdesc='Desktop Globe'
arch=(x86_64)
url='https://apps.kde.org/marble/'
license=(GPL-2.0-or-later)
makedepends=(extra-cmake-modules
             gpsd
             kdoctools
             knewstuff
             kparts
             krunner
             libplasma
             libwlocate
             phonon-qt6
             protobuf
             qt6-serialport
             qt6-tools
             qt6-webengine
             shapelib)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgbase-$pkgver.tar.xz{,.sig})
sha256sums=('a347f134d1428bd46b886bb41a292a2fec4aea4de274bff73cd8b8994f920576'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgbase-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_CXX_STANDARD=17 \
    -DQT_PLUGINS_DIR=lib/qt6/plugins \
    -DBUILD_QT_AND_KDE=ON \
    -DBUILD_TESTING=OFF \
    -DBUILD_TOUCH=ON \
    -DBUILD_MARBLE_EXAMPLES=OFF \
    -DMOBILE=OFF
  cmake --build build
}

package_marble-common() {
  pkgdesc='Common libraries and plugins for Marble'
  depends=(abseil-cpp
           gcc-libs
           glibc
           phonon-qt6
           protobuf
           qt6-5compat
           qt6-base
           qt6-declarative
           qt6-positioning
           qt6-svg
           qt6-webchannel
           qt6-webengine
           zlib)
  optdepends=('gpsd: GPS based geolocation'
              'libwlocate: WLAN based geolocation'
              'qt6-serialport: APRS plugin'
              'shapelib: SHP plugin')

  DESTDIR="$pkgdir" cmake --install build
  rm -r "$pkgdir"/usr/share/{config.kcfg,kxmlgui5,metainfo} \
        "$pkgdir"/usr/bin \
        "$pkgdir"/usr/lib/qt6/plugins/{kf6,*.so} \
        "$pkgdir"/usr/share/applications/{marble_geo.desktop,marble_worldwind.desktop,org.kde.marble*.desktop} \
        "$pkgdir"/usr/share/icons/hicolor/scalable/apps/org.kde.marble*.svg \
        "$pkgdir"/usr/share/plasma
}

package_marble-qt() {
  pkgdesc+=' (Qt version)'
  depends=(gcc-libs
           glibc
           marble-common
           qt6-base)

  DESTDIR="$pkgdir" cmake --install build/src/apps/marble-qt
}

package_marble() {
  depends=(gcc-libs
           glibc
           kconfig
           kconfigwidgets
           kcoreaddons
           kcrash
           ki18n
           kio
           kparts
           kwidgetsaddons
           kxmlgui
           marble-common
           qt6-base)
  groups=(kde-applications
          kde-education)

  DESTDIR="$pkgdir" cmake --install build/src/apps/marble-kde
  DESTDIR="$pkgdir" cmake --install build/src/plasma
  DESTDIR="$pkgdir" cmake --install build/src/plasmarunner
  DESTDIR="$pkgdir" cmake --install build/src/thumbnailer
  rm -r "$pkgdir"/usr/share/{icons,doc,locale}
}

package_marble-maps() {
  pkgdesc='OpenStreetMap Navigation'
  depends=(gcc-libs
           glibc
           kconfig
           kcoreaddons
           kcrash
           ki18n
           kirigami
           kirigami-addons
           marble-common
           qt6-base
           qt6-declarative
           qt6-webengine)

  DESTDIR="$pkgdir" cmake --install build/src/apps/marble-maps
}

package_marble-behaim() {
  pkgdesc='Behaim Globe'
  depends=(gcc-libs
           glibc
           kcoreaddons
           kcrash
           ki18n
           kirigami-addons
           marble-common
           qt6-base
           qt6-declarative
           qt6-webengine)

  DESTDIR="$pkgdir" cmake --install build/src/apps/behaim
}
