# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=kalzium
pkgver=25.08.2
pkgrel=1
pkgdesc='Periodic Table of Elements'
url='https://apps.kde.org/kalzium/'
arch=(x86_64)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(avogadrolibs
         avogadrolibs-qt
         gcc-libs
         glibc
         kcompletion
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         ki18n
         kio
         kitemviews
         knewstuff
         kplotting
         ktextwidgets
         kwidgetsaddons
         kxmlgui
         kunitconversion
         openbabel
         qt6-5compat
         qt6-base
         qt6-declarative
         qt6-scxml
         qt6-svg)
makedepends=(eigen
             extra-cmake-modules
             facile
             kdoctools
             ocaml
             python)
groups=(kde-applications
        kde-education)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('99f1da3eb04aee31b45d928b5dd30a1675dc204194ba643f8973f4ae6fdbcfed'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
