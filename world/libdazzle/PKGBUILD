# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libdazzle
pkgver=3.44.0
pkgrel=2
pkgdesc="A library to delight your users with fancy features"
url="https://gitlab.gnome.org/Archive/libdazzle"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  cairo
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  pango
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  meson
  vala
)
checkdepends=(xorg-server-xvfb)
provides=(libdazzle-1.0.so)
source=("git+https://gitlab.gnome.org/Archive/libdazzle.git#tag=$pkgver")
b2sums=('24cb4cc73cf5e77f1143bd1a49ab40a41e48de11579d01ee64bd4cce9894b0e56a8bbc3ed6441d1899156f81fe71f0eb4638365117372f48f2f0268007bdcf58')

build() {
  artix-meson $pkgname build -D enable_gtk_doc=true
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
