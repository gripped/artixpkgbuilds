# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-ruby_memcheck
pkgver=3.0.0
pkgrel=1
pkgdesc='Use Valgrind memcheck without going crazy'
arch=(any)
url='https://github.com/Shopify/ruby_memcheck'
license=(MIT)
depends=(
  ruby
  ruby-nokogiri
  valgrind
)
makedepends=(
  git
)
checkdepends=(
  ruby-bundler
  ruby-minitest
  ruby-racc
  ruby-rake
  ruby-rake-compiler
  ruby-rspec-core
)
options=(!emptydirs)
source=(git+https://github.com/Shopify/ruby_memcheck.git#tag=$pkgver)
sha256sums=('16bbffdb6f83f99e4b8f558457356a773d2841ab3a902859a12d042fa8078c6f')

prepare() {
  cd ruby_memcheck
  # Backport file descriptor limit workaround for Valgrind 3.21+:
  # https://github.com/Shopify/ruby_memcheck/pull/63
  git cherry-pick -n 98d79499263d77d49ad774be03a78d2a5e56e376

  # Backport Ruby 3.4 rb_vm_exec suppression:
  # https://github.com/Shopify/ruby_memcheck/pull/69
  git cherry-pick -n 988312b8e192220f1089e28a31c8a8ca3714788b

  # Ruby 3.4's free-at-exit mode intentionally reports native Init_* leaks;
  # remove the artificial test fixture leaks so unrelated checks remain useful.
  sed -i '/Memory leaks in the Init functions should be ignored\./,+1d' \
    test/ruby_memcheck/ext/ruby_memcheck_c_test_one.c \
    test/ruby_memcheck/ext/ruby_memcheck_c_test_two.c
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd ruby_memcheck
  gem build ruby_memcheck.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    ruby_memcheck-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd ruby_memcheck
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd ruby_memcheck
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
