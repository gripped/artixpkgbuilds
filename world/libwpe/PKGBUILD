# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=libwpe
pkgname=(
  libwpe
  libwpe-docs
)
pkgver=1.16.3
pkgrel=1
pkgdesc="General-purpose library for WPE WebKit"
url="https://wpewebkit.org"
arch=(x86_64)
license=(BSD-2-Clause)
depends=(
  gcc-libs
  glibc
  libegl
  libxkbcommon
)
makedepends=(
  clang
  git
  hotdoc
  llvm
  mesa
  meson
)
source=("git+https://github.com/WebPlatformForEmbedded/libwpe?signed#tag=$pkgver")
b2sums=('2bd7f66864fe465093b6ddf02417392dbf9871c56d8736ca29f107787e0b7d5f58b37c1feb602e0167f16b3d685bd1c65ebe7bf5aad6029b5dba742727b13c21')
validpgpkeys=(
  5AA3BC334FD7E3369E7C77B291C559DBE4C9123B  # Adrián Pérez de Castro <aperez@igalia.com>
)

prepare() {
  cd libwpe
}

build() {
  artix-meson libwpe build -D build-docs=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_libwpe() {
  provides=(libwpe-1.0.so)

  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 libwpe/COPYING

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_libwpe-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 libwpe/COPYING
}

# vim:set sw=2 sts=-1 et:
