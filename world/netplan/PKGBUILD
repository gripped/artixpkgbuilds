# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor:  Conrad Hoffmann <ch@bitfehler.net>

pkgname=netplan
pkgver=0.106.1
pkgrel=1
pkgdesc="Network configuration abstraction renderer"
arch=('x86_64')
url="https://github.com/CanonicalLtd/netplan"
license=('GPL3')
depends=(
  'glib2'
  'python-yaml'
  'python-netifaces'
  'iproute2'
  'libelogind'
)
makedepends=(
  'python'
  'pkgconf'
  'pandoc'
  'elogind'
)
source=(
  "0001-no-systemd.patch"
  "https://github.com/CanonicalLtd/${pkgname}/archive/${pkgver}.tar.gz"
)
sha512sums=('02a9145ef718e2360ca3f9b995863eee2ac8a57d759b6344d6468ce8dd2240292f625524e2ef3122155e47249421a30b08e518e6a31e652ffb94bf0eb1cd24ce'
            '0592079888dc6be8802f5d5b01b62e1a570ac1fa340628bab9d7a4a38e796a1cde8c22ec98448e29a83569381035e9d7b06a92418a82547d0b1030151d94a9e2')
b2sums=('552dd7505e15fa48f77a7a9693d67eebb2d8876816d6f164185ad9bceac1b1f077794393d7605ab6a881795cf134a2cd9c1c5b10eabae8775b5afbda3e2b293e'
        '3c77c8193269b372f79366bf31160a53fc59a5fff8f18fb12bcba89073da053e174349acd9ccd05c128023dd4e56d379d380a91ed309e651686dbbecfa5c7fb6')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i ../0001-no-systemd.patch
}

build() {
  cd "${pkgname}-${pkgver}"

  # Drop -Werror to avoid the following error:
  # /usr/include/glib-2.0/glib/glib-autocleanups.h:28:3: error: 'ip_str' may be used uninitialized in this function [-Werror=maybe-uninitialized]
  sed -e "s/-Werror//g" -i Makefile

  env
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" ROOTPREFIX=/usr SBINDIR=/usr/bin install

  # Remove stray file ( Artix )
  rm "$pkgdir"/netplan
}
