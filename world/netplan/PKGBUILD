# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor:  Conrad Hoffmann <ch@bitfehler.net>

pkgname=netplan
pkgver=0.107
pkgrel=1
pkgdesc="Network configuration abstraction renderer"
arch=('x86_64')
url="https://github.com/CanonicalLtd/netplan"
license=('GPL3')
depends=(
  'dbus-python'
  'gcc-libs'
  'glibc'
  'glib2'
  'libyaml'
  'python'
  'python-cffi'
  'python-yaml'
  'python-netifaces'
  'python-rich'
  'iproute2'
  'libelogind'
  'util-linux-libs'
)
makedepends=(
  'bash-completion'
  'cmocka'
  'meson'
  'pandoc'
  'elogind'
  'python-coverage'
  'python-pycodestyle'
  'python-pyflakes'
  'python-pytest'
  'python-pytest-cov'
)
provides=(libnetplan.so)
source=(
  "0001-disable-systemd.patch"
  "$pkgname-$pkgver.tar.gz::$url/archive/${pkgver}.tar.gz"
  "$pkgname-0.107-disable_tests.patch"
)
sha512sums=('f99bec0fbe4d93d58775e711a35078a3194dbe1a330dd4162215d09d4c03680621d0783872551208d1ab786087c19cfd87209276787fc8fcec410a6753c24120'
            '6f6e3e4f179a2a74b46239cbd86919555a769d001b6fc8ba8daba3894b26415dbb8124f6a594de9afc14dc50d6979e58f195adfba2a53854b882d7cb6621fea7'
            '7f0b512a29c458e7e25f1387ce7f6730df47af48b35a5f2915a9f6a57fddf53a2c1d0690dfd81cd2dbbf40efa16f332bac63180ab3e589dcf5087f13775d040b')
b2sums=('36eb577d57697d55558aa863f6734b5d126026e54799c209f741e9097a86e4952a8f7f2d857b7369d20775f9061e40b53cb338962a13a763d5f2f44ca7f45fe2'
        '8fcd3ae48e0f3e1eb0486cf4f7d76d17f61b3b8212363c9f00a1c1e6d265b1093cd7bc283e659091a44ef01c1346f47fc663d43387d72b95305ce2bb2c5b808a'
        'd8f5e36ef67fa43fa7c93eed2fbec54a4e934b9e994178820abda2473be646df009fc537ebc5fc067f8400c0ce3e28f66dfcf681b378cf2363f82acd79930149')

prepare() {
  patch -Np1 -d $pkgname-$pkgver -i ../0001-disable-systemd.patch
  # adapt names of python things to our names
  # TODO: fix this upstream!
  sed -e 's/pytest3/pytest/;s/pyflakes3/pyflakes/;s/coverage-3/coverage/' -i $pkgname-$pkgver/meson.build
  # disable linting tests as they are broken and useless to us
  # disable unit-tests as we run them manually to be able to disable things that are broken
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-0.107-disable_tests.patch
}

build() {
  artix-meson $pkgname-$pkgver build
  meson compile -C build
}

package() {
  rm "$pkgdir"/netplan
  meson install -C build --destdir "$pkgdir"
}
