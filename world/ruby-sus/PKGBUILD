# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-sus
pkgver=0.37.0
pkgrel=1
pkgdesc='A fast and scalable test runner'
arch=(any)
url='https://github.com/socketry/sus'
license=(MIT)
depends=(
  ruby
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-decode
)
options=(!emptydirs)
source=(git+https://github.com/socketry/sus.git#tag=v$pkgver)
sha512sums=('9a7df32de281dc5e40ca8e539a351474e97a47056dce466f994bc086a418c34eecc575e2cab9e32b8781307729130bce349020dfa3237ea4b930813831cdeb75')
b2sums=('5c7852ac2be7b001ed704284e598f9d08cfec32c830a2013b9a5d80b2c5e4ae6c70a387fee3b97fdfedca88de8a77d62ef7db5790ad8d8df1b28e8aa07135cf8')

prepare() {
  cd sus

  sed -r \
    -e 's|~>|>=|g' \
    -e '/signing_key/d' \
    -i sus.gemspec

  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/agent-context/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd sus
  gem build sus.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    sus-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd sus
  PATH="$PWD/tmp_install/usr/bin:$PATH" GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd sus
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 license.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
