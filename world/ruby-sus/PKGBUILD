# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-sus
pkgver=0.33.1
pkgrel=1
pkgdesc='A fast and scalable test runner'
arch=(any)
url='https://github.com/socketry/sus'
license=(MIT)
depends=(
  ruby
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-decode
)
options=(!emptydirs)
source=(git+https://github.com/socketry/sus.git#tag=v$pkgver)
sha512sums=('9402b6752a18debea6b9ba28c3eef3747ceba545cf44dd1bf61ab9ca32e28c756496247278b7363881e035704a9f61cca4ff136b5f0c7d7ccb116e09e11689b8')
b2sums=('c76b611a26024f5924a258fbadaa40f8dfce907aa0bbf9fdfbe023fdc8ef234233c50df2f4115ba508f8ba439bbe18a27a492fbd9b7f86ca08caec25d2437463')

prepare() {
  cd sus

  sed -r \
    -e 's|~>|>=|g' \
    -e '/signing_key/d' \
    -i sus.gemspec

  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/agent-context/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd sus
  gem build sus.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    sus-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd sus
  PATH="$PWD/tmp_install/usr/bin:$PATH" GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd sus
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 license.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
