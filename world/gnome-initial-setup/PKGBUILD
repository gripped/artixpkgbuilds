# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=gnome-initial-setup
pkgver=46.3
pkgrel=1
pkgdesc='Helps you to set up your OS when you boot for the first time'
url='https://gitlab.gnome.org/GNOME/gnome-initial-setup'
arch=('x86_64')
license=('GPL-2.0-or-later')
depends=(
  'accountsservice'
  'fontconfig'
  'gcc-libs'
  'gdk-pixbuf2'
  'gdm'
  'geoclue'
  'geocode-glib-2'
  'glib2'
  'glibc'
  'gnome-control-center'
  'gnome-desktop-4'
  'gnome-keyring'
  'gsettings-desktop-schemas'
  'gtk4'
  'harfbuzz'
  'krb5'
  'libadwaita'
  'libgdm'
  'libgweather-4'
  'libibus'
  'libmalcontent'
  'libnm'
  'libnma-gtk4'
  'libpwquality'
  'libsecret'
  'malcontent'
  'pango'
  'polkit'
  'tecla'
)
makedepends=(
  'git'
  'glib2-devel'
  'meson'
)
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-initial-setup.git?signed#tag=${pkgver/[a-z]/.&}"
)
b2sums=('7138717e56b140e880c0ab0ec290c883499fe4307db105451055b7588fa51d56d2183d79979fafb8e8fc1ed4cd4b16dd3354668a84b7867f2f396644b522f308')
validpgpkeys=(
  1E68E58CF255888301645B563422DC0D7AD482A7 # Will Thompson <will@willthompson.co.uk>
)

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build -Dsystemd=false -D webkitgtk=disabled
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  # Setup system user and group
  echo 'u gnome-initial-setup - "GNOME Initial Setup" /run/gnome-initial-setup' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  echo 'd /run/gnome-initial-setup 0700 gnome-initial-setup gnome-initial-setup -' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
}
