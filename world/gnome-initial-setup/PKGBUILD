# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=gnome-initial-setup
pkgver=51.0
pkgrel=1
pkgdesc='Helps you to set up your OS when you boot for the first time'
url='https://gitlab.gnome.org/GNOME/gnome-initial-setup'
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  accountsservice
  fontconfig
  gdk-pixbuf2
  gdm
  geoclue
  geocode-glib-2
  glib2
  glibc
  gnome-control-center
  gnome-desktop-4
  gsettings-desktop-schemas
  gtk4
  harfbuzz
  json-glib
  krb5
  libadwaita
  libgcc
  libgdm
  libgweather-4
  libibus
  libmalcontent
  libnm
  libnma-gtk4
  libpwquality
  libsecret
  malcontent
  pango
  polkit
  tecla
)
makedepends=(
  git
  glib2-devel
  meson
)
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-initial-setup.git#tag=${pkgver/[a-z]/.&}"
)
b2sums=('c590f5212fc17c159a36c73aff6d0cb3a1fc1dd9cb8036ddbae8d7109d3f1c5430a3d424b6d0ecdb14707a92aa86fbe5c46161a872e51578814785752a0d1ea6')

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build \
    -D webkitgtk=disabled
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"
}
