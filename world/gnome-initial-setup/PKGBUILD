# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=gnome-initial-setup
pkgver=49.0
pkgrel=1
pkgdesc='Helps you to set up your OS when you boot for the first time'
url='https://gitlab.gnome.org/GNOME/gnome-initial-setup'
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  accountsservice
  fontconfig
  gcc-libs
  gdk-pixbuf2
  gdm
  geoclue
  geocode-glib-2
  glib2
  glibc
  gnome-control-center
  gnome-desktop-4
  gsettings-desktop-schemas
  gtk4
  harfbuzz
  krb5
  libadwaita
  libgdm
  libgweather-4
  libibus
  libmalcontent
  libnm
  libnma-gtk4
  libpwquality
  libsecret
  malcontent
  pango
  polkit
  tecla
)
makedepends=(
  git
  glib2-devel
  meson
)
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-initial-setup.git#tag=${pkgver/[a-z]/.&}"
)
b2sums=('1bc491d65c60735716d91fea8c8c89fc26f5a5a120005b7d6d0d70dbd1b6acf5b220b2a52dad315147de36c9cef586d3d854bc74fbc9f6ce871f29998dcfbd5b')

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build \
    -D webkitgtk=disabled
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
