# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=gnome-initial-setup
pkgver=47.3
pkgrel=1
pkgdesc='Helps you to set up your OS when you boot for the first time'
url='https://gitlab.gnome.org/GNOME/gnome-initial-setup'
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  accountsservice
  fontconfig
  gcc-libs
  gdk-pixbuf2
  gdm
  geoclue
  geocode-glib-2
  glib2
  glibc
  gnome-control-center
  gnome-desktop-4
  gnome-keyring
  gsettings-desktop-schemas
  gtk4
  harfbuzz
  krb5
  libadwaita
  libgdm
  libgweather-4
  libibus
  libmalcontent
  libnm
  libnma-gtk4
  libpwquality
  libsecret
  malcontent
  pango
  polkit
  tecla
)
makedepends=(
  git
  glib2-devel
  meson
)
source=("git+https://gitlab.gnome.org/GNOME/gnome-initial-setup.git?signed#tag=${pkgver/[a-z]/.&}"
        "0001-Use-fully-locked-system-account.patch")
b2sums=('28d08288abb1c4a122387fd18d2c2a76bd0a1e0ebbf5b4a8e561149136600f0f73f79ab02192ec8f45db970f16fdf1978c30434d3e84b4bcb9c43b251eda05c0'
        '30e7c97efc1e92e6a3a4f9f077a2d9ecad158d4b7dac7792f0ae57e1c974f051c3a077089403eb7dde0ff52409ddaf36865c9039db7af21589c6e6fc1fa891bb')
validpgpkeys=(1E68E58CF255888301645B563422DC0D7AD482A7) # Will Thompson <will@willthompson.co.uk>

prepare() {
  cd $pkgname

  # Use fully locked system account
  ## https://archlinux.org/todo/change-sysusers-to-fully-locked-system-accounts/
  git apply -3 ../0001-Use-fully-locked-system-account.patch
}

build() {
  artix-meson $pkgname build -Dsystemd=false \
    -D webkitgtk=disabled
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
