# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=jedi-language-server
pkgver=0.43.1
pkgrel=1
pkgdesc='Language server for Jedi'
arch=(any)
url=https://github.com/pappasam/jedi-language-server
license=(MIT)
depends=(
  python-docstring-to-markdown
  python-jedi
  python-pygls
)
makedepends=(
  git
  python-build
  python-installer
  python-poetry-core
)
checkdepends=(
  python-lsp-jsonrpc
  python-pyhamcrest
  python-pytest
)
source=("git+$url.git#tag=v$pkgver")
b2sums=('dfce577c8a4c1d2f6f0861b832814912b59f4942dfb777769668fc7a782c3d101af5cb7fc6986bd320f9d9eb1533dd6fe8bf6638a4287c59ebcf0aaf5c16d69a')

build() {
  cd $pkgname
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $pkgname
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  python -m installer --destdir=temp dist/*.whl
  PATH="$PWD/temp/usr/bin:$PATH" test-env/bin/python -m pytest tests
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/${pkgname//-/_}-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
