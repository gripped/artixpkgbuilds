# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=decibels
pkgver=49.0
pkgrel=1
pkgdesc='Audio player for GNOME'
arch=(any)
url='https://apps.gnome.org/Decibels/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  gjs
  glib2
  graphene
  gst-plugins-bad-libs
  gst-plugins-base
  gst-plugins-base-libs
  gst-plugins-good
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  appstream
  blueprint-compiler
  git
  meson
  typescript
)
optdepends=(
  'gst-libav: Extra media codecs'
  'gst-plugins-bad: Extra media codecs'
  'gst-plugins-ugly: Extra media codecs'
)
groups=(gnome)
source=(
  "git+https://gitlab.gnome.org/GNOME/decibels.git#tag=${pkgver/[a-z]/.&}"
  "git+https://gitlab.gnome.org/BrainBlasted/gi-typescript-definitions.git"
)
b2sums=('330e92db065cdcb78738fe4dd25a6d7efb47274ef4b8590f69c72ad7e0c744fea07b3485949c6b15e1e0b0196b1f977a76ab6b6d28b4a76501c4a55f0c273209'
        'SKIP')

prepare() {
  cd $pkgname

  git submodule init
  git submodule set-url gi-types "$srcdir/gi-typescript-definitions"
  git -c protocol.file.allow=always -c protocol.allow=never submodule update
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  ln -s org.gnome.Decibels "$pkgdir/usr/bin/decibels"
}
