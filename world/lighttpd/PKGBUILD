# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=lighttpd
pkgver=1.4.75
pkgrel=1
pkgdesc='A secure, fast, compliant and very flexible web-server'
license=('BSD-3-Clause')
arch=('x86_64')
url="https://www.lighttpd.net/"
depends=(
	'brotli'
	'etmpfiles'
	'pcre2'
)
makedepends=(
	'e2fsprogs'
	'libdbi'
	'libmaxminddb'
	'libxml2'
	'lua'
	'mariadb-libs'
	'pkgconfig'
	'sqlite'
)
optdepends=(
	'krb5: mod_authn_gssapi'
	'libdbi: mod_authn'
	'libldap: mod_authn_ldap'
	'libmaxminddb: mod_maxminddb'
	'libxml2: mod_webdav'
  'lua: mod_cml/mod_magnet'
  'mariadb-libs: mod_mysql_vhost/mod_authn_mysql'
  'sqlite: mod_webdav'
)
backup=(
	'etc/lighttpd/lighttpd.conf'
	'etc/logrotate.d/lighttpd'
)
options=('emptydirs')
source=(
	"https://download.lighttpd.net/lighttpd/releases-1.4.x/lighttpd-${pkgver}.tar.xz"{,.asc}
  'lighttpd.logrotate.d'
	'lighttpd.conf'
	'lighttpd.tmpfiles'
)
sha256sums=('8b721ca939d312afaa6ef31dcbd6afb5161ed385ac828e6fccd4c5b76be189d6'
            'SKIP'
            '41f6c0042bb61021553779f861910e335834f6c15e4411756cdc6233b31076fe'
            '162a7db641ec46bb3436c4986fe7541aebd6dd38a463b86647c9113278cb876e'
            'da6878d1649572259f29ae50236a129ff131c9b1730af81498581824cf8c4c35')
validpgpkeys=('C7CA1E9E29DC77F5480894B2E0E7D0171E95BAD7'
              '649D0DD767FF206202A76C5158F14A786FE198C8'
              'EAAF41A8BE3BB8D001CACD136DE62CA242909B84')

build() {
	cd "${srcdir}"/$pkgname-$pkgver

	./configure --prefix=/usr \
		--sbindir=/usr/bin \
		--libdir=/usr/lib/lighttpd/ \
		--sysconfdir=/etc/lighttpd \
		--with-attr \
		--with-brotli \
		--with-dbi \
		--with-krb5 \
		--with-ldap \
		--with-lua \
		--with-maxminddb \
		--with-mysql \
		--with-openssl \
		--with-pcre2 \
		--with-webdav-props \
		--with-webdav-locks

	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
	make
}

check() {
	cd "${srcdir}"/${pkgname}-${pkgver}
	make check
}

package() {
	cd "${srcdir}"/${pkgname}-${pkgver}
	make DESTDIR="$pkgdir" install

	install -D -m644 "${srcdir}"/lighttpd.logrotate.d "${pkgdir}"/etc/logrotate.d/lighttpd
	install -D -m644 "${srcdir}"/lighttpd.conf "${pkgdir}"/etc/lighttpd/lighttpd.conf
	install -d -m700 -o 33 -g 33 "${pkgdir}"/var/{log,cache}/lighttpd/
	install -D -m644 "${srcdir}"/lighttpd.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/lighttpd.conf

	pushd doc/config >/dev/null
	find . -type f ! -name 'Makefile*' -exec install -D -m644 {} "${pkgdir}"/usr/share/doc/lighttpd/config/{} \;
	popd >/dev/null

	install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}
