# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Andri Yngvason <andri@yngvason.is>

pkgname=neatvnc
pkgver=1.0.2
pkgrel=1
pkgdesc='Fast and neat VNC server library'
arch=(x86_64)
url=https://github.com/any1/neatvnc
license=(ISC)
depends=(
  glibc
  gmp
  gnutls
  libaml.so
  libavcodec.so
  libdrm
  libpixman-1.so
  libturbojpeg.so
  mesa
  nettle
  zlib
)
makedepends=(
  git
  meson
  ninja
)
provides=(libneatvnc.so)
source=(git+https://github.com/any1/neatvnc.git#tag=v${pkgver})
b2sums=('7435c2b8672a19b324096d76c28c63112b4d731f54426f91d1ea224f24a3ee089e2d234858c995a84e11211d0880f79f1f768156eddae64438d306970861b88e')

prepare() {
  cd neatvnc
  git cherry-pick -n 5b190f0fd9e6b0bfd32752a9115242f87ec36c59
  git cherry-pick -n f97805deaaea489a1c8c851324163a92a3125195
}

build() {
  artix-meson neatvnc build \
    -Djpeg=enabled \
    -Dtls=enabled
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
  install -Dm 644 neatvnc/COPYING -t "${pkgdir}"/usr/share/licenses/neatvnc
}

# vim: ts=2 sw=2 et:
