# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Andri Yngvason <andri@yngvason.is>

pkgname=neatvnc
pkgver=1.0.0
pkgrel=3
pkgdesc='Fast and neat VNC server library'
arch=(x86_64)
url=https://github.com/any1/neatvnc
license=(ISC)
depends=(
  glibc
  gmp
  gnutls
  libaml.so
  libavcodec.so
  libdrm
  libpixman-1.so
  libturbojpeg.so
  mesa
  nettle
  zlib
)
makedepends=(
  git
  meson
  ninja
)
provides=(libneatvnc.so)
source=(git+https://github.com/any1/neatvnc.git#tag=v${pkgver})
b2sums=('0f6bfdccec2f7cc9617161c04c4b8dbddab577598c048d300492c3972e3fe5ac5309965d400e00f9c066b28e9c50003ebe0a78cadcd9259c822f25de0f7c38b1')

prepare() {
  cd neatvnc
  git cherry-pick -n 5b190f0fd9e6b0bfd32752a9115242f87ec36c59
  git cherry-pick -n f97805deaaea489a1c8c851324163a92a3125195
}

build() {
  artix-meson neatvnc build \
    -Djpeg=enabled \
    -Dtls=enabled
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
  install -Dm 644 neatvnc/COPYING -t "${pkgdir}"/usr/share/licenses/neatvnc
}

# vim: ts=2 sw=2 et:
