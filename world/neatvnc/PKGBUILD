# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Andri Yngvason <andri@yngvason.is>

pkgname=neatvnc
pkgver=1.0.0
pkgrel=1
pkgdesc='Fast and neat VNC server library'
arch=(x86_64)
url=https://github.com/any1/neatvnc
license=(custom:ISC)
depends=(
  glibc
  gnutls
  libaml.so
  libavcodec.so
  libdrm
  libpixman-1.so
  libturbojpeg.so
  mesa
  zlib
)
makedepends=(
  git
  aml
  meson
  ninja
)
provides=(libneatvnc.so)
_tag=57cc64b3871c429800e25ed5724b1b2c7a04e134
source=(git+https://github.com/any1/neatvnc.git#tag=${_tag}
        nettle-4.patch)
b2sums=('0f6bfdccec2f7cc9617161c04c4b8dbddab577598c048d300492c3972e3fe5ac5309965d400e00f9c066b28e9c50003ebe0a78cadcd9259c822f25de0f7c38b1'
        'ca18df1a0b648bee2c5ceebd94e0f0922ac7602f6f8fddb0bbe8c04bfaa548593f5bb0dbdf7968d1eaba0ea60a1c51577db538f1ad8848aa1f561c6130e4a6a3')


prepare() {
  cd neatvnc
  patch -p1 -i ../nettle-4.patch
}

build() {
  artix-meson neatvnc build \
    -Djpeg=enabled \
    -Dtls=enabled
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
  install -Dm 644 neatvnc/COPYING -t "${pkgdir}"/usr/share/licenses/neatvnc
}

# vim: ts=2 sw=2 et:
