# Maintainer: George Hu <integral@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: AliReza AmirSamimi <alireza.amirsamimi at gmail dot com>
# Contributor: M A <morealaz at gmail dot com>

pkgname=persepolis
pkgver=5.2.0
pkgrel=3
pkgdesc="Download manager written in Python"
arch=('any')
url="https://${pkgname}dm.github.io"
license=('GPL-3.0-or-later')
depends=('hicolor-icon-theme' 'pyside6' 'python-dasbus' 'python-psutil' 'python-pysocks'
         'python-requests' 'python-setproctitle' 'python-urllib3' 'qt6-multimedia' 'qt6-svg')
makedepends=('meson')
optdepends=('ffmpeg: Download videos from YouTube'
            'yt-dlp: Download videos from YouTube')
source=("https://github.com/${pkgname}dm/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('8d002e369955fd77e5353714185ce3edb98463b7117a26583b72f4db4e51b2c8')

build() {
	artix-meson build "${pkgname}-${pkgver}"
	meson compile -C build
}

package() {
	meson install -C build --destdir="${pkgdir}"
}
