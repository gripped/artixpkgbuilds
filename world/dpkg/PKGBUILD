# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Anatol Pomozov
# Contributor: Carl Smedstad <carsme@archlinux.org>
# Contributor: Alad Wenter <alad (at) archlinux.info>
# Contributor: Jochen Schalanda <jochen+aur (at) schalanda.name>
# Contributor: C. Dominik Bódi <dominik.bodi@gmx.de>
# Contributor: Pierre Carrier <pierre@spotify.com>
# Contributor: Thomas Dziedzic <gostrc (at) gmail>
# Contributor: Chris Giles <Chris.G.27 (at) Gmail.com>
# Contributor: seblu <seblu+arch (at) seblu.net>
# Contributor: squiddo <squiddo (at) intheocean.net>
# Contributor: dront78 <dront78 (at) gmail.com>
# Contributor: hugelgupf <ckoch (at) cs.nmt.edu>

pkgname=dpkg
pkgver=1.23.5
pkgrel=1
pkgdesc='The Debian Package Manager tools'
arch=('x86_64')
url="https://tracker.debian.org/pkg/dpkg"
license=('GPL-2.0-or-later')
depends=(
  'bzip2'
  'gcc-libs'
  'glibc'
  'libmd'
  'ncurses'
  'perl'
  'xz'
  'zlib-ng'
  'zstd'
)
makedepends=(
  'git'
  'perl-io-string'
  'perl-timedate'
)
checkdepends=(
  'perl-io-string'
  'perl-test-pod'
)
options=('!emptydirs')
install=dpkg.install
source=(
  "git+https://git.dpkg.org/git/dpkg/dpkg.git?signed#tag=$pkgver"
  origin.archlinux
  origin.artixlinux
)
b2sums=('70a5abe0c9296a7c340655ade12cc939abeab99bbee61beae4a30880a2df1047dd883c133fc9cdb1817a609e2e606f5c6eb0ef609bf2d962dd09ff897eac00ed'
        '03555ab4aad197ea9d6492268ffdf6727ada21b50275eb7530f6a4647d8ea8dc32a7f67f2cad049ab9a70fe4010da528ea6aedc07279c2fe0403a2bcdf921811'
        '3c0fa14a5a49805829b5ff80a21083d933c42766b7a361288af2817b6d442f0969fa9d1386383b48765adbe49db2232a18992c7217e7b55659c0580002a42ac1')
validpgpkeys=('4F3E74F436050C10F5696574B972BF3EA4AE57A3') # Guillem Jover <guillem@hadrons.org>

prepare() {
  cd $pkgname
  # Fix zsh completions install location
  # https://gitlab.archlinux.org/archlinux/packaging/packages/dpkg/-/issues/1
  sed -i 's|zsh/vendor-completions|zsh/site-functions|' configure.ac

  ./autogen
}

build() {
  cd $pkgname
  CFLAGS+=' -ffat-lto-objects' \
    ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --disable-start-stop-daemon
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -vDm644 /dev/null "$pkgdir/var/lib/dpkg/status"
  install -vDm644 /dev/null "$pkgdir/var/lib/dpkg/available"
  install -vDm644 "$srcdir/origin.archlinux" "$pkgdir/etc/dpkg/origins/archlinux"
  install -vDm644 "$srcdir/origin.artixlinux" "$pkgdir/etc/dpkg/origins/artixlinux"
  ln -vsf artixlinux "$pkgdir/etc/dpkg/origins/default"
}
