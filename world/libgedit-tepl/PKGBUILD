# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=libgedit-tepl
pkgver=6.12.0
pkgrel=2
pkgdesc='Library that eases the development of GtkSourceView-based text editors and IDEs'
arch=('x86_64')
url='https://gitlab.gnome.org/World/gedit/libgedit-tepl'
license=('LGPL-3.0-or-later')
depends=(
  cairo
  glib2
  glibc
  gsettings-desktop-schemas
  gtk3
  icu
  libgedit-amtk
  libgedit-gfls
  libgedit-gtksourceview
  libhandy
  pango
)
makedepends=(
  git
  glib2-devel
  gtk-doc
  gobject-introspection
  meson
)
checkdepends=(xorg-server-xvfb)
conflicts=(tepl)
replaces=(tepl)
provides=(libgedit-tepl-6.so)
source=("git+https://gitlab.gnome.org/World/gedit/libgedit-tepl.git#tag=${pkgver/[a-z]/.&}")
b2sums=('0b68933a8c485dbfe14d1f4f5504a3abadb005297a0dc85c843c9c9c9f3a128632c89570ee355102ffb84c51da6bdcdbaa56b06c4e4de16f53728d07ca728173')

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  xvfb-run -s '-screen 0 1920x1080x24 -nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
