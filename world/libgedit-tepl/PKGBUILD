# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=libgedit-tepl
pkgver=6.13.0
pkgrel=2
pkgdesc='Library that eases the development of GtkSourceView-based text editors and IDEs'
arch=(x86_64)
url='https://gitlab.gnome.org/World/gedit/libgedit-tepl'
license=(LGPL-3.0-or-later)
depends=(
  cairo
  glib2
  glibc
  gsettings-desktop-schemas
  gtk3
  icu
  libgedit-amtk
  libgedit-gfls
  libgedit-gtksourceview
  libhandy
  pango
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  meson
)
checkdepends=(xorg-server-xvfb)
conflicts=(tepl)
replaces=(tepl)
provides=(libgedit-tepl-6.so)
source=("git+https://gitlab.gnome.org/World/gedit/libgedit-tepl.git?signed#tag=${pkgver/[a-z]/.&}")
b2sums=(bfc9d4582727f04e93e206a0f7303ec34d50b54f3435dd372533920b280d037c5b8b25515a86cf090257e9dd4432cc3a7fa67d82be2a44ced6a07d57edee55fb)
validpgpkeys=(08C0A6B1A08310A45363B328426F014A261FD87F) # Sébastien Wilmet <swilmet@mailfence.com>

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  xvfb-run -s '-screen 0 1920x1080x24 -nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
