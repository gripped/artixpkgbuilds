# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Eric Berquist <eric dot berquist at gmail dot com>
# Contributor: Joel Goguen <contact+aur@jgoguen.ca>

pkgname=ruby-mixlib-config
_pkgname="${pkgname#ruby-}"
pkgver=3.1.2
pkgrel=1
pkgdesc='A class based configuration library'
arch=(any)
url='https://github.com/chef/mixlib-config'
license=(Apache-2.0)
depends=(ruby-tomlrb)
makedepends=(git ruby-rdoc ruby-rake ruby-bundler ruby-rspec)
options=(!emptydirs)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('05dd9c1216297548b355bd182b88a8aac34b8f0b9045f89f8c68fa5218f65a58c99aa96932c4c9778279f1fa37283f17ca4f7f9461e853060bc8671808785d34')
b2sums=('158f89c4aab6edfcaf9da40f4b39721155aa19862d4b924352b7e60a96878f922b3d76ceed898059cc5f9dc3bf071bf20eb176c03208b25cdf06a48221c9a57d')

build() {
  cd "$pkgname"

  rake build
}

package() {
  local _gemdir="$(gem env gemdir)"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$pkgname/pkg/$_pkgname-$pkgver.gem"

  # delete cache
  rm -vrf "$pkgdir/$_gemdir/cache"

  # license
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  ln -sf "$_gemdir/gems/$_pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
