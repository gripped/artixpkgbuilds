# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Eric Berquist <eric dot berquist at gmail dot com>
# Contributor: Joel Goguen <contact+aur@jgoguen.ca>

pkgname=ruby-mixlib-config
_pkgname="${pkgname#ruby-}"
pkgver=3.1.3
pkgrel=1
pkgdesc='A class based configuration library'
arch=(any)
url='https://github.com/chef/mixlib-config'
license=(Apache-2.0)
depends=(ruby-tomlrb)
makedepends=(git ruby-rdoc ruby-rake ruby-bundler ruby-rspec)
options=(!emptydirs)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('420b1c26320b6cdd0a177c7b12410265e8b2ac89fa5549b3eafcaa7b078e79f1e49a23e0c4c30671dd53f6314117b2fda8ed2092f2b3755cca517313cfb79cbe')
b2sums=('416a41cc5ef70ebcbeb7055a51f28bf47278af5f63d88d93630febb3c83de497c63918662be7be84d4c4f7f36c623dec1597f817bc64cbb94c00e212083deeda')

build() {
  cd "$pkgname"

  rake build
}

package() {
  local _gemdir="$(gem env gemdir)"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$pkgname/pkg/$_pkgname-$pkgver.gem"

  # delete cache
  rm -vrf "$pkgdir/$_gemdir/cache"

  # license
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  ln -sf "$_gemdir/gems/$_pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
