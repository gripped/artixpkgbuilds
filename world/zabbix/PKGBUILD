# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Florian Pritz <bluewind@xinu.at>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgbase=zabbix
pkgname=('zabbix-common' 'zabbix-server' 'zabbix-frontend-php' 'zabbix-web-service' 'zabbix-proxy' 'zabbix-agent' 'zabbix-agent2')
pkgver=7.4.6
pkgrel=2
pkgdesc="The universal, open-source observability solution for IT & OT"
url="https://www.zabbix.com"
arch=('x86_64')
license=('GPL-2.0-only')
makedepends=('postgresql-libs' 'libxml2' 'unixodbc' 'net-snmp' 'mariadb-libs' 'libldap' 'libevent' 'go' 'pkgconfig' 'pcre2' 'openipmi' 'git')
source=("git+https://git.zabbix.com/scm/zbx/zabbix.git#tag=${pkgver}"
        'zabbix-common.sysusers'
        'zabbix-common.tmpfiles'
        'zabbix-server.tmpfiles'
        'zabbix-web-service.tmpfiles'
        'zabbix-proxy.tmpfiles'
        'zabbix-agent.tmpfiles'
        'zabbix-agent2.tmpfiles'
        'write_log_to_syslog.patch'
        'set_socket_paths.patch'
        'reproducible_build.patch')
sha512sums=('0c97737dbc57667b9b28a61b92bd00165d49eed847346015cd305f919cbfc8013a5b60948f2d9886d8e1c87a8ff79610802f9a10ff41e7f4f6a885556cc44f33'
            '8570843ad927d900f9e9aa95dfa8a24ebb0f01beda3073b24c5963aedd92e3ebed75bbb6139127830eaf289df233c2734d81b52b25a1b8f4db57336152514194'
            '7c39d1cb1d269e8a2a22c634612c7d110e9e67c170f32add33312f97bc2e7f8f19bf5bb1d1c71df43afc893043472069b9f828dfc5a9f2946606798ed54c73a0'
            '3123591ea18b285db8dd40119e4699e022369b49c460184a2d9d8560899e9624a618859a21d66c3dbca30c31cea0a594ddd786a16a2eb13e7a989b0c00b02d07'
            '8932c4feda770c976ef1e326a555c0ed4c7a38062a8127c1ddcf2c927ea0c32012bc5a2f6f7b21cfb8f3b9935a7b637097ffa3a7f8a04211280f0ffd15e84a6a'
            'c82056591146e7ef0020a3cd9865322407a116e20beb1ddd6fc2afdec09306b6d249d9a2afe8d9234932861b506b3b50a3b528944ab3889c076e5088cd7e63ed'
            'e0c5fd049af65f8bab2759f5ca877b2a4d501c0804bc675720ae8d83fcd26cc3340e4b24f21a19f90be394fa52eb96092b4c5fdeedfef65536a1e56ca381cb2f'
            '3c3e64be4dd050b3cc483098e1a2165c87ba9a4a421aa29f1bc49b9536ce446457949bf85b3bd87cd3f1842b6c4d5db675e343078ef90820557a8278293019f7'
            '28fbdf6261ccbcd2f859d1e0e193a0097b0001945abf467071974c9044b00d17c342aeced91aa36e7ce7cb03d217a3e2242c59060b53e0cae192c36fcd4ce4d5'
            '2d95f9acd77dd589b6edda142e26e2204e3f3f0488000d7a519792a729ff2afde21c36caeffd66b3088d6a8166a419e4412c836aeb98a134f791ec887a841dbe'
            'ba43cbff7af59dca1ae53ab88490e07c79bb85e7fc697ba07bf5f8da216743f562fc861c59154f9051b174fb6935799e8185602139a5b4252906b51b32c8a989'
            'd4d7fd47bb0aaaec02802edb1d6895791300ab2491c71ff9dc59ec26659c906918d0ea9162b4bc799e358abef186356544e5a2cc9e14a6ec82c08f6240bc0946'
            'ab3ecc109e44dea93ace28ada44d2b3e1163649a5a5a041a3095c76ff5b2f8885039033261df607fbd430be6499cbe7a8c1dbecdb0ca23f4270f2cd86d7c058f'
            '02c2107d37221ce1a987d4aaea3673008df2553f00a6a0b044e3f4bb39a52b2ff68477575cc968209fd397ecfe27a21bf89a0d3ca2010e7c942c2d9546df4d8d'
            'cb2a6571edb88db442f107717fba636e45bbeab2c9fc08d4d9d110a1d808299e3ba384d1ce1496deb9013f8724218bb0ed5ea2e0b25769a6cce02eda98e47560'
            '0f8d0db314f8e0ed1cfc1bc1951aaa81455f52ea3012f2eb54bed37056d7d89ad1dd22af18039f1ce9c96430050b2b30e4d24ff8606f3f364f1596838435252b'
            '49a197a97536745818664593cf597540eb51e1f0c2429efea6be9ab85ee57f2a4b6e72a197bf02440c9b49a80a7ea0417f9b5a23bef003dc3337d7f08792cbf5'
            '55dec0ef08d54e7972421e3958119cb7df9e0ef10ff6c58836306d4ad6e3724fb902e819070d76e4a5fa24428610d838db7eb0425f05b19493b5e2c9b5919931'
            '71aaf2cdf6885547931c212fa312b4d684af1b765be78f7ad4cded4700305d81602d76fae859338357ef9dda8c7fe265c829ed1a4a964e67591f4390def1b23b')

prepare() {
	cd "${pkgbase}"

	./bootstrap.sh

	# Write logs to syslog (rather than log files)
	patch -Np1 -i "${srcdir}/write_log_to_syslog.patch"

	# Set socket paths to `/run/zabbix`
	patch -Np1 -i "${srcdir}/set_socket_paths.patch"

	# Honor SDE for reproducible builds
	patch -Np1 -i "${srcdir}/reproducible_build.patch"

	autoreconf -fiv

	# Clean up git residues and delete development tests
	find . -type f -name ".gitignore" -exec rm {} \;
	rm -rf ui/tests
}

build() {
	export GOPATH="${srcdir}/go"
	export AGENT_LDFLAGS="${LDFLAGS}"

	_configure_flags=(
		--disable-static
		--prefix=/usr
		--infodir=/usr/share/info
		--mandir=/usr/share/man
		--sysconfdir=/etc/zabbix
		--enable-agent
		--enable-agent2
		--enable-ipv6
		--with-ldap
		--with-libcurl
		--with-libxml2
		--with-net-snmp
		--with-openssl
		--with-libpcre2
		--with-ssh2
		--with-unixodbc
		--with-openipmi
		--enable-webservice
	)

	cd "${pkgbase}"

	for db in postgresql mysql; do
		./configure "${_configure_flags[@]}" --enable-server --with-"${db}"
		make clean
		make gettext
		make dbschema
		make
		mkdir -p "${srcdir}/copies/database_server/${db}"
		cp database/"${db}"/*.sql "${srcdir}/copies/database_server/${db}/"
		mv src/zabbix_server/zabbix_server{,_"${db}"}
	done

	for db in postgresql mysql sqlite3; do
		./configure "${_configure_flags[@]}" --enable-proxy --with-"${db}"
		make clean
		make gettext
		make dbschema
		make
		mkdir -p "${srcdir}/copies/database_proxy/${db}"
		cp database/"${db}"/*.sql "${srcdir}/copies/database_proxy/${db}/"
		mv src/zabbix_proxy/zabbix_proxy{,_"${db}"}
	done
}

package_zabbix-common() {
	pkgdesc="Zabbix user and group required by zabbix packages"

	install -Dm 644 "${pkgname}.sysusers" "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
	install -Dm 644 "${pkgname}.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
}

package_zabbix-server() {
	pkgdesc="Monitoring software for networks and applications"
	depends=('zabbix-common' 'net-snmp' 'curl' 'libxml2' 'unixodbc' 'libldap' 'libevent' 'pcre2' 'openipmi')
	optdepends=('postgresql-libs: for PostgreSQL support'
	            'mariadb-libs: for MariaDB support'
	            'zabbix-web-service: for scheduled PDF report generation')
	backup=('etc/zabbix/zabbix_server.conf')

	cd "${pkgbase}"

	for db in postgresql mysql; do
		install -Dm 755 "src/zabbix_server/zabbix_server_${db}" "${pkgdir}/usr/bin/zabbix_server_${db}"
		install -d "${pkgdir}/usr/share/${pkgname}/${db}"
		install -m 644 "${srcdir}/copies/database_server/${db}"/*.sql -t "${pkgdir}/usr/share/${pkgname}/${db}/"
	done

	install -Dm 755 src/zabbix_get/zabbix_get "${pkgdir}/usr/bin/zabbix_get"

	install -Dm 644 man/zabbix_server.man "${pkgdir}/usr/share/man/man8/zabbix_server.8"
	install -Dm 644 man/zabbix_get.man "${pkgdir}/usr/share/man/man1/zabbix_get.1"

	install -Dm 644 conf/zabbix_server.conf "${pkgdir}/etc/zabbix/zabbix_server.conf"
	install -Dm 644 "${srcdir}/${pkgname}.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"

}

package_zabbix-frontend-php() {
	pkgdesc="PHP frontend for Zabbix"
	depends=('zabbix-server' 'php' 'php-gd')

	cd "${pkgbase}"

	install -d "${pkgdir}/usr/share/webapps/zabbix"
	cp -av ui/* "${pkgdir}/usr/share/webapps/zabbix"

	# Write access for http user
	chown 33:33 "${pkgdir}/usr/share/webapps/zabbix/conf/"
}

package_zabbix-web-service() {
	pkgdesc="Zabbix web service used to generate and send scheduled reports"
	depends=('zabbix-common' 'chromium')
	backup=('etc/zabbix/zabbix_web_service.conf')

	cd "${pkgbase}"

	install -Dm 755 src/go/bin/zabbix_web_service "${pkgdir}/usr/bin/zabbix_web_service"

	install -Dm 644 src/go/conf/zabbix_web_service.conf "${pkgdir}/etc/zabbix/zabbix_web_service.conf"
	install -Dm 644 "${srcdir}/${pkgname}.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"

}

package_zabbix-proxy() {
	pkgdesc="Data collecting proxy for Zabbix"
	depends=('zabbix-common' 'net-snmp' 'curl' 'libxml2' 'sqlite' 'unixodbc' 'libldap' 'pcre2' 'libevent' 'openipmi')
	optdepends=('mariadb-libs: for MariaDB support'
	            'postgresql-libs: for PostgreSQL support')
	backup=('etc/zabbix/zabbix_proxy.conf')

	cd "${pkgbase}"

	for db in postgresql mysql sqlite3; do
		install -Dm 755 "src/zabbix_proxy/zabbix_proxy_${db}" "${pkgdir}/usr/bin/zabbix_proxy_${db}"
		install -Dm 644 "${srcdir}/copies/database_proxy/${db}/schema.sql" "${pkgdir}/usr/share/${pkgname}/${db}/schema.sql"
	done

	install -Dm 644 conf/zabbix_proxy.conf "${pkgdir}/etc/zabbix/zabbix_proxy.conf"
	install -Dm 644 "${srcdir}/${pkgname}.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"

}

package_zabbix-agent() {
	pkgdesc="Monitoring agent for Zabbix"
	depends=('zabbix-common' 'curl' 'pcre2')
	backup=('etc/zabbix/zabbix_agentd.conf')

	cd "${pkgbase}"

	install -Dm 755 src/zabbix_agent/zabbix_agentd "${pkgdir}/usr/bin/zabbix_agentd"
	install -Dm 755 src/zabbix_sender/zabbix_sender "${pkgdir}/usr/bin/zabbix_sender"

	install -Dm 644 conf/zabbix_agentd.conf "${pkgdir}/etc/zabbix/zabbix_agentd.conf"
	install -Dm 644 "${srcdir}/${pkgname}.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"

	install -Dm 644 conf/zabbix_agentd/userparameter_examples.conf "${pkgdir}/usr/share/zabbix-agent/userparameter_examples.conf"
	install -Dm 644 conf/zabbix_agentd/userparameter_mysql.conf "${pkgdir}/usr/share/zabbix-agent/userparameter_mysql.conf"

	install -Dm 644 man/zabbix_agentd.man "${pkgdir}/usr/share/man/man8/zabbix_agentd.8"
	install -Dm 644 man/zabbix_sender.man "${pkgdir}/usr/share/man/man1/zabbix_sender.1"

}

package_zabbix-agent2() {
	pkgdesc="Monitoring agent for Zabbix (version 2)"
	depends=('zabbix-common' 'curl' 'pcre2')
	backup=('etc/zabbix/zabbix_agent2.conf')

	cd "${pkgbase}"

	install -Dm 755 src/go/bin/zabbix_agent2 "${pkgdir}/usr/bin/zabbix_agent2"

	mkdir -p "${pkgdir}/etc/zabbix/zabbix_agent2.d/plugins.d/"
	install -Dm 644 src/go/conf/zabbix_agent2.conf "${pkgdir}/etc/zabbix/zabbix_agent2.conf"
	install -Dm 644 "${srcdir}/${pkgname}.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"

	install -Dm 644 man/zabbix_agent2.man "${pkgdir}/usr/share/man/man8/zabbix_agent2.8"

}
