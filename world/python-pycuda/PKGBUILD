# Maintainer: Torsten Keßler <tpkessler@archlinux.org>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>

_name=pycuda
pkgname=python-pycuda
pkgver=2025.1.2
pkgrel=5
pkgdesc="Python wrapper for Nvidia CUDA"
arch=(x86_64)
url="https://documen.tician.de/pycuda/"
license=(
  Apache-2.0
  MIT
)
depends=(
  boost-libs
  cuda
  gcc-libs
  glibc
  nvidia-utils
  python
  python-numpy
  python-pytools
)
makedepends=(
  boost
  ctags
  git
  mesa
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
)
provides=(pycuda-headers)
conflicts=(pycuda-headers)
replaces=(pycuda-headers)
source=("git+https://github.com/inducer/pycuda.git#tag=v${pkgver}"
        git+https://github.com/inducer/compyte)
sha512sums=('5aca325c694ca16bfd522ba47adcdd138080241832db118684008e9e63c36b456ca63f94428cfc32323fc3f2a98ee1c2b8f1c60a76eb58ee2771d63faf5e0fbe'
            'SKIP')
b2sums=('0e2b556e586f5267994b81125b1a7d3f84b4a72618b9acd4e2fa941d9ddad87d3121028d658b680787bd3ea0e0e6b8e394edea8d6c0f02cad7562094a16de564'
        'SKIP')

prepare() {
  cd $_name
  git submodule init
  git config submodule.pycuda/compyte.url ../compyte
  git -c protocol.file.allow=always submodule update

  local lib_arch=''
  [[ "$CARCH" = "x86_64" ]] && lib_arch='64'

  # create local siteconf.py for build system
  # defaults can be compared from running the ./configure.py script
  {
    printf "BOOST_PYTHON_LIBNAME = ['boost_python3']\n"
    printf "CUDA_ROOT = '/opt/cuda'\n"
    printf "USE_SHIPPED_BOOST = False\n"
  } > siteconf.py

  # we ship python-numpy
  sed -e 's/oldest-supported-numpy/numpy/' -i pyproject.toml
}

build() {
  cd $_name
  python -m build --wheel --no-isolation
}

# requires an Nvidia graphics card to run
# check() {
#   local pytest_options=(
#     -vv
#     --ignore examples/from-wiki/simple_speed_test.py
#   )
#   local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

#   cd $_name
#   # install to temporary location, as importlib is used
#   python -m installer --destdir=test_dir dist/*.whl
#   export PYTHONPATH="test_dir/$site_packages:$PYTHONPATH"
#   pytest "${pytest_options[@]}"
# }

package() {
  cd $_name
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
 
