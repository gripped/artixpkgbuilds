# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: l0gic <l0gic@invalid.invalid>

pkgname=hyprland-protocols
pkgver=0.6.3
pkgrel=1
pkgdesc='Wayland protocol extensions for Hyprland'
arch=(x86_64 aarch64)
url="https://github.com/hyprwm/$pkgname"
license=(BSD-3-Clause)
makedepends=(meson)
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('7ddadfe117f5f314c7f73937a56cdbd65c42fb8fb90054201944c1ec8dae53a8')

build() {
	artix-meson "$_archive" build
	meson compile -C build
}

package() {
	DESTDIR="$pkgdir" meson install -C build
	cd "$_archive"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
