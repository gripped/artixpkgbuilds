# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>

_name=PyMuPDF
pkgname=python-pymupdf
pkgver=1.24.0
pkgrel=1
pkgdesc="Python bindings for MuPDF's rendering library"
arch=(x86_64)
url="https://github.com/pymupdf/PyMuPDF"
license=(AGPL-3.0-or-later)
depends=(
  gcc-libs
  glibc
  libmupdf
  python
  python-mupdf
)
makedepends=(
  python-build
  python-distro
  python-installer
  python-psutil
  python-setuptools
  python-wheel
  swig
)
checkdepends=(
  python-fonttools
  python-pillow
  python-pytest
  tesseract-data-eng
)
optdepends=(
  #'python-pymupdf-fonts: for extra fonts'  # TODO: package
  'python-fonttools: for building font subsets using fontTools'
  'python-pillow: for image file saving using pillow'
)
source=(
  $_name-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz
  remove-clang-and-swig-dependencies.patch
)
sha512sums=('2980701e47d01e20a582abdf9a18f9c67bf887834a0e3e7217f963bfe9e35e62836b9bea5591e5194159f907b95d09cebe63ea8d7bbe0177bae637d247f52e03'
            'fb2da9db1ba9b34ecfe6556d912659c36f9603eefeaa9836d2b692f42952955a6de10d8629a8f2bafd813aaf678782aca12033031b6ccc52c1bc1a4ee5fb897c')
b2sums=('c313c7ffc8a127b5ac5e642b77b86ef07c594808d2c3ada8e66e8d4ddd5aa5d1268edc9d8ab11cd211b7ad86ecae6e0c860ff4d49b5d6b4fad33179a6dd1fc01'
        '8c4f93146385bd2e74b8245b4b8d8ece390ef97ad369602adcfcc56c8f9858f4c754886e290f0f6bfea1727fca6897715f3e8eeb961609fdad6fadf57493e354')

prepare() {
  # remove bundled mupdf sources
  rm -frv $pkgname/*.tgz
  # we package clang and swig and don't need python-clang or python-swig
  cd $_name-$pkgver
  patch -Np1 < $srcdir/remove-clang-and-swig-dependencies.patch
}

build() {
  local cflags=(
    -I/usr/include
    -I/usr/include/freetype2
    -I/usr/include/harbuzz
    -I/usr/include/mupdf
  )
  local ldflags=(
    -lfreetype
    -lgumbo
    -lharfbuzz
    -ljbig2dec
    -ljpeg
    -lleptonica
    -lmupdf
    -lopenjp2
    -ltesseract
  )

  cd $_name-$pkgver
  # build against system libmupdf
  export PYMUPDF_SETUP_MUPDF_BUILD=''
  # provide tessdata location
  export TESSDATA_PREFIX="/usr/share/tessdata"
  # build against mupdf's C++/ Python language bindings
  export PYMUPDF_SETUP_IMPLEMENTATIONS=b
  CFLAGS+=" ${cflags[@]}"
  LDFLAGS+=" ${ldflags[@]}"

  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    # disable broken test, that upstream is not interested in fixing or disabling: https://github.com/pymupdf/PyMuPDF/issues/2127
    --deselect tests/test_pixmap.py::test_color_count
    --deselect tests/test_pixmap.py::test_3050
    # disable test that shells out to pip: https://github.com/pymupdf/PyMuPDF/issues/2950
    --deselect tests/test_font.py::test_fontarchive
    --deselect tests/test_general.py::test_subset_fonts
    # we do not care about flake8
    --deselect tests/test_flake8.py::test_flake8
  )
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$_site_packages:$PYTHONPATH"
  export PATH="$PWD/test_dir/usr/bin:$PATH"
  pytest "${pytest_options[@]}" tests/
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
