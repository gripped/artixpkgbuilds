# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>

_name=PyMuPDF
pkgname=python-pymupdf
pkgver=1.26.7
pkgrel=3.1
pkgdesc="Python bindings for MuPDF's rendering library"
arch=(x86_64)
url="https://github.com/pymupdf/PyMuPDF"
license=(AGPL-3.0-or-later)
depends=(
  gcc-libs
  glibc
  libmupdf
  python
  python-mupdf
)
makedepends=(
  python-build
  python-distro
  python-installer
  python-psutil
  python-setuptools
  python-wheel
  swig
  udev
)
checkdepends=(
  python-fonttools
  python-pillow
  python-pytest
  tesseract-data-eng
)
optdepends=(
  #'python-pymupdf-fonts: for extra fonts'  # TODO: package
  'python-fonttools: for building font subsets using fontTools'
  'python-pillow: for image file saving using pillow'
)
source=(
  $_name-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz
  remove-clang-and-swig-dependencies.patch
  remove-pip-call-in-tests.patch
)
sha512sums=('69bfaddf820dfe39f6d21c6e3b41946d32923dfa87750a881d1716706929a3b75b59b929a20b96cb3ae8f4c0ebe73024b545defcf4b2684cf079146639e4d938'
            '8362c20cad0df07f14493b1456ca8ffa880a363c95c336a5ba102a7089c93e409e9abc9707653e9073500964b4cb17354d92206748f8fedb4e3815164a90fb7a'
            '8ddd524e124d579091f98b75ab02ed87a7dc431c1d85cf9b8da645a4935d3f7c1361d6796618cb1da05be95be05a694ab9b039a42c92012434cda6283158a0e7')
b2sums=('f26595d88b2249e850e40fbcea588dd7e3ac652d3357176559049e564394a7f22656fd2f7a41eabee6666999bcc99d7a7e2e83b61e2abd9c421a14d90e9f23a1'
        '8bc15767ceeb691d3b4d43bebaf69978e4e24e654537f2cbe6de852b98db523f1b30f45545c3bfa48b915b05af1fb85925350c50e57fdbe5409c6725036d375c'
        '411b25296ed19f033270fe62afe12fe635c6c11e7d5d078188c76bef193e28ec2b461450d1366c80e2edad919baf730342ca84f02d9adbfbe7f3bce912884d88')

prepare() {
  # remove bundled mupdf sources
  rm -frv $pkgname/*.tgz
  cd $_name-$pkgver
  # we package clang and swig and don't need python-clang or python-swig
  patch -Np1 < $srcdir/remove-clang-and-swig-dependencies.patch
  # patch out hardcoded pip call to install test dependencies
  patch -Np1 < $srcdir/remove-pip-call-in-tests.patch
}

build() {
  local cflags=(
    -I/usr/include
    -I/usr/include/freetype2
    -I/usr/include/harbuzz
    -I/usr/include/mupdf
  )
  local ldflags=(
    -lfreetype
    -lgumbo
    -lharfbuzz
    -ljbig2dec
    -ljpeg
    -lleptonica
    -lmupdf
    -lopenjp2
    -ltesseract
  )

  cd $_name-$pkgver
  # build against system libmupdf
  export PYMUPDF_SETUP_MUPDF_BUILD=''
  # provide tessdata location
  export TESSDATA_PREFIX="/usr/share/tessdata"
  # build against mupdf's C++/ Python language bindings
  export PYMUPDF_SETUP_IMPLEMENTATIONS=b
  CFLAGS+=" ${cflags[@]}"
  LDFLAGS+=" ${ldflags[@]}"

  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    # disable broken test, that upstream is not interested in fixing or disabling: https://github.com/pymupdf/PyMuPDF/issues/2127
    --deselect tests/test_pixmap.py::test_color_count
    --deselect tests/test_pixmap.py::test_3050
    --deselect tests/test_pixmap.py::test_3854
    --deselect tests/test_textextract.py::test_4180
    # disable test that shells out to pip: https://github.com/pymupdf/PyMuPDF/issues/2950
    --deselect tests/test_font.py::test_fontarchive
    --deselect tests/test_general.py::test_subset_fonts
    --deselect tests/test_general.py::test_4533
    --deselect tests/test_general.py::test_4702
    # we do not care about flake8
    --deselect tests/test_flake8.py::test_flake8
    # no point testing code quality or spellcheck on our side
    --deselect tests/test_pylint.py::test_pylint
    --deselect tests/test_codespell.py::test_codespell
    # some textbox stuff again: https://github.com/pymupdf/PyMuPDF/issues/3398
    --deselect tests/test_textbox.py::test_textbox3
    # tries to download / install stuff through git and pip
    --deselect tests/test_barcode.py::test_barcode
    --deselect tests/test_font.py::test_4457
    --deselect tests/test_general.py::test_open2
    --deselect tests/test_pixmap.py::test_4445
  )
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$_site_packages:$PYTHONPATH"
  export PATH="$PWD/test_dir/usr/bin:$PATH"
  pytest "${pytest_options[@]}" tests/
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
 
