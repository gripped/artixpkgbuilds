# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>

_name=PyMuPDF
pkgname=python-pymupdf
pkgver=1.24.5
pkgrel=1
pkgdesc="Python bindings for MuPDF's rendering library"
arch=(x86_64)
url="https://github.com/pymupdf/PyMuPDF"
license=(AGPL-3.0-or-later)
depends=(
  gcc-libs
  glibc
  libmupdf
  python
  python-mupdf
)
makedepends=(
  python-build
  python-distro
  python-installer
  python-psutil
  python-setuptools
  python-wheel
  swig
  udev
)
checkdepends=(
  python-fonttools
  python-pillow
  python-pytest
  tesseract-data-eng
)
optdepends=(
  #'python-pymupdf-fonts: for extra fonts'  # TODO: package
  'python-fonttools: for building font subsets using fontTools'
  'python-pillow: for image file saving using pillow'
)
source=(
  $_name-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz
  remove-clang-and-swig-dependencies.patch
)
sha512sums=('b86e757aca3d55b7633669a6c4f106975f08d4d93c360bb695b50615f7f345b3021d04d1a6d12bd6933448fa77a23415fe34cf62c9d5ab8f285d62e00594c9bd'
            '3ad7fcc092288fd973a6fdaff5702919473607bcc6007d1a8e464d613a11a550bb5a06f0ebab8b171e1188be702abbecc999a0ad0d2fca121516b69f7d36bae5')
b2sums=('0862b1b19408e3361bba3380a0ce4186ef1ca7dac509fa16daca31a1e13728f4126655e8d612c899a2308fd63f9cd03138a47a6c02ecd6f78dce674281d8b101'
        'a0307f03e70f18547e095d5f9e80776e7e7ef38da170fa331a77a4465488f619228cd2367976f3be5fced6a77fe5e639c1d321c636b8a8825aa6310787aa1d39')

prepare() {
  # remove bundled mupdf sources
  rm -frv $pkgname/*.tgz
  # we package clang and swig and don't need python-clang or python-swig
  cd $_name-$pkgver
  patch -Np1 < $srcdir/remove-clang-and-swig-dependencies.patch
}

build() {
  local cflags=(
    -I/usr/include
    -I/usr/include/freetype2
    -I/usr/include/harbuzz
    -I/usr/include/mupdf
  )
  local ldflags=(
    -lfreetype
    -lgumbo
    -lharfbuzz
    -ljbig2dec
    -ljpeg
    -lleptonica
    -lmupdf
    -lopenjp2
    -ltesseract
  )

  cd $_name-$pkgver
  # build against system libmupdf
  export PYMUPDF_SETUP_MUPDF_BUILD=''
  # provide tessdata location
  export TESSDATA_PREFIX="/usr/share/tessdata"
  # build against mupdf's C++/ Python language bindings
  export PYMUPDF_SETUP_IMPLEMENTATIONS=b
  CFLAGS+=" ${cflags[@]}"
  LDFLAGS+=" ${ldflags[@]}"

  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    # disable broken test, that upstream is not interested in fixing or disabling: https://github.com/pymupdf/PyMuPDF/issues/2127
    --deselect tests/test_pixmap.py::test_color_count
    --deselect tests/test_pixmap.py::test_3050
    # disable test that shells out to pip: https://github.com/pymupdf/PyMuPDF/issues/2950
    --deselect tests/test_font.py::test_fontarchive
    --deselect tests/test_general.py::test_subset_fonts
    # we do not care about flake8
    --deselect tests/test_flake8.py::test_flake8
    # no point testing code quality on our side
    --deselect tests/test_pylint.py::test_pylint
    # some textbox stuff again: https://github.com/pymupdf/PyMuPDF/issues/3398
    --deselect tests/test_textbox.py::test_textbox3
  )
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$_site_packages:$PYTHONPATH"
  export PATH="$PWD/test_dir/usr/bin:$PATH"
  pytest "${pytest_options[@]}" tests/
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
