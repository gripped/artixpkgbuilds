# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>

_name=PyMuPDF
pkgname=python-pymupdf
pkgver=1.26.4
pkgrel=2
pkgdesc="Python bindings for MuPDF's rendering library"
arch=(x86_64)
url="https://github.com/pymupdf/PyMuPDF"
license=(AGPL-3.0-or-later)
depends=(
  gcc-libs
  glibc
  libmupdf
  python
  python-mupdf
)
makedepends=(
  python-build
  python-distro
  python-installer
  python-psutil
  python-setuptools
  python-wheel
  swig
  udev
)
checkdepends=(
  python-fonttools
  python-pillow
  python-pytest
  tesseract-data-eng
)
optdepends=(
  #'python-pymupdf-fonts: for extra fonts'  # TODO: package
  'python-fonttools: for building font subsets using fontTools'
  'python-pillow: for image file saving using pillow'
)
source=(
  $_name-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz
  remove-clang-and-swig-dependencies.patch
)
sha512sums=('0adb7b1d0b21080f34ab4263ffcde5fb6b12672cdbde20f3b71ee0b21eb4f3f4b7460b5fc62b63df7b804fd69d4166d754abf557b75a36c02982069a8ea0d3b7'
            'e92face12865334f6730438c5d6973583f6fee18714a3e4d3e76ac6717fa1aad3416e400858c8dda319717facbf37b2b22793781342afeff6eb6183d97d9de63')
b2sums=('1ce65954f570c6960e98afd96d739c9658a9e544a8422ed2da84e90088ee22f4d34c2eb931d8680a34d6fcaab9cca2cadff2254521d630690a4675273f9e5c8e'
        '3aa763d6b203a04eafdfaa0b89d89411af8b9d7b9631f4ad46c6f88a8310b683e8ff20f6c8cd03c2ab42dc3fcc6f21bf937ee0db9037ac54aa2442291e2fe332')

prepare() {
  # remove bundled mupdf sources
  rm -frv $pkgname/*.tgz
  # we package clang and swig and don't need python-clang or python-swig
  cd $_name-$pkgver
  patch -Np1 < $srcdir/remove-clang-and-swig-dependencies.patch
}

build() {
  local cflags=(
    -I/usr/include
    -I/usr/include/freetype2
    -I/usr/include/harbuzz
    -I/usr/include/mupdf
  )
  local ldflags=(
    -lfreetype
    -lgumbo
    -lharfbuzz
    -ljbig2dec
    -ljpeg
    -lleptonica
    -lmupdf
    -lopenjp2
    -ltesseract
  )

  cd $_name-$pkgver
  # build against system libmupdf
  export PYMUPDF_SETUP_MUPDF_BUILD=''
  # provide tessdata location
  export TESSDATA_PREFIX="/usr/share/tessdata"
  # build against mupdf's C++/ Python language bindings
  export PYMUPDF_SETUP_IMPLEMENTATIONS=b
  CFLAGS+=" ${cflags[@]}"
  LDFLAGS+=" ${ldflags[@]}"

  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    # disable broken test, that upstream is not interested in fixing or disabling: https://github.com/pymupdf/PyMuPDF/issues/2127
    --deselect tests/test_pixmap.py::test_color_count
    --deselect tests/test_pixmap.py::test_3050
    --deselect tests/test_pixmap.py::test_3854
    --deselect tests/test_textextract.py::test_4180
    # disable test that shells out to pip: https://github.com/pymupdf/PyMuPDF/issues/2950
    --deselect tests/test_font.py::test_fontarchive
    --deselect tests/test_general.py::test_subset_fonts
    --deselect tests/test_general.py::test_4533
    # we do not care about flake8
    --deselect tests/test_flake8.py::test_flake8
    # no point testing code quality or spellcheck on our side
    --deselect tests/test_pylint.py::test_pylint
    --deselect tests/test_codespell.py::test_codespell
    # some textbox stuff again: https://github.com/pymupdf/PyMuPDF/issues/3398
    --deselect tests/test_textbox.py::test_textbox3
    # tries to download / install stuff through git and pip
    --deselect tests/test_barcode.py::test_barcode
    --deselect tests/test_font.py::test_4457
    --deselect tests/test_general.py::test_open2
    --deselect tests/test_pixmap.py::test_4445
  )
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$_site_packages:$PYTHONPATH"
  export PATH="$PWD/test_dir/usr/bin:$PATH"
  pytest "${pytest_options[@]}" tests/
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
