# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=accessibility-inspector
pkgver=25.12.2
pkgrel=1
pkgdesc='Inspect your application accessibility tree'
url='https://apps.kde.org/accessibilityinspector/'
arch=(x86_64)
license=(GPL-2.0-or-later
         LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kconfigwidgets
         kcoreaddons
         kcrash
         kdbusaddons
         ki18n
         kxmlgui
         libqaccessibilityclient-qt6
         qt6-base)
makedepends=(extra-cmake-modules)
groups=(kde-applications
        kde-accessibility)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('bc314517ddaba1723c3614df64d8ac3889436f5f1cb5113f6f7c8d8ed69afa5b'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
