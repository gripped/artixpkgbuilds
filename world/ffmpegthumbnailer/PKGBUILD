# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor:
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: boromil@gmail.com

pkgname=ffmpegthumbnailer
pkgver=2.3.0
pkgrel=1
pkgdesc="Lightweight video thumbnailer that can be used by file managers"
url="https://github.com/dirkvdb/ffmpegthumbnailer"
license=('GPL-2.0-or-later')
arch=('x86_64')
depends=('ffmpeg' 'libjpeg-turbo' 'libpng')
makedepends=('cmake' 'git')
optdepends=('gvfs: support for gio uris')
source=(git+https://github.com/dirkvdb/ffmpegthumbnailer#tag=v$pkgver)
sha256sums=('36790613cfbc8dc0dbd125c9cd4ce9813925c1c9b5c88a1aa735f63bf13b8240')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_GIO=ON \
    -DENABLE_THUMBNAILER=ON
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
