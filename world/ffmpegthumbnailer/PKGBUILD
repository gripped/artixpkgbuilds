# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor:
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: boromil@gmail.com

pkgname=ffmpegthumbnailer
pkgver=2.2.4
pkgrel=1
pkgdesc="Lightweight video thumbnailer that can be used by file managers"
url="https://github.com/dirkvdb/ffmpegthumbnailer"
license=('GPL-2.0-or-later')
arch=('x86_64')
depends=('ffmpeg' 'libjpeg-turbo' 'libpng')
makedepends=('cmake' 'git')
optdepends=('gvfs: support for gio uris')
source=(git+https://github.com/dirkvdb/ffmpegthumbnailer#tag=v$pkgver)
sha256sums=('52f57cee6963e99e2a367ff60a55ec542643688b69f4bf2d6b941fd679e6fac0')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_GIO=ON \
    -DENABLE_THUMBNAILER=ON
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
