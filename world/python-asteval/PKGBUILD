# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-asteval
pkgver=0.9.33
pkgrel=3
pkgdesc="Minimalistic evaluator of python expression using ast module"
url="https://github.com/lmfit/asteval"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-pytest' 'python-numpy' 'tk')
source=("git+https://github.com/lmfit/asteval.git#tag=$pkgver")
sha512sums=('02370327f2b5fb86b928e26d6f9650f7c134f69ba194b312af12022b775fd6e141d7fed4456f4c11f395b4aed974d48ac814365b72bdc45d6d410d52194537e2')

build() {
  cd asteval
  python -m build -nw
}

check() {
  cd asteval
  python -m installer --destdir=tmp_install dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  PYTHONPATH="$PWD/tmp_install/$site_packages" pytest --override-ini="addopts="
}

package() {
  cd asteval
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
