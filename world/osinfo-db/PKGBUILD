# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=osinfo-db
pkgver=20251212
pkgrel=1
pkgdesc='Osinfo database of information about operating systems for virtualization provisioning tools'
arch=(any)
url='https://libosinfo.org/'
license=(GPL-2.0-or-later)
makedepends=(
  git
  osinfo-db-tools
)
checkdepends=(
  python-lxml
  python-pytest
)
source=("git+https://gitlab.com/libosinfo/$pkgname.git?signed#tag=v$pkgver")
b2sums=(82334fe94d4e89e10b1adde643c4e47bcab3412b854d3527b2c5aa29a951a241448e85f3804960d1fe76598b2a0bed5da0112b102b6ccf31932499b788cbed01)
validpgpkeys=(
  DAF3A6FDB26B62912D0E8E3FBE86EBB415104FDF # Daniel P. Berrange
  09B9C8FF223EF113AFA06A39EE926C2BDACC177B # Fabiano Fidêncio
  206D3B352F566F3B0E6572E997D9123DE37A484F # Victor Toso de Carvalho <me@victortoso.com>
  4252D86A52041137C291CADFC85C5E957062A701 # Pavel Hrdina <phrdina@redhat.com>
)

prepare() {
  cd $pkgname
  sed -i "s/TODAY = .*/TODAY = $pkgver/" Makefile
}

build() {
  cd $pkgname
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make install DESTDIR="$pkgdir" OSINFO_DB_TARGET="--system"
}
