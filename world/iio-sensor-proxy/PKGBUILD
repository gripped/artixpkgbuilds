# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Eric Lehmann <katyl@katyl.info>
# Contributor: Thomas Fanninger <thomas@fanninger.at>
# Contributor: ultraviolet <ultravioletnanokitty@gmail.com>
# Contributor: Pablo Lezeta <prflr88@gmail.com>

pkgname=iio-sensor-proxy
pkgver=3.6
pkgrel=1
pkgdesc='IIO accelerometer sensor to input device proxy'
arch=('x86_64')
url='https://gitlab.freedesktop.org/hadess/iio-sensor-proxy/'
license=('GPL2')
depends=('libgudev' 'glib2' 'polkit')
makedepends=('gtk3' 'meson')
checkdepends=('python-gobject' 'python-dbusmock' 'python-psutil' 'umockdev')
source=("$url/-/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f9d1c19b74cb1e6c478ee51d31952a00e66dbbe29ce8b37a3a09ab168052d22e54ef438bf554af7f080c035cef2e8bb1e049ea653fc49f498a22680784c59ca6')

build() {
  mkdir $pkgname-$pkgver/build
  cd $pkgname-$pkgver/build

  artix-meson .. \
    -Dsystemdsystemunitdir='' \
    -Dudevrulesdir=/usr/lib/udev/rules.d \
    -Dsysconfdir=/usr/share

  ninja
}

check() {
  cd $pkgname-$pkgver/build

#  needs French locale
#  ninja test
}

package() {
  cd $pkgname-$pkgver/build

  DESTDIR="$pkgdir" ninja install
}

