# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdepim-runtime
pkgver=24.02.0
pkgrel=4
pkgdesc='Extends the functionality of kdepim'
arch=(x86_64)
url='https://kontact.kde.org'
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(akonadi
         akonadi-calendar
         akonadi-contacts
         akonadi-mime
         akonadi-notes
         gcc-libs
         glibc
         kcalendarcore
         kcmutils
         kcodecs
         kcompletion
         kconfig
         kconfigwidgets
         kcontacts
         kcoreaddons
         kdav
         ki18n
         kidentitymanagement
         kimap
         kio
         kjobwidgets
         kldap
         kmailtransport
         kmime
         kmbox
         knotifications
         knotifyconfig
         kservice
         ktextaddons
         ktextwidgets
         kwallet
         kwidgetsaddons
         kwindowsystem
         libakonadi
         libkgapi
         libkolabxml
         libsasl
         qca-qt6
         qt6-base
         qt6-networkauth
         qt6-speech
         qt6-webengine
         qtkeychain-qt6)
makedepends=(boost
             extra-cmake-modules
             kdoctools
             libetebase)
optdepends=('libetebase: EteSync resource')
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/pim/kdepim-runtime/-/commit/89a1f96b.patch
        https://invent.kde.org/pim/kdepim-runtime/-/commit/65edf9bb.patch)
sha256sums=('5cda8aa7b3d8a7bd526fbdd20df30f56a2dcf10a844d88a6f71b9122067bab35'
            'SKIP'
            '8102fc618ac357ceb5d6cedef80d36e6d24bbabe4f9388ce6d032a9331612cf5'
            'cb7e31cae78dabb3bf9b0a8510f6ff5d7fc3a53eed2af775879d9771459bf64e')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  sed -e 's|QLatin1StringView|QLatin1String|' -i 89a1f96b.patch
  patch -d $pkgname-$pkgver -p1 < 89a1f96b.patch # Fix EWS resource losing authentication
  sed -e 's|QtConcurrentRun|QtConcurrent|' -i 65edf9bb.patch
  patch -d $pkgname-$pkgver -p1 < 65edf9bb.patch # Fix build of etesync resource
}

build() {
  artix-cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
