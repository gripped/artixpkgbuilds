# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=libgedit-amtk
pkgver=5.9.2
pkgrel=1
pkgdesc='Actions, Menus and Toolbars Kit for GTK applications'
arch=(x86_64)
url='https://gitlab.gnome.org/World/gedit/libgedit-amtk'
license=(LGPL-3.0-or-later)
depends=(
  glib2
  glibc
  gtk3
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  meson
)
replaces=(amtk)
provides=(libgedit-amtk-5.so)
conflicts=(amtk)
source=("git+https://gitlab.gnome.org/World/gedit/libgedit-amtk.git#tag=$pkgver")
b2sums=(c64a7d938056e9214ba6c98da5f678d154a7a64fe2d787b98a0317c348bc68d37fe1f038320f4b2e6fd2bd37da613f9140033fa3ca747054167af847acdf4249)
validpgpkeys=(08C0A6B1A08310A45363B328426F014A261FD87F) # Sébastien Wilmet <swilmet@mailfence.com>

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
