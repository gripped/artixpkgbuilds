# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Sarah Hay <sarah@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=faac
pkgver=2.0
pkgrel=1
pkgdesc="Freeware Advanced Audio Coder"
arch=(x86_64)
url="https://github.com/knik0/faac"
license=(LGPL-2.1-or-later)
depends=(glibc)
makedepends=(
  git
  meson
)
provides=(libfaac.so)
source=(
  $pkgname::git+$url#tag=$pkgname-$pkgver
)
sha512sums=('51e7a60b10e15889ef707449c4c64127bc801d647eb2f173cb6a678d68f3ea88da6ef84449daf882dbb54491277f38d828e31409c925f3fd9cf1baa55c4b0cb0')
b2sums=('7370b0b21bbfeff0012fc1c05aeea6a16fa9db9d8bf2d4b7432b17deca5a47ed971b20b90a6dae444214b1fe1bde11637794a2810e6461047183cf2ce29406c6')

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -vDm 644 $pkgname/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname/{AUTHORS,ChangeLog,NEWS,README.md,TODO} -t "$pkgdir/usr/share/doc/$pkgname/"
}
