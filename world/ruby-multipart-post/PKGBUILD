# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Rhys Davies <rhys@johnguant.com>

pkgname=ruby-multipart-post
pkgver=2.4.0
pkgrel=1
pkgdesc='A multipart form post accessory for Net::HTTP'
arch=(any)
url='https://github.com/socketry/multipart-post'
license=(MIT)
depends=(
  ruby
)
makedepends=(
  git
  ruby-covered
  ruby-rspec
)
checkdepends=(
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
)
options=(!emptydirs)
source=(git+https://github.com/socketry/multipart-post.git#tag=v$pkgver)
sha256sums=('9d8063508b913af93cca38ab2d074ee18587c49c76c479d85889add38a549301')

prepare() {
  cd multipart-post
  sed --in-place \
    --expression '/signing_key/d' \
    multipart-post.gemspec

  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd multipart-post
  gem build multipart-post.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    multipart-post-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd multipart-post
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd multipart-post
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 license.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
