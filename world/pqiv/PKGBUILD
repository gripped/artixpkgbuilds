# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Bruno Pagani <archange@archlinux.org>
# Contributor: Caleb Maclennan <caleb@alerque.com>

pkgname=pqiv
pkgver=2.13.3
pkgrel=1
pkgdesc='Powerful image viewer with minimal UI'
arch=(x86_64)
url="https://github.com/phillipberndt/$pkgname"
license=(GPL-3.0-only)
depends=(at-spi2-core libatk-1.0.so
         cairo libcairo.so libcairo-gobject.so
         gdk-pixbuf2 libgdk_pixbuf-2.0.so
         glib2 libgio-2.0.so libglib-2.0.so libgobject-2.0.so libgmodule-2.0.so
         glibc # libc.so libm.so
         gtk3 libgdk-3.so libgtk-3.so
         harfbuzz libharfbuzz.so
         libx11 # libX11.so
         pango libpango-1.0.so libpangocairo-1.0.so
         zlib libz.so)
makedepends=(ffmpeg # libavcodec.so libavformat.so libavutil.so libswscale.so
             imagemagick # libMagickWand-7.Q16HDRI.so
             libarchive # libarchive.so
             libspectre # libspectre.so
             libwebp # libwebp.so
             poppler-glib) # libpoppler-glib.so
optdepends=('ffmpeg: rudimentary video support'
            'imagemagick: support for various images formats like PSD'
            'libarchive: archives and cbX comic book support'
            'libspectre: PS/EPS support'
            'libwebp: WebP support'
            'poppler-glib: rudimentary PDF support')
_archive="$pkgname-$pkgver"
source=("$url/archive/$pkgver/$_archive.tar.gz")
sha256sums=('f0ffaa33e93299b38058c507da2945976a4b350c92cf1c4b3649586444395dfd')

build() {
	cd "$_archive"
	./configure --backends-build=shared
	make
}

package() {
	cd "$_archive"
	make DESTDIR="$pkgdir" install
}
