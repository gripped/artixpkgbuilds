# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-async-service-supervisor
pkgver=0.18.0
pkgrel=1.1
pkgdesc='A supervisor for managing multiple container processes'
arch=(any)
url='https://github.com/socketry/async-service-supervisor'
license=(MIT)
depends=(
  ruby
  ruby-async
  ruby-async-bus
  ruby-async-service
  ruby-async-utilization
  ruby-io-endpoint
  ruby-memory
  ruby-memory-leak
  ruby-process-metrics
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-decode
  ruby-metrics
  ruby-sus
  ruby-sus-fixtures-async
  ruby-sus-fixtures-console
  ruby-traces
)
options=(!emptydirs)
source=("git+https://github.com/socketry/async-service-supervisor.git#tag=v$pkgver")
sha512sums=('d683cb9d421de2e062a97ab350bf0a16ee52ce9ea3985aebaf994c836db499f6d68c57c6b9de38f4512bb5f3e81902b2ac033ac4eb61fb78283302055678cce1')
b2sums=('d09c26ec4045a88a177b6a7131b70de8f4f768e86661b1df93ea8cbaec02ffffa0e93e42333f47b0b17720a6a64f06ade3ab3626037719a460ba5a08896cac6c')

prepare() {
  cd async-service-supervisor

  sed -r -e 's|~>|>=|g' -e '/signing_key/d' -i async-service-supervisor.gemspec
  sed -i \
    -e '/gem "falcon"/d' \
    -e '/group :maintenance/,/end/d' \
    -e '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd async-service-supervisor

  gem build async-service-supervisor.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    async-service-supervisor-$pkgver.gem

  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  rm -r "tmp_install/$_gemdir/cache"
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd async-service-supervisor

  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd async-service-supervisor

  cp -a tmp_install/* "$pkgdir"/
  install -vDm 644 readme.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 license.md -t "$pkgdir/usr/share/licenses/$pkgname/"
}
