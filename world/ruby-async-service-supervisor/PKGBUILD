# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-async-service-supervisor
pkgver=0.20.0
pkgrel=1
pkgdesc='A supervisor for managing multiple container processes'
arch=(any)
url='https://github.com/socketry/async-service-supervisor'
license=(MIT)
depends=(
  ruby
  ruby-async
  ruby-async-bus
  ruby-async-service
  ruby-async-utilization
  ruby-io-endpoint
  ruby-memory
  ruby-memory-leak
  ruby-process-metrics
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-decode
  ruby-metrics
  ruby-sus
  ruby-sus-fixtures-async
  ruby-sus-fixtures-console
  ruby-traces
)
options=(!emptydirs)
source=("git+https://github.com/socketry/async-service-supervisor.git#tag=v$pkgver")
sha512sums=('d248660657732b515111f308ffa8236fcff6be7f0a47a8b3ea500bb366f61543a8ac55e32696ec8b53b72da5182e4d1e9233dc51e9942ea80dcae4eaadf60ffb')
b2sums=('327f17f6ad6af9d85de526ee74bceac4fff347a7c3d9a45a2788706fbdeb6ff9f20a5043909dc365d0f837f98811bce187fb8559183014b6d826401e2097acff')

prepare() {
  cd async-service-supervisor

  sed -r -e 's|~>|>=|g' -e '/signing_key/d' -i async-service-supervisor.gemspec
  sed -i \
    -e '/gem "falcon"/d' \
    -e '/group :maintenance/,/end/d' \
    -e '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd async-service-supervisor

  gem build async-service-supervisor.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    async-service-supervisor-$pkgver.gem

  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  rm -r "tmp_install/$_gemdir/cache"
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd async-service-supervisor

  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd async-service-supervisor

  cp -a tmp_install/* "$pkgdir"/
  install -vDm 644 readme.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 license.md -t "$pkgdir/usr/share/licenses/$pkgname/"
}
