# Maintainer: Fabian Bornschein <fabiscafe-at-mailbox-dot-org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=libdex
pkgname=(
  libdex
  libdex-docs
)
pkgver=0.4.2
pkgrel=1
pkgdesc="A library supporting 'Deferred Execution'"
url="https://gitlab.gnome.org/GNOME/libdex"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  glib2
  liburing
)
makedepends=(
  gi-docgen
  git
  gobject-introspection
  libsoup3
  libsysprof-capture
  meson
  vala
)
_commit=e81b047444d45c4c05c325c49a43c35ae65971bf  # tags/0.4.2^0
source=("git+$url.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd libdex
  git describe --tags | sed -r 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libdex
}

build() {
  local meson_options=(
    -D docs=true
    -D sysprof=true
  )

  artix-meson libdex build "${meson_options[@]}"
  meson compile -C build
}

check() (
  meson test -C build --print-errorlogs
)

package_libdex() {
  provides=(libdex-1.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_libdex-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
