# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Robin Broda <coderobe @ archlinux.org>
# Contributor: Peter Wu <peter@lekensteyn.nl>
# Contributor: Evaggelos Balaskas <eblaskas _AT_ ebalaskas _DOT_ gr>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: Nikos Skalkotos <skalkoto (at) Gmail.com>

pkgname=libguestfs
pkgver=1.54.1
pkgrel=1
pkgdesc="Access and modify virtual machine disk images"
arch=(x86_64)
url="https://libguestfs.org/"
license=(
  GPL-2.0-or-later
  LGPL-2.1-or-later
)
# NOTE: see appliance/packagelist.in
_appliancedeps=(
  btrfs-progs
  debootstrap
  dhcpcd
  dosfstools
  exfatprogs
  f2fs-tools
  gptfdisk
  grub
  iproute2
  iputils
  jfsutils
  libldm
  lrzip
  lsof
  lsscsi
  lvm2
  lzop
  mdadm
  mtools
  multipath-tools
  nilfs-utils
  ntfs-3g
  openssh
  pciutils
  procps-ng
  psmisc
  rsync
  squashfs-tools
  strace
  syslinux
  vim
  xfsprogs
  yara
)
depends=(
  augeas
  bash
  fuse2
  gcc-libs
  glib2
  glibc
  hivex
  jansson
  sleuthkit
  libconfig
  libtirpc
  libvirt
  libxml2
  ncurses
  pcre2
  perl-libintl-perl
  qemu
  readline
  supermin
  "${_appliancedeps[@]}"
)
makedepends=(
  bash-completion
  cdrtools
  cpio
  erlang-erl_interface
  # ghc  # TODO: build haskell bindings
  go
  gobject-introspection
  gperf
  ocaml
  ocaml-augeas
  ocaml-compiler-libs
  ocaml-findlib
  perl-module-build
  python
  java-environment
  lua
  php
  ruby
  ruby-rake
  ruby-rdoc
  rust
  vala
)
checkdepends=(
  linux-lts  # test against LTS kernel: https://github.com/libguestfs/libguestfs/issues/139
  ruby-test-unit
  ruby-minitest
)
optdepends=(
  'gobject-introspection: GObject-Introspection Bindings'
  'java-runtime: for Java bindings'
  'ocaml: OCaml Bindings'
  'php: for PHP module'
  'python: Python Bindings'
  'ruby: for Ruby bindings'
)
provides=(
  libguestfs.so
  libguestfs-gobject-1.0.so
)
backup=(etc/libguestfs-tools.conf)
source=(
  https://download.libguestfs.org/${pkgver%.*}-stable/$pkgname-$pkgver.tar.gz{,.sig}
  $pkgname-1.48.1-disable_php_tests.patch
)
sha512sums=('0113ea1cd26aba9b935b65299cf34d53c3d669508238d642a1a97622eb24948df05b55072d4c715cacb4fdfc7339c69ca25f65ca51ac87c40c5325175e939a0d'
            'SKIP'
            'd59cad07e275f1fa5e82448993db5b3a6dc8cdd1fc97a8839ef0403ad3f1753a5d13df27b184d6c73fa8dda8bd75e63ad68aaad26001986682d5bc7eeb58273a')
b2sums=('13fe3a536ec04901a69192ecc9fefe055099d057ba3609bd1ec51b8853e5ad50fd608ef4de7d6296f9bf53a19c1bc81f2ae54c185c18c6e68f7a541f34222f93'
        'SKIP'
        '2f7c429875d80b2b3a70286f83764ce6ed8be86055232fc059ab55ebf8393f9d29a2241401c7ab55af7ba9180ea85909647fe06ca51880f93bc66b488bcec78c')
validpgpkeys=('F7774FB1AD074A7E8C8767EA91738F73E1B768A0') # Richard W.M. Jones <rjones@redhat.com>

prepare() {
  # disable php tests, as missing arginfo definition makes them fail: https://github.com/libguestfs/libguestfs/issues/78
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-1.48.1-disable_php_tests.patch

  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  local configure_options=(
    --disable-haskell
    --disable-static
    --prefix=/usr
    --sbindir=/usr/bin
    --sysconfdir=/etc
    --with-extra-packages="libldm openssh qemu"
    --with-guestfs-path=/usr/lib/guestfs
  )

  cd $pkgname-$pkgver
  CFLAGS+=" -ffat-lto-objects" \
  ./configure "${configure_options[@]}"
  make
}


package() {
  make INSTALLDIRS=vendor DESTDIR="$pkgdir" install -C $pkgname-$pkgver
}
