# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Robin Broda <coderobe @ archlinux.org>
# Contributor: Peter Wu <peter@lekensteyn.nl>
# Contributor: Evaggelos Balaskas <eblaskas _AT_ ebalaskas _DOT_ gr>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: Nikos Skalkotos <skalkoto (at) Gmail.com>

pkgname=libguestfs
pkgver=1.56.2
pkgrel=4
pkgdesc="Access and modify virtual machine disk images"
arch=(x86_64)
url="https://libguestfs.org/"
license=(
  GPL-2.0-or-later
  LGPL-2.1-or-later
)
# NOTE: see appliance/packagelist.in
_appliancedeps=(
  btrfs-progs
  dhcpcd
  dosfstools
  exfatprogs
  f2fs-tools
  gptfdisk
  iproute2
  iputils
  jfsutils
  libldm
  lrzip
  lsof
  lsscsi
  lvm2
  lzop
  mdadm
  mtools
  nilfs-utils
  ntfs-3g
  openssh
  pciutils
  procps-ng
  psmisc
  rsync
  squashfs-tools
  strace
  syslinux
  vim
  xfsprogs
  yara
)
depends=(
  augeas
  bash
  fuse2
  gcc-libs
  glib2
  glibc
  hivex
  json-c
  sleuthkit
  libconfig
  libtirpc
  libvirt
  libxml2
  ncurses
  pcre2
  perl-libintl-perl
  qemu
  readline
  supermin
  "${_appliancedeps[@]}"
)
makedepends=(
  bash-completion
  cdrtools
  cpio
  erlang-erl_interface
  # ghc  # TODO: build haskell bindings
  go
  gobject-introspection
  gperf
  ocaml
  ocaml-augeas
  ocaml-compiler-libs
  ocaml-findlib
  perl-module-build
  python
  java-environment
  lua
  php
  ruby
  ruby-rake
  ruby-rdoc
  rust
  vala
  wget
)
checkdepends=(
  linux-lts  # test against LTS kernel: https://github.com/libguestfs/libguestfs/issues/139
  ruby-test-unit
  ruby-minitest
)
optdepends=(
  'gobject-introspection: GObject-Introspection Bindings'
  'java-runtime: for Java bindings'
  'ocaml: OCaml Bindings'
  'php: for PHP module'
  'python: Python Bindings'
  'ruby: for Ruby bindings'
)
provides=(
  libguestfs.so
  libguestfs-gobject-1.0.so
)
backup=(etc/libguestfs-tools.conf)
source=(
  https://download.libguestfs.org/${pkgver%.*}-stable/$pkgname-$pkgver.tar.gz{,.sig}
  $pkgname-1.48.1-disable_php_tests.patch
)
sha512sums=('80d99319bc6d13f0f4252c21250d4191a9dec94c6d10b572806043f3ecc36581c2169ddac4ebf7c7c7a1a560c6e85c5ce366baea2e63efcaa093f601d1031c62'
            'SKIP'
            'd59cad07e275f1fa5e82448993db5b3a6dc8cdd1fc97a8839ef0403ad3f1753a5d13df27b184d6c73fa8dda8bd75e63ad68aaad26001986682d5bc7eeb58273a')
b2sums=('0da837826673b75faac3bc05c6eddf7df60fbac50fcb5dedbe877777f450344217b6fd58892695efcb241cfb3c1fb19d251a8fef9ba5e015b250afb6aad17143'
        'SKIP'
        '2f7c429875d80b2b3a70286f83764ce6ed8be86055232fc059ab55ebf8393f9d29a2241401c7ab55af7ba9180ea85909647fe06ca51880f93bc66b488bcec78c')
validpgpkeys=('F7774FB1AD074A7E8C8767EA91738F73E1B768A0') # Richard W.M. Jones <rjones@redhat.com>

prepare() {
  # disable php tests, as missing arginfo definition makes them fail: https://github.com/libguestfs/libguestfs/issues/78
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-1.48.1-disable_php_tests.patch

  cd $pkgname-$pkgver
  autoreconf -fiv -I /usr/share/gettext/m4
}

build() {
  local configure_options=(
    --disable-haskell
    --disable-static
    --prefix=/usr
    --sbindir=/usr/bin
    --sysconfdir=/etc
    --with-guestfs-path=/usr/lib/guestfs
  )

  cd $pkgname-$pkgver
  CFLAGS+=" -ffat-lto-objects" \
  ./configure "${configure_options[@]}"
  make
}


package() {
  make INSTALLDIRS=vendor DESTDIR="$pkgdir" install -C $pkgname-$pkgver
}
