# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Robin Broda <coderobe @ archlinux.org>
# Contributor: Peter Wu <peter@lekensteyn.nl>
# Contributor: Evaggelos Balaskas <eblaskas _AT_ ebalaskas _DOT_ gr>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: Nikos Skalkotos <skalkoto (at) Gmail.com>

pkgname=libguestfs
pkgver=1.52.1
pkgrel=1
pkgdesc="Access and modify virtual machine disk images"
arch=(x86_64)
url="https://libguestfs.org/"
license=(
  GPL-2.0-or-later
  LGPL-2.1-or-later
)
# NOTE: see appliance/packagelist.in
_appliancedeps=(
  btrfs-progs
  debootstrap
  dhcpcd
  dosfstools
  exfatprogs
  f2fs-tools
  gptfdisk
  grub
  iproute2
  iputils
  jfsutils
  libldm
  lrzip
  lsof
  lsscsi
  lvm2
  lzop
  mdadm
  mtools
  multipath-tools
  nilfs-utils
  ntfs-3g
  openssh
  pciutils
  procps-ng
  psmisc
  reiserfsprogs
  rsync
  squashfs-tools
  strace
  syslinux
  vim
  xfsprogs
  yara
)
depends=(
  augeas
  bash
  fuse2
  gcc-libs
  glib2
  glibc
  hivex
  jansson
  sleuthkit
  libconfig
  libtirpc
  libvirt
  libxml2
  ncurses
  pcre2
  perl-libintl-perl
  qemu
  readline
  supermin
  "${_appliancedeps[@]}"
)
makedepends=(
  bash-completion
  cdrtools
  cpio
  erlang-nox
  # ghc  # TODO: build haskell bindings
  go
  gobject-introspection
  gperf
  ocaml
  ocaml-augeas
  ocaml-compiler-libs
  ocaml-findlib
  perl-module-build
  python
  java-environment
  lua
  php
  ruby
  ruby-rake
  ruby-rdoc
  rust
  vala
)
checkdepends=(
  linux-lts  # test against LTS kernel: https://github.com/libguestfs/libguestfs/issues/139
  ruby-test-unit
  ruby-minitest
)
optdepends=(
  'gobject-introspection: GObject-Introspection Bindings'
  'java-runtime: for Java bindings'
  'ocaml: OCaml Bindings'
  'php: for PHP module'
  'python: Python Bindings'
  'ruby: for Ruby bindings'
)
provides=(
  libguestfs.so
  libguestfs-gobject-1.0.so
)
backup=(etc/libguestfs-tools.conf)
source=(
  https://download.libguestfs.org/${pkgver%.*}-stable/$pkgname-$pkgver.tar.gz{,.sig}
  $pkgname-1.48.1-disable_php_tests.patch
  $pkgname-Rust-bindings-Handle-null-pointer-when-creating-slic.patch
)
sha512sums=('13ccc03a72ddf8dee33ca3b2f4c26235b43d395a9dfc75d8020d7f55350c0f79a0fe557591c8fcf893e02dd868e1420480fa7b356dfe25f275bdee62aeb189ed'
            'SKIP'
            'd59cad07e275f1fa5e82448993db5b3a6dc8cdd1fc97a8839ef0403ad3f1753a5d13df27b184d6c73fa8dda8bd75e63ad68aaad26001986682d5bc7eeb58273a'
            '2353df92a0a0b46fc034d1d654fca0ec6cb28307d2eb2217f174e6c430c6e4c77eb6aefba6e6a478e4ede83b685e1a875577fc93fa08b023d0d3642017415eaf')
b2sums=('7c1d8da7fcabca385ce92dc7860d8ca4ef3db0bd7be74ea0dc5e5f21facb26322a10039d0f2c78636645cb56c46d42add27218d30151ef054cad6758097dc455'
        'SKIP'
        '2f7c429875d80b2b3a70286f83764ce6ed8be86055232fc059ab55ebf8393f9d29a2241401c7ab55af7ba9180ea85909647fe06ca51880f93bc66b488bcec78c'
        '1cf7221270192dae5675bbec592d3709322cce4bd590dd59ac9455810a351644e0d92c79c154fb0e88f8798f6eb52627dc5dbc253caa9fb2c55c6a294fc51232')
validpgpkeys=('F7774FB1AD074A7E8C8767EA91738F73E1B768A0') # Richard W.M. Jones <rjones@redhat.com>

prepare() {
  # disable php tests, as missing arginfo definition makes them fail: https://github.com/libguestfs/libguestfs/issues/78
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-1.48.1-disable_php_tests.patch

  # fix "Rust test 410_close_event fails" https://github.com/libguestfs/libguestfs/issues/136
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-Rust-bindings-Handle-null-pointer-when-creating-slic.patch
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  local configure_options=(
    --disable-haskell
    --disable-static
    --prefix=/usr
    --sbindir=/usr/bin
    --sysconfdir=/etc
    --with-extra-packages="libldm openssh qemu"
    --with-guestfs-path=/usr/lib/guestfs
  )

  cd $pkgname-$pkgver
  CFLAGS+=" -ffat-lto-objects" \
  ./configure "${configure_options[@]}"
  make
}


package() {
  make INSTALLDIRS=vendor DESTDIR="$pkgdir" install -C $pkgname-$pkgver
}
