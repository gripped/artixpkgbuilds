# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=akonadi-contacts
pkgver=25.08.2
pkgrel=2
pkgdesc='Libraries and daemons to implement Contact Management in Akonadi'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         grantleetheme
         ki18n
         kcodecs
         kcolorscheme
         kcompletion
         kconfig
         kcontacts
         kcoreaddons
         kguiaddons
         kiconthemes
         kio
         kmime
         ktextaddons
         ktexttemplate
         ktextwidgets
         kwidgetsaddons
         kxmlgui
         libakonadi
         prison
         qt6-base)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('650c034cdf2aab5e53a8be579cad677a9d8fcc35edb704e77202e905b0f8e202'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
