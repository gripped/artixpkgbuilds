# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=akonadi-calendar-tools
pkgver=25.12.0
pkgrel=1
pkgdesc='CLI tools to manage akonadi calendars'
arch=(x86_64)
url='https://kontact.kde.org'
license=(GPL-2.0-or-later)
depends=(akonadi
         akonadi-calendar
         calendarsupport
         gcc-libs
         glibc
         kcalendarcore
         kcoreaddons
         ki18n
         libakonadi
         qt6-base)
makedepends=(extra-cmake-modules
             kdoctools)
groups=(kde-applications
        kde-pim)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('ac7bd4c6e1d2c817cf13e5695f22fa7828d1a2c2b7c142ded77d563b636a877a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
