# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>

pkgname=python-cssselect
pkgver=1.6.0
pkgrel=1
license=('BSD-3-Clause')
arch=('any')
url="https://pypi.python.org/pypi/cssselect"
pkgdesc="A Python3 library that parses CSS3 Selectors and translates them to XPath 1.0"
depends=('python')
makedepends=('git' 'python-hatchling' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest' 'python-lxml')
source=("git+https://github.com/scrapy/cssselect.git#tag=v$pkgver")
sha512sums=('dedd36cdf55bfc7e8054a0b691c57e2fa76cf7977973d35cc75b22a2a4eecee3e6381cf76c927b28296eda0e046c413a46e2380442d15cb5c30f9611499b2110')

build() {
  cd cssselect
  python -m build --wheel --no-isolation
}

check() {
  cd cssselect
  pytest
}

package() {
  cd cssselect
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
