# Maintainer: Alad Wenter <alad@mailbox.org>
# Contributor: carstene1ns <arch carsten-teibes de>
# Contributor: Sam S. <smls75@gmail.com>
# Contributor: Daniel Scharrer <daniel@constexpr.org>
pkgname=innoextract
pkgver=1.9
pkgrel=13
pkgdesc="A tool to extract installers created by Inno Setup"
url='https://constexpr.org/innoextract/'
arch=('x86_64')
license=('ZLIB')
depends=('boost-libs' 'xz')
makedepends=('boost' 'cmake')
source=("https://constexpr.org/innoextract/files/$pkgname-$pkgver.tar.gz"
        "https://constexpr.org/innoextract/files/$pkgname-$pkgver.tar.gz.sig"
         https://github.com/dscharrer/innoextract/commit/264c2fe6.patch)
sha256sums=('6344a69fc1ed847d4ed3e272e0da5998948c6b828cb7af39c6321aba6cf88126'
            'SKIP'
            '891436a9d3278ddc3342fef24004b28b8fb6c49cd3534843bea1cb4f0f8508ed')
validpgpkeys=("ADE9653703D4ADE0E997758128555A66D7E1DEC9") # Daniel Scharrer <daniel@constexpr.org>

prepare() {
# Fix build with boost 1.85
  patch -d $pkgname-$pkgver -p1 < 264c2fe6.patch
}

build() {
    cd "$pkgname-$pkgver"

    export CMAKE_POLICY_VERSION_MINIMUM=3.5 # Fix build with cmake 4

    # https://github.com/dscharrer/innoextract/issues/88
    cmake . -DCMAKE_INSTALL_PREFIX=/usr \
            -DBoost_NO_BOOST_CMAKE=ON
    make
}

package() {
    cd "$pkgname-$pkgver"
    make DESTDIR="$pkgdir/" install

    install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
    install -m644  CHANGELOG "$pkgdir/usr/share/doc/$pkgname/CHANGELOG"
    install -Dm644 LICENSE   "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
