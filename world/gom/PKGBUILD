# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgbase=gom
pkgname=(gom gom-docs)
pkgver=0.5.2
pkgrel=1
pkgdesc='GObject to SQLite object mapper library'
arch=('x86_64')
url='https://wiki.gnome.org/Projects/Gom'
license=('LGPL-2.1-or-later')
depends=('glib2' 'glibc' 'sqlite')
makedepends=('gdk-pixbuf2' 'gi-docgen' 'git' 'gobject-introspection' 'meson' 'python-gobject')
source=("git+https://gitlab.gnome.org/GNOME/$pkgname.git#tag=$pkgver")
b2sums=('b6ce604f70c775ef80705447ae060d9c373158dfdf5ed33e823fd0a95b1fcebc4f59c497f95ec8a2e654c2bf0f6f89397578a1068c957862af8566573ab448b5')

build() {
  artix-meson $pkgname build -D enable-gtk-doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_gom() {
  meson install -C build --destdir "$pkgdir"

  python -m compileall -d /usr/lib "$pkgdir/usr/lib"
  python -O -m compileall -d /usr/lib "$pkgdir/usr/lib"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_gom-docs() {
  pkgdesc+=" - documentation"
  depends=()

  mv doc/* "$pkgdir"
}
