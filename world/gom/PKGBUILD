# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgbase=gom
pkgname=(
  gom
  gom-docs
)
pkgver=0.5.5
pkgrel=2
pkgdesc='GObject to SQLite object mapper library'
arch=(x86_64)
url='https://wiki.gnome.org/Projects/Gom'
license=(LGPL-2.1-or-later)
depends=(
  glib2
  glibc
  sqlite
)
makedepends=(
  gdk-pixbuf2
  gi-docgen
  git
  gobject-introspection
  meson
  python-gobject
)
source=(
  "git+https://gitlab.gnome.org/GNOME/$pkgbase.git#tag=$pkgver"
  0001-build-Use-python-install_sources.patch
)
b2sums=('4d64037206fc2b012800eef6c3786e413741ab0e4217bc06cedcba42f8ddaf971e3ffbdd757924074bd13a84c9acdc0130eb92fe199373c2dc57f4ac089794ba'
        '47801bd8817da2f867ea28b2e10ce5ead75f1b0c8906f97b0c5f47252655c04591b13e00d14d2ea19e7a04a0494934c73d87797f917d8ba17431fd012bedce4e')

prepare() {
  # Compile python sources
  git -C $pkgbase apply -3 ../0001-build-Use-python-install_sources.patch
}

build() {
  artix-meson $pkgbase build \
    -D enable-gtk-doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_gom() {
  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_gom-docs() {
  pkgdesc+=" - documentation"
  depends=()

  mv doc/* "$pkgdir"
}
