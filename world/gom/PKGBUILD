# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgbase=gom
pkgname=(
  gom
  gom-docs
)
pkgver=0.5.4
pkgrel=1
pkgdesc='GObject to SQLite object mapper library'
arch=(x86_64)
url='https://wiki.gnome.org/Projects/Gom'
license=(LGPL-2.1-or-later)
depends=(
  glib2
  glibc
  sqlite
)
makedepends=(
  gdk-pixbuf2
  gi-docgen
  git
  gobject-introspection
  meson
  python-gobject
)
source=("git+https://gitlab.gnome.org/GNOME/$pkgbase.git#tag=$pkgver")
b2sums=('b59748bd2dacb4980fdc850d3ef98e27130654ac59423f0d27e22b8b3b9d9ab02ba19e086c4229c7fe9d02f8600d1cf01bd9cf90ec7d613f3cd09c27b5b8cae6')

build() {
  artix-meson $pkgbase build \
    -D enable-gtk-doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_gom() {
  meson install -C build --destdir "$pkgdir"

  python -m compileall -d /usr/lib "$pkgdir/usr/lib"
  python -O -m compileall -d /usr/lib "$pkgdir/usr/lib"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_gom-docs() {
  pkgdesc+=" - documentation"
  depends=()

  mv doc/* "$pkgdir"
}
