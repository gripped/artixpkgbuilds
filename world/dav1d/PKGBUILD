# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgbase=dav1d
pkgname=('dav1d' 'dav1d-doc')
pkgver=1.5.1
pkgrel=1
pkgdesc="AV1 cross-platform decoder focused on speed and correctness"
url="https://code.videolan.org/videolan/dav1d"
arch=('x86_64')
license=('BSD-2-Clause')
makedepends=('doxygen' 'glibc' 'graphviz' 'meson' 'nasm' 'ninja' 'xxhash')
source=("https://downloads.videolan.org/pub/videolan/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz"{,.asc}
        "https://code.videolan.org/videolan/dav1d-test-data/-/archive/${pkgver}/dav1d-test-data-${pkgver}.tar.gz")
sha512sums=('3582dff4f797ebe2c137b28549a7214f11869ae308e3c78440849a3a501b12c326d3fa604126ccff4b648f37ab260c003c63385c70ba91e11f3442f20ad455db'
            'SKIP'
            '37d77cf5d9db2a08f8364b854b7e43277660778411a38ae1d9ea5ba173c47c716f930da774688d2059617e3cf4d19f9a35975c3fdf8a431d32b1aa26a98ce3a2')
b2sums=('482eefcf40aaf780dcb7eb888a83feb946e2ab4249d92b68bc629f2c84e3c9e5170210f367d7b1506449d6f3f7ea176b5f571f584d9557c5af6d11ca643ff378'
        'SKIP'
        '67f7c7dae02edb5d54c1e915da4164cad87135119bfc650268a69dd79be9f73ca4dcf785caa4c92e1f34bd7df04ff1ba8ccd991092d39f369530a85dfee51ee7')
validpgpkeys=('65F7C6B4206BD057A7EB73787180713BE58D1ADC') # VideoLAN Release Signing Key

prepare() {
	cd "${pkgbase}-${pkgver}"
	ln -s "${srcdir}/dav1d-test-data-${pkgver}" tests/dav1d-test-data
}

build() {
	cd ${pkgbase}-${pkgver}
	artix-meson \
		-Dtestdata_tests='true' \
		-Denable_docs='true' \
		build
	ninja -C build all doc/html
}

check() {
	cd "${pkgbase}-${pkgver}/build"
	meson test
}

package_dav1d() {
	depends=('glibc')
	provides=('libdav1d.so')
	optdepends=('dav1d-doc: HTML documentation')

	cd "${pkgbase}-${pkgver}"
	DESTDIR="${pkgdir}" ninja -C build install
	install -Dm 644 README.md CONTRIBUTING.md NEWS -t "${pkgdir}/usr/share/doc/${pkgname}"
	install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_dav1d-doc() {
	pkgdesc+=" (documentation)"

	cd "${pkgbase}-${pkgver}"
	install -d "${pkgdir}/usr/share/doc/${pkgbase}"
	cp -r build/doc/html -t "${pkgdir}/usr/share/doc/${pkgbase}"
	install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
