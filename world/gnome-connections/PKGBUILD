# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=gnome-connections
pkgver=49.0
pkgrel=1
pkgdesc='Remote desktop client for the GNOME desktop environment'
arch=(x86_64)
url='https://apps.gnome.org/Connections/'
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  freerdp
  fuse3
  gdk-pixbuf2
  glib2
  glibc
  gtk-vnc
  gtk3
  hicolor-icon-theme
  libhandy
  libsecret
  libxml2
  spice-gtk
)
makedepends=(
  appstream
  git
  gobject-introspection
  itstool
  meson
  vala
)
groups=(gnome)
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-connections.git?signed#tag=${pkgver/[a-z]/.&}"
  git+https://gitlab.gnome.org/GNOME/gtk-frdp.git
  0001-Use-GApplication-to-parse-command-line-options.patch
)
b2sums=('8c9ed2e842e247f8a9de3cac7acf7234fb9dedfb9f65c6c5d011766eefae88b1dbc8e0213e242b7fa4c689dd88b6f2d16e6ed3e9cdb94509b64b743eb18e9fb6'
        'SKIP'
        '5c4026d67d6f5771b7ace9d2c5b4a73d5f4980dcf8ca1b50f9266fe3c110f53d6d08a32373611eae81f2b3fbfd56a91a47445bccb4700e757ddd852d61f2e8e7')
validpgpkeys=(F55CDAB508C3ACBCB1C8B930C910F152653B1688) # Marek Kasik <mkasik@redhat.com>

prepare() {
  cd $pkgname

  git submodule init
  git submodule set-url subprojects/gtk-frdp "$srcdir/gtk-frdp"
  git -c protocol.file.allow=always -c protocol.allow=never submodule update

  # Use GApplication to parse command line options
  # https://gitlab.gnome.org/GNOME/gnome-connections/-/merge_requests/168
  git apply -3 ../0001-Use-GApplication-to-parse-command-line-options.patch
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
