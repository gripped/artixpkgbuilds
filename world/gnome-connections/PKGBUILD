# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=gnome-connections
pkgver=47.2.1
pkgrel=1
pkgdesc='Remote desktop client for the GNOME desktop environment'
arch=(x86_64)
url='https://apps.gnome.org/Connections/'
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  freerdp
  fuse3
  gdk-pixbuf2
  glib2
  glibc
  gtk-vnc
  gtk3
  hicolor-icon-theme
  libhandy
  libsecret
  libxml2
)
makedepends=(
  appstream-glib
  git
  gobject-introspection
  itstool
  meson
  vala
)
groups=(gnome)
source=(
  "git+https://gitlab.gnome.org/GNOME/connections.git?signed#tag=${pkgver/[a-z]/.&}"
  git+https://gitlab.gnome.org/GNOME/gtk-frdp.git
)
b2sums=('fc98cf431945229c302de6162680ac6816c0079022a51c17207d07847e1cbf1efc79c8f5150ea1ac3e268b765f8b283e37625a70f3bd901fd93b1ce6835dfbf1'
        'SKIP')
validpgpkeys=(F55CDAB508C3ACBCB1C8B930C910F152653B1688) # Marek Kasik <mkasik@redhat.com>

prepare() {
  cd connections

  git submodule init
  git submodule set-url subprojects/gtk-frdp "$srcdir/gtk-frdp"
  git -c protocol.file.allow=always submodule update
}

build() {
  artix-meson connections build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
