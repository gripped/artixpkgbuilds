# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Jonas Witschel <diabonas@archlinux.org>

pkgname=python-python-socks
pkgver=2.7.0
pkgrel=1
pkgdesc='Core proxy client (SOCKS4, SOCKS5, HTTP) functionality for Python'
arch=('any')
url='https://github.com/romis2012/python-socks'
license=('Apache-2.0')
depends=('python')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'python-anyio'
  'python-curio'
  'python-flask'
  'python-pytest'
  'python-pytest-asyncio'
  'python-pytest-trio'
  'python-tiny-proxy'
  'python-trio'
  'python-trustme'
  'python-yarl'
)
optdepends=(
  'python-anyio: anyio support'
  'python-async-timeout: asyncio support'
  'python-curio: curio support'
  'python-trio: trio support'
)
provides=('python-python_socks')
replaces=('python-python_socks')
source=("git+$url.git?signed#tag=v$pkgver")
sha512sums=('26748559e7c06915c0b4b746302e70d546b477dbfd06fbb8f5a0e7be8358b05c08cbbdf0344f64f03631a2168595eb235c6127625448fe8a8110ecdb197d17c6')
validpgpkeys=('706A8935FED55F3BFB7C188B5EEAD7400A5D80ED') # Roman Snegirev <rsng@mail.ru>

build() {
  cd "${pkgname#python-}"
  python -m build --wheel --no-isolation
}

# check() {
#   cd "${pkgname#python-}"
#   pytest
# }

package() {
  cd "${pkgname#python-}"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
