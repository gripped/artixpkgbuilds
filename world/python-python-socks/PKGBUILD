# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Jonas Witschel <diabonas@archlinux.org>

pkgname=python-python-socks
pkgver=2.7.2
pkgrel=1
pkgdesc='Core proxy client (SOCKS4, SOCKS5, HTTP) functionality for Python'
arch=('any')
url='https://github.com/romis2012/python-socks'
license=('Apache-2.0')
depends=('python')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'python-anyio'
  'python-curio'
  'python-flask'
  'python-pytest'
  'python-pytest-asyncio'
  'python-pytest-trio'
  'python-tiny-proxy'
  'python-trio'
  'python-trustme'
  'python-yarl'
)
optdepends=(
  'python-anyio: anyio support'
  'python-curio: curio support'
  'python-trio: trio support'
)
provides=('python-python_socks')
replaces=('python-python_socks')
source=("git+$url.git?signed#tag=v$pkgver")
sha512sums=('36813194fe7306781c452635008adc88cc456ec8d859d36690b721bad86109bce22a5af65c08fff9ab93f9c95bc466098fb1f610c30939655f1d6c2a1d64c09e')
validpgpkeys=('706A8935FED55F3BFB7C188B5EEAD7400A5D80ED') # Roman Snegirev <rsng@mail.ru>

build() {
  cd "${pkgname#python-}"
  python -m build --wheel --no-isolation
}

# check() {
#   cd "${pkgname#python-}"
#   pytest
# }

package() {
  cd "${pkgname#python-}"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
