# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Dan McGee <dan@archlinux.org>

pkgname=pgbouncer
pkgver=1.25.1
pkgrel=2
pkgdesc='Lightweight connection pooler for PostgreSQL'
url='https://www.pgbouncer.org/'
arch=(x86_64)
license=(ISC)
depends=(
  c-ares
  glibc
  libcares.so
  libevent
  libpam.so
  openssl
  pam
)
makedepends=(
  pandoc
  python
)
checkdepends=(
  postgresql
  postgresql-libs
)
backup=(
  etc/pgbouncer/pgbouncer.ini
  etc/logrotate.d/pgbouncer
)
source=(
  "https://pgbouncer.github.io/downloads/files/${pkgver}/pgbouncer-${pkgver}.tar.gz"
  pgbouncer.ini
  pgbouncer.logrotate
  pgbouncer.tmpfiles
  pgbouncer.sysusers
)
sha512sums=('c28633f27d13d499c85def6e048a080de85f6c7c9276d2fde89d34e6bc59a15a4bae540eeaae90d137120f8667b659f5e82fa9b8084ecbe54353f04a9177caf6'
            '39abc7b11c9d7a2593941b4d2a82db998ac1b1e3da131ae276da73c7afc4eda7b69bbfd0acd39f7bce20ecb911baf0adba341ff58dcab1a57e419708e7c8d26f'
            '1dc86704fce211b23afe7962c947c7de80a15bef219928acbf486b915d80d44a6590fbc509fe650c97b694a508bcf95d2152663863f0c372323286e644c6d60a'
            '0757205e5cb5f679bab00bdbed2c828a536239f380d537498bc893c77162e0978308a5f38fae4cb6470f18d6ed0fe4a3ce8f00e85aef89b14bcf6e2eab05a696'
            'ec4064e9eefc1e08148ea465ae1ec3fcf8d7e17ec83fd3e031d197d257b7bd2cc678ff49dd4ee9cd54f73d50f3d0f9f4d92513395e125036482afe83ced06088')

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --disable-debug \
    --with-pam
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -C test check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -vDm 644 NEWS.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 COPYRIGHT -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 ../pgbouncer.ini "${pkgdir}/etc/pgbouncer/pgbouncer.ini"
  install -vDm 644 ../pgbouncer.logrotate "${pkgdir}/etc/logrotate.d/pgbouncer"
  install -vDm 644 ../pgbouncer.tmpfiles "${pkgdir}/usr/lib/tmpfiles.d/pgbouncer.conf"
  install -vDm 644 ../pgbouncer.sysusers "${pkgdir}/usr/lib/sysusers.d/pgbouncer.conf"
}

# vim: ts=2 sw=2 et:
