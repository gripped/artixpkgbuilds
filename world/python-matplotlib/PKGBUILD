# Maintainer: Jakub Klinkovský <lahwaacz at archlinux dot org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgname=python-matplotlib
pkgver=3.10.6
pkgrel=1
_mpl_images_ver=3.10.0
_mpl_images_ftver=2.13.1
pkgdesc="A python plotting library, making publication quality plots"
arch=(x86_64)
url="https://matplotlib.org"
license=(PSF-2.0)
depends=(
  gcc-libs
  glibc
  freetype2
  python
  python-contourpy
  python-cycler
  python-dateutil
  python-fonttools
  python-kiwisolver
  python-numpy
  python-packaging
  python-pillow
  python-pyparsing
  qhull
)
optdepends=(
  'tk: Tk{Agg,Cairo} backends'
  'pyside6: alternative for Qt6{Agg,Cairo} backends'
  'python-pyqt5: Qt5{Agg,Cairo} backends'
  'python-pyqt6: Qt6{Agg,Cairo} backends'
  'python-gobject: for GTK{3,4}{Agg,Cairo} backend'
  'python-wxpython: WX{Agg,Cairo} backend'
  'python-cairo: {GTK{3,4},Qt{5,6},Tk,WX}Cairo backends'
  'python-cairocffi: alternative for Cairo backends'
  'python-tornado: WebAgg backend'
  'ffmpeg: for saving movies'
  'imagemagick: for saving animated gifs'
  'ghostscript: usetex dependencies'
  'texlive-binextra: usetex dependencies'
  'texlive-fontsrecommended: usetex dependencies'
  'texlive-latexrecommended: usetex usage with pdflatex'
  'python-certifi: https support'
)
makedepends=(
  git
  meson-python
  pybind11
  python-build
  python-certifi
  python-installer
  python-setuptools-scm
  python-wheel
)
checkdepends=(
  ffmpeg
  ghostscript
  gtk4
  imagemagick
  inkscape
  jupyter-nbconvert
  jupyter-nbformat
  noto-fonts-cjk
  pyside6
  python-cairo
  python-cairocffi
  python-gobject
  python-ipykernel
  python-pandas
  python-pikepdf
  python-pyqt5
  python-pyqt6
  python-pytest
  python-pytest-rerunfailures
  python-pytest-xdist
  python-pytz
  python-tornado
  python-wxpython
  python-xarray
 
  texlive-bin
  texlive-fontsrecommended
  texlive-latexextra
  texlive-luatex
  texlive-pictures
  tk
  ttf-dejavu
  wqy-zenhei
  xorg-server-xvfb
)
source=(git+https://github.com/matplotlib/matplotlib.git?signed#tag=v${pkgver}
        https://github.com/QuLogic/mpl-images/archive/v${_mpl_images_ver}-with-freetype-${_mpl_images_ftver}/mpl-images-${_mpl_images_ver}-ft${_mpl_images_ftver}.tar.gz
        freetype.patch
        test_polar_title_position.patch
        test_imshow_masked_interpolation.patch
)
b2sums=('7a42ce70d8a75f9d6c55bd21d007c85a9e14a7d9a2c8bb4e082698b4efa04190d952701549d61147fd396acbb898b7b1a4f65e4fc13c4c2d15d1d036e54c63cc'
        '63f89667204996c6a78c297bda22526ceb25112c0ccbe14a07b92772e8f9b4bad213e8fd27a4ce25bd2fa983bbc6450b66b5315d92f444f8105f6ece5aca0601'
        'c66bec7fd0c9236432e814c34216f7c3d3f7634a3d37669c92c442d077ee762ed631737697423e4ea8cc249b8dd961bdb4ffb9ab32eddfb1bfa45cbafcfd5f94'
        '0c2dfbcda86be5d9bdf19c16fc031ae28b10e6aa89eaf3701b88d67b4158bb5dc0469a0bcb7c0050004f45b7aa07e8ed3fe68512f94a0b54b4e03c9b6e33349c'
        '2378456c277ad20d4c2d0781e70f2db63c2f2892ec35e9f0569c8f8943fea97b5efaab740bd582b7dece55572d65b01a13364382ef1fd1aeaa9f24076ddc6e08')
validpgpkeys=(23CAB59E3332F94D26BEF0378D86E7FAE5EB0C10  # Elliott Sales de Andrade <quantum.analyst@gmail.com>
              EB8322187FD451192E430A7279B3FEC456F12599) # Kyle Sunden <contact@ksunden.space>

prepare() {
  cd matplotlib

  # Allow to use new meson-python
  sed -i 's|,<0.17.0||' pyproject.toml

  # From Fedora/upstream
  patch -p1 < ../freetype.patch

  # Use appropriate baseline images for tests
  for _module in matplotlib mpl_toolkits
  do
    cp -r ../mpl-images-${_mpl_images_ver}-with-freetype-${_mpl_images_ftver}/lib/${_module}/* lib/${_module}
  done

  # Version 3.10.1 added a new test but for some reason the space below the title is slightly off in our runs
  # https://github.com/matplotlib/matplotlib/pull/29382/files
  patch -p1 < ../test_polar_title_position.patch

  # Workaround test failure due to image comparison (version 3.10.5)
  patch -p1 < ../test_imshow_masked_interpolation.patch
}

build() {
  cd matplotlib
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
  python -m build --wheel --no-isolation \
    -C setup-args=-Dsystem-freetype=true \
    -C setup-args=-Dsystem-qhull=true
}

check() {
  cd matplotlib
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl

  # Use appropriate baseline images for tests
  local python_version=`python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))'`
  for _module in matplotlib mpl_toolkits
  do
    cp -r lib/${_module}/* test-env/lib/python${python_version}/site-packages/${_module}
    cp -r ../mpl-images-${_mpl_images_ver}-with-freetype-${_mpl_images_ftver}/lib/${_module}/* test-env/lib/python${python_version}/site-packages/${_module}
  done

  local pytest_options=(
      # show more verbose output
      -vv
      # distribute tests across multiple CPUs
      -n auto
      # run tests from the following python packages
      --pyargs matplotlib mpl_toolkits.{axes_grid1,axisartist,mplot3d}
      # test_ipynb: https://github.com/matplotlib/matplotlib/issues/21654 fixed but `ModuleNotFoundError: No module named 'matplotlib'`
      # test_compressed1: https://github.com/QuLogic/mpl-images/issues/4
      # test_constrained_layout, test_tight_layout, test_axisline_style_tight, test_diff_cell_table: baseline images changed
      #   in version 3.10.3, but the QuLogic/mpl-images repo was not updated yet
      # test_mathtext_rendering_lightweight: TypeError: 'str' object is not callable (in version 3.10.3)
      -k 'not test_ipynb and not test_compressed1 and not test_multi_font_type3 and not test_multi_font_type42 and not test_figure_legend_outside and not test_constrained_layout and not test_tight_layout and not test_constrainedlayout and not test_tightlayout and not test_axisline_style_tight and not test_diff_cell_table and not test_mathtext_rendering_lightweight'
  )
  XDG_RUNTIME_DIR=/tmp/runtime-build \
  PYTHONPATH=test-env/lib/python${python_version}/site-packages \
  xvfb-run -a -s "-screen 0 640x480x24" \
    test-env/bin/python -m pytest "${pytest_options[@]}"
}

package() {
  cd matplotlib
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
  python -m installer --destdir="${pkgdir}" dist/*.whl
  # Install license files
  install -vDm 644 LICENSE/* -t "${pkgdir}"/usr/share/licenses/${pkgname}/
  # Remove tests
  rm -r "${pkgdir}"$(python -c "import site; print(site.getsitepackages()[0])")/{matplotlib,mpl_toolkits/*}/tests/
}
