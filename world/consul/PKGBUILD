# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=consul
pkgver=1.22.1
pkgrel=1
pkgdesc="A tool for service discovery, monitoring and configuration."
arch=('x86_64')
url="https://www.consul.io"
license=('BUSL-1.1')
depends=('glibc')
makedepends=('git' 'go' 'gox' 'go-tools')
makedepends+=('procps-ng' 'zip' 'yarn' 'nodejs-lts-iron' 'pnpm')  # makedepends for the UI
source=("git+https://github.com/hashicorp/consul#tag=v${pkgver}"
        'consul.default'
        'consul.sysusers'
        'example.json'
        )
install=consul.install
backup=('etc/default/consul')
sha512sums=('4d84b7633a1bd87fa3cf92922a1fd4191012d319bc4f6d7062492cc1f9a7539304e05bff0e32e337d8f3f07ce3ea48ca25df14a185b7e90e606bc96ba1980c83'
            'ec5a800529a297c709fa383c094ecf106351cf0f8ac7b613b972d415d77fe001088902d7ab805e63e78a8e6360323fec1b795db5a4446df1e21b9b4ed31e7079'
            'f6d66e970f7cdbba2f1a3d15d89df5dfa33810d6330dae0aaf42dee0938354667a505c39c65db74fe3c06c8f753bc6155a64419fa31b4445ccbe23f5e6a685c6'
            'c4292b8f56ee955ed7385a49843fd90d6434029891b3e1e724cb2fc841514c06e2554a26d3937c114371b18c2168c4e64319eb2cbd726ee8b35870df19089348')

export CGO_LDFLAGS="${LDFLAGS}"
export CGO_CFLAGS="${CFLAGS}"
export CGO_CPPFLAGS="${CPPFLAGS}"
export CGO_CXXFLAGS="${CXXFLAGS}"
# consul is incompatible with -buildmode=pie
export GOFLAGS="-trimpath -mod=readonly -modcacherw"

export GOOS='linux'
export GOARCH='amd64'
export XC_OSARCH='linux/amd64'

prepare() {
  cd "${srcdir}/${pkgname}"

  local filename
  for filename in "${source[@]}"; do
    if [[ "$filename" =~ \.patch$ ]]; then
      patch -p1 -N -l -i "$srcdir/${filename##*/}"
    fi
  done

  mkdir -p build
}

build() {
  # https://github.com/hashicorp/consul/blob/v1.19.1/.github/workflows/build.yml#L128
  cd "${srcdir}/${pkgname}/ui/packages/consul-ui"
  make build
  rm -Rf "${srcdir}/${pkgname}/agent/uiserver/dist"
  mv dist "${srcdir}/${pkgname}/agent/uiserver/"

  cd "${srcdir}/${pkgname}"
  go build -o build './...'
}

check() {
  cd "${srcdir}/${pkgname}"
  # assert version number from commit hash matches pkgver metadata
  ./build/consul --version | head -1 | grep -qF -- "v${pkgver}"
}

package() {
  cd "${srcdir}/${pkgname}"

  install -D -d -m750 -o 208 -g 208 "${pkgdir}/var/lib/consul"
  install -D -d -m750 -o   0 -g 208 "${pkgdir}/etc/consul.d"

  install -D -m644 "${srcdir}/consul.default" "${pkgdir}/etc/default/consul"
  install -D -m644 -o 0 -g 0 "${srcdir}/example.json" "${pkgdir}/usr/share/doc/consul/config.example.json"
  install -Dm755 "build/consul" "${pkgdir}/usr/bin/consul"

  install -Dm644 "${srcdir}/consul.sysusers" "${pkgdir}/usr/lib/sysusers.d/consul.conf"

  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
