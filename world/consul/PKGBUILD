# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=consul
pkgver=1.20.4
pkgrel=2
pkgdesc="A tool for service discovery, monitoring and configuration."
arch=('x86_64')
url="https://www.consul.io"
license=('BUSL-1.1')
depends=('glibc')
makedepends=('git' 'go' 'gox' 'go-tools')
makedepends+=('procps-ng' 'zip' 'yarn' 'nodejs-lts-iron' 'npm')  # makedepends for the UI
source=("git+https://github.com/hashicorp/consul#tag=v${pkgver}"
        'consul.default'
        'consul.sysusers'
        'example.json'
        # Switch to NodeJS LTS 20 "Iron"
        'https://github.com/hashicorp/consul/commit/3ecdb19cc69396a3232bdb74116f30b7b3a72d04.patch'
        )
install=consul.install
backup=('etc/default/consul')
sha512sums=('2a0d6d0879f8845329315947c06180e8a428c2670daad370b5f5c231eef48ab7039cb07b313164ecf975a693216a3069ba365d71c074e22ba3a1aadb840f7b09'
            'ec5a800529a297c709fa383c094ecf106351cf0f8ac7b613b972d415d77fe001088902d7ab805e63e78a8e6360323fec1b795db5a4446df1e21b9b4ed31e7079'
            'ef872aedb2bc022a29292b7972a792b22e684c1ccb904a2b2cfec6d8966c28fb19be1452ce060821c419f1b646b236ba2e783175595e4bb6926d164c27a15c87'
            'c4292b8f56ee955ed7385a49843fd90d6434029891b3e1e724cb2fc841514c06e2554a26d3937c114371b18c2168c4e64319eb2cbd726ee8b35870df19089348'
            '39aa5761a725fab8e9cf3eabd7c47d64b91489c03c059747849ec4a4b1032e44f2770a238338fb43c963516c6e941fd82157b91128c2c1899ba5448a9dc49979')

export CGO_LDFLAGS="${LDFLAGS}"
export CGO_CFLAGS="${CFLAGS}"
export CGO_CPPFLAGS="${CPPFLAGS}"
export CGO_CXXFLAGS="${CXXFLAGS}"
# consul is incompatible with -buildmode=pie
export GOFLAGS="-trimpath -mod=readonly -modcacherw"

export GOOS='linux'
export GOARCH='amd64'
export XC_OSARCH='linux/amd64'

prepare() {
  cd "${srcdir}/${pkgname}"

  local filename
  for filename in "${source[@]}"; do
    if [[ "$filename" =~ \.patch$ ]]; then
      patch -p1 -N -l -i "$srcdir/${filename##*/}"
    fi
  done

  mkdir -p build
}

build() {
  # https://github.com/hashicorp/consul/blob/v1.19.1/.github/workflows/build.yml#L128
  cd "${srcdir}/${pkgname}/ui/packages/consul-ui"
  make build
  rm -Rf "${srcdir}/${pkgname}/agent/uiserver/dist"
  mv dist "${srcdir}/${pkgname}/agent/uiserver/"

  cd "${srcdir}/${pkgname}"
  go build -o build './...'
}

check() {
  cd "${srcdir}/${pkgname}"
  # assert version number from commit hash matches pkgver metadata
  ./build/consul --version | head -1 | grep -qF -- "v${pkgver}"
}

package() {
  cd "${srcdir}/${pkgname}"

  install -D -d -m750 -o 208 -g 208 "${pkgdir}/var/lib/consul"
  install -D -d -m750 -o   0 -g 208 "${pkgdir}/etc/consul.d"

  install -D -m644 "${srcdir}/consul.default" "${pkgdir}/etc/default/consul"
  install -D -m644 -o 0 -g 0 "${srcdir}/example.json" "${pkgdir}/usr/share/doc/consul/config.example.json"
  install -Dm755 "build/consul" "${pkgdir}/usr/bin/consul"

  install -Dm644 "${srcdir}/consul.sysusers" "${pkgdir}/usr/lib/sysusers.d/consul.conf"

  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
