# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alex Filgueira <alexfilgueira (at antergos.com)>
# Contributor: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xapp
pkgver=2.8.4
pkgrel=1
pkgdesc="Common library for X-Apps project"
arch=(x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL)
depends=(libdbusmenu-gtk3 libgnomekbd)
optdepends=('python: for mate-desktop status applet')
makedepends=(meson samurai gobject-introspection python-gobject vala glib2-devel)
provides=(xapps)
conflicts=(xapps)
replaces=(xapps)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('cdcf13037451c0e74064c113d93aa438fb259d206199d023defa2690db5b9a19')
b2sums=('7f3a17b96e236e65eca4369cc21c0f5e913d5727ef17dba03bc7e894ba62285ec9421ad4c53c0e7136ea1ba569f6894a73b11485c6629176f32b69f4dec1587f')

build() {
  artix-meson ${pkgname}-${pkgver} build \
    --buildtype=debugoptimized
# https://github.com/linuxmint/xapp/issues/169
  samu -C build
}

package(){
  DESTDIR="${pkgdir}" samu -C build install

  # byte-compile python modules since meson does not implement autotoolsâ€™ py-compile.
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  python -m compileall -o 0 -o 1 -o 2 --hardlink-dupes -s "${pkgdir}" "${pkgdir}"${site_packages}
}
