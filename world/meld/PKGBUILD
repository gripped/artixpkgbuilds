# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=meld
pkgver=3.22.3
pkgrel=2
pkgdesc="Compare files, directories and working copies"
url="https://meldmerge.org/"
license=(GPL-2.0-or-later)
arch=(any)
depends=(
  dconf
  gdk-pixbuf2
  glib2
  gsettings-desktop-schemas
  gtk3
  gtksourceview4
  hicolor-icon-theme
  pango
  python
  python-cairo
  python-gobject
)
makedepends=(
  git
  meson
  yelp-tools
)
checkdepends=(appstream-glib)
source=("git+https://gitlab.gnome.org/GNOME/meld.git#tag=$pkgver")
b2sums=('7f6b807b841ef34a09cc04dc045ea11f5c3909ecce5fd238b07625d5c9fd4465cb300a5a5158ac9f66d7487828b88acd16b81270eed01fca97b17fe8c15d84ca')

prepare() {
  cd meld
}

build() {
  artix-meson meld build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
