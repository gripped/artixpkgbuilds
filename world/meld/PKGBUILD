# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=meld
pkgver=3.24.0
pkgrel=1
pkgdesc="Compare files, directories and working copies"
url="https://meldmerge.org/"
license=(GPL-2.0-or-later)
arch=(any)
depends=(
  dconf
  gdk-pixbuf2
  glib2
  gsettings-desktop-schemas
  gtk3
  gtksourceview4
  hicolor-icon-theme
  pango
  python
  python-cairo
  python-gobject
)
makedepends=(
  git
  meson
  yelp-tools
)
checkdepends=(appstream-glib)
source=("git+https://gitlab.gnome.org/GNOME/meld.git#tag=$pkgver")
b2sums=('6d2462ca38255183156c6d8de68eab0fd28c52bfc5ffe6fb44dd33d7c6b8bdd0bdf5dcb84a104d558e351d7c8fc35d2868e173142689b902fa87bcc946b65d41')

prepare() {
  cd meld
}

build() {
  artix-meson meld build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
