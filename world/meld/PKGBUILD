# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=meld
pkgver=3.22.1
pkgrel=1
pkgdesc="Compare files, directories and working copies"
url="https://meldmerge.org/"
license=(GPL-2.0-or-later)
arch=(any)
depends=(
  dconf
  gsettings-desktop-schemas
  gtksourceview4
  python-cairo
  python-gobject
)
makedepends=(
  git
  meson
  yelp-tools
)
checkdepends=(appstream-glib)
_commit=1138db7d42e5db1e426fbe09d2588103febfc5df  # tags/3.22.1^0
source=("git+https://gitlab.gnome.org/GNOME/meld.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd meld
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd meld
}

build() {
  artix-meson meld build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
