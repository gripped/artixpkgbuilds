# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-sequel
pkgver=5.95.1
pkgrel=1
pkgdesc='Sequel: The Database Toolkit for Ruby'
arch=(any)
url='https://github.com/jeremyevans/sequel'
license=(MIT)
depends=(
  ruby
  ruby-bigdecimal
  ruby-csv
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-minitest
  ruby-minitest-global_expectations
  ruby-minitest-hooks
  ruby-rake
)
options=(!emptydirs)
source=(git+https://github.com/jeremyevans/sequel.git#tag=$pkgver)
sha512sums=('b6730dba85d11c80f9add6a260a1d23f5abe7894a61139828d66f3d232033ca2293cf1cf61102f1e5b5813e0d22b157607195428a8e502e003badaad2ccfe3cd')
b2sums=('ade7244372abf7b231cce63b6e01e114a1a680b69f9f7b3d36a379ff21ee14b896a145519da058dea5248f2f605acfcc2fe534f7db5be9469d935def3e533462')

prepare() {
  cd sequel
  sed '/activemodel/d' -i sequel.gemspec
  rm spec/extensions/active_model_spec.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd sequel
  gem build sequel.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    sequel-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd sequel
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd sequel
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 MIT-LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
