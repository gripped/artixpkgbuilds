# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-sequel
pkgver=5.91.0
pkgrel=1
pkgdesc='Sequel: The Database Toolkit for Ruby'
arch=(any)
url='https://github.com/jeremyevans/sequel'
license=(MIT)
depends=(
  ruby
  ruby-bigdecimal
  ruby-csv
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-minitest
  ruby-minitest-global_expectations
  ruby-minitest-hooks
  ruby-rake
)
options=(!emptydirs)
source=(git+https://github.com/jeremyevans/sequel.git#tag=$pkgver)
sha512sums=('08026ec1cad1c8e6366ea96371cc4eb706e3bb113aefe8b1f80c10bf9a33bdd7d9c86e6be2741ed6b40115ec15197dc3667c360cd0a61d04de40c3210704a28c')
b2sums=('5594402710724de523012aa0146a6ec72057effb1d4c4013db676e2e449c2ccd8964363e881c8b3637437cd9a6e7ae11dbdf7cf41b43265294a3f799a72690b1')

prepare() {
  cd sequel
  sed '/activemodel/d' -i sequel.gemspec
  rm spec/extensions/active_model_spec.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd sequel
  gem build sequel.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    sequel-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd sequel
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd sequel
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 MIT-LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
