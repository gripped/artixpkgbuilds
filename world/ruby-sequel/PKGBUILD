# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-sequel
pkgver=5.95.0
pkgrel=1
pkgdesc='Sequel: The Database Toolkit for Ruby'
arch=(any)
url='https://github.com/jeremyevans/sequel'
license=(MIT)
depends=(
  ruby
  ruby-bigdecimal
  ruby-csv
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-minitest
  ruby-minitest-global_expectations
  ruby-minitest-hooks
  ruby-rake
)
options=(!emptydirs)
source=(git+https://github.com/jeremyevans/sequel.git#tag=$pkgver)
sha512sums=('44ce8668279fd06dfb8e1571ca19d2a3f63c413471dc0f7fb2605408b3e4c32b75d5de4f586913c89d532c67a1a915667eb1560914d79b7bcb87bdaab7d6b86d')
b2sums=('d65534f9d099cf4d32a7a27b12c26949c5030add19b64e87ebadad6db96be7a43527c3754eb5d9a998deed7722e67a828bf24e9c9d7014315f823b665d49bf8b')

prepare() {
  cd sequel
  sed '/activemodel/d' -i sequel.gemspec
  rm spec/extensions/active_model_spec.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd sequel
  gem build sequel.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    sequel-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd sequel
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd sequel
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 MIT-LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
