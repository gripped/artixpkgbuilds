# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-sequel
pkgver=5.103.0
pkgrel=1
pkgdesc='Sequel: The Database Toolkit for Ruby'
arch=(any)
url='https://github.com/jeremyevans/sequel'
license=(MIT)
depends=(
  ruby
  ruby-bigdecimal
  ruby-csv
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-minitest
  ruby-minitest-global_expectations
  ruby-minitest-hooks
  ruby-rake
)
options=(!emptydirs)
source=(git+https://github.com/jeremyevans/sequel.git#tag=$pkgver)
sha512sums=('de53b18acca6a8c1c1bd67127513a95e68ac40b0b4fb8480dbcfed8ff5df47ca8d2a8fb2b357e61da145d4b84bb540fea6eef0ad71834323295440a3636e4038')
b2sums=('212eec401c9c4f5316fb36ccb0955974664f6e06a289765a0b55598774f3bab70380b81d358526469a37d0b32734b697ee907e727e7ea7e63483f450776c7aaa')

prepare() {
  cd sequel
  sed '/activemodel/d' -i sequel.gemspec
  rm spec/extensions/active_model_spec.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd sequel
  gem build sequel.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    sequel-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd sequel
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd sequel
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 MIT-LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
