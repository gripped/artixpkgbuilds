# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=folks
pkgver=0.15.8
pkgrel=1
pkgdesc="Library to aggregates people into metacontacts"
url="https://wiki.gnome.org/Projects/Folks"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  evolution-data-server
  libgee
  libxml2
)
makedepends=(
  git
  gobject-introspection
  gtk-doc
  libsysprof-capture
  meson
  python-dbusmock
  vala
)
provides=(
  libfolks.so
  libfolks-{dummy,eds}.so
)
options=(
  !lto # LTO copies some GType constructors
)
_commit=554fca9ae1e37d0a5723f1d4ca8701b75c691a68  # tags/0.15.8^0
source=("git+https://gitlab.gnome.org/GNOME/folks.git#commit=$_commit")
b2sums=('6285dba30fc4a78d4df533345946528fe601eafccf6899edd4d317efef0e3caca53a830ac3e8fd528882a99817fd90fdcdd37d2f0b286d22f1cf4a8f9674ae84')

pkgver() {
  cd folks
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd folks
}

build() {
  local meson_options=(
    -D docs=true
    -D profiling=true
    -D telepathy_backend=false
  )

  artix-meson folks build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs -t 4
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
