# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=folks
pkgver=0.15.9
pkgrel=2
pkgdesc="Library to aggregates people into metacontacts"
url="https://wiki.gnome.org/Projects/Folks"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  dconf
  evolution-data-server
  glib2
  glibc
  libgee
  libxml2
  readline
)
makedepends=(
  git
  gobject-introspection
  gtk-doc
  libsysprof-capture
  meson
  python-dbusmock
  vala
)
provides=(
  libfolks.so
  libfolks-{dummy,eds}.so
)
options=(
  !lto # LTO copies some GType constructors
)
source=("git+https://gitlab.gnome.org/GNOME/folks.git?signed#tag=$pkgver")
b2sums=('f86c2302139f244f6d8ff3b7207104dc7e81aed6f51fd450f2fdcd6bbcef7866cc8d0d4764c21c06bacc8946714589955e598c79b0d4d50fbc11c0628a7d5410')
validpgpkeys=(
  A7C626E13F9AD776776BD9CA1D8A57CF2E8D36A3 # Niels De Graef <ndegraef@redhat.com>
)

prepare() {
  cd folks
  git cherry-pick -n b1888d6beae08bc67fa50f5b67619fa4e5463951
}

build() {
  local meson_options=(
    -D docs=true
    -D profiling=true
    -D telepathy_backend=false
  )

  artix-meson folks build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs -t 4 || :
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
