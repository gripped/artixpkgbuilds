# Maintainer: Jonas Witschel <diabonas@archlinux.org>

pkgname=mpv-mpris
pkgver=1.2
pkgrel=1
pkgdesc='MPRIS plugin for mpv'
arch=('x86_64')
url='https://github.com/hoyon/mpv-mpris'
license=('MIT')
depends=('glib2' 'libavformat.so' 'mpv')
checkdepends=('jq' 'playerctl' 'socat' 'sound-theme-freedesktop' 'xorg-server-xvfb')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
sha512sums=('251348a14b4efbfd3eb8a482bcabb2a53a9ee4d33b89f6066efcfc60b6c8bcad6d953b10977d9cfbd23e28621f9117b436002b74ffa33deaec5ea224d9ce6753')

build() {
	cd "$pkgname-$pkgver"
	make
}

check() {
	cd "$pkgname-$pkgver"
	make test
}

package() {
	cd "$pkgname-$pkgver"
	make PREFIX=/usr DESTDIR="$pkgdir" install
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
