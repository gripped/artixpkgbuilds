# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kate
pkgver=24.02.0
pkgrel=3
arch=(x86_64)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
pkgdesc='Advanced text editor'
groups=(kde-applications
        kde-utilities)
url='https://apps.kde.org/kate/'
depends=(gcc-libs
         glibc
         kbookmarks
         kcolorscheme
         kcompletion
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         kdbusaddons
         kguiaddons
         ki18n
         kiconthemes
         kio
         knewstuff
         kparts
         kservice
         ktexteditor
         ktextwidgets
         kuserfeedback
         kwidgetsaddons
         kwindowsystem
         kxmlgui
         qt6-base
         syntax-highlighting)
optdepends=('konsole: open a terminal in Kate'
            'clang: C and C++ LSP support'
            'python-lsp-server: Python LSP support'
            'texlab: LaTeX LSP support'
            'rust: Rust LSP support'
            'git: git-blame plugin')
makedepends=(extra-cmake-modules
             kdoctools)
conflicts=(kwrite)
provides=(kwrite)
replaces=(kwrite)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/utilities/kate/-/commit/1eb6fea0.patch)
sha256sums=('9b1efcccf4e36efcdbc2eada6399cd8128ce187a19182400313062f75ee35f48'
            'SKIP'
            '6823fdc4beb19a78b1b7a8f69d2192302880943dd002467fee13cdcfaa0414ad')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < 1eb6fea0.patch # Fix LSP server crashes
}

build() {
  artix-cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
