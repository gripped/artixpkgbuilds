# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux,org>

pkgname=krita
pkgver=5.2.15
pkgrel=1
pkgdesc='Edit and paint images'
arch=(x86_64)
url='https://krita.org'
license=(GPL3)
depends=(exiv2
         ffmpeg
         fftw
         fontconfig
         freetype2
         fribidi
         gcc-libs
         giflib
         glibc
         gsl
         harfbuzz
         imath
         kcompletion5
         kconfig5
         kcoreaddons5
         kcrash5
         kguiaddons5
         ki18n5
         kitemviews5
         kwidgetsaddons5
         kwindowsystem5
         lcms2
         libjpeg-turbo
         libkdcraw5
         libpng
         libtiff
         libunibreak
         libwebp
         libx11
         mlt
         opencolorio
         openexr
         openjpeg2
         qt5-base
         qt5-svg
         qt5-x11extras
         quazip-qt5
         zlib)
makedepends=(boost
             eigen
             extra-cmake-modules
             immer
             kseexpr
             lager
             libheif
             libjxl
             libmypaint
             poppler-qt5
             python-pyqt5
             qt5-tools
             sip
             xsimd
             zug)
optdepends=('kimageformats5: PSD support'
            'krita-plugin-gmic: GMic plugin'
            'kseexpr: SeExpr generator layer'
            'libheif: HEIF filter'
            'libjxl: JPEG-XL filter'
            'libmypaint: support for MyPaint brushes'
            'poppler-qt5: PDF filter'
            'python-pyqt5: for the Python plugins')
source=(https://download.kde.org/stable/krita/$pkgver/$pkgname-$pkgver.tar.gz{,.sig}
        https://invent.kde.org/graphics/krita/-/commit/2d71c476.patch
        xsimd-14.patch)
sha256sums=('9b94f8421d9767c294def598fb10707dde6eb237baeca2590669fb0d4b903b39'
            'SKIP'
            '10b41e078a70296c026c97efb7d3a56c99426a9b675ea5e691d0a14d69829eec'
            'd52a6d31e98c1de730c3a5ead5239a786d1b6f3a1aec731f449e9f429a5b8a94')
validpgpkeys=('05D00A8B73A686789E0A156858B9596C722EA3BD'  # Boudewijn Rempt <foundation@krita.org>
              'E9FB29E74ADEACC5E3035B8AB69EB4CF7468332F'  # Dmitry Kazakov (main key) <dimula73@gmail.com>
              '064182440C674D9F8D0F6F8B4DA79EDA231C852B') # Stichting Krita Foundation <foundation@krita.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < xsimd-14.patch
  patch -d $pkgname-$pkgver -p1 < 2d71c476.patch # Specify minor sip abi version
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_KRITA_QT_DESIGNER_PLUGINS=ON \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
