# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux,org>

pkgname=krita
_pkgver=5.2.13
pkgver=${_pkgver/-/}
pkgrel=1.1
pkgdesc='Edit and paint images'
arch=(x86_64)
url='https://krita.org'
license=(GPL3)
depends=(exiv2
         ffmpeg
         fftw
         fontconfig
         freetype2
         fribidi
         gcc-libs
         giflib
         glibc
         gsl
         harfbuzz
         imath
         kcompletion5
         kconfig5
         kcoreaddons5
         kcrash5
         kguiaddons5
         ki18n5
         kitemviews5
         kwidgetsaddons5
         kwindowsystem5
         lcms2
         libjpeg-turbo
         libkdcraw5
         libpng
         libtiff
         libunibreak
         libwebp
         libx11
         mlt
         opencolorio
         openexr
         openjpeg2
         qt5-base
         qt5-svg
         qt5-x11extras
         quazip-qt5
         zlib)
makedepends=(boost
             eigen
             extra-cmake-modules
             immer
             kdoctools5
             kseexpr
             lager
             libheif
             libjxl
             libmypaint
             poppler-qt5
             python-pyqt5
             qt5-tools
             sip
             xsimd
             zug)
optdepends=('kimageformats5: PSD support'
            'krita-plugin-gmic: GMic plugin'
            'kseexpr: SeExpr generator layer'
            'libheif: HEIF filter'
            'libjxl: JPEG-XL filter'
            'libmypaint: support for MyPaint brushes'
            'poppler-qt5: PDF filter'
            'python-pyqt5: for the Python plugins')
source=(https://download.kde.org/stable/krita/$_pkgver/$pkgname-$_pkgver.tar.gz{,.sig}
        sip-6.8.patch
        eigen-5.patch)
sha256sums=('cb909430c99e2bf10684f1c13d76876691ee62572d9aaa54d7058931f9615531'
            'SKIP'
            'bb3b503993030bb98a99c6a0376c65ee74d0c32c1e8932110698682eed1e3d3c'
            '2c021e18347d6f895dd9a14d7cebc76b9e5349cb5554743a78650b26a12a0833')
validpgpkeys=('05D00A8B73A686789E0A156858B9596C722EA3BD'  # Boudewijn Rempt <foundation@krita.org>
              'E9FB29E74ADEACC5E3035B8AB69EB4CF7468332F'  # Dmitry Kazakov (main key) <dimula73@gmail.com>
              '064182440C674D9F8D0F6F8B4DA79EDA231C852B') # Stichting Krita Foundation <foundation@krita.org>

prepare() {
  patch -d $pkgname-$_pkgver -p1 < sip-6.8.patch
  patch -d $pkgname-$_pkgver -p1 < eigen-5.patch
}

build() {
  cmake -B build -S $pkgname-$_pkgver \
    -DBUILD_KRITA_QT_DESIGNER_PLUGINS=ON \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
