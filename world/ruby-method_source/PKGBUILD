# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='method_source'
pkgname="ruby-${_gemname}"
pkgver=1.1.0
pkgrel=1
pkgdesc='retrieve the sourcecode for a method'
arch=('any')
url='https://github.com/banister/method_source'
license=('MIT')
options=(!emptydirs)
depends=(
  ruby
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  # ruby-bundler
  ruby-rake
  ruby-rspec
)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('3f72aaa937ff7c99fc6a9c147a4247f0869f0f68dabf90ca3acde7a2d2512392e0cac58747d03a5a0f7a6afd8d53f7694073561594d8a2442f9ee93158e4f8e2')
b2sums=('38f05704154685f212ac95d784c20da36a15f1d66d6814f01831e14bb44d6c4c8eebc79963bec7732fc288513280f2ca26fefa6e6b1864b5b433e82e8b02cedb')

prepare() {
  cd "${_gemname}-${pkgver}"

  # we built based on a tar archive, not a git repo
  sed --in-place --regexp-extended 's|git ls-files -- spec/*|find spec -type f|' Rakefile
  sed --in-place --regexp-extended 's|git ls-files|find . -type f -not -path "*/.git*"|' Rakefile

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"
}

build() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unreproducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rake spec
}

package() {
  cd "${_gemname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md *.markdown --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
