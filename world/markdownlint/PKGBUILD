# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Robin Candau <antiz@archlinux.org>
# Contributor: Eric Berquist <eric dot berquist at gmail dot com>

pkgname=markdownlint
_pkgname=mdl
pkgver=0.18.0
pkgrel=1
pkgdesc='A lint tool to check markdown files and flag style issues'
arch=(any)
url='https://github.com/markdownlint/markdownlint'
license=(MIT)
depends=(
  ruby
  ruby-kramdown
  ruby-kramdown-parser-gfm
  ruby-mixlib-cli
  ruby-mixlib-config
  ruby-mixlib-shellout
)
makedepends=(git ruby-rdoc)
provides=(ruby-mdl)
replaces=(ruby-mdl)
options=(!emptydirs)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('7e0df93923e02928386585514f58cb32200254b5274bef08a62d557accb743631c1bd307dd8b9247f5a0358b0c5a4fe7f83d3e818f9ea97587c1063853fb107a')
b2sums=('0e0917fd2cc367a7540eaf5c36c8596f85fbed5b1695fdeab2835d04de6123b5483bc4504e30b8c4d609ec71ee27856ebbfce32b6449c192734ab3a2ed6a89fb')

prepare() {
  cd "$pkgname"

  # we do not do version pinning
  sed --regexp-extended 's,~>,>=,g' --in-place *.gemspec Gemfile Rakefile
}

build() {
  cd "$pkgname"

  gem build "$_pkgname.gemspec"
}

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --no-document \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$pkgname/$_pkgname-$pkgver.gem"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname/LICENSE.txt"
}
