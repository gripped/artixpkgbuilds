# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=lightsoff
pkgver=48.0
pkgrel=1
pkgdesc="Turn off all the lights"
url="https://wiki.gnome.org/Apps/Lightsoff"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  dconf
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  git
  meson
  vala
  yelp-tools
)
groups=(gnome-extra)
source=("git+https://gitlab.gnome.org/GNOME/lightsoff.git#tag=${pkgver/[a-z]/.&}")
b2sums=('4e89c25d7dd616121861bd02760f029a0649345bbe3b236e09cb6c78cc2b62764c30a69e0c7777c3da492ff1670b009106c289418be370c91ba03ec88e7f398b')

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
