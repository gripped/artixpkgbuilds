# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=libxmlb
pkgver=0.3.26
pkgrel=1
pkgdesc="Library to help create and query binary XML blobs"
arch=(x86_64)
url="https://github.com/hughsie/libxmlb"
license=(LGPL-2.1-only)
depends=(glib2
         glibc
         libgcc
         libstemmer
         xz
         zstd)
makedepends=(git
             gobject-introspection
             gtk-doc
             meson)
checkdepends=(shared-mime-info)
source=(git+https://github.com/hughsie/libxmlb#tag=$pkgver?signed)
sha256sums=('62a8f7ae81fbc0a08a291f49c3d4990644ae793582c7ef4920086d9d5bc74525')
validpgpkeys=(163EB50119225DB3DF8F49EA17ACBA8DFA970E17) # Richard Hughes <richard@hughsie.com>

build() {
  artix-meson $pkgname build -D stemmer=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  rm -r "$pkgdir"/usr/{lib,share}/installed-tests/
}
