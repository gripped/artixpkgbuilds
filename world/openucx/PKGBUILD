# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

_name=ucx
pkgname=openucx
pkgver=1.18.1
pkgrel=2
pkgdesc="Communication framework for data-centric and high-performance applications"
arch=(x86_64)
url="https://openucx.org/"
license=(BSD-3-Clause)
depends=(
  binutils
  gcc-libs
  glibc
  zlib
  zstd
)
makedepends=(
  rdma-core
  cuda
  rocm-language-runtime
)
optdepends=(
  'rdma-core: for InfiniBand and RDMA support'
  'cuda: for CUDA support'
  'rocm-language-runtime: for ROCm support'
)
provides=(
  libucm.so
  libucp.so
  libucs.so
  libucs_signal.so
  libuct.so
)
source=(
  $pkgname-$pkgver.tar.gz::https://github.com/openucx/$_name/archive/refs/tags/v$pkgver.tar.gz
  fix-headers-before-c-decls.patch
)
b2sums=('4fcf866c6a446f367fc1e8981b1272f405a081d0f241f7f7bc0e7a1cef961230a8b4e07d7e72e4366c80846d3d517f3998cfdc79afcf7c67ebdb8cd82bbf985b'
        'e34e1b1fc3c0bbda825a29a0d907f79ceac7f63ed664b66f82c54d4e06aea5fe2a41ab3d77c703d0addb76be9339870f3f13b7bb2a07a4d455c611d65458bfeb')

prepare() {
  cd $_name-$pkgver

  # TOOLS/PERF: Include headers before BEGIN_C_DECLS
  # https://github.com/openucx/ucx/pull/10668/commits/1adf2aecb6fedd82227e9c293a77473ad42632be
  patch -Np1 -i ../fix-headers-before-c-decls.patch
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
    --with-cuda=/opt/cuda
    --with-rocm=/opt/rocm
    --with-verbs
    --with-rc
    --with-ud
    --with-dc
    --with-mlx5-dv
    --enable-mt
  )

  # this uses malloc_usable_size, which is incompatible with fortification level 3
  export CFLAGS="${CFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"
  export CXXFLAGS="${CXXFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"

  # workaround to fix building with gcc 15
  CFLAGS+=' -std=gnu17'

  cd $_name-$pkgver
  ./autogen.sh
  # NOTE: upstream suggests running ./contrib/configure-release instead of
  # ./configure for maximal performance, but it contains options that would
  # make debugging much harder
  ./configure "${configure_options[@]}"
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make V=1
}

package() {
  # this uses malloc_usable_size, which is incompatible with fortification level 3
  export CFLAGS="${CFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"
  export CXXFLAGS="${CXXFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"

  make DESTDIR="$pkgdir" install -C $_name-$pkgver

  # install the license
  install -vDm 644 $_name-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
