# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-markly
pkgver=0.13.0
_cmark_gfm_commit=766f161ef6d61019acf3a69f5099489e7d14cd49
pkgrel=3
pkgdesc='CommonMark parser and renderer. Written in C, wrapped in Ruby.'
arch=(x86_64)
url='https://github.com/ioquatix/markly'
license=(MIT)
depends=(
  ruby
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bake
  ruby-bake-test
  ruby-build-files
  ruby-covered
  ruby-kramdown
  ruby-redcarpet
  ruby-sus
)
options=(!emptydirs)
source=(https://github.com/ioquatix/markly/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        https://github.com/github/cmark-gfm/archive/$_cmark_gfm_commit/cmark-gfm-$_cmark_gfm_commit.tar.gz)
sha256sums=('46ebe03fe49eed65f2b63ddde9e056f0b7fc49cdf56e15e69bb13d5d4c4a787d'
            'e95a1300cfe4c5b5ad68af6a77855df823b5d813334910a41991af0f70575d66')

prepare() {
  cd markly-$pkgver

  rmdir cmark-gfm
  ln -s ../cmark-gfm-$_cmark_gfm_commit cmark-gfm

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended \
    --expression 's|~>|>=|g' \
    --expression '/signing_key/d' \
    markly.gemspec

  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd markly-$pkgver
  gem build markly.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    markly-$pkgver.gem

  # remove unreproducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd markly-$pkgver
  GEM_HOME="tmp_install/$_gemdir" sus
}

package() {
  cd markly-$pkgver
  cp -a tmp_install/* "$pkgdir"/
}
