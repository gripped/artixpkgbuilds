# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=khelpcenter
pkgver=26.04.2
pkgrel=1
pkgdesc='Application to show KDE Applications documentation'
arch=(x86_64)
url='https://apps.kde.org/khelpcenter/'
license=(LGPL-2.0-or-later)
depends=(glibc
         karchive
         kbookmarks
         kcompletion
         kconfig
         kconfigwidgets
         kcoreaddons
         kdbusaddons
         kdoctools
         ki18n
         kio
         kservice
         ktexttemplate
         kwidgetsaddons
         kwindowsystem
         kxmlgui
         libgcc
         libstdc++
         libxml2
         qt6-base
         qt6-webengine
         xapian-core)
makedepends=(extra-cmake-modules)
groups=(kde-applications
        kde-system)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('e3f95b6c8eaa6d7e3479652a762ded77384af8747961731b414fac3414d59799'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
