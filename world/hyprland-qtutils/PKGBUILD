# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Charles Dong <chardon_cs@proton.me>

pkgname=hyprland-qtutils
pkgver=0.1.5
pkgrel=1
pkgdesc='Qt/QML utility apps for Hyprland'
arch=(x86_64 aarch64)
url="https://github.com/hyprwm/$pkgname"
license=(BSD-3-Clause)
depends=(gcc-libs # libgcc_s.so libstdc++.so
         glibc # libc.so libm.so
         hyprutils libhyprutils.so
         hyprland-qt-support
         qt6-base # libQt6Core.so libQt6Gui.so libQt6Widgets.so
         qt6-declarative # libQt6Qml.so libQt6QuickControls2.so
         qt6-wayland)
makedepends=(cmake)
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('c02e47c9f720c20a5adca1baebbcfee0db5d3d36ce9595779563a2e5d27bafca')

build() {
	cd "$_archive"
	cmake -B build \
		-D CMAKE_INSTALL_PREFIX=/usr \
		-D CMAKE_BUILD_TYPE=Release
	cmake --build build
}

package() {
	cd "$_archive"
	DESTDIR="$pkgdir" cmake --install build
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
