# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Morten Linderud <foxboron@archlinux.org>
# Contributor: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=rclone
pkgver=1.70.3
pkgrel=1
pkgdesc="Sync files to and from Google Drive, S3, Swift, Cloudfiles, Dropbox and Google Cloud Storage"
arch=('x86_64')
url="https://github.com/rclone/rclone"
license=('MIT')
depends=('glibc')
optdepends=('fuse3: for rclone mount')
makedepends=('python' 'go' 'git' 'fuse3')
source=("git+https://github.com/rclone/rclone.git#tag=v${pkgver}?signed")
sha512sums=('29ff79151486eabaccec273b06aa0cf49f782d7925d8ca16adfddbdff174fe7c79d2516f89ef828a93cc3265dfa5e2a6784dc3e24a31b2c6652577b5b67d76d4')
validpgpkeys=(E3B358DC858FB307F48170B9CB0DBEBC5F32C81D) # Nick Craig-Wood
options=(!lto)

build() {
  cd rclone
  export GOFLAGS="-buildmode=pie -trimpath"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  PATH="/build/go/bin:$PATH" make TAG=v$pkgver rclone
  PATH="/build/go/bin:$PATH" rclone genautocomplete bash rclone.bash_completion
  PATH="/build/go/bin:$PATH" rclone genautocomplete zsh rclone.zsh_completion
  PATH="/build/go/bin:$PATH" rclone genautocomplete fish rclone.fish_completion
}

check() {
  cd rclone
  PATH="/build/go/bin:$PATH" make TAG=v$pkgver test || true
}

package() {
  cd rclone

  install -Dm755 rclone "$pkgdir"/usr/bin/rclone

  install -Dm644 rclone.bash_completion "$pkgdir"/usr/share/bash-completion/completions/rclone
  install -Dm644 rclone.zsh_completion "$pkgdir"/usr/share/zsh/site-functions/_rclone
  install -Dm644 rclone.fish_completion "$pkgdir"/usr/share/fish/vendor_completions.d/rclone.fish

  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING

  install -Dm644 rclone.1 "$pkgdir"/usr/share/man/man1/rclone.1
  install -d "$pkgdir"/usr/share/doc/$pkgname
  install -t "$pkgdir"/usr/share/doc/$pkgname -m644 MANUAL.html MANUAL.txt

  # Mount helper symlinks
  ln -s /usr/bin/rclone "${pkgdir}"/usr/bin/mount.rclone
  ln -s /usr/bin/rclone "${pkgdir}"/usr/bin/rclonefs
}
