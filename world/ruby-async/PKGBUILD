# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname=async
pkgname=ruby-${_gemname}
pkgver=2.17.0
pkgrel=1
pkgdesc='Composable asynchronous I/O'
url='https://github.com/socketry/async'
arch=('any')
license=('MIT')
depends=(
  ruby
  ruby-console
  ruby-io-event
  ruby-nio4r
  ruby-timers
)
makedepends=(
  ruby-rdoc
)
options=('!emptydirs')
source=("https://github.com/socketry/async/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('3c4047466911bfecce1d0afbd3472927ab06654893a9b450ebaffd39b0c9079d27a118b8fd20f12c3dd9c39fbed66c9139503d8f5aea91f368fab30a73a81533')
b2sums=('cc2663894cdb9e4e2d562325bd5ccbad41cc8e97e88f0e85c8c98acdf1e93eff91a0a54252ad2c5c81d6ecf1777caae37df089402f906996348d652171717d9d')

prepare() {
  cd ${_gemname}-${pkgver}

  sed --in-place 's|git ls-files|find|' "${_gemname}.gemspec"
  sed --in-place 's|~>|>=|g' "${_gemname}.gemspec"
  sed --in-place '/release\.pem/d' "${_gemname}.gemspec"
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" \
    -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem

  install -Dm 644 readme.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 license.md -t "${pkgdir}/usr/share/licenses/${pkgname}"

  cd "${pkgdir}/${_gemdir}"
  rm -rf cache gems/${_gemname}-${pkgver}/{ext,lib/*/*.so} \
    extensions/*/*/${_gemname}-${pkgver}/{mkmf.log,gem_make.out}
}

# vim: ts=2 sw=2 et:
