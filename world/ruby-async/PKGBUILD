# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname=async
pkgname=ruby-${_gemname}
pkgver=2.17.0
pkgrel=2
pkgdesc='Composable asynchronous I/O'
url='https://github.com/socketry/async'
arch=('any')
license=('MIT')
depends=(
  ruby
  ruby-console
  ruby-fiber-annotation
  ruby-io-event
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-decode
  ruby-sus
  ruby-sus-fixtures-async
  ruby-sus-fixtures-console
)
options=('!emptydirs')
source=("https://github.com/socketry/async/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('3c4047466911bfecce1d0afbd3472927ab06654893a9b450ebaffd39b0c9079d27a118b8fd20f12c3dd9c39fbed66c9139503d8f5aea91f368fab30a73a81533')
b2sums=('cc2663894cdb9e4e2d562325bd5ccbad41cc8e97e88f0e85c8c98acdf1e93eff91a0a54252ad2c5c81d6ecf1777caae37df089402f906996348d652171717d9d')

prepare() {
  cd ${_gemname}-${pkgver}

  sed -r \
    -e 's|~>|>=|g' \
    -e '/signing_key/d' \
    -i "${_gemname}.gemspec"

  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/benchmark-ips/d' \
    --expression '/covered/d' \
    --expression '/rubocop/d' \
    gems.rb
  sed --in-place \
    --expression '/benchmark/d' \
    test/async/reactor.rb
  rm config/sus.rb
}

build() {
  cd ${_gemname}-${pkgver}

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd ${_gemname}-${pkgver}

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" bake test
}

package() {
  cd ${_gemname}-${pkgver}

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 license.md --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 readme.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
