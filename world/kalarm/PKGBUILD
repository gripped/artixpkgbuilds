# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=kalarm
pkgver=25.12.1
pkgrel=1
pkgdesc='Personal alarm scheduler'
arch=(x86_64)
url='https://apps.kde.org/kalarm/'
license=(GPL-2.0-or-later)
depends=(akonadi-contacts
         akonadi-mime
         gcc-libs
         glibc
         kauth
         kcalendarcore
         kcalutils
         kcodecs
         kcolorscheme
         kcompletion
         kconfig
         kconfigwidgets
         kcontacts
         kcoreaddons
         kcrash
         kdbusaddons
         kglobalaccel
         kguiaddons
         kholidays
         ki18n
         kiconthemes
         kidentitymanagement
         kio
         kitemmodels
         kmailtransport
         kmime
         knotifications
         knotifyconfig
         kstatusnotifieritem
         ktextaddons
         ktextwidgets
         kwidgetsaddons
         kwindowsystem
         kxmlgui
         libakonadi
         qt6-base)
makedepends=(extra-cmake-modules
             kdoctools
             libvlc
             mpv)
optdepends=('akonadi: birthday import, email functions, email address book'
            'mpv: MPV audio backend'
            'vlc-plugins-base: VLC audio backend')
groups=(kde-applications
        kde-pim)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('c345f9f0419ae311d4b3c08db1a3ffbf49f74737287d53f23252def3e9157cdd'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

# Allow using kernel alarm timer
  setcap cap_wake_alarm+ep "$pkgdir"/usr/bin/kalarm
}
