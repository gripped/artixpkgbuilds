# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=korganizer
pkgver=25.04.3
pkgrel=1
pkgdesc='Calendar and scheduling Program'
arch=(x86_64)
url='https://apps.kde.org/korganizer/'
license=(GPL-2.0-or-later)
depends=(akonadi-calendar
         akonadi-contacts
         calendarsupport
         eventviews
         gcc-libs
         glibc
         incidenceeditor
         kcalendarcore
         kcalutils
         kcmutils
         kcolorscheme
         kcompletion
         kconfig
         kconfigwidgets
         kcontacts
         kcoreaddons
         kcrash
         kdepim-runtime
         kholidays
         ki18n
         kiconthemes
         kidentitymanagement
         kio
         kitemmodels
         kitemviews
         kjobwidgets
         kmailtransport
         kmime
         knewstuff
         kontactinterface
         kparts
         kuserfeedback
         kwidgetsaddons
         kwindowsystem
         kxmlgui
         libakonadi
         libkdepim
         pimcommon
         qt6-base)
makedepends=(extra-cmake-modules
             kdoctools
             qt6-tools)
optdepends=('kdepim-addons: additional plugins')
groups=(kde-applications
        kde-pim)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('dd18b2c26bb1af4ccd4ee66a4a203c8c6ab59f639bc6f5a09a3b4c8d1bf55cee'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
