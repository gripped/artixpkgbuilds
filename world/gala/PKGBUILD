# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=gala
pkgver=8.2.5
pkgrel=1
pkgdesc='The Pantheon Window Manager'
arch=(x86_64)
url=https://github.com/elementary/gala
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  cairo
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gnome-desktop
  graphene
  gtk3
  gtk4
  libcanberra
  libgee
  libgl
  libgranite.so
  libgranite-7.so
  libhandy-1.so
  libxfixes
  mutter46
  sqlite
  wayland
)
makedepends=(
  cmake
  git
  gnome-common
  intltool
  meson
  vala
)
provides=(libgala.so)
_tag=97e5524b757910ab7700af1c733989a4b26a4dee
source=(git+https://github.com/elementary/gala.git#tag=${_tag})
b2sums=('77d96b3f503353b4f1ce22360e25e7f9261d0c79658274c848067fed24f99c8a3ba95634c6cfc061c1d587866812cb0ba3f1be15897b2ce3f8504730e64272aa')

pkgver() {
  cd gala
  git describe --tags
}

build() {
  artix-meson gala build -Dsystemd=false
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim:set sw=2 sts=-1 et:
