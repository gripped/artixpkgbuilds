# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=gala
pkgver=8.0.4
pkgrel=1
pkgdesc='The Pantheon Window Manager'
arch=(x86_64)
url=https://github.com/elementary/gala
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  cairo
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gnome-desktop
  graphene
  gtk3
  gtk4
  libcanberra
  libgee
  libgl
  libgranite.so
  libgranite-7.so
  libhandy-1.so
  libxfixes
  mutter46
  sqlite
  wayland
)
makedepends=(
  cmake
  git
  gnome-common
  intltool
  meson
  vala
)
provides=(libgala.so)
_tag=30ab86838a1f666b864d3aa833afe8b292a7c0f3
source=(git+https://github.com/elementary/gala.git#tag=${_tag})
b2sums=('d6ef143f233b4297d187649eb98116a8033579c3f4a85719fb3c4bf1fe528f69e3145c5567cf1a51b00717322b927106823b44c29f13637dc5a75844bf327df1')

pkgver() {
  cd gala
  git describe --tags
}

build() {
  artix-meson gala build -Dsystemd=false
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim:set sw=2 sts=-1 et:
