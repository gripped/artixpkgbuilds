# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=gala
pkgver=8.3.0
pkgrel=1
pkgdesc='The Pantheon Window Manager'
arch=(x86_64)
url=https://github.com/elementary/gala
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  cairo
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gnome-desktop
  graphene
  gtk3
  gtk4
  libcanberra
  libgee
  libgl
  libgranite.so
  libgranite-7.so
  libhandy-1.so
  libxfixes
  mutter46
  sqlite
  wayland
)
makedepends=(
  cmake
  git
  gnome-common
  intltool
  meson
  vala
)
provides=(libgala.so)
_tag=608d13719c3c21d26336aa78857f2e11a9bc0aea
source=(git+https://github.com/elementary/gala.git#tag=${_tag})
b2sums=('6cd523f5bf00b53211360ca7287b0e11081097e88f031e235768e4db04a02e03dc752a22a4ae7ec5e7d8c8173b085f59b43d78b8c39e0cc8af746a86cda2fee4')

pkgver() {
  cd gala
  git describe --tags
}

build() {
  artix-meson gala build -Dsystemd=false
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim:set sw=2 sts=-1 et:
