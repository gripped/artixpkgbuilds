# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=gala
pkgver=8.6.0
pkgrel=1
pkgdesc='The Pantheon Window Manager'
arch=(x86_64)
url=https://github.com/elementary/gala
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  at-spi2-core
  cairo
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gnome-desktop-4
  granite
  granite7
  graphene
  gtk3
  gtk4
  json-glib
  libgee
  libgl
  libhandy
  libibus
  libxext
  libxfixes
  mutter
  pango
  sqlite
  wayland
)
makedepends=(
  git
  meson
  vala
)
provides=(libgala.so)
source=(git+https://github.com/elementary/gala.git#tag=${pkgver})
b2sums=('381ad56f1bf9fac82dbfa3fd2513de4058e03c1602078bcdb1897b9fea577d18b5556d9a74e0f2b6967dfe094153308e03fbf31139ecdab703d131f905db8b0d')

build() {
  artix-meson gala build -Dsystemd=false
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
