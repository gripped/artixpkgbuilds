# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=gala
pkgver=8.1.0
pkgrel=1
pkgdesc='The Pantheon Window Manager'
arch=(x86_64)
url=https://github.com/elementary/gala
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  cairo
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gnome-desktop
  graphene
  gtk3
  gtk4
  libcanberra
  libgee
  libgl
  libgranite.so
  libgranite-7.so
  libhandy-1.so
  libxfixes
  mutter46
  sqlite
  wayland
)
makedepends=(
  cmake
  git
  gnome-common
  intltool
  meson
  vala
)
provides=(libgala.so)
_tag=c1860811d50229f7d53c3673551338de832e8d46
source=(git+https://github.com/elementary/gala.git#tag=${_tag})
b2sums=('999afcab2ed8734b981560086a3effe53a1fe2e97642b63f47c1c12def1fe900d6298dc917a1ff1fd36cbaf11618880625633840d8c34cc67c3a655be596aee0')

pkgver() {
  cd gala
  git describe --tags
}

build() {
  artix-meson gala build -Dsystemd=false
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim:set sw=2 sts=-1 et:
