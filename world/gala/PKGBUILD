# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=gala
pkgver=8.5.1
pkgrel=2
pkgdesc='The Pantheon Window Manager'
arch=(x86_64)
url=https://github.com/elementary/gala
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  at-spi2-core
  cairo
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gnome-desktop-4
  graphene
  gtk3
  gtk4
  libgee
  libgl
  libgranite.so
  libgranite-7.so
  libhandy-1.so
  libxext
  libxfixes
  mutter
  pango
  sqlite
  wayland
)
makedepends=(
  cmake
  git
  gnome-common
  intltool
  meson
  vala
)
provides=(libgala.so)
source=(git+https://github.com/elementary/gala.git#tag=${pkgver})
b2sums=('6b7e96aa372f2db2d49b53085718937ac5f43f0c467aa6a3341908dd54a5fae78a6b7db2cf68661c40e2f4f31a84e46136ca8bfe7c314f21a7fa2a0e9d8eacd0')

build() {
  artix-meson gala build -Dsystemd=false
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
