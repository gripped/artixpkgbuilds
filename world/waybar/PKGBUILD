# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Maintainer: Maxim Baz <archlinux at maximbaz dot com>

pkgname=waybar
pkgver=0.10.0
pkgrel=2
pkgdesc='Highly customizable Wayland bar for Sway and Wlroots based compositors'
arch=('x86_64')
url="https://github.com/Alexays/Waybar/"
license=('MIT')
depends=(
    'fmt'
    'gtk-layer-shell'
    'gtkmm3'
    'jack' 'libjack.so'
    'libatkmm-1.6.so'
    'libcairomm-1.0.so'
    'libdbusmenu-gtk3'
    'libevdev'
    'libgtk-3.so'
    'libinput'
    'libjsoncpp.so'
    'libmpdclient'
    'libnl'
    'libpipewire-0.3.so'
    'libpulse'
    'libsigc++'
    'libsndio.so'
    'libspdlog.so'
    'libudev.so'
    'libupower-glib.so'
    'libwireplumber'
    'libxkbcommon'
    'playerctl'
    'upower'
    'wayland'
)
makedepends=(
    'cmake'
    'catch2'
    'meson'
    'scdoc' # For generating manpages
    'wayland-protocols'
)
backup=(
    etc/xdg/waybar/config.jsonc
    etc/xdg/waybar/style.css
)
optdepends=(
    'otf-font-awesome: Icons in the default configuration'
)
source=(
    "$pkgname-$pkgver.tar.gz::https://github.com/Alexays/Waybar/archive/$pkgver.tar.gz"
    0001-Update-Wireplumber-API-to-0.5.patch
)
b2sums=('f27a61662444cb0d91ecb5c860271939ba81813af4d005a3297f9996fab5edd54df2e4a126aa33edf9049e2d91d301a03f6352e25eb9c86ffb003b0b238e9015'
        '83e5d2cd97643b63a53b1ac1042321cc7540cfb34666e5a1bb0f3ab483d970c806bc04a5c2d062130a3fed8de3ea2f85621f0b4c4a7ca6813c6022092266d633')

prepare() {
    cd "Waybar-$pkgver"

    # https://github.com/Alexays/Waybar/pull/2919, adjusted
    patch -Np1 -i ../0001-Update-Wireplumber-API-to-0.5.patch
}

build() {
    cd "Waybar-$pkgver"
    # TODO tests depend on catch2 v3
    CXXFLAGS+=" -std=c++20" \
    meson --prefix=/usr \
          --buildtype=plain \
          --auto-features=enabled \
          --wrap-mode=nodownload \
          -Dsystemd=disabled \
          -Dexperimental=true \
          -Dcava=disabled \
          -Dtests=enabled \
          build
    ninja -C build
}

check() {
    cd "Waybar-$pkgver"
    meson test -C build --no-rebuild --suite waybar

}

package() {
    cd "Waybar-$pkgver"
    DESTDIR="$pkgdir" ninja -C build install
    install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
