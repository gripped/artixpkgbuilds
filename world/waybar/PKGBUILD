# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Maintainer: Maxim Baz <archlinux at maximbaz dot com>
# Contributor: Parmjot Singh <parmjotsinghrobot at gmail dot com>

pkgname=waybar
pkgver=0.10.0
pkgrel=4
pkgdesc='Highly customizable Wayland bar for Sway and Wlroots based compositors'
arch=('x86_64')
url="https://github.com/Alexays/Waybar/"
license=('MIT')
depends=(
    'atkmm'
    'cairomm'
    'fmt'
    'gcc-libs'
    'glib2'
    'glibc'
    'glibmm'
    'gtk-layer-shell'
    'gtk3'
    'gtkmm3'
    'jack'
    'jsoncpp'
    'libdbusmenu-gtk3'
    'libevdev'
    'libinput'
    'libmpdclient'
    'libnl'
    'libpipewire'
    'libpulse'
    'libsigc++'
    'libwireplumber'
    'libxkbcommon'
    'playerctl'
    'sndio'
    'spdlog'
    'upower'
    'wayland'
)
makedepends=(
    'catch2'
    'cmake'
    'meson'
    'python-packaging' # gdbus-codegen
    'scdoc' # For generating manpages
    'wayland-protocols'
)
backup=(
    etc/xdg/waybar/config.jsonc
    etc/xdg/waybar/style.css
)
optdepends=(
    'otf-font-awesome: Icons in the default configuration'
)
source=(
    "$pkgname-$pkgver.tar.gz::https://github.com/Alexays/Waybar/archive/$pkgver.tar.gz"
    0001-Update-Wireplumber-API-to-0.5.patch
)
b2sums=('f27a61662444cb0d91ecb5c860271939ba81813af4d005a3297f9996fab5edd54df2e4a126aa33edf9049e2d91d301a03f6352e25eb9c86ffb003b0b238e9015'
        'eb745507d5547e8af63a52f801c1329f905363283ffcc754b841368e84f5b5d3f407b063b86811768e3bcaa27451589216ba4f8a44dde693465dc7a7ef956126')

prepare() {
    cd "Waybar-$pkgver"

    # https://github.com/Alexays/Waybar/pull/2919
    patch -Np1 -i ../0001-Update-Wireplumber-API-to-0.5.patch
}

build() {
    cd "Waybar-$pkgver"
    meson --prefix=/usr \
          --buildtype=plain \
          --auto-features=enabled \
          --wrap-mode=nodownload \
          -Dsystemd=disabled \
          -Dcpp_std=c++20 \
          -Dexperimental=true \
          -Dcava=disabled \
          -Dtests=enabled \
          build
    meson compile -C build
}

check() {
    cd "Waybar-$pkgver"
    meson test -C build --print-errorlogs --no-rebuild --suite waybar
}

package() {
    depends+=(
      'libatkmm-1.6.so'
      'libcairomm-1.0.so'
      'libgtk-3.so'
      'libjack.so'
      'libjsoncpp.so'
      'libpipewire-0.3.so'
      'libsndio.so'
      'libspdlog.so'
      'libudev.so'
      'libupower-glib.so'
    )

    cd "Waybar-$pkgver"
    meson install -C build --destdir "$pkgdir"
    install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
