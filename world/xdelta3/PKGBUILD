# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Eduard "bekks" Warkentin <eduard.warkentin@gmail.com>
# Contributor: Henning Garus <henning.garus@gmail.com>

pkgname=xdelta3
pkgver=3.2.1
pkgrel=1
pkgdesc='Diff utility for binary files'
arch=(x86_64)
url='https://github.com/jmacd/xdelta'
license=(Apache-2.0)
depends=(xz)
makedepends=(git cmake ninja)
provides=(libxdelta3.so)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('b448af0bb26547a04121aca3f7fe7d6515b3c812ff879866c6c91d0a06bdfd78aedf4cff6614775ccb9a11643e93333cc69e6435712fb2f40f430cc17d5c4482')
b2sums=('44ee7e5c38662ec3c3275a81ec9b4a31887365d1852f80e12b190969ba016ffdbfba16a7c0d97d8b65ba9763d6f75e11cf08728682c7d3ee8cb79c0d3855ac2f')

build() {
  cd "$pkgname"

  local cmake_options=(
    -B build
    -S xdelta3
    -G Ninja
    -W no-dev
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D BUILD_SHARED_LIBS=ON
    -D XD3_ARMOR=OFF
  )

  cmake "${cmake_options[@]}"

  cmake --build build
}

check() {
  cd "$pkgname"

  local excluded_tests="test_armor"
  local ctest_flags=(
    --test-dir build
    # show the stdout and stderr when the test fails
    --output-on-failure
    # execute tests in parallel
    --parallel $(nproc)
    # exclude problematic tests
    --exclude-regex "$excluded_tests"
  )
  ctest "${ctest_flags[@]}"
}
package() {
  depends+=(glibc)

  cd "$pkgname"

  DESTDIR="$pkgdir" cmake --install build
}
