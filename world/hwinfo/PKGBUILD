# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Gergely Imreh <imrehg@gmail.com>
# Contributor: champus
# Contributor: madeye
# Contributor: ranguvar
# Contributor: ninja_pt

pkgname=hwinfo
pkgver=24.0
pkgrel=1
pkgdesc='Hardware detection tool from openSUSE'
arch=(x86_64)
url='https://github.com/openSUSE/hwinfo'
license=(GPL-2.0-or-later)
depends=(libx86emu perl-xml-parser perl-xml-writer)
makedepends=(flex git sysfsutils)
options=(!emptydirs)
source=("git+$url#tag=$pkgver"
        custom_ioctl.patch)
b2sums=('4875bab5dbf3e7aef7d04ea47edbc5663162824f17df101d7ce750f9c4966f06f9b2962cc807b0084b1e418eb1f291370855b08123cc867e5219be2b6b3d2e30'
        '2b1bd3b53bbfc1e545e1a70ffd6cca08f704639a104928b2c02ccca3e82000f07a470fbdf129566ece6dfb2b98fdad0e82f18cc5c2016ebfeed043b4edb295b4')

prepare() {
  cd $pkgname
  patch -p1 -i ../custom_ioctl.patch
  echo 'touch changelog' > git2log
  sed -i '/linux\/fs.h/d' src/hd/hd.c
}

build() {
  make -C $pkgname -j1 CFLAGS+=" -fPIC -I$srcdir/$pkgname/src/hd -w" HWINFO_VERSION="$pkgver" LIBDIR=/usr/lib
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" LIBDIR=/usr/lib install
  mv "$pkgdir/usr/sbin" "$pkgdir/usr/bin"

  cd doc
  for f in *.1; do install -Dm644 $f "$pkgdir/usr/share/man/man1/$f"; done
  for f in *.8; do install -Dm644 $f "$pkgdir/usr/share/man/man8/$f"; done
}
