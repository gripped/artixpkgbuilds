# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgbase=papers
pkgname=(
  papers
  papers-lib-docs
)
pkgver=48.5
pkgrel=1
pkgdesc='Document viewer for PDF and other document formats aimed at the GNOME desktop'
arch=(x86_64)
url='https://apps.gnome.org/Papers/'
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  djvulibre
  exempi
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  graphene
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  libarchive
  libnautilus-extension
  libspelling
  libtiff
  pango
  poppler-glib
)
makedepends=(
  appstream
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  itstool
  libsysprof-capture
  meson
  rust
)
source=("git+https://gitlab.gnome.org/GNOME/papers.git#tag=${pkgver/[a-z]/.&}")
b2sums=('2f2c6146aac10258304fb540cde94f0b0591654e4a6f51f28526e00d86a5d5bd592414c07f33a7c77cbd5c32c4c7f724041f2178690a6348172a0376a9a49b97')

# Use debug
export CARGO_PROFILE_RELEASE_DEBUG=2 CARGO_PROFILE_RELEASE_STRIP=false

# Use LTO
export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

prepare() {
  cd $pkgbase

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  artix-meson $pkgbase build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package_papers() {
  meson install -C build --destdir "$pkgdir" --no-rebuild

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_papers-lib-docs() {
  pkgdesc+=" (library API documentation)"
  depends=()

  mv doc/* "$pkgdir"
}
