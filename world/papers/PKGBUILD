# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgbase=papers
pkgname=(
  papers
  papers-lib-docs
)
pkgver=49.2
pkgrel=1
pkgdesc='Document viewer for PDF and other document formats aimed at the GNOME desktop'
arch=(x86_64)
url='https://apps.gnome.org/Papers/'
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  djvulibre
  exempi
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  graphene
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  libarchive
  libnautilus-extension
  libspelling
  libtiff
  pango
  poppler-glib
)
makedepends=(
  appstream
  blueprint-compiler
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  itstool
  libsysprof-capture
  meson
  rust
)
source=("git+https://gitlab.gnome.org/GNOME/papers.git#tag=${pkgver/[a-z]/.&}")
b2sums=('38630b957d92b51be859efc519910897c3e9d7e0593953ba203ba918646d10911e3286eb96e6650e71c97ac2af5fbde51fb44a2ac3c2c081a3fff0950cc94e00')

# Use debug
export CARGO_PROFILE_RELEASE_DEBUG=2 CARGO_PROFILE_RELEASE_STRIP=false

# Use LTO
export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

prepare() {
  cd $pkgbase

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  artix-meson $pkgbase build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package_papers() {
  groups=(gnome)

  meson install -C build --destdir "$pkgdir" --no-rebuild

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_papers-lib-docs() {
  pkgdesc+=" (library API documentation)"
  depends=()

  mv doc/* "$pkgdir"
}
