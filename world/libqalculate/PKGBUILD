# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=libqalculate
pkgver=5.6.0
pkgrel=1
pkgdesc='Multi-purpose desktop calculator'
arch=(x86_64)
url='https://qalculate.github.io/'
license=(GPL-2.0-only)
depends=(curl
         icu
         gcc-libs
         glibc
         gmp
         libxml2
         mpfr
         readline)
makedepends=(doxygen
             git
             intltool)
optdepends=('gnuplot: for plotting support')
source=(git+https://github.com/Qalculate/libqalculate#tag=v$pkgver)
sha256sums=('b92a61e14b6de0eec88ae11f9216ad4df32e373f719689a70800e741dab4ca0b')

prepare() {
  cd $pkgname
  ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
