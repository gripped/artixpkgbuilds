# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=kquickimageeditor
pkgver=0.6.2
pkgrel=1
pkgdesc='QML image editing components'
license=(LGPL-2.1-or-later)
arch=(x86_64)
url='https://invent.kde.org/libraries/kquickimageeditor'
depends=(glibc
         kconfig
         kirigami
         libgcc
         libstdc++
         opencv
         qt6-base
         qt6-declarative)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        opencv.patch)
sha256sums=('dfa8d0c8e41404937f3361c41b19cf541bb746e1b4b6970886de639122f1c9bb'
            'SKIP'
            'b050b45c9c4c1ffbb28f36eac5198073532d53f4ed4aae44b8d0b36e0be3e6df')
validpgpkeys=(39FFA93CAE9C6AFC212AD00202325448204E452A  # Carl Schwan <carl@carlschwan.eu>
              931703E04546328CEE35B5A51F7428FFC412A679) # Noah Davis <noahadvs@gmail.com>

prepare() {
  patch -d $pkgname-$pkgver -p1 < opencv.patch
}

build() {
  cmake -B build -S $pkgbase-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
