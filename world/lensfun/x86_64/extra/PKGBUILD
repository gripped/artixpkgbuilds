# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=lensfun
pkgver=0.3.4
pkgrel=1
epoch=1
pkgdesc='Database of photographic lenses and a library that allows advanced access to the database'
arch=(x86_64)
url='https://lensfun.github.io/'
license=(LGPL3)
depends=(glib2)
makedepends=(python-setuptools libpng doxygen cmake)
optdepends=('python: for lensfun-update-data and lensfun-add-adapter')
source=(https://github.com/lensfun/lensfun/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('dafb39c08ef24a0e2abd00d05d7341b1bf1f0c38bfcd5a4c69cf5f0ecb6db112')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS -fno-delete-null-pointer-checks"
  cmake --build build  
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
