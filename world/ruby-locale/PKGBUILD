# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Tim Meusel <tim@bastelfreak.de>
# Contributor: Vojtěch Aschenbrenner <v@asch.cz>

_gemname='locale'
pkgname="ruby-${_gemname}"
pkgver=2.1.5
pkgrel=1
pkgdesc='A pure ruby library which provides basic and general purpose APIs for localization.'
arch=('any')
url="https://github.com/ruby-gettext/locale"
license=('Ruby' 'LGPL-3.0-or-later')
makedepends=('ruby-rdoc' 'ruby-rake' 'ruby-bundler' 'ruby-yard')
checkdepends=('ruby-test-unit' 'ruby-test-unit-rr')
depends=('ruby')
options=(!emptydirs)
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('72e9db211150b0151bb672da63e63f30e9e82bf639b637d52088ff9de916461f985fbbca93f2da128eaa7832ead10d840d199fe56e850b95e3162419fac00544')

build() {
  cd "${_gemname}-${pkgver}"
  rake build
}

check() {
  cd "${_gemname}-${pkgver}"
  rake test
}

package() {
  cd "${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "pkg/${_gemname}-${pkgver}.gem"

  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -Dm 644 README.rdoc ChangeLog doc/text/news.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm -rf "${pkgdir}/${_gemdir}/cache"
}

# vim: ts=2 sw=2 et:
