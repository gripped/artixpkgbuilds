# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kde-dev-scripts
pkgver=25.12.0
pkgrel=1
pkgdesc='Scripts and setting files useful during development of KDE software'
url='https://www.kde.org/'
arch=(any)
license=(GPL-2.0-or-later
         LGPL-2.0-or-later)
depends=(perl
         python
         sh)
makedepends=(extra-cmake-modules
             kdoctools)
groups=(kde-applications
        kde-sdk)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('71db3b06379e98efa76561f08c5cd8ca780a5d64465e46d5508a299f2f76049c'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

# Install additional files
  cd "$srcdir"/$pkgname-$pkgver
  mkdir -p "$pkgdir"/usr/share/kde-dev-scripts
  cp -r kde-devel-* gdb "$pkgdir"/usr/share/kde-dev-scripts
}
