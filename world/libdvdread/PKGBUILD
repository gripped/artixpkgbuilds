# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Thomas Baechler <thomas@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=libdvdread
pkgver=7.1.0
pkgrel=1
pkgdesc='Library for reading DVD video disks'
arch=(x86_64)
url='https://www.videolan.org/developers/libdvdnav.html'
license=(GPL-2.0-or-later)
depends=(glibc)
makedepends=(
  git
  meson
)
optdepends=('libdvdcss: Decoding encrypted DVDs')
source=("git+https://code.videolan.org/videolan/libdvdread.git#tag=$pkgver")
b2sums=(89468c8e31f9e5e1ac6b02db327e1b9b6ccc67bfd84b74ae34c31fad4c40caad0a78dc262eafcdffc832483aaf618388c1641ab15240f0f487a7c4829ff6448e)
validpgpkeys=(65F7C6B4206BD057A7EB73787180713BE58D1ADC) # VideoLAN Release Signing Key (2015)

prepare() {
  cd $pkgname

  # Fix out of tree build
  sed -i "s|'git', 'log'|'git', '-C', meson.project_source_root(), 'log' |" meson.build
}

build() {
  artix-meson $pkgname build \
    -D libdvdcss=disabled
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
