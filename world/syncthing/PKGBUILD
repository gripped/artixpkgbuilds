# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Caleb Maclennan <caleb@alerque.com>
# Contributor: Martin Wimpress <code@flexion.org>
# Contributor: Sauyon Lee <sauyonl@sauyon.com>
# Contributor: Kevin MacMartin <prurigro@gmail.com>
# Contributor: korjjj <korjjj+aur@gmail.com>

pkgname=(syncthing syncthing-discosrv syncthing-relaysrv)
pkgver=2.0.13
pkgrel=1
pkgdesc='Open Source Continuous Replication / Cluster Synchronization Thing'
arch=(x86_64)
url='https://syncthing.net/'
license=('MPL-2.0')
depends=(glibc)
makedepends=(git go)
source=("https://github.com/syncthing/syncthing/releases/download/v$pkgver/syncthing-source-v$pkgver.tar.gz"{,.asc}
        syncthing-{disco,relay}srv.sysusers
        syncthing-{disco,relay}srv.tmpfiles
        gcc-pie-segfaults-race.patch)
options=(!lto) # https://github.com/golang/go/pull/53528
validpgpkeys=('37C84554E7E0A261E4F76E1ED26E6ED000654A3E') # Syncthing Release Management
sha256sums=('fd7c93e01a6d61faa84adda8b22479dd7fd106144fbbb2eb15d773707d8a382e'
            'SKIP'
            'f6e704dd7b10127b1642c889a29e95468daa75d895493b235ce1ed5e97155b70'
            '6ab96a5949e223d315d49663734ad27b70c72469c691a61ac198cb7794cb5eaa'
            'a05a66efbbd13f20b5c3af56692fe219fadc967dba4eedb71494827795d2c140'
            'f899998985f2d374af57f17ef6362c37a305467858195b74d1c1def71d97273f'
            '6a58227c09a18fc7049ec38351a0fd98195688d15821d67d5af4facda401cbba')

_srcdir="src/github.com/syncthing"

prepare() {
  rm -rf "${_srcdir}"
  install -d "${_srcdir}"
  mv "${pkgbase}" "${_srcdir}/${pkgbase}"
  patch -Np1 -i gcc-pie-segfaults-race.patch
}

build() {
  cd "$_srcdir/${pkgbase}"
  export CGO_ENABLED=1
  export BUILD_HOST=archlinux
  export BUILD_USER=${pkgbase}
  export GOPATH="${srcdir}" GOROOT_FINAL="/usr/bin"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -mod=readonly -modcacherw"
  go run build.go -no-upgrade -version v${pkgver} build
  go run build.go -no-upgrade -version v${pkgver} build strelaysrv
  go run build.go -no-upgrade -version v${pkgver} build stdiscosrv
}

check() {
  cd "$_srcdir/${pkgbase}"
  export GOPATH="${srcdir}" GOROOT_FINAL="/usr/bin"
  if [ "${CARCH}" == "x86_64" ] ; then
    go run build.go -no-upgrade test
  fi
}

package_syncthing() {
  install=$pkgname.install

  cd "$_srcdir/${pkgbase}"
  install -Dm755 ${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"

  # license
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  # ufw preset
  install -Dm644 etc/firewall-ufw/syncthing \
    "${pkgdir}/etc/ufw/applications.d/ufw-syncthing"

  # sysctl conf
  install -Dm644 etc/linux-sysctl/30-syncthing.conf \
    "${pkgdir}/usr/lib/sysctl.d/30-syncthing.conf"

  # man pages
  pushd man
  install -Dm644 $pkgname.1 "${pkgdir}"/usr/share/man/man1/$pkgname.1
  for file in $(find . -name '*.5' -print); do
    install -Dm644 $file "${pkgdir}"/usr/share/man/man5/$file
  done
  for file in $(find . -name '*.7' -print); do
    install -Dm644 $file "${pkgdir}"/usr/share/man/man7/$file
  done
}

package_syncthing-discosrv() {
  pkgdesc='Discovery server for Syncthing'

  cd "$_srcdir/${pkgbase}"
  install -Dm755 stdiscosrv "${pkgdir}"/usr/bin/${pkgname}

  install -Dm644 man/stdiscosrv.1 \
    "${pkgdir}"/usr/share/man/man1/$pkgname.1

  install -Dm644 "${srcdir}/${pkgname}.tmpfiles" \
    "$pkgdir/usr/lib/tmpfiles.d/${pkgname}.conf"
  install -Dm644 "${srcdir}/${pkgname}.sysusers" \
    "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
}

package_syncthing-relaysrv() {
  pkgdesc='Relay server for Syncthing'

  cd "$_srcdir/${pkgbase}"
  install -Dm755 strelaysrv "${pkgdir}"/usr/bin/${pkgname}

  install -Dm644 man/strelaysrv.1 \
    "${pkgdir}"/usr/share/man/man1/$pkgname.1

  install -Dm644 "${srcdir}/${pkgname}.tmpfiles" \
    "$pkgdir/usr/lib/tmpfiles.d/${pkgname}.conf"
  install -Dm644 "${srcdir}/${pkgname}.sysusers" \
    "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
}
