# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libppd
pkgver=2.1.0
pkgrel=1
pkgdesc="OpenPrinting - the legacy support library for PPD files"
arch=('x86_64')
url="https://github.com/OpenPrinting/libppd"
# the CUPS exception text is the same as LLVM exception
license=('Apache-2.0 WITH LLVM-exception')
depends=('libcups' 'libcupsfilters'
         'zlib' 'glibc' 'gcc-libs')
makedepends=('ghostscript' 
#            'mupdf-tools'
)
source=(https://github.com/OpenPrinting/libppd/releases/download/$pkgver/$pkgname-$pkgver.tar.xz)
sha256sums=('bc4d7f8b749a8809f532459a5dd2f3513556ea2b96b3e12aced3e078c2697cba')

build() {
  cd "$pkgname"-$pkgver
  ./configure --prefix=/usr  \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --localstatedir=/var \
    --disable-mutool 
  make
}

check() {
  cd "$pkgname"-$pkgver
  make check
}

package() {
  cd "$pkgname"-$pkgver
  make DESTDIR="$pkgdir/" install
  # license
  mkdir -p "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 "${srcdir}"/${pkgname}-${pkgver}/{COPYING,NOTICE} "${pkgdir}"/usr/share/licenses/${pkgname}/
}
