# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-protocol-rack
pkgver=0.10.1
pkgrel=1
pkgdesc='An implementation of the Rack protocol/specification'
arch=(any)
url='https://github.com/socketry/protocol-rack'
license=(MIT)
depends=(
  ruby
  ruby-protocol-http
  ruby-rack
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-async-http
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-decode
  ruby-sus
  ruby-sus-fixtures-async-http
)
options=(!emptydirs)
source=(https://github.com/socketry/protocol-rack/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('3a279d0da3d5ea1a47cd3dd29a2aff7df1893f2842e6a821885cbe65d41def165d59f21b91ff26afb0307ee61ad249fea78d56f67ef9ae565f02b7dc4d70a4bf')
b2sums=('9ab9398b091c02d61db5f1cf4f61f519ce145b16638e5bd601c888ec1569e0ad5adf54e757acbb77ac8ba6312f13966fca355d0e481c2337b2172160b07c7686')

prepare() {
  cd protocol-rack-$pkgver

  sed -r \
    -e 's|~>|>=|g' \
    -e '/signing_key/d' \
    -i protocol-rack.gemspec

  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd protocol-rack-$pkgver
  gem build protocol-rack.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    protocol-rack-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd protocol-rack-$pkgver
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd protocol-rack-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 license.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
