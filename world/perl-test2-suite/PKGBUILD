# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-test2-suite
pkgver=0.000158
pkgrel=1
pkgdesc="Distribution with a rich set of tools built upon the Test2 framework."
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Test2-Suite"
depends=('perl-module-pluggable' 'perl-term-table' 'perl-test-simple>=1.302176')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/E/EX/EXODIST/Test2-Suite-$pkgver.tar.gz")
sha512sums=('4c96a4d61e4d4f71a77c8dc739b7370147e18a98edaa4911ea3241cc23e1c9877d8c15ef7847b6a86ad6dc34eb12a2bbfec6e84be58d415876396971177b7429')

build() {
  cd Test2-Suite-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Test2-Suite-$pkgver
  make test
}

package() {
  cd Test2-Suite-$pkgver
  make DESTDIR="$pkgdir" install
}
