# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: loqs
# Contributor: Fredy García <frealgagu at gmail dot com>
# Contributor: Youngbin Han <sukso96100@gmail.com>
# Contributor: blainester <theblainestory@gmail.com>
# Contributor: mar77i <mar77i at protonmail dot ch>

pkgname=micro
pkgver=2.0.15
pkgrel=2
# manually set date instead of "go run tools/build-date.go", for reproducible builds
_build_date='January 28, 2026'
pkgdesc='Modern and intuitive terminal-based text editor'
arch=(x86_64)
url='https://micro-editor.github.io/'
license=(MIT)
depends=('glibc' 'hicolor-icon-theme')
makedepends=(git go)
optdepends=('wl-clipboard: for copying/pasting text using Wayland'
            'xclip: for copying/pasting text using X')
source=("git+https://github.com/zyedidia/micro#commit=6a62575bcfdf4965f187eedafceb3400316e612b") # tag: v2.0.15
b2sums=('49b68b8b3e5c1f501d97921162f8585f7796c024fb1a9877e1fdbe99beb6873b6cfd5fd5a718f0eb3f35bdd826fbd2341b418212b468c8d630527bb18b7318b2')

prepare() {
  cd $pkgname
  # Let tools/build-date.go always return the same date, to make the build reproducible
  echo -e '//+build ignore\npackage main\nimport "fmt"\nfunc main() { fmt.Println("'$_build_date'") }' > tools/build-date.go
  # Modify Go build flags
  sed -i 's/-trimpath/-buildmode=pie -trimpath/g;s/GS)/GS) -linkmode=external -extldflags \\"$(LDFLAGS)\\"/g' Makefile
}

build() {
  CGO_ENABLED=1 make -C $pkgname
}

check() {
  make -C $pkgname test
}

package() {
  cd $pkgname
  install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm644 assets/packaging/micro.1 -t "$pkgdir/usr/share/man/man1"
  install -Dm644 assets/packaging/micro.desktop -t "$pkgdir/usr/share/applications"
  install -Dm644 assets/micro-logo-mark.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/$pkgname.svg"
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE LICENSE-THIRD-PARTY
}
