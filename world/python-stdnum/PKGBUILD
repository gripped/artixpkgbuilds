# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-stdnum
pkgver=2.2
pkgrel=1
pkgdesc="Python module to handle standardized numbers and codes"
arch=('any')
url="https://arthurdejong.org/python-stdnum/"
license=('LGPL-2.1-or-later')
depends=('python')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools')
checkdepends=('python-pytest' 'python-pytest-cov')
optdepends=('python-zeep: SOAP-based online number validation')
source=("git+https://github.com/arthurdejong/python-stdnum.git#tag=$pkgver")
sha512sums=('881eec86a86515be16669ad6ffa73decde655f54ef23ca05131487715734373ca899c1006839d5241d0eaf4a9f7d0e96fc8d9178f79d02ca36a002128147687e')

build() {
  cd python-stdnum
  python -m build --wheel --no-isolation
}

check() {
  cd python-stdnum
  python -m pytest
}

package() {
  cd python-stdnum
  python -m installer --destdir="$pkgdir" dist/*.whl
}
