# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Mark Wagie <mark dot wagie at tutanota dot com>
# Contributor: Mantas <grawity at gmail dot com>

pkgname=switcheroo-control
pkgver=3.0
pkgrel=1
pkgdesc="D-Bus service to check the availability of dual-GPU"
url="https://gitlab.freedesktop.org/hadess/switcheroo-control"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  libdrm
  libgudev
  python
  python-gobject
)
makedepends=(
  git
  glib2-devel
  gtk-doc
  meson
  python-dbus
  python-dbusmock
  linux-api-headers
)
checkdepends=(umockdev)
source=("git+$url.git?signed#tag=$pkgver")
b2sums=('25ccf0ba490088c4155bb0eec527ad2f87d25a8241e914cf2a3032665b0e28f1088d9f00bb120d9889530dace044e746ac5d4ca7f54445574723cf3f3b985b61')
validpgpkeys=(
  8307C0A224BABDA1BABD0EB9A6EEEC9E0136164A # Jonas Ã…dahl <jadahl@gmail.com>
)

build() {
  local meson_options=(
    -D systemdsystemunitdir=/usr/lib/systemd # dummy dir
    -D gtk_doc=true
    -D tests=true
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  rm -r $pkgdir/usr/lib/systemd
}

# vim:set sw=2 sts=-1 et:
