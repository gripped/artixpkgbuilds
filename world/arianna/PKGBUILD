# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=arianna
pkgver=24.08.0
pkgrel=1
pkgdesc='EPub Reader for mobile devices'
url='https://apps.kde.org/arianna/'
arch=(x86_64)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(baloo
         gcc-libs
         glibc
         karchive
         kconfig
         kcoreaddons
         kdbusaddons
         kfilemetadata
         ki18n
         kirigami
         kirigami-addons
         kitemmodels
         kquickcharts
         kwindowsystem
         qqc2-desktop-style
         qt6-base
         qt6-declarative
         qt6-httpserver
         qt6-webchannel
         qt6-webengine)
makedepends=(extra-cmake-modules)
groups=(kde-applications
        kde-graphics)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        qt-6.8.patch)
sha256sums=('0d3aea7d6acd4a0ec074cd7a3f88c6650d58e02306be30fa43f0ac3beedf2cab'
            'SKIP'
            '28d8209c12cef4a3144a14f371bdfe4a831b126feed0ec2804814965db586be0')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < qt-6.8.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
