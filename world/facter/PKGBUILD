# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Tim Meusel <tim@bastelfreak.de>
# Contributor: Christian Rebischke <chris.rebischke at archlinux.org>
# Contributor: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Greg Sutcliffe <facter_aur (at) emeraldreverie.orgr>
# Contributor: Hyacinthe Cartiaux <hyacinthe.cartiaux@free.fr>
# Contributor: Thomas S Hatch <thatch45 (at) Gmail.com>
# Contributor: Dave Simons <miouhpi (at) Gmail (dot) com>
# Contributor: Niels Abspoel <aboe76 (at) Gmail (dot) com>

pkgname=facter
pkgver=4.8.0
pkgrel=3
pkgdesc="Collect and display system facts"
arch=('x86_64')
url="https://github.com/puppetlabs/${pkgname}"
license=('Apache-2.0')
depends=('dmidecode' 'pciutils' 'ruby-hocon' 'ruby-sys-filesystem' 'ruby-thor' 'virt-what')
makedepends=('git' 'ruby-rdoc')
checkdepends=('rubocop' 'ruby-bundler' 'ruby-rake' 'ruby-rubocop-performance' 'ruby-rubocop-rspec' 'ruby-rspec' 'ruby-simplecov' 'ruby-octokit' 'ruby-webmock' 'ruby-yard')
optdepends=('puppet: retrieve puppet facts')
conflicts=('libwhereami' 'cpp-hocon' 'leatherman')
replaces=('libwhereami' 'cpp-hocon' 'leatherman')
source=("https://downloads.puppet.com/$pkgname/$pkgname-$pkgver.gem"{,.asc}
        "git+https://github.com/puppetlabs/facter.git#tag=$pkgver")
validpgpkeys=('D6811ED3ADEEB8441AF5AA8F4528B6CD9E61EF26') # "Puppet, Inc. Release Key (Puppet, Inc. Release Key) <release@puppet.com>"
sha512sums=('fd3967f08bfad695b170c20002b9c3085b1eb1de1d91f9c8685f511cbca155ade120efb27b07b435e295ebb9194c0b6fbb07b1b31541784897f4d064b4da828e'
            'SKIP'
            '7293f7433293dc535f760cb7fe6a71d1223ada0e975250573a97ace6680edc71ff1862e9adca095ac283fa448183146b0e4014d93a5378c3c062db41832c4338')
noextract=($pkgname-$pkgver.gem)

prepare() {
  cd $pkgname

  sed -i -e '/packaging/d' -e '/ronn/d' Gemfile
}

build() {
  cd $pkgname

  local _gemdir="$(gem env gemdir)"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "../${pkgname}-${pkgver}.gem"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended --expression 's|~>|>=|g' \
    --expression "s/, *' *< *[0-9\.]+ *'//" \
    facter.gemspec Gemfile \
    tmp_install"${_gemdir}"/specifications/facter-4.8.0.gemspec

  # remove upper version constraint for thor
  # https://github.com/puppetlabs/facter/pull/2751
  sed --in-place 's/, "< 1.3"//' facter.gemspec tmp_install"${_gemdir}"/specifications/facter-4.8.0.gemspec

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check(){
  cd $pkgname

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install/${_gemdir}" rspec --exclude-pattern ./spec/framework/detector/os_detector_spec.rb
}

package() {
  cd $pkgname

  cp --archive --verbose tmp_install/* "${pkgdir}"
}

# vim: ts=2 sw=2 et:
