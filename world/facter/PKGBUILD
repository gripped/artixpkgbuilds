# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Tim Meusel <tim@bastelfreak.de>
# Contributor: Christian Rebischke <chris.rebischke at archlinux.org>
# Contributor: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Greg Sutcliffe <facter_aur (at) emeraldreverie.orgr>
# Contributor: Hyacinthe Cartiaux <hyacinthe.cartiaux@free.fr>
# Contributor: Thomas S Hatch <thatch45 (at) Gmail.com>
# Contributor: Dave Simons <miouhpi (at) Gmail (dot) com>
# Contributor: Niels Abspoel <aboe76 (at) Gmail (dot) com>

pkgname=facter
pkgver=4.10.0
pkgrel=2
pkgdesc="Collect and display system facts"
arch=('x86_64')
url="https://github.com/puppetlabs/${pkgname}"
license=('Apache-2.0')
depends=(
  dmidecode
  pciutils
  ruby-base64
  ruby-hocon
  ruby-sys-filesystem
  ruby-thor
  virt-what
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-bundler
  ruby-rake
  ruby-rspec
  ruby-octokit
  ruby-webmock
  ruby-yard
 
)
optdepends=('puppet: retrieve puppet facts')
conflicts=('libwhereami' 'cpp-hocon' 'leatherman')
replaces=('libwhereami' 'cpp-hocon' 'leatherman')
source=(
  "https://downloads.puppet.com/$pkgname/$pkgname-$pkgver.gem"{,.asc}
  "git+https://github.com/puppetlabs/facter.git#tag=$pkgver"
  "disable-simplecov.patch"
  "${pkgname}_fix_tests.patch"
)
validpgpkeys=('D6811ED3ADEEB8441AF5AA8F4528B6CD9E61EF26') # "Puppet, Inc. Release Key (Puppet, Inc. Release Key) <release@puppet.com>"
sha512sums=('2d624aa1b40cf057d36fb52ed884662c306d8e98d9dd00f5f3a4f4e990a1ac2ab54c31b60089c67530f478c4f264444775b15dd0ee6b90f50a5187df3392049e'
            'SKIP'
            '316710cc78935f87758332bbc47333bf2439603f9153e4332a56371ef12f342380e3bae6475bae83d0a16ab2f4e4af834abe86f7791dfc8ef5f5ecde7e89080f'
            'fd44b3ad3832ee5443733fee3bd2f865c467bae6af6faeac1d1f3671170db83a3f2207a16ba7cd65f10af21b2a9851a9b1d3ccf4d6473fd538ba09414a8cad88'
            '8924ce1245e04c7c41e4eb400c485b264beb7146e4f69f067cf88da687d0e0693747c69be6f6ee10a61f0f50c5ff67d732447981e4e951e3f92cc9f3081271a8')
noextract=($pkgname-$pkgver.gem)

prepare() {
  cd $pkgname

  # remove development deps
  rm Gemfile
  sed --in-place '/add_development_dependency/g' facter.gemspec

  # don't try to load simplecov
  patch --forward --verbose --strip=1 --input='../disable-simplecov.patch'

  rm tasks/rubocop.rake

  patch --verbose --strip=1 --input="../${pkgname}_fix_tests.patch"
}

build() {
  cd $pkgname

  local _gemdir="$(gem env gemdir)"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "../${pkgname}-${pkgver}.gem"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended --expression 's|~>|>=|g' \
    --expression "s/, *' *< *[0-9\.]+ *'//" \
    facter.gemspec \
    "tmp_install${_gemdir}/specifications/facter-${pkgver}.gemspec"

  # remove upper version constraint for thor
  # https://github.com/puppetlabs/facter/pull/2751
  sed --in-place 's/, "< 1.3"//' facter.gemspec "tmp_install${_gemdir}/specifications/facter-${pkgver}.gemspec"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check(){
  cd $pkgname

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install/${_gemdir}" rspec --exclude-pattern ./spec/framework/detector/os_detector_spec.rb
}

package() {
  cd $pkgname

  cp --archive --verbose tmp_install/* "${pkgdir}"
}

# vim: ts=2 sw=2 et:
