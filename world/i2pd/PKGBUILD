# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Contributor: robertfoster
# Contributor: kurych
# Contributor: redfish
# Contributor: atommixz
# Contributor: denn
# Contributor: post-factum
# Contributor: wrdcrrtmnstr
# Contributor: r4sas

pkgname=i2pd
pkgver=2.50.0
pkgrel=1
pkgdesc='A full-featured C++ implementation of the I2P router'
arch=('x86_64')
url='https://i2pd.website/'
license=('BSD')
depends=('boost-libs' 'libminiupnpc.so' 'openssl' 'zlib')
makedepends=('cmake' 'boost' 'check')
provides=('i2p-router')
backup=('etc/i2pd/i2pd.conf'
        'etc/i2pd/tunnels.conf'
        'etc/logrotate.d/i2pd')
source=("https://github.com/PurpleI2P/i2pd/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        '005-i2pd-tests-use-arch-flags.conf'
        '010-i2pd-config.patch'
        '020-i2pd-do-not-override-config.patch'
        '030-i2pd-systemd-service-hardening.patch'
        '040-i2pd-tunnels-d-readme.patch'
        '050-i2pd-openssl3.2.0-fix.patch'::'https://github.com/PurpleI2P/i2pd/commit/0e98dd5c7049d79bdcdba341357c504e562eb61f.patch'
        '060-i2pd-add-eddsa-test.patch'::'https://github.com/PurpleI2P/i2pd/commit/8ffc1486a4cbacb774fd5b8834f9a6022ed2044b.patch'
        'i2pd.sysusers'
        'i2pd.tmpfiles')
sha256sums=('67c8ba5ea03b09fe2a85820f6d5b3025ad6c4301cbca3fa44c0accfbe5c7def7'
            '2ddf15f1c1cdf5d747a0af667145238023fd126ab00c65f2897cacae935015b1'
            'ed1bde650139731921bf3c8091b3332620404e7700fb9c486a4a806fe34e2d3b'
            'e98eaa783fcd8e1ab84980f68158e3bb9eb5ec101f26c748946a313152643f11'
            '2b84d85d4234eb3b640925d0dd244c8abe3b48bc69c8456629af923de17acf10'
            'cfcb6b07b67aff3e3af12767f4649d88b9320dc71907b6c01b465e5c138cdaa3'
            '896917031962be6faffcd4b4cc8a6141b742b19f8a0affaa1bcadfd1e1ed5e3f'
            '609d58440ec7bc4f212b6b98d2eceb4ec912ed32774f3f59274c499d31bea8c5'
            '88b2e709228049ba11f37863f87de75ab6cde295104852871384337cfdc906a3'
            'fe8cc2ec83cb5b5c2b2ec8cce9a989e0cb6fd347e00b84e03a17b12efd152fac')

prepare() {
    patch -d "${pkgname}-${pkgver}" -Np1 -i "${srcdir}/005-i2pd-tests-use-arch-flags.conf"
    patch -d "${pkgname}-${pkgver}" -Np1 -i "${srcdir}/010-i2pd-config.patch"
    patch -d "${pkgname}-${pkgver}" -Np1 -i "${srcdir}/020-i2pd-do-not-override-config.patch"
    patch -d "${pkgname}-${pkgver}" -Np1 -i "${srcdir}/030-i2pd-systemd-service-hardening.patch"
    patch -d "${pkgname}-${pkgver}" -Np1 -i "${srcdir}/040-i2pd-tunnels-d-readme.patch"
    patch -d "${pkgname}-${pkgver}" -Np1 -i "${srcdir}/050-i2pd-openssl3.2.0-fix.patch"
    patch -d "${pkgname}-${pkgver}" -Np1 -i "${srcdir}/060-i2pd-add-eddsa-test.patch"
}

build() {
    cmake \
        -B build \
        -S "${pkgname}-${pkgver}/build" \
        -G 'Unix Makefiles' \
        -DCMAKE_BUILD_TYPE:STRING='None' \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -DBUILD_SHARED_LIBS:BOOL='ON' \
        -DWITH_UPNP:BOOL='ON' \
        -DBUILD_TESTING:BOOL='ON' \
        -Wno-dev
    cmake --build build
}

check() {
    ctest --test-dir build --output-on-failure
}

package() {
    DESTDIR="$pkgdir" cmake --install build
    cd "${pkgname}-${pkgver}"
    
    # config
    install -D -m644 contrib/{i2pd,tunnels}.conf -t "${pkgdir}/etc/i2pd"
    install -d -m755 "${pkgdir}/etc/i2pd/tunnels.d"
    
    # certificates
    install -d -m755 "${pkgdir}/usr/share/i2pd"
    cp -dr --no-preserve='ownership' contrib/certificates "${pkgdir}/usr/share/i2pd"
    
    # systemd
    install -D -m644 "${srcdir}/i2pd.sysusers" "${pkgdir}/usr/lib/sysusers.d/i2pd.conf"
    install -D -m644 "${srcdir}/i2pd.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/i2pd.conf"
    
    # logrotate
    install -D -m644 contrib/i2pd.logrotate "${pkgdir}/etc/logrotate.d/i2pd"
    
    # tunnels.d examples
    install -D -m644 contrib/tunnels.d/{*.conf,README} -t "${pkgdir}/usr/share/doc/i2pd/tunnels.d"
    
    # headers
    install -D -m644 {i18n,libi2pd{,_client}}/*.h -t "${pkgdir}/usr/include/i2pd"
    
    # man page
    install -D -m644 debian/i2pd.1 -t "${pkgdir}/usr/share/man/man1"
    
    # license
    install -D -m644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
