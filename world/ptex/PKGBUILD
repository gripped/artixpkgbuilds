# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=ptex
pkgver=2.5.1
pkgrel=1
pkgdesc="Per-Face Texture Mapping for Production Rendering"
arch=('x86_64')
url="http://ptex.us/"
license=('BSD-3-Clause')
depends=('zlib' 'gcc-libs' 'libdeflate')
makedepends=('git' 'doxygen' 'cmake' 'ninja')
source=("$pkgname-$pkgver.tar.gz::https://github.com/wdas/ptex/archive/v${pkgver}.tar.gz")
sha512sums=('26265899d3bb47eca67052d69b08efb89a01cf06a01a4aabdd8118e0497aac87319317450719ac56ea676bbb2ea771bd9b8fe73bc41caa8a2a6818cbc3d83bea')

build() {
    cd "$pkgname-$pkgver"

    CXXFLAGS+=" -ffat-lto-objects"
    cmake \
        -Bbuild \
        -GNinja \
        -DPTEX_SHA=$_commit_sha \
        -DPTEX_VER=$pkgver \
        -DPTEX_BUILD_STATIC_LIBS=OFF \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_INSTALL_PREFIX=/usr

    ninja -C build
}

check() {
    cd "$pkgname-$pkgver"
    ctest \
      --test-dir build \
      --exclude-regex "rtest"
}

package() {
    cd "$pkgname-$pkgver"

    DESTDIR="$pkgdir" ninja -C build install
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
