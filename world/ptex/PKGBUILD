# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=ptex
pkgver=2.5.4
pkgrel=1
pkgdesc="Per-Face Texture Mapping for Production Rendering"
arch=('x86_64')
url="http://ptex.us/"
license=('BSD-3-Clause')
depends=('glibc' 'zlib' 'libgcc' 'libstdc++' 'libdeflate')
makedepends=('git' 'doxygen' 'cmake' 'ninja')
source=("$pkgname-$pkgver.tar.gz::https://github.com/wdas/ptex/archive/v${pkgver}.tar.gz")
sha512sums=('f405fada625e792d9ca5796ee28667d75a16bb4a0a0ac65d02dc675c21f6b5d39d0922c95f74b694c78b9366cd04b2deb96e069845b1f5ae5ca8c83fad204681')

build() {
    cd "$pkgname-$pkgver"

    CXXFLAGS+=" -ffat-lto-objects"
    cmake \
        -Bbuild \
        -GNinja \
        -DPTEX_SHA=$_commit_sha \
        -DPTEX_VER=$pkgver \
        -DPTEX_BUILD_STATIC_LIBS=OFF \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_INSTALL_PREFIX=/usr

    ninja -C build
}

check() {
    cd "$pkgname-$pkgver"
    ctest \
      --test-dir build \
      --exclude-regex "rtest"
}

package() {
    cd "$pkgname-$pkgver"

    DESTDIR="$pkgdir" ninja -C build install
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
