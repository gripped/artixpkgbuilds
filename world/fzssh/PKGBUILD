# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=fzssh
pkgver=1.2.1
pkgrel=1
pkgdesc="a SSH/SFTP library based on libfilezilla"
url="https://filezilla-project.org"
arch=('x86_64')
license=('GPL-3.0-or-later')
depends=('nettle' 'gmp' 'libfilezilla' 'argon2' 'libstdc++' 'libgcc' 'glibc')
makedepends=('meson')
# Upstream download link became (purposely) unstable
# See https://trac.filezilla-project.org/ticket/13186
source=("https://sources.archlinux.org/other/packages/${pkgname}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('a058f599e6a12c5d34b7486ee116b8cef7c6e6cabfeb1721779c438c61f1fe1d')
sha512sums=('cf0483badc5cbdf0303fdc72647ea77e54937c1dd68bf997e1d8c7609d8424afb58649a21a94b319d51d60aaf03a755fd3ab95cf27543465360eaff1c95a8602')

build() {
	artix-meson "${pkgname}-${pkgver}" build
	meson compile -C build
}

check() {
	meson test -C build --print-errorlogs
}

package() {
	meson install -C build --destdir "${pkgdir}"
}
