# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=fzssh
pkgver=1.1.8
pkgrel=1
pkgdesc="a SSH/SFTP library based on libfilezilla"
url="https://filezilla-project.org"
arch=('x86_64')
license=('GPL-3.0-or-later')
depends=('nettle' 'gmp' 'libfilezilla' 'argon2' 'libstdc++' 'libgcc' 'glibc')
makedepends=('meson')
# Upstream download link became (purposely) unstable
# See https://trac.filezilla-project.org/ticket/13186
source=("https://sources.archlinux.org/other/packages/${pkgname}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('6542efec4ea87dc0b36a83541c1c07e051b1f352f177df15712daafc894fcd1a')
sha512sums=('512ef17505a743fa50c3903f3b8b6fb238b817d1f3d56f49e2d8364d8a4580ee19b6d22bc80241479c2dc27773e4d7261744ed358230b6fd226cfef0c719d288')

build() {
	artix-meson "${pkgname}-${pkgver}" build
	meson compile -C build
}

check() {
	meson test -C build --print-errorlogs
}

package() {
	meson install -C build --destdir "${pkgdir}"
}
