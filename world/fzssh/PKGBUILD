# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=fzssh
pkgver=1.3.0
pkgrel=3
pkgdesc="a SSH/SFTP library based on libfilezilla"
url="https://filezilla-project.org"
arch=('x86_64')
license=('GPL-3.0-or-later')
depends=('nettle' 'gmp' 'libfilezilla' 'argon2' 'libstdc++' 'libgcc' 'glibc')
makedepends=('meson')
# Upstream download link became (purposely) unstable
# See https://trac.filezilla-project.org/ticket/13186
source=("https://sources.archlinux.org/other/packages/${pkgname}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('864582b203726588b5a16d86141ff95ab77d2399762febbe1fded92e4f047996')
sha512sums=('6ebe301df01b24a4d3692053e65c9e4c1b510b197d7da1f353df60e3ba3dff935d68007f21d5403ba3686133b39305758afc89c0a734cfddc8f549b714bdfb22')

build() {
	artix-meson "${pkgname}-${pkgver}" build
	meson compile -C build
}

check() {
	meson test -C build --print-errorlogs
}

package() {
	meson install -C build --destdir "${pkgdir}"
}
