# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=fzssh
pkgver=1.2.0
pkgrel=2
pkgdesc="a SSH/SFTP library based on libfilezilla"
url="https://filezilla-project.org"
arch=('x86_64')
license=('GPL-3.0-or-later')
depends=('nettle' 'gmp' 'libfilezilla' 'argon2' 'libstdc++' 'libgcc' 'glibc')
makedepends=('meson')
# Upstream download link became (purposely) unstable
# See https://trac.filezilla-project.org/ticket/13186
source=("https://sources.archlinux.org/other/packages/${pkgname}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('ab8888adc6a0c935740837078977eeb671651723031df260687c45180e20165a')
sha512sums=('1ec5d278740989da05e2a23fecb48f77f63e892f1372f98f2d214683e80b5fb0417ce76d5761516fc3885626c61c93c1146af6bc040758c7bdf1c4bdee34d30d')

build() {
	artix-meson "${pkgname}-${pkgver}" build
	meson compile -C build
}

check() {
	meson test -C build --print-errorlogs
}

package() {
	meson install -C build --destdir "${pkgdir}"
}
