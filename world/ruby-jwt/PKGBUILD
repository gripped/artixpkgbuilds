# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Tim Meusel <tim@bastelfreak.de>

_gemname='jwt'
pkgname="ruby-${_gemname}"
pkgver=2.10.1
pkgrel=2
pkgdesc='A pure ruby implementation of the RFC 7519 OAuth JSON Web Token (JWT) standard'
arch=('any')
url="https://github.com/jwt/${pkgname}"
license=('MIT')
depends=(
  ruby
  ruby-base64
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-bundler
  ruby-rake
  ruby-rspec
  ruby-simplecov
)
options=('!emptydirs')
source=("git+${url}#tag=v${pkgver}")
sha512sums=('7cc18b0e5804deea6c745369c584b457f6f41bf5140c701014a2b16880f457fae46cb42216f9c305d818f665757159f8ce86a7ab43f3409d4a5481009b3591f7')
b2sums=('b81de4ae5129cb8a9677ae88ad624908aa32a9248c2018eefebcf3593a5334d5227ae509cb6f2e2cbbdf948309a930676c8789479686543fb9148fbcfa0389ce')

prepare() {
  cd "${pkgname}"

  # update gemspec/Gemfile to allow newer version of the dep
  sed --in-place --regexp-extended \
    --expression 's|~>|>=|g' \
    --expression '/appraisal/d' \
    --expression '/rubocop/d' \
    "${pkgname}.gemspec" \
    Gemfile \
    Rakefile
}

build() {
  cd "${pkgname}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${pkgname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unreproducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${pkgname}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rake test
}

package() {
  cd "${pkgname}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/\${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/\${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
