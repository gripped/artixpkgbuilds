# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=akonadi-calendar
pkgver=25.08.2
pkgrel=1
pkgdesc='Akonadi calendar integration'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL-2.0-or-later)
depends=(akonadi-contacts
         akonadi-mime
         gcc-libs
         glibc
         gpgmepp
         kcalendarcore
         kcalutils
         kcodecs
         kconfig
         kcontacts
         kcoreaddons
         kcrash
         kdbusaddons
         ki18n
         kidentitymanagement
         kio
         kitemmodels
         kmailtransport
         kmime
         knotifications
         kservice
         kwidgetsaddons
         kxmlgui
         libakonadi
         libkleo
         messagelib
         qgpgme
         qt6-base)
makedepends=(doxygen
             extra-cmake-modules
             qt6-doc
             qt6-tools)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('c27aedc92cf29570636b3a1dc4044abee29a6a36d3536deeb32d558d79b35461'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
