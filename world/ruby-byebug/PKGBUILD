# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-byebug
pkgver=11.1.3
pkgrel=6
pkgdesc='A simple to use and feature rich debugger for Ruby'
arch=(x86_64)
url='https://github.com/deivid-rodriguez/byebug'
license=(BSD)
depends=(
  ruby
  ruby-irb
)
checkdepends=(
  markdownlint
  ruby-bundler
  ruby-minitest
  ruby-pry
  ruby-rake
  ruby-rake-compiler
  ruby-rexml
  ruby-simplecov
  ruby-yard
)
options=(!emptydirs)
source=(https://github.com/deivid-rodriguez/byebug/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        minitest-5.16.patch)
sha256sums=('1482205e562425e5adf1525b0b72e06e49a6ae7640ff3a742f5b60c485fdc3b7'
            '5082ae41ad5dd2c096a532b0d64fc8ae7d40050193a0b42528f5a3f3d38b131a')

prepare() {
  cd byebug-$pkgver
  patch -p1 -i ../minitest-5.16.patch
  sed -e '/chandler/d;/[rR]ubo[cC]op/d' -e 's|~>|>=|' -e 's/"mdl", "0.9.0"/"mdl", ">= 0.9.0"/' -i Rakefile Gemfile
  rm Gemfile.lock
  sed -i '/bundler/d' bin/minitest
  # TODO
  sed -i '/test_finish_inside_autoloaded_files/a skip' test/commands/finish_test.rb

  # remove non working tests - see https://github.com/deivid-rodriguez/byebug/pull/850
  rm --verbose \
    test/commands/continue_test.rb \
    test/commands/interrupt_test.rb \
    test/commands/next_test.rb \
    test/processors/command_processor_test.rb
}

build() {
  cd byebug-$pkgver

  local _gemdir="$(gem env gemdir)"

  gem build --verbose byebug.gemspec

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    byebug-$pkgver.gem

  # remove unreproducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd byebug-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rake test
}

package() {
  cd byebug-$pkgver

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}
