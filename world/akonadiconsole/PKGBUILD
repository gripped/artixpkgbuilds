# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=akonadiconsole
pkgver=24.12.3
pkgrel=1
pkgdesc='Akonadi management and debugging console'
arch=(x86_64)
url='https://kontact.kde.org'
license=(GPL-2.0-or-later)
depends=(akonadi
         akonadi-contacts
         akonadi-search
         calendarsupport
         gcc-libs
         glibc
         kcalendarcore
         kcolorscheme
         kcompletion
         kconfig
         kconfigwidgets
         kcontacts
         kcoreaddons
         kcrash
         kdbusaddons
         ki18n
         kitemmodels
         kmime
         ktextwidgets
         kwidgetsaddons
         kxmlgui
         libakonadi
         libkdepim
         messagelib
         qt6-base
         xapian-core)
makedepends=(extra-cmake-modules
             kdoctools)
groups=(kde-applications
        kde-pim)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('fd78539f4a67d51d0ca9f8ba9bcabcb2690c22b580ff041c74cad559af2e8f75'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
