# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=merkuro
pkgver=24.08.0
pkgrel=3
pkgdesc='A calendar application using Akonadi to sync with external services'
arch=(x86_64)
url='https://apps.kde.org/merkuro.calendar/'
license=(GPL-2.0-or-later)
depends=(akonadi-calendar
         akonadi-contacts
         akonadi-mime
         gcc-libs
         glibc
         gpgme
         kcalendarcore
         kcodecs
         kconfig
         kconfigwidgets
         kcontacts
         kcoreaddons
         kdbusaddons
         kdeclarative
         kdepim-runtime
         ki18n
         kidentitymanagement
         kirigami
         kirigami-addons
         kitemmodels
         kmailtransport
         kmbox
         kmime
         knotifications
         ksvg
         kwidgetsaddons
         kwindowsystem
         libakonadi
         libkdepim
         libkleo
         libplasma
         mailcommon
         messagelib
         mimetreeparser
         pimcommon
         prison
         qqc2-desktop-style 
         qt6-5compat
         qt6-base
         qt6-declarative
         qt6-location
         qt6-positioning)
makedepends=(extra-cmake-modules
             patchelf)
conflicts=(kalendar)
replaces=(kalendar)
groups=(kde-applications
        kde-pim)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/pim/merkuro/-/commit/097e0db8.patch)
sha256sums=('726070bef38453eadd3dee68b68fb294dc215107b98c1a8edb461bad1c9fbdae'
            'SKIP'
            'f3add2db9476adc502fd93e54e8f5d4e9ee952de17a82acbb69c26ae3afcf381')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < 097e0db8.patch # Fix breakage with --as-needed
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
