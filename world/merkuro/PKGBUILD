# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=merkuro
pkgver=25.12.1
pkgrel=1
pkgdesc='Application suite designed to handle emails, calendars, contacts and tasks'
arch=(x86_64)
url='https://apps.kde.org/merkuro/'
license=(GPL-2.0-or-later)
depends=(akonadi-calendar
         akonadi-contacts
         akonadi-mime
         akonadi-search
         gcc-libs
         glibc
         gpgmepp
         kcalendarcore
         kcodecs
         kconfig
         kconfigwidgets
         kcontacts
         kcoreaddons
         kcrash
         kdbusaddons
         kdeclarative
         kdepim-runtime
         kholidays
         ki18n
         kiconthemes
         kidentitymanagement
         kio
         kirigami
         kirigami-addons
         kitemmodels
         kmailtransport
         kmbox
         kmime
         knotifications
         ksvg
         kwidgetsaddons
         kwindowsystem
         libakonadi
         libkdepim
         libkleo
         libplasma
         mailcommon
         messagelib
         mimetreeparser
         pimcommon
         prison
         qgpgme
         qqc2-desktop-style 
         qt6-5compat
         qt6-base
         qt6-declarative
         qt6-location
         qt6-positioning)
makedepends=(extra-cmake-modules)
conflicts=(kalendar)
replaces=(kalendar)
groups=(kde-applications
        kde-pim)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('c8248da65a12383e0098f3b1d18cb6ff994e8e0b9fc11fc2419ff720d9644c60'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
