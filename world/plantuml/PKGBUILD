# Maintainer: Jiachen YANG <farseerfc@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# AUR Contributor: juantascon <juantascon.aur@horlux.org>
# Contributor : Jingbei Li <i@jingbei.li>

pkgname=plantuml
pkgver=1.2026.1
pkgrel=1
pkgdesc="Component that allows to quickly write uml diagrams"
arch=(any)
url="https://plantuml.com/"
license=('GPL-3.0-or-later')
depends=(
  "bash"
  "java-runtime>=8"
)
makedepends=(
  "gradle"
  "java-environment>=8"
)
checkdepends=(
  "fontconfig"
  "ttf-dejavu"
  "ttf-liberation"
)
optdepends=(
  'plantuml-ascii-math: allow use AsciiMath or JLaTeXMath notation'
  'ditaa: allow use ditaa notation'
  'graphviz: allow use Graphviz/DOT notation'
)
source=(
  "https://github.com/plantuml/plantuml/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
  "$pkgname.run"
)
b2sums=('68af787dfbea0f7d86fb17f46cfe41789b0f0239d3b6db050079a542c84a301447d34b33bc32b7432d9292c42355e1643daf45783928cb50b296d035dfc08ac9'
        'ef64a657bf688fc103f68a5181035ad2ab6dee50f190664b62309ef2322add4648bbdffac4598c24395680e73b4d25c9105a6259cb50676818b558dfc50a2b7d')

build() {
  cd $pkgname-$pkgver
  gradle jar
}

check() {
  cd $pkgname-$pkgver
  gradle test
}

package() {
  cd $pkgname-$pkgver
  install -vDm755 "$srcdir/$pkgname.run" "$pkgdir/usr/bin/$pkgname"

  install -vDm644 "build/libs/$pkgname-$pkgver.jar" "$pkgdir/usr/share/java/$pkgname/$pkgname.jar"
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" plantuml-mit/mit-license.txt
}
