# Maintainer: Jiachen YANG <farseerfc@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# AUR Contributor: juantascon <juantascon.aur@horlux.org>
# Contributor : Jingbei Li <i@jingbei.li>

pkgname=plantuml
pkgver=1.2026.2
pkgrel=1
pkgdesc="Component that allows to quickly write uml diagrams"
arch=(any)
url="https://plantuml.com/"
license=('GPL-3.0-or-later')
depends=(
  "bash"
  "java-runtime>=8"
)
makedepends=(
  "git"
  "gradle"
  "java-environment>=8"
)
checkdepends=(
  "fontconfig"
  "ttf-dejavu"
  "ttf-liberation"
)
optdepends=(
  'plantuml-ascii-math: allow use AsciiMath or JLaTeXMath notation'
  'ditaa: allow use ditaa notation'
  'graphviz: allow use Graphviz/DOT notation'
)
source=(
  "git+https://github.com/plantuml/plantuml.git#tag=v$pkgver"
  "$pkgname.run"
)
b2sums=('0b67f7b5b68d93f59764a36a072535f32a8cc7a4bbe847f3c4d7ddb87055b6a9990aff4f543513a54f717a437bbdecfdd6c477309ffe90fe87a043c3834ced4c'
        'ef64a657bf688fc103f68a5181035ad2ab6dee50f190664b62309ef2322add4648bbdffac4598c24395680e73b4d25c9105a6259cb50676818b558dfc50a2b7d')

build() {
  cd $pkgname
  gradle jar
}

check() {
  cd $pkgname
  gradle test
}

package() {
  cd $pkgname
  install -vDm755 "$srcdir/$pkgname.run" "$pkgdir/usr/bin/$pkgname"

  install -vDm644 "build/libs/$pkgname-$pkgver.jar" "$pkgdir/usr/share/java/$pkgname/$pkgname.jar"
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" plantuml-mit/mit-license.txt
}
