# Maintainer: Jiachen YANG <farseerfc@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# AUR Contributor: juantascon <juantascon.aur@horlux.org>
# Contributor : Jingbei Li <i@jingbei.li>

pkgname=plantuml
pkgver=1.2025.4
pkgrel=1
pkgdesc="Component that allows to quickly write uml diagrams"
arch=(any)
url="https://plantuml.com/"
license=('GPL-3.0-or-later OR LGPL-3.0-or-later OR Apache-2.0 OR EPL-2.0 OR MIT')
depends=(
  "bash"
  "java-runtime>=8"
)
makedepends=(
  "gradle"
  "java-environment>=8"
)
checkdepends=(
  "fontconfig"
  "ttf-dejavu"
  "ttf-liberation"
)
optdepends=(
  'plantuml-ascii-math: allow use AsciiMath or JLaTeXMath notation'
  'ditaa: allow use ditaa notation'
  'graphviz: allow use Graphviz/DOT notation'
)
source=(
  "https://github.com/plantuml/plantuml/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
  "$pkgname.run"
)
sha256sums=('823b64910d9eaf63be31efe87c6a83db37873c9dfc67e7286d75e382db665fc6'
            'a3fb528f4c719cfd0ff6154c60fd54ce341011d132caf950cc30af4989f6aac8')

build() {
  cd $pkgname-$pkgver
  gradle jar
}

check() {
  cd $pkgname-$pkgver
  gradle test
}

package() {
  cd $pkgname-$pkgver
  install -vDm755 "$srcdir/$pkgname.run" "$pkgdir/usr/bin/$pkgname"

  install -vDm644 "build/libs/$pkgname-$pkgver.jar" "$pkgdir/usr/share/java/$pkgname/$pkgname.jar"
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" plantuml-mit/mit-license.txt
}
