# Maintainer: Jiachen YANG <farseerfc@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# AUR Contributor: juantascon <juantascon.aur@horlux.org>
# Contributor : Jingbei Li <i@jingbei.li>

pkgname=plantuml
pkgver=1.2026.8
pkgrel=1
pkgdesc="Component that allows to quickly write uml diagrams"
arch=(any)
url="https://plantuml.com/"
license=('GPL-3.0-or-later')
depends=(
  "bash"
  "java-runtime>=8"
)
makedepends=(
  "git"
  "gradle"
  "java-environment>=8"
)
checkdepends=(
  "fontconfig"
  "ttf-dejavu"
  "ttf-liberation"
)
optdepends=(
  'plantuml-ascii-math: allow use AsciiMath or JLaTeXMath notation'
  'ditaa: allow use ditaa notation'
  'graphviz: allow use Graphviz/DOT notation'
)
source=(
  "git+https://github.com/plantuml/plantuml.git#tag=v$pkgver"
  "$pkgname.run"
)
b2sums=('bdd6f0f08c14209d8b2a4e7680aea98c044760f621b2e8065ec58867dafc369334a6b96fc272009f97d5f7ff3db1eb46d7bb6ed1030bccca7928c6d9a4bae3f2'
        'ef64a657bf688fc103f68a5181035ad2ab6dee50f190664b62309ef2322add4648bbdffac4598c24395680e73b4d25c9105a6259cb50676818b558dfc50a2b7d')

build() {
  cd $pkgname
  gradle patchCompilationInfo jar
}

check() {
  cd $pkgname
  gradle test
}

package() {
  cd $pkgname
  install -vDm755 "$srcdir/$pkgname.run" "$pkgdir/usr/bin/$pkgname"

  install -vDm644 "build/libs/$pkgname-$pkgver.jar" "$pkgdir/usr/share/java/$pkgname/$pkgname.jar"
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" plantuml-mit/mit-license.txt
}
