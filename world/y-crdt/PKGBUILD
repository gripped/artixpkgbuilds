# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor:

pkgname=y-crdt
pkgver=0.28.0
pkgrel=1
pkgdesc='Rust port of Yjs'
arch=(x86_64)
url='https://docs.rs/yrs/'
license=(MIT)
depends=(glibc
         libgcc)
makedepends=(cargo
             git)
source=(git+https://github.com/y-crdt/y-crdt#tag=v$pkgver
        yrs.pc.in)
b2sums=('03c95b9a4756f5922c7d1045038bc6a700e579e3e7add1722048ba4f9a908ea086eb28f194c6dd06ff6326261d306e139a08b79d0faf777f06d683446aa7efbd'
        '8e4075b0547e0399053d62de07ba7895f4ca4fdd55912729ed530da88b260536fc587ce580b40f698e489eddbc71494fb03823a9186914ecebba3b43c16cfd7f')

prepare() {
  cd y-crdt
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd y-crdt
  cargo build --release --locked
}

package() {
  cd y-crdt
  install -Dm644 target/release/*.so -t "$pkgdir"/usr/lib
  install -Dm644 tests-ffi/include/libyrs.h -t "$pkgdir"/usr/include
  install -d "$pkgdir"/usr/lib/pkgconfig
  sed -e "s|@VERSION@|$pkgver|" "$srcdir"/yrs.pc.in > "$pkgdir"/usr/lib/pkgconfig/yrs.pc
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
