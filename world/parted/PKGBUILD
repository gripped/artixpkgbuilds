# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=parted
pkgver=3.8
pkgrel=1
pkgdesc='A program for creating, destroying, resizing, checking and copying partitions'
arch=('x86_64')
license=('GPL-3.0-or-later')
url='https://www.gnu.org/software/parted/parted.html'
depends=('glibc'
         'device-mapper' 'libdevmapper.so'
         'util-linux-libs' 'libblkid.so' 'libuuid.so'
         'readline' 'libncursesw.so'
         'ncurses' 'libncursesw.so')
makedepends=('pkg-config')
validpgpkeys=('1B49F933916A37A3F45A1812015F4DD4A70FB705'  # Phillip Susi <psusi@ubuntu.com>
              'B4C6B451E4FA8B4232CA191E117E8C168EFE3A7F'  # Brian C. Lane <bcl@redhat.com>
              '872F3A8A0B84905AFFBC8766FF9868A2D488A5A9') # Brian C. Lane <bcl@redhat.com>
source=("https://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('a2b7811f47b0ddb1f7b1d0aa456f7c1270da70708ce231c2fe054c7199eafa63'
            'SKIP')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Do *not* add --disable-debug, it prevents building fatresize
  # https://github.com/ya-mouse/fatresize/issues/9
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --disable-rpath
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
