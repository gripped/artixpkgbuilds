# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=ruby-mize
_pkgname="${pkgname#ruby-}"
pkgver=0.6.1
pkgrel=1
pkgdesc='Memoize methods/functions in Ruby'
arch=(any)
url='https://github.com/flori/mize'
license=(MIT)
depends=(ruby)
makedepends=(git ruby-rake)
options=(!emptydirs)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('cdd78b57e8f790238f8931bd591cd88486f873514946528ab6ce840c4e69b67df7ecc11df62a00e308c6600a4d6188749ac133cb69266ad10cd8be8bc6521cf9')
b2sums=('fa131d8e0a7e65582e2d66b4c55e9936bcfd689b1ce3db8edd86ab17727b3bc654f8721ef7ab7ddca3b23004a40e607f06c7458b19fa4e16292a98a79eb9489a')

build() {
  cd "$pkgname"

  gem build mize.gemspec
}

package() {
  cd "$pkgname"

  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$_pkgname-$pkgver.gem"

  # delete cache
  rm -rf "$pkgdir/$_gemdir/cache"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
