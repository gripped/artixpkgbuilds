# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=eventviews
pkgver=25.04.1
pkgrel=2
pkgdesc='Library for creating events'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL-2.0-or-later)
depends=(akonadi-calendar
         calendarsupport
         gcc-libs
         glibc
         kcalendarcore
         kcalutils
         kcodecs
         kcolorscheme
         kcompletion
         kconfig
         kconfigwidgets
         kcontacts
         kcoreaddons
         kdiagram
         kguiaddons
         kholidays
         ki18n
         kiconthemes
         kitemmodels
         kwidgetsaddons
         libakonadi
         libkdepim
         qt6-base)
makedepends=(doxygen
             extra-cmake-modules
             qt6-doc
             qt6-tools)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/pim/eventviews/-/commit/976f0dea.patch)
sha256sums=('138db44ebeff29598f8fa5266ccd3200ed1ae68cfc40e8a4351054c400232175'
            'SKIP'
            'f33ce16a673eb8124343ce36137da05bc5792e49837a9a27d28d4c48be89ece4')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < 976f0dea.patch # Fix todo view
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
