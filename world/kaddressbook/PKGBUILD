# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=kaddressbook
pkgver=25.12.0
pkgrel=1
pkgdesc='KDE contact manager'
arch=(x86_64)
url='https://apps.kde.org/kaddressbook/'
license=(GPL-2.0-or-later)
depends=(akonadi-contacts
         akonadi-search
         gcc-libs
         glibc
         grantleetheme
         kcmutils
         kcolorscheme
         kconfig
         kconfigwidgets
         kcontacts
         kcoreaddons
         kcrash
         kdepim-runtime
         ki18n
         kiconthemes
         kitemmodels
         kldap
         kontactinterface
         kparts
         kuserfeedback
         kwidgetsaddons
         kxmlgui
         libakonadi
         libkdepim
         pimcommon
         qt6-base)
makedepends=(extra-cmake-modules
             kdoctools)
groups=(kde-applications
        kde-pim)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('8fc971227c1ae1d059adf8e4d217f2d3facaca69fdd4979af73f097b9417d95e'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
