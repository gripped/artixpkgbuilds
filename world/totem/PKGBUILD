# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=totem
pkgver=43.2
pkgrel=2
pkgdesc="Movie player for the GNOME desktop based on GStreamer"
url="https://apps.gnome.org/Totem/"
arch=(x86_64)
license=('GPL-2.0-or-later WITH GStreamer-exception-2008')
depends=(
  at-spi2-core
  cairo
  dconf
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gnome-desktop
  grilo
  gsettings-desktop-schemas
  gst-plugin-gtk
  gst-plugins-bad
  gst-plugins-base
  gst-plugins-base-libs
  gst-plugins-good
  gstreamer
  gtk3
  hicolor-icon-theme
  iso-codes
  libepoxy
  libgirepository
  libhandy
  libpeas
  libportal
  libportal-gtk3
  pango
  python
  python-gobject
  totem-plparser
)
makedepends=(
  appstream-glib
  docbook-xsl
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  meson
  yelp-tools
)
checkdepends=(xorg-server-xvfb)
optdepends=(
  'gst-plugins-ugly: Extra media codecs'
  'gst-libav: Extra media codecs'
  'grilo-plugins: Media discovery'
)
provides=(libtotem.so)
conflicts=(totem-plugin)
replaces=(totem-plugin)
groups=(gnome)
source=(
  "git+https://gitlab.gnome.org/GNOME/totem.git?signed#tag=${pkgver/[a-z]/.&}"
  "git+https://gitlab.gnome.org/GNOME/libgd.git"
)
b2sums=('c9c3fce36c82b3cc529443b68b37a457d6f07dddb8827042c8e2c3418166f119e8d04be5af895ce71da3a32fd697a6b82a6090a4e7b5220015eeda751d56acdc'
        'SKIP')
validpgpkeys=(
  90B845BA6595B902DDC9D4A62541290C393E86DF # Bastien Nocera <hadess@hadess.net>
)

prepare() {
  cd totem

  git submodule init subprojects/libgd # Skip flatpak/shared-modules
  git submodule set-url subprojects/libgd "$srcdir/libgd"
  git -c protocol.file.allow=always -c protocol.allow=never submodule update

  # Raise window on activation
  # https://gitlab.gnome.org/GNOME/totem/-/merge_requests/450
  git cherry-pick -n 65cffb5535a201d2896ab6b88a545a8b3f8e9a6a
}

build() {
  local meson_options=(
    -D enable-gtk-doc=true
  )

  artix-meson totem build "${meson_options[@]}"
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 totem/COPYING
}

# vim:set sw=2 sts=-1 et:
