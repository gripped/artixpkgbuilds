# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=totem
pkgver=43.0
pkgrel=3
pkgdesc="Movie player for the GNOME desktop based on GStreamer"
url="https://apps.gnome.org/Totem/"
arch=(x86_64)
license=(GPL2 custom)
depends=(totem-plparser iso-codes libpeas grilo gsettings-desktop-schemas
         python-gobject gnome-desktop gst-plugins-base gst-plugins-good
         gst-plugins-bad gst-plugin-gtk libhandy libportal-gtk3)
makedepends=(docbook-xsl gobject-introspection git appstream-glib gtk-doc meson
             yelp-tools glib2-devel)
checkdepends=(xorg-server-xvfb)
optdepends=('gst-plugins-ugly: Extra media codecs'
            'gst-libav: Extra media codecs'
            'grilo-plugins: Media discovery')
provides=(libtotem.so)
conflicts=(totem-plugin)
replaces=(totem-plugin)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/totem.git#tag=${pkgver/[a-z]/.&}"
        "git+https://gitlab.gnome.org/GNOME/libgd.git"
        totem-gtksink-fallback.patch)
b2sums=('7101db21b91e67919416ebd8d3efe6dae77d1c937b6aa4955afba195533981f45968441a75370868b90e502ba285687b27c0a82f0fc3e79c282567957c8e1bf7'
        'SKIP'
        '4d13d6671261be6c641591500e6e679c1a2cc1908ba9d619e5c5fbe2822633e669ef9c67c1b993f31546135c327b41437c11bad5dc08e8345fd0b2c24ec42b29')

prepare() {
  cd totem

  git submodule init
  git submodule set-url subprojects/libgd "$srcdir/libgd"
  git -c protocol.file.allow=always submodule update

  # Fallback to gtksink if OpenGL is unavailable
  # https://gitlab.gnome.org/GNOME/totem/-/merge_requests/379
  git apply -3 ../totem-gtksink-fallback.patch
}

build() {
  local meson_options=(
    -D enable-gtk-doc=true
  )

  artix-meson totem build "${meson_options[@]}"
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 totem/COPYING
}

# vim:set sw=2 sts=-1 et:
