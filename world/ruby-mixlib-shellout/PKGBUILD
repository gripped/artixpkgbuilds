# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Joel Goguen <contact+aur@jgoguen.ca>
# Contributor: Christopher Loen <christopherloen at gmail dot com>
# Contributor: Artem Vorotnikov <artem@vorotnikov.me>

pkgname=ruby-mixlib-shellout
_pkgname="${pkgname#ruby-}"
pkgver=3.4.10
pkgrel=1
pkgdesc='Run external commands on Unix'
arch=(any)
url='https://github.com/chef/mixlib-shellout'
license=(Apache-2.0)
depends=(ruby ruby-chef-utils)
makedepends=(git ruby-rake ruby-rdoc)
options=(!emptydirs)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('08da6ef5555dd3ad7dfe51e60a8e16fbb2f9f6f06bd514d4cc95ac8db03c4a392004a8db58f21cbd3e426387d8b85df81d85db13e0d5f10f1b04646604b6541f')
b2sums=('2e5230dd17a0178b5013337ea75d8def9f01c612f3d3dd58dd41b6dddd9d3b99dcd278e54e7da4954a7a5f4eaebdd58248bb21451332a0ae4bb5e21546ef599d')

build() {
  cd "$pkgname"

  gem build "$_pkgname.gemspec"
}

package() {
  cd "$pkgname"

  local _gemdir="$(gem env gemdir)"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$_pkgname-$pkgver.gem"
}
