# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Joel Goguen <contact+aur@jgoguen.ca>
# Contributor: Christopher Loen <christopherloen at gmail dot com>
# Contributor: Artem Vorotnikov <artem@vorotnikov.me>

pkgname=ruby-mixlib-shellout
_pkgname="${pkgname#ruby-}"
pkgver=3.4.13
pkgrel=1
pkgdesc='Run external commands on Unix'
arch=(any)
url='https://github.com/chef/mixlib-shellout'
license=(Apache-2.0)
depends=(ruby ruby-chef-utils)
makedepends=(git ruby-rake ruby-rdoc)
options=(!emptydirs)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('48866f3b88cec22dad99d965f568287a7f73d28d3c01fef6ec41e35cbf066addbf0776b26cb89afb5fb19eabefc8e5cfe0fb778fa16fc7c41d2e9f4fc953ea40')
b2sums=('dc3cf19b286b05e68d555602fa079ffc6196344d3000af6259485c7b387c0976703261434e4d860bb6e8241128c932ab4f51b44c84cbea7216139b3be7be1d37')

build() {
  cd "$pkgname"

  gem build "$_pkgname.gemspec"
}

package() {
  cd "$pkgname"

  local _gemdir="$(gem env gemdir)"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$_pkgname-$pkgver.gem"
}
