# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Joel Goguen <contact+aur@jgoguen.ca>
# Contributor: Christopher Loen <christopherloen at gmail dot com>
# Contributor: Artem Vorotnikov <artem@vorotnikov.me>

pkgname=ruby-mixlib-shellout
_pkgname="${pkgname#ruby-}"
pkgver=3.4.11
pkgrel=1
pkgdesc='Run external commands on Unix'
arch=(any)
url='https://github.com/chef/mixlib-shellout'
license=(Apache-2.0)
depends=(ruby ruby-chef-utils)
makedepends=(git ruby-rake ruby-rdoc)
options=(!emptydirs)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('5d7c7c476f9860638d6beb30c1e0ef6cde26bdf35e17705c9e439d33b38ae6370e9a8103150f7648c169ed11d30052649aa16238df99090f392258d38a78edf0')
b2sums=('d5ababcf5c46198c12f4dd4668b2f5f06f5386442526d98ef10c94589f00655732017183d6ec71d897248d75b296d76cd84c9bd7e153951ecb4c69e87bbb3fe6')

build() {
  cd "$pkgname"

  gem build "$_pkgname.gemspec"
}

package() {
  cd "$pkgname"

  local _gemdir="$(gem env gemdir)"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$_pkgname-$pkgver.gem"
}
