# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: hexchian <i at hexchain dot org>

pkgname=crun
pkgver=1.11.2
pkgrel=1
pkgdesc="A fast and lightweight fully featured OCI runtime and C library for running containers"
url="https://github.com/containers/crun"
license=('LGPL')
arch=('x86_64')
provides=('oci-runtime')
depends=('yajl' 'libudev' 'libcap' 'libseccomp' 'criu')
makedepends=('libtool' 'python' 'go-md2man' 'udev' 'git')
source=("https://github.com/containers/crun/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"{,.asc})
validpgpkeys=('AC404C1C0BF735C63FF4D562263D6DF2E163E1EA')
sha256sums=('208da963a9c8dcebad63e284d6af4e569ec798ef37380e9b5b38dbbbccd5f0cd'
            'SKIP')

build() {
    cd "$pkgname-$pkgver"
    ./autogen.sh
    ./configure \
        --prefix=/usr \
        --disable-systemd \
        --enable-shared \
        --enable-dynamic \
        --with-python-bindings
    make
}

package() {
    cd "$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
}
