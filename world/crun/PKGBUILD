# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: hexchian <i at hexchain dot org>

pkgbase=crun
pkgname=(crun krun)
pkgver=1.28
pkgrel=1
pkgdesc="A fast and lightweight fully featured OCI runtime and C library for running containers"
url="https://github.com/containers/crun"
license=('LGPL-2.1-or-later')
arch=('x86_64')
provides=('oci-runtime')
makedepends=(
  'criu'
  'git'
  'go-md2man'
  'json-c'
  'libblake3'
  'libcap'
  'libkrun'
  'libseccomp'
  'libtool'
  'python'
  'udev'
)
source=(
  "git+https://github.com/containers/crun.git#tag=$pkgver?signed"
  0001-fix-missing-json-c-symbols-in-build.patch
)
validpgpkeys=('AC404C1C0BF735C63FF4D562263D6DF2E163E1EA')
sha256sums=('0d01636b19653202e5de7fe94beacdb7099e333a41d7bb6dec63c127364422f1'
            '8d487cb316b802961d53620eac4b2c8742e14b68cee04511d36a070227635510')

prepare() {
    cd "$pkgname"
    patch -Np1 < ../0001-fix-missing-json-c-symbols-in-build.patch
}

build() {
    cd "$pkgname"
    ./autogen.sh
    ./configure --disable-systemd \
        --prefix=/usr \
        --enable-shared \
        --enable-dynamic \
        --with-python-bindings \
        --with-libkrun
    make
}

package_crun() {
    depends=(
      'criu'
      'json-c'
      'libblake3'
      'libcap'
      'libseccomp'
      'libudev'
    )

    make -C "$pkgname" DESTDIR="$pkgdir" install
    rm -v "$pkgdir/usr/bin/krun"

    mkdir -vp "krun/"
    mv -v "$pkgdir/usr/share/man/man1"/krun.1* "krun/"
}

package_krun() {
    pkgdesc="crun based OCI runtime using libkrun to run containerized programs in isolated KVM environments"
    depends=(
      'crun'
      'libkrun'
    )

    mkdir -pv "$pkgdir/usr/bin"
    ln -sv crun "$pkgdir/usr/bin/krun"

    cd "$pkgname"
    mkdir -pv "$pkgdir/usr/share/man/man1/"
    mv -v krun.1* "$pkgdir/usr/share/man/man1/"
}
