# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: hexchian <i at hexchain dot org>

pkgbase=crun
pkgname=(crun krun)
pkgver=1.29.1
pkgrel=1
pkgdesc="A fast and lightweight fully featured OCI runtime and C library for running containers"
url="https://github.com/containers/crun"
license=('LGPL-2.1-or-later')
arch=('x86_64')
provides=('oci-runtime')
makedepends=(
  'criu'
  'git'
  'go-md2man'
  'json-c'
  'libblake3'
  'libcap'
  'libkrun'
  'libseccomp'
  'libtool'
  'python'
  'udev'
)
source=(
  "git+https://github.com/containers/crun.git#tag=$pkgver?signed"
  0001-fix-missing-json-c-symbols-in-build.patch
  https://github.com/containers/crun/pull/2188.patch
)
validpgpkeys=('AC404C1C0BF735C63FF4D562263D6DF2E163E1EA')
sha256sums=('d5f2f27ea25554ad28c4163749cd3060f39ba48461b5dc6e80cf676b53ba5515'
            '8d487cb316b802961d53620eac4b2c8742e14b68cee04511d36a070227635510'
            'd90a2cf783c1d498d3bd345fc25794d3489625ca302c9829da9b8317e7c1fd4c')

prepare() {
    cd "$pkgname"
    patch -Np1 < ../0001-fix-missing-json-c-symbols-in-build.patch
    patch -Np1 < ../2188.patch
}

build() {
    cd "$pkgname"
    ./autogen.sh
    ./configure --disable-systemd \
        --prefix=/usr \
        --enable-shared \
        --enable-dynamic \
        --with-python-bindings \
        --with-libkrun
    make
}

package_crun() {
    depends=(
      'criu'
      'json-c'
      'libblake3'
      'libcap'
      'libseccomp'
      'libudev'
    )

    make -C "$pkgname" DESTDIR="$pkgdir" install
    rm -v "$pkgdir/usr/bin/krun"

    mkdir -vp "krun/"
    mv -v "$pkgdir/usr/share/man/man1"/krun.1* "krun/"
}

package_krun() {
    pkgdesc="crun based OCI runtime using libkrun to run containerized programs in isolated KVM environments"
    depends=(
      'crun'
      'libkrun'
    )

    mkdir -pv "$pkgdir/usr/bin"
    ln -sv crun "$pkgdir/usr/bin/krun"

    cd "$pkgname"
    mkdir -pv "$pkgdir/usr/share/man/man1/"
    mv -v krun.1* "$pkgdir/usr/share/man/man1/"
}
