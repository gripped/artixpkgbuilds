# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: hexchian <i at hexchain dot org>

pkgname=crun
pkgver=1.8.6
pkgrel=1
pkgdesc="A fast and lightweight fully featured OCI runtime and C library for running containers"
url="https://github.com/containers/crun"
license=('LGPL')
arch=('x86_64')
provides=('oci-runtime')
depends=('yajl' 'libudev' 'libcap' 'libseccomp' 'criu')
makedepends=('libtool' 'python' 'go-md2man' 'udev' 'git')
source=("https://github.com/containers/crun/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"{,.asc})
source=("https://github.com/containers/crun/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"{,.asc}
        "$pkgname-$pkgver-fix-missingsymbol.patch::https://github.com/containers/crun/commit/080e56021ee3e2d4b9054fdb6134f49b4d6d9c7b.patch")
validpgpkeys=('AC404C1C0BF735C63FF4D562263D6DF2E163E1EA')
sha256sums=('1808235641932a89efc42756db7774e1337c130ed8307b8b588542ca372d9467'
            'SKIP'
            'ee88aad33638ddff7adad140c51924520739108eedf8facb04d27d1e339a1cb5')

prepare() {
    cd "${pkgname}-${pkgver}"
    patch -Np1 < "$srcdir/crun-1.8.6-fix-missingsymbol.patch"
}

build() {
    cd "$pkgname-$pkgver"
    ./autogen.sh
    ./configure \
        --prefix=/usr \
        --disable-systemd \
        --enable-shared \
        --enable-dynamic \
        --with-python-bindings
    make
}

package() {
    cd "$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
}
