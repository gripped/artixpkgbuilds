# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-usage
pkgver=48.0
pkgrel=1
pkgdesc="View information about use of system resources"
url="https://wiki.gnome.org/Apps/Usage"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  glib2
  glibc
  graphene
  gtk4
  hicolor-icon-theme
  json-glib
  libadwaita
  libgee
  libgtop
  libnm
  pango
  tinysparql
)
makedepends=(
  accountsservice
  gamemode
  git
  meson
  vala
)
optdepends=(
  'accountsservice: show user tags for processes'
  'gamemode: show processes requesting game mode'
)
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-usage.git#tag=${pkgver/[a-z]/.&}"
  0001-Add-D-Bus-service-file.patch
)
b2sums=('e52ebf855515a26a171cac84dfcc0a9fc2fe8850c820b7d403d606eb1e98c85794d70f1f76931b2f9d2fa0f1c377841972795c9d2e3c2972861b8324b4d73cdb'
        '34066b9a090b5466bfbb215a726e566a3268e7036075e712db580c2cb6e5d83ec1eaee5f5f71737ac383939d1183aff05ab284e75ddb15f59e3c577bb16ee155')

prepare() {
  cd $pkgname

  # Add D-Bus service file
  # https://gitlab.gnome.org/GNOME/gnome-usage/-/merge_requests/167
  git apply -3 ../0001-Add-D-Bus-service-file.patch
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
