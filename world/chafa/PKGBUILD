# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=chafa
pkgver=1.18.3
pkgrel=1
pkgdesc="Image-to-text converter supporting a wide range of symbols and palettes, transparency, animations, etc."
arch=("x86_64")
url="https://hpjansson.org/chafa/"
license=("LGPL-3.0-or-later")
depends=('libavif' 'libheif' 'libjxl' 'librsvg' 'libwebp' 'libxslt')
makedepends=('git' 'gtk-doc')
provides=('libchafa.so')
source=(git+https://github.com/hpjansson/chafa.git#tag=$pkgver?signed)
sha512sums=('192ac6fd4ad14bce7808ede2f2b22fa2f201b31c13f60170a0e707a5b8bdc1bba8f2bfaec1db2b953a3a71c534cc9e48e87bfaf9398a15b42c5b1846f38fdf2c')
validpgpkeys=('C01EDE5BB0D91E26D003662EC76BB9FEEAD12EA7')  # Hans Petter Jansson

build() {
  cd $pkgname
  ./autogen.sh --prefix=/usr --enable-man --enable-gtk-doc
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  make -C tools/completions PREFIX="$pkgdir"/usr/share install-zsh-completion install-fish-completion install-bash-completion
}
