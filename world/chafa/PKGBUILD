# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=chafa
pkgver=1.16.1
pkgrel=1
pkgdesc="Image-to-text converter supporting a wide range of symbols and palettes, transparency, animations, etc."
arch=("x86_64")
url="https://hpjansson.org/chafa/"
depends=('libavif' 'libjxl' 'librsvg' 'libwebp' 'libxslt')
makedepends=('git' 'gtk-doc')
provides=('libchafa.so')
license=("LGPL-3.0-or-later")
source=(git+https://github.com/hpjansson/chafa.git#tag=$pkgver?signed)
sha512sums=('cf5c988419e900d4de64f99461e9f765c45b986c51ee08c8ec33924b48ad9b8e3f8679f69c05388c4176172fded67a7ed50378df682d681e12b262674b547127')
validpgpkeys=('C01EDE5BB0D91E26D003662EC76BB9FEEAD12EA7')  # Hans Petter Jansson

build() {
  cd $pkgname
  ./autogen.sh --prefix=/usr --enable-man --enable-gtk-doc
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  make -C tools/completions PREFIX="$pkgdir"/usr/share install-zsh-completion
}
