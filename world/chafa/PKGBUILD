# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=chafa
pkgver=1.18.0
pkgrel=1
pkgdesc="Image-to-text converter supporting a wide range of symbols and palettes, transparency, animations, etc."
arch=("x86_64")
url="https://hpjansson.org/chafa/"
license=("LGPL-3.0-or-later")
depends=('libavif' 'libheif' 'libjxl' 'librsvg' 'libwebp' 'libxslt')
makedepends=('git' 'gtk-doc')
provides=('libchafa.so')
source=(git+https://github.com/hpjansson/chafa.git#tag=$pkgver?signed)
sha512sums=('4f3fb77e4d62857fe7769146e2df882a5a9b538c99ff0897979643b6abd6c6ea5c7d56a7a56e369efcad27eef0aa233da9d5eb25ab69969e6f473da4b6154f85')
validpgpkeys=('C01EDE5BB0D91E26D003662EC76BB9FEEAD12EA7')  # Hans Petter Jansson

build() {
  cd $pkgname
  ./autogen.sh --prefix=/usr --enable-man --enable-gtk-doc
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  make -C tools/completions PREFIX="$pkgdir"/usr/share install-zsh-completion install-fish-completion
}
