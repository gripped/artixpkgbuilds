# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ignacio Galmarino <igalmarino@gmail.com>

pkgname=openresolv
pkgver=3.17.3
pkgrel=1
pkgdesc="resolv.conf management framework (resolvconf)"
url="https://roy.marples.name/projects/openresolv"
arch=(any)
license=(BSD-2-Clause)
provides=(resolvconf)
conflicts=(resolvconf)
depends=(bash)
makedepends=(
  git
 
)
backup=(etc/resolvconf.conf)
source=("git+https://github.com/NetworkConfiguration/openresolv?signed#tag=v$pkgver")
b2sums=('a9a5a7a6aaf47adb405aa34f1e88b4639f0339dadb23e7e53ffc649b65594a94c4c02fa1912f7808e7075ecd5741db17374b8d0a0383d6b75fe2ceb58cc85792')
validpgpkeys=(
  A785ED2755955D9E93EA59F6597F97EA9AD45549 # Roy Marples <roy@marples.name>
)

prepare() {
  cd $pkgname
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
    --libexecdir=/usr/lib/resolvconf
    --sbindir=/usr/bin
  )

  cd $pkgname
  ./configure "${configure_options[@]}"
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 sts=-1 et:
