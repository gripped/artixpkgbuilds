# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ignacio Galmarino <igalmarino@gmail.com>

pkgname=openresolv
pkgver=3.17.1
pkgrel=1
pkgdesc="resolv.conf management framework (resolvconf)"
url="https://roy.marples.name/projects/openresolv"
arch=(any)
license=(BSD-2-Clause)
provides=(resolvconf)
conflicts=(resolvconf)
depends=(bash)
makedepends=(
  git
 
)
backup=(etc/resolvconf.conf)
source=("git+https://github.com/NetworkConfiguration/openresolv?signed#tag=v$pkgver")
b2sums=('4cce83195f6ef48185ab4c155596cea871001be20e6d607583705c0ebbe149c253cdc4aee8b7fbf37cbedf5aa4b6d9cc2b09318e5ddc75361b076363b43bd019')
validpgpkeys=(
  A785ED2755955D9E93EA59F6597F97EA9AD45549 # Roy Marples <roy@marples.name>
)

prepare() {
  cd $pkgname
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
    --libexecdir=/usr/lib/resolvconf
    --sbindir=/usr/bin
  )

  cd $pkgname
  ./configure "${configure_options[@]}"
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 sts=-1 et:
