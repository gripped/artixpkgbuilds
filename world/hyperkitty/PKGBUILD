# Maintainer: David Runge <dvzrv@archlinux.org>

_name=HyperKitty
pkgname=hyperkitty
pkgver=1.3.12
pkgrel=7
pkgdesc="A web interface to access GNU Mailman v3 archives"
arch=(any)
url="https://gitlab.com/mailman/hyperkitty"
license=(
  0BSD
  GPL-3.0-or-later
)
depends=(
  python
  python-dateutil
  python-django
  python-django-allauth  # via python-django-mailman3
  python-django-compressor
  python-django-extensions
  python-django-gravatar
  python-django-haystack
  python-django-mailman3
  python-django-q2
  python-django-rest-framework
  python-flufl-lock
  python-mailmanclient
  python-mistune
  python-networkx
  python-robot-detection
  sassc
)
makedepends=(
  git
  python-build
  python-installer
  python-isort
  python-pdm-backend
)
checkdepends=(
  python-beautifulsoup4
  python-django-debug-toolbar
  python-elasticsearch
  python-lxml
  python-html5lib  # via python-lxml
  python-pytest
  python-pytest-django
  python-whoosh
)
optdepends=(
  'dart-sass: alternative to sassc'
  'python-django-debug-toolbar: for debugging'
  'python-elasticsearch: for using elasticsearch as search backend'
  'python-whoosh: for using whoosh as search backend'
  'python-xapian-haystack: for using xapian as search backend'
  'uwsgi-plugin-python: for running inside uwsgi'
)
backup=(
  etc/uwsgi/$pkgname.ini
  etc/webapps/$pkgname/settings_local.py
  etc/webapps/$pkgname/urls.py
)
install=$pkgname.install
source=(
  "git+$url?signed#tag=$pkgver"
  $pkgname-1.3.5-settings.patch
  $pkgname.sysusers
  $pkgname.tmpfiles
  $pkgname.uwsgi
  fix_compatibility_with_mistune_3.1.x.patch
  LICENSE
)
sha512sums=('526df54d64d4449e3ddd3b71359f4a57a5bae99f9344560d591434d83a2de00c8e7acab6f6d1193af8d63c3655112b2cf67d4f96e0bb3c3ae28d64f24ae0dbfe'
            'd33a2ec2fb4547da56795c450882bbedbcce962ed2e96e26817fdaa8045859c444f215107fabcf71d62f2b76f41212f5fb49b7e2ed432ccd970eb13988f2f67d'
            '7fa8473a3c905645a552a9586657d00c65f101e434827a280b7a52988217604187d2a3e0460aa222b7f3d0613a903139934f71aacfc78c0bbb42f92179a9fbda'
            'c943d82b8640a513728e3f07fa44c0ddb5a3bb8ec84a3a70990e51287ca01977887f895b901688fc4643d8fcb106d524259a86a51bccaafd77c69b241f1b23ba'
            '351f40a6bc1fd1735c883408f9941808e081206927a7c02467e03228243dd156378cac21f851b498702bd4f96fde8e4c80536dbcc6f3c384bf708bed4e988971'
            'bb9595b7db9bb50e089a234c8c99be7661dbfb7bd670a79d0d6ff28d29d35e69d7b854eebb7fdc165d1c23f8175584c1ab4224cba3a8e03df465f23a7f42b982'
            'a33658d9271e5c537ccd41bf540b463ad2a5eca4a060c80486ff42a736f0aa042d10436e7177c34d792177cb11285243dee1f31c4df54fb0bfaabbc306406930')
b2sums=('594db03c1a35ddff5458cf2af080e31e9424e0908ebe97221ef99536b57cefad18fadb7a3e22330078ef9ee472161cdf89578eadd1d0db22a4c52f907fc439fb'
        '62793fe38541dd570163ab0cd740fd6aeae9eb652164fbb066c1b958dcdba845c5ee3d5ca05770a8179ef292c584c9fbd293ad88956c58ad306ab8b352ea679c'
        '9af6a59ab5672fbac9c5589e5ce855cc096a73f5dc900b0ee1a5a6eb07d11bc3af2591d0b2b7a7ee530902cc3cc14e52f060b7adfbdb2302b97b4415e8610ae0'
        'e4fd83e38703d8ff9a714b238296e4e12791b8ce0c19446709e5c20b4012ed10b97504cafda66cdbe12e56a9f943c094147b6a02db3bd9c854e609adca38ff01'
        '767f334add0dd0e83b27e198415a2ef8ae3c505aa7f37d12138e37a98dc5575e43f83eb665431464ed488a5bf6b8913cd675eb112dc880cf17ff51511a3f07e6'
        'a77f7e0d75b89f2e92e98b9352c7988e77a497d96b0f7c66f69e1939967305eb3b3d36fbdc6ba5eb37d5380edfa07e81bd11f10fd735565b28ff5b8aab767675'
        'a29664104e1ee73ca0aee1d633e9095d92a57c92787f8d8740bdb7211ba3205782ed8677f539bdb8cae3dd75a3694be3132e185fa3fc4b3f401e1f88eb776101')
validpgpkeys=('541EA0448453394FF77A0ECC9D9B2BA061D0A67C') # Abhilash Raj <raj.abhilash1@gmail.com>

pkgver() {
  cd $pkgname
  git describe | sed 's/\([^-]*-g\)/r\1/;s/v//g;s/-/./g'
}

prepare() {
  # setting FHS compliant default paths, remove debug options
  patch -d $pkgname -p1 -i ../$pkgname-1.3.5-settings.patch

  # Temporary patch to fix compatibility with `python-mistune` >=3.1
  # See https://gitlab.com/mailman/hyperkitty/-/commit/dd3ba04a2fc1fa6a9494d15e08a13693b1417270
  # Can be dropped at next upstream release
  patch -d $pkgname -p1 -i ../fix_compatibility_with_mistune_3.1.x.patch

  touch $pkgname/settings_local.py
}

build() {
  cd $pkgname
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    # we don't care about variations in help output
    --deselect hyperkitty/tests/commands/test_attachments_to_file.py::CommandTestCase::test_help_output
  )

  cd $pkgname
  pytest "${pytest_options[@]}"
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  # remove example_project from top level site-packages:
  # https://gitlab.com/mailman/hyperkitty/issues/279
  rm -rfv "$pkgdir/$site_packages/example_project/"
  # remove tests
  rm -rfv "$pkgdir/$site_packages/$pkgname/tests/"

  # doc
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname"
  # django project
  install -vDm 644 example_project/{__init__,manage,settings,wsgi}.py -t "$pkgdir/usr/share/webapps/$pkgname"
  # symlink locale and static dirs to state dir
  ln -svf /var/lib/$pkgname/locale/ "$pkgdir/usr/share/webapps/$pkgname"
  ln -svf /var/lib/$pkgname/static/ "$pkgdir/usr/share/webapps/$pkgname"
  # state dir
  install -vdm 750 "$pkgdir/var/lib/$pkgname/data"
  install -vdm 755 "$pkgdir/var/lib/$pkgname/"{locale,static}
  # log dir
  install -vdm 750 "$pkgdir/var/log/$pkgname"
  # config
  install -vDm 640 settings_local.py example_project/urls.py -t "$pkgdir/etc/webapps/$pkgname"
  ln -svf /etc/webapps/$pkgname/settings_local.py "$pkgdir/usr/share/webapps/$pkgname/settings_local.py"
  ln -svf /etc/webapps/$pkgname/urls.py "$pkgdir/usr/share/webapps/$pkgname/urls.py"
  # systemd service
  # uwsgi
  install -vDm 644 ../$pkgname.uwsgi "$pkgdir/etc/uwsgi/$pkgname.ini"
  # tmpfiles.d
  install -vDm 644 ../$pkgname.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
  # sysusers.d
  install -vDm 644 ../$pkgname.sysusers "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  install -vDm 644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/0BSD.txt"
}
