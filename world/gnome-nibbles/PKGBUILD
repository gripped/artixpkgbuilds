# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-nibbles
pkgver=4.2.1
pkgrel=1
pkgdesc="Guide a worm around a maze"
url="https://wiki.gnome.org/Apps/Nibbles"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  glib2
  glibc
  gsound
  gtk4
  hicolor-icon-theme
  libadwaita
  libgee
  libgnome-games-support-2
  pango
)
makedepends=(
  appstream
  git
  gobject-introspection
  meson
  vala
  yelp-tools
)
groups=(gnome-extra)
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-nibbles.git?signed#tag=$pkgver"
)
b2sums=('25418d6f99cf35713b8e5bebe70757070c32bbdc12a751a361b02e4160b1f35d546c5c0b9a55452a3284de6d3f8a26f866f3e94a250b0d54b02606af1536bc15')
validpgpkeys=(
  9CE6D700965C6A1D6F7B4A1C18B04BB8340E3EC5 # Ben Corby <bcorby@new-ms.com>
)

prepare() {
  cd $pkgname
}

build() {
  # Unbreak vala checks in meson.build
  # Without this, the build fails to link against libadwaita
  export VALAFLAGS="$VALAFLAGS --Xcc=-Wno-error=incompatible-pointer-types"
  CFLAGS+=" -Wno-error=incompatible-pointer-types"

  # Suppress a lot of unimportant warnings
  CFLAGS+=" -Wno-unused"

  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
