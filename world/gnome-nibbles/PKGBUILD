# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-nibbles
pkgver=4.0.3
pkgrel=1
pkgdesc="Guide a worm around a maze"
url="https://wiki.gnome.org/Apps/Nibbles"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  gsound
  libgee
  libgnome-games-support-2
  gtk4
)
makedepends=(
  appstream
  git
  gobject-introspection
  meson
  vala
  yelp-tools
)
groups=(gnome-extra)
_commit=d41f6efa1b9191d07107e222511c6954620480f2  # tags/4.0.3^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-nibbles.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
