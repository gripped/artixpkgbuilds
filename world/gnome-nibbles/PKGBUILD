# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-nibbles
pkgver=4.2.0
pkgrel=1
pkgdesc="Guide a worm around a maze"
url="https://wiki.gnome.org/Apps/Nibbles"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  libgee
  libgnome-games-support-2
  pango
)
makedepends=(
  appstream
  git
  gobject-introspection
  meson
  vala
  yelp-tools
)
groups=(gnome-extra)
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-nibbles.git?signed#tag=$pkgver"
)
b2sums=('8c4345648321c9c3298e0c58c1aa4a714d5ef2a337fca987fcaef5663b693db3620e2704cdc4efe589393e80bf7ffc65eec477141d74c0be96c64a79d397c497')
validpgpkeys=(
  31F449AE968CBE194119A3AD1F1DC770CE79E68B # Ben Corby <bcorby@new-ms.com>
)

prepare() {
  cd $pkgname
}

build() {
  # Unbreak vala checks in meson.build
  # Without this, the build fails to link against libadwaita
  export VALAFLAGS="$VALAFLAGS --Xcc=-Wno-error=incompatible-pointer-types"
  CFLAGS+=" -Wno-error=incompatible-pointer-types"

  # Suppress a lot of unimportant warnings
  CFLAGS+=" -Wno-unused"

  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
