# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-build-environment
pkgver=1.13.1
pkgrel=1
pkgdesc='A nested hash data structure for controlling build environments'
arch=(any)
url='https://github.com/ioquatix/build-environment'
license=(MIT)
depends=(
  ruby
)
makedepends=(
  git
)
checkdepends=(
  ruby-covered
  ruby-sus
  ruby-decode
)
options=(!emptydirs)
source=(git+https://github.com/ioquatix/build-environment.git#tag=v$pkgver)
sha256sums=('51986e0605dd394d18a59abe67d4d92d7b4b84dd0d84053feb4a888311aca1c4')

prepare() {
  cd build-environment
  sed -r -e 's|~>|>=|g' -i build-environment.gemspec
  sed -i '/signing_key/d' build-environment.gemspec
  rm gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd build-environment
  gem build build-environment.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    build-environment-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd build-environment
  GEM_HOME="tmp_install/$_gemdir" sus
}

package() {
  cd build-environment
  cp -a tmp_install/* "$pkgdir"/
}
