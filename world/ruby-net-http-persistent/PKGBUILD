# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-net-http-persistent
pkgver=4.0.3
pkgrel=3
pkgdesc='Thread-safe persistent connections with Net::HTTP'
arch=(any)
url='https://github.com/drbrain/net-http-persistent'
license=(MIT)
depends=(ruby-connection_pool)
makedepends=(git)
checkdepends=(ruby-rake ruby-minitest)
options=(!emptydirs)
source=(git+https://github.com/drbrain/net-http-persistent.git#tag=v$pkgver)
sha256sums=('dcb9ca9efbb82fe47df32f885fcf08989033fce68cdd194b1963ed0e83778e46')

prepare() {
  cd net-http-persistent
  sed 's/~>/>=/' -i net-http-persistent.gemspec
  echo -e 'require "rake/testtask"\nRake::TestTask.new' > Rakefile
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd net-http-persistent
  gem build net-http-persistent.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    net-http-persistent-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd net-http-persistent
  GEM_HOME="tmp_install/$_gemdir" rake test
}

package() {
  cd net-http-persistent
  cp -a tmp_install/* "$pkgdir"/
}
