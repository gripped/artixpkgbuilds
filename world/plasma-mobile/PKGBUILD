# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Devin Lin <devin@kde.org>

pkgname=plasma-mobile
pkgver=6.7.1
_dirver=$(echo $pkgver | cut -d. -f1-3)
pkgrel=1
pkgdesc='Plasma shell for mobile devices'
arch=(x86_64)
url='https://plasma-mobile.org/'
license=(GPL-3.0-or-later)
depends=(
  bash
  bluez-qt
  glibc
  kauth
  kcmutils
  kconfig
  kcoreaddons
  kdbusaddons
  kdeclarative
  ki18n
  kio
  kirigami
  kirigami-addons
  kitemmodels
  kjobwidgets
  knewstuff
  knotifications
  kpackage
  kpipewire
  kservice
  ksvg
  kwayland
  kwin
  kwindowsystem
  layer-shell-qt
  libgcc
  libkscreen
  libplasma
  libstdc++
  milou
  modemmanager-qt
  modemmanager-qt
  networkmanager-qt
  plasma-keyboard
  plasma-nano
  plasma-nm
  plasma-pa
  plasma-workspace
  powerdevil
  qcoro
  qt6-5compat
  qt6-base
  qt6-declarative
  qt6-sensors
  solid
  libudev
  wayland
)
makedepends=(
  cmake
  extra-cmake-modules
  plasma-wayland-protocols
  vulkan-headers
)
optdepends=(
  'bluedevil: Bluetooth management'
  'kscreen: Screen management'
  'plasma-settings: System settings'
)
source=("https://download.kde.org/stable/plasma/$_dirver/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('3e29de1f06423b3364cb7550de37adf76621497df13a6abaf89a6647cb8f4bfe'
            'SKIP')
validpgpkeys=(
  E0A3EB202F8E57528E13E72FD7574483BB57B18D # Jonathan Esk-Riddell <jr@jriddell.org>
  0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D # Bhushan Shah <bshah@kde.org>
  D07BD8662C56CB291B316EB2F5675605C74E02CF # David Edmundson <davidedmundson@kde.org>
  1FA881591C26B276D7A5518EEAAF29B42A678C20 # Marco Martin <notmart@gmail.com>
)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -D CMAKE_INSTALL_LIBEXECDIR=lib
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

