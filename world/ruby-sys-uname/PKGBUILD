# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-sys-uname
pkgver=1.4.1
pkgrel=1
pkgdesc='An interface for gathering information about your current platform'
arch=(any)
url='https://github.com/djberg96/sys-uname'
license=(Apache)
depends=(
  ruby
  ruby-ffi
  ruby-memoist
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-rake
  ruby-rspec
)
options=(!emptydirs)
source=(https://github.com/djberg96/sys-uname/archive/sys-uname-$pkgver.tar.gz)
sha256sums=('a63f84d77ba26be4e8c0d8644ff69ea5016db62470cbb34e29c846b4f2b7a393')

prepare() {
  cd sys-uname-sys-uname-$pkgver
  sed -r 's|~>|>=|g' -i sys-uname.gemspec

  sed -r -i \
    -e '/[rR]ubo[cC]op/d' \
    Rakefile \
    sys-uname.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd sys-uname-sys-uname-$pkgver
  gem build sys-uname.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    sys-uname-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd sys-uname-sys-uname-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd sys-uname-sys-uname-$pkgver
  cp -a tmp_install/* "$pkgdir"/
}
