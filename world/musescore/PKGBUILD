# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Cristian Maureira <saint@archlinux.cl>
# Contributor: Dr.Egg <rwhite@archlinux.us>

pkgname=musescore
pkgver=4.6.5
pkgrel=1
pkgdesc='Create, play and print beautiful sheet music'
arch=(x86_64)
url=https://musescore.org/
license=(GPL-3.0-only)
groups=(pro-audio)
depends=(
  glibc
  flac
  harfbuzz
  hicolor-icon-theme
  libasound.so
  libfreetype.so
  libopusenc
  libsndfile.so
  libgcc
  libstdc++
  opus
  qt6-5compat
  qt6-base
  qt6-declarative
  qt6-networkauth
  qt6-scxml
  qt6-svg
  tinyxml2
  zlib
  zstd
)
makedepends=(
  cmake
  doxygen
  git
  lame
  ninja
  python
  qt6-tools
  vulkan-headers
)
optdepends=('lame: MP3 export')
options=(!lto)
source=("$pkgname::git+https://github.com/musescore/MuseScore.git#tag=v${pkgver}")
sha512sums=('4f1fa2fc8684662bfafbefbbf63519a3f8077c077e063f2144e95a1eeedf5c54a97013c01461f55474379a8d0bcffc71e54a6ab994c6f61cc48aee7b97e64352')
b2sums=('d8d43fe137d76b84caf36d9be81f11caeb1f925c4e81286b1f5b1b950ab4fceb8d91c8ae3380efd5cf906c898a41664f3985eebe092fe63280d1369b6d52cf9b')

build() {
  local cmake_options=(
    -B build
    -S "$pkgname"
    -G Ninja
    -W no-dev
    -D CMAKE_BUILD_TYPE=Release
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_C_FLAGS_RELEASE="$CFLAGS"
    -D CMAKE_CXX_FLAGS_RELEASE="$CXXFLAGS"
    -D CMAKE_POSITION_INDEPENDENT_CODE=ON
    -D CMAKE_SKIP_RPATH=ON
    -D MUSE_APP_BUILD_MODE=release
    -D MUSESCORE_BUILD_CONFIGURATION=app
    -D MUSESCORE_REVISION="$(git -C "$pkgname" rev-parse --short=7 HEAD)"
    -D MUE_COMPILE_USE_SYSTEM_FLAC=ON
    -D MUE_COMPILE_USE_SYSTEM_FREETYPE=ON
    -D MUE_COMPILE_USE_SYSTEM_HARFBUZZ=ON
    -D MUE_COMPILE_USE_SYSTEM_OPUS=ON
    -D MUE_COMPILE_USE_SYSTEM_OPUSENC=ON
    -D MUE_COMPILE_USE_SYSTEM_TINYXML=ON
    -D MUE_INSTALL_SOUNDFONT=ON
  )

  cmake "${cmake_options[@]}"

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

# vim: ts=2 sw=2 et:
