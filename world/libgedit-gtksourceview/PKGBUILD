# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Ionut Biru <ibiru@archlinux.org>


pkgname=libgedit-gtksourceview
pkgver=299.1.0
pkgrel=1
pkgdesc="A source code editing widget"
arch=(x86_64)
url="https://gedit-technology.github.io"
license=('LGPL-2.1-or-later')
depends=(
  cairo
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  libxml2
  pango
)
makedepends=(
  git
  gobject-introspection
  gtk-doc
  meson
)
checkdepends=(xorg-server-xvfb)
provides=(libgedit-gtksourceview-300.so)
_commit=b271a625ca057cc3fa7d5ac83285c5d4e038202e  # tags/299.1.0^0
source=("git+https://github.com/gedit-technology/libgedit-gtksourceview.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D gtk_doc=true
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
