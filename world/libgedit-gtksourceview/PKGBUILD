# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=libgedit-gtksourceview
pkgver=299.6.0
pkgrel=1
pkgdesc='Source code editing widget library'
arch=(x86_64)
url='https://gitlab.gnome.org/World/gedit/libgedit-gtksourceview'
license=(LGPL-2.1-or-later)
depends=(
  cairo
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  libxml2
  pango
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  meson
)
checkdepends=(xorg-server-xvfb)
provides=(libgedit-gtksourceview-300.so)
source=("git+https://gitlab.gnome.org/World/gedit/libgedit-gtksourceview.git#tag=${pkgver/[a-z]/.&}")
b2sums=(fdd041bceb9ffbefe2e48df47048a4726f60c1f9897f12029b1725b4c7446baab151bb0c6ef0a4d47cd570b41bc5c8b0dfa2252942180c7c3d0ab6d5f226ddbe)
validpgpkeys=(08C0A6B1A08310A45363B328426F014A261FD87F) # Sébastien Wilmet <swilmet@mailfence.com>

build() {
  artix-meson $pkgname build \
    -D gtk_doc=true
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
