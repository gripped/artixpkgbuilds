# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Ionut Biru <ibiru@archlinux.org>


pkgname=libgedit-gtksourceview
pkgver=299.4.0
pkgrel=1
pkgdesc="A source code editing widget"
arch=(x86_64)
url="https://gedit-technology.github.io"
license=('LGPL-2.1-or-later')
depends=(
  cairo
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  libxml2
  pango
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  meson
)
checkdepends=(xorg-server-xvfb)
provides=(libgedit-gtksourceview-300.so)
source=("git+https://gitlab.gnome.org/World/gedit/libgedit-gtksourceview.git#tag=${pkgver/[a-z]/.&}")
b2sums=('211accdf3f9ae35516a129f58e22ae121e562bbe745c009561442b512edf92a1bb76ed3915fe42939e45474486f06922887d9bb41d365393b5e88b8a116313e5')

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D gtk_doc=true
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
