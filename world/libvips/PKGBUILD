# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=libvips
pkgver=8.17.3
pkgrel=1
pkgdesc="A fast image processing library with low memory needs"
arch=('x86_64')
license=('LGPL-2.1-or-later')
url="https://libvips.github.io/libvips/"
depends=('cfitsio' 'fftw' 'libexif' 'libarchive' 'libimagequant' 'librsvg' 'libwebp' 'openexr'
         'highway' 'pango' 'libcgif' 'cairo' 'lcms2' 'openjpeg2')
makedepends=('gobject-introspection' 'libheif' 'libjxl' 'imagemagick' 'openslide'
             'poppler-glib' 'meson' 'gi-docgen' 'glib2-devel')
optdepends=('libheif: for heif module'
            'imagemagick: for magick module'
            'openslide: for openslide module'
            'poppler-glib: for poppler module'
            'python: for vipsprofile'
            'libjxl: for jxl module')
checkdepends=('python-pytest' 'python-pyvips')
source=("https://github.com/libvips/libvips/releases/download/v$pkgver/vips-$pkgver.tar.xz")
sha512sums=('31467a4d9b33c00f41aec05de2ca2dac328199eeb25dbfd2ee085fc45e8abf3903d50b32d65a8581647e4a83cfe2d351054ab00e556bad610afacbf24e19010f')

build() {
  meson build vips-$pkgver \
    --prefix=/usr \
    -Ddocs=true
  meson compile -C build
}

check() {
  export LD_LIBRARY_PATH="$srcdir/build/libvips"
  meson test -C build
  pytest -k "not test_text" -sv --log-cli-level=WARNING vips-$pkgver/test/test-suite
}

package() {
  meson install -C build --destdir="$pkgdir"
}
