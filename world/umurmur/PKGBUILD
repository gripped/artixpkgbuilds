# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Robert Knauer <robert@privatdemail.net>
# Contributor: xav <xav at ethertricks dot net>

pkgname=umurmur
pkgver=0.3.0
pkgrel=1
pkgdesc='Minimalistic Mumble server'
url="https://github.com/umurmur/umurmur"
arch=(x86_64)
license=(BSD-3-Clause)
depends=(
  glibc
  openssl
)
makedepends=(
  cmake
  libconfig
  protobuf-c
)
backup=(etc/umurmur/umurmur.conf)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz
  $pkgname-0.3.0-config-namespace.patch::https://github.com/umurmur/umurmur/commit/def963eebc81a977992acb609d6bc4c41ce98ad2.patch
  umurmur.sysusers
  umurmur.tmpfiles
)
sha512sums=('73ecfe5e1187efb19e09fa1b30de5dd1003e9b97634d0e3d7d02bed5d611c8838d35ad9993d469603fa54e304219baa314a90c7c48dad1004c301284ed3bcabb'
            'f18b5509e28b79ca7cc81425466fe9a483a3a644109e5bcb4aa01117649a0da355bc34cbf70610914d0b46092e3b0904f3b52a46f7363e51dc68a2a7cf37fef9'
            'd84950a32ab8a2e84f5fe333cd2894e52aba624531644d106c982aa4ff04271d318543398fa7f48c719f26338679fa971bb5332472e9040ac9aa8a9b4a1f2832'
            'b49b65a1e87cba9d8b453dee23f9f1e89d9eeb326e9ce98a32605ac62f72d36c1efbca70ed1a87efe9294ef137d3f673429cd70b5051e03e0e85db310943c39e')
b2sums=('2aa316670eb91096d5668fc786025752032b9b8618e67a8e566c05d7312976397e2a2b9682dae8739c048db1cb4da0850b13530dcd25a69b19d576de19073194'
        '77784dbc65eb7b5e238ae0888387f88d5c26d0402e5d6b6fd03e312339d68be05945c9ddf934a15a2c0643f685a01a7bbc12931e574b2adbf3a4422329a18101'
        '549dda6277c3758d221a259d08d3f91658d7615b0c06ebf2af6f3966fd798ce6228ff9ccb653daeb1d2b592e029e96e756df779ad0d4a809e224f2071e5d76cc'
        '935f8ea09c8cbb32dc508959181b707bfadd564c038b2c7a4ce213372242cdefaf75c7f3d76644f28246b666c93a0d89c5b4741f12f63261f19b99d8c0603219')

prepare() {
  # https://github.com/umurmur/umurmur/pull/170
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-0.3.0-config-namespace.patch
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  depends+=(
    libconfig libconfig.so
    protobuf-c libprotobuf-c.so
  )

  DESTDIR="$pkgdir" cmake --install build

  install -vdm 750 "$pkgdir/etc/$pkgname/"
  install -vDm 644 $pkgname.sysusers "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  install -vDm 644 $pkgname.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"

  cd $pkgname-$pkgver
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {AUTHORS,ChangeLog,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}

# vim: ts=2 sw=2 et:
