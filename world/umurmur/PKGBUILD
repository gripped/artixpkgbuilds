# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Robert Knauer <robert@privatdemail.net>
# Contributor: xav <xav at ethertricks dot net>

pkgname=umurmur
pkgver=0.4.1
pkgrel=1
pkgdesc='Minimalistic Mumble server'
url="https://github.com/umurmur/umurmur"
arch=(x86_64)
license=(BSD-3-Clause)
depends=(
  glibc
  openssl
)
makedepends=(
  cmake
  libconfig
  protobuf-c
)
backup=(etc/umurmur/umurmur.conf)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
  umurmur.sysusers
  umurmur.tmpfiles
)
sha512sums=('12fc24866cffe337cc0ce78fbae3723946016f392fcea3971047d40813deb405249f9a745e26403638d40e9ff6803bfdee3ff7522d26b5a6bc3a11388395618d'
            '0b965915585d03e9b461adfa1f25c655e470c53ca043f91a55267f11bc00cac34f5f3a206097a100eea92e48a71672a2ce2eb06c7af38827b373233dc261c426'
            'b49b65a1e87cba9d8b453dee23f9f1e89d9eeb326e9ce98a32605ac62f72d36c1efbca70ed1a87efe9294ef137d3f673429cd70b5051e03e0e85db310943c39e')
b2sums=('3947947dee53f68254f48a1e9991f56f5948052f075f38affdad681c2632f0dbfa322ba927a67e7c70fa0e6f52854e85a856a9518b5b1268cc1eeb141e438ac8'
        '9963646f3020b35e0064c8334fef40b59efa38d335a117b2e5095dc64e5331323ce3ad3489afd88661fb1500fe0d276f957dcb9954e5470fe4c18764648bf780'
        '935f8ea09c8cbb32dc508959181b707bfadd564c038b2c7a4ce213372242cdefaf75c7f3d76644f28246b666c93a0d89c5b4741f12f63261f19b99d8c0603219')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  depends+=(
    libconfig libconfig.so
    protobuf-c libprotobuf-c.so
  )

  DESTDIR="$pkgdir" cmake --install build

  install -vdm 750 "$pkgdir/etc/$pkgname/"  # ensure the mode matches our tmpfiles.d integration
  install -vDm 644 $pkgname.sysusers "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  install -vDm 644 $pkgname.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"

  cd $pkgname-$pkgver
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {AUTHORS,{CHANGELOG,README}.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}

# vim: ts=2 sw=2 et:
