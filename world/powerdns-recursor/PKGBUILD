# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Remi Gacogne <rgacogne[at]archlinux[dot]org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Jan Steffens <heftig@archlinux.org>

pkgname=powerdns-recursor
pkgver=5.3.0
pkgrel=1
pkgdesc='Resolving DNS server'
url='https://www.powerdns.com/'
arch=('x86_64')
license=('GPL-2.0-only')
depends=('gcc-libs' 'boost-libs' 'libcap' 'libcurl.so' 'libsodium' 'esysusers' 'luajit' 'openssl'
         'libboost_context.so' 'fstrm' 'net-snmp')
makedepends=('boost' 'meson' 'pandoc' 'python' 'python-yaml' 'ragel' 'rust' )
options=(!lto)
provides=('pdns-recursor')
conflicts=('pdns-recursor')
backup=('etc/powerdns/recursor.conf')
source=(https://downloads.powerdns.com/releases/pdns-recursor-${pkgver}.tar.xz{,.asc}
        sysusers.conf)
sha512sums=('014799c024dafc98f09c987661b5e6c547074245a9fb60bc55448ea1f7c4e52547280ef80ccda4804e8e7eaba6c72801c9e7f3007219195edc1aad53056c10ce'
            'SKIP'
            '7aada0f186bc1cd6af9a4af598511e7f517f0c103973397d260796c8e83c789d720990579d6ccba53e4aa4a8a6c5b2107f4d32d09ce9a72118ae5030b03bb6ab')
validpgpkeys=('FBAE0323821C7706A5CA151BDCF513FA7EED19F3'  # Peter van Dijk <peter.van.dijk@powerdns.com>
              '16E12866B7738C73976A57436FFC33439B0D04DF') # Winkels, Erik <erik.winkels@open-xchange.com>

build() {
  meson setup "pdns-recursor-${pkgver}" build \
    --prefix        /usr \
    --libexecdir    lib \
    --sbindir       bin \
    --sysconfdir    /etc/powerdns \
    --auto-features enabled \
    --wrap-mode     nodownload \
    -Ddebug=true \
    -Dhardening-fortify-source=disabled \
    -Doptimization=2 \
    -Db_lto=true \
    -Db_pie=true \
    -Dauto-var-init=zero \
    -Ddns-over-tls=enabled \
    -Dreproducible=true \
    -Dsigners-libsodium=enabled \
    -Dsnmp=enabled \
    -Dsystemd-service=enabled \
    -Dunit-tests=true \
    -Dsigners-libcrypto-path=/usr # workaround for https://github.com/PowerDNS/pdns/issues/14084
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir "$pkgdir"

  mv "${pkgdir}/etc/powerdns/recursor.yml-dist" "${pkgdir}/etc/powerdns/recursor.conf"
  install -Dm 644 "${srcdir}/sysusers.conf" "${pkgdir}/usr/lib/sysusers.d/powerdns-recursor.conf"
}

# vim: ts=2 sw=2 et:
