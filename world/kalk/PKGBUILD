# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=kalk
pkgver=24.12.1
pkgrel=3
pkgdesc='A powerful cross-platform calculator application built with the Kirigami framework'
url='https://apps.kde.org/kalk/'
arch=(x86_64)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kconfig
         kcoreaddons
         ki18n
         kirigami
         kirigami-addons
         kunitconversion
         libqalculate
         qqc2-desktop-style
         qt6-base
         qt6-declarative)
makedepends=(extra-cmake-modules)
groups=(kde-applications
        kde-utilities)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/utilities/kalk/-/commit/952c54d0.patch)
sha256sums=('f329050da30a35816928d90a9b505742989b23713a6b37cadcdc886401edc05c'
            'SKIP'
            '4c0874e49ca7e525b2f838777279a3438ec4a8d34859f0df90b16a54197e04cd')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < 952c54d0.patch # Fix history display
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
