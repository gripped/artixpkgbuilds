# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=eog
pkgname=(
  eog
  eog-docs
)
pkgver=50.0
pkgrel=1
pkgdesc="Eye of Gnome: An image viewing and cataloging program"
url="https://gitlab.gnome.org/GNOME/eog"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  exempi
  gdk-pixbuf2
  glib2
  glibc
  gnome-desktop
  gtk3
  hicolor-icon-theme
  lcms2
  libexif
  libgcc
  libhandy
  libjpeg-turbo
  libpeas
  librsvg
  libx11
  zlib
)
makedepends=(
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  meson
  yelp-tools
)
source=(
  "git+https://gitlab.gnome.org/GNOME/eog.git#tag=${pkgver/[a-z]/.&}"
)
b2sums=('d0ef4a310b69b40cdb7ef2d4442711d4ae65eb60ec7ae3a00090f9e32624c2cf87fa3a195c6df8357745be1ec0e48d385f7ab182507ef939230ee5c90032dbb1')

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D gtk_doc=true
    -D libportal=false
  )

  artix-meson eog build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_eog() {
  optdepends=('eog-plugins: Additional features')

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/gtk-doc
}

package_eog-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
