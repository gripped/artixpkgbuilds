# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=eog
pkgname=(
  eog
  eog-docs
)
pkgver=49.2
pkgrel=1
pkgdesc="Eye of Gnome: An image viewing and cataloging program"
url="https://wiki.gnome.org/Apps/EyeOfGnome"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  exempi
  gdk-pixbuf2
  glib2
  glibc
  gnome-desktop
  gtk3
  hicolor-icon-theme
  lcms2
  libexif
  libgcc
  libgirepository
  libhandy
  libjpeg-turbo
  libpeas
  librsvg
  libx11
  zlib
)
makedepends=(
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  meson
  yelp-tools
)
source=(
  "git+https://gitlab.gnome.org/GNOME/eog.git#tag=${pkgver/[a-z]/.&}"
)
b2sums=('fb0d3379c57ec9ef03827e5cad4c3169a5a187615b9d6e0e86f174df3e0794d15986fb9a83d18d180bb87eb0ff7918285f4f964963b6905ea682b953d0d738cf')

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D gtk_doc=true
    -D libportal=false
  )

  artix-meson eog build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_eog() {
  optdepends=('eog-plugins: Additional features')

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/gtk-doc
}

package_eog-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
