# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=eog
pkgname=(
  eog
  eog-docs
)
pkgver=50.1
pkgrel=1
pkgdesc="Eye of Gnome: An image viewing and cataloging program"
url="https://gitlab.gnome.org/GNOME/eog"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  exempi
  gdk-pixbuf2
  glib2
  glibc
  gnome-desktop
  gtk3
  hicolor-icon-theme
  lcms2
  libexif
  libgcc
  libhandy
  libjpeg-turbo
  libpeas
  librsvg
  libx11
  zlib
)
makedepends=(
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  meson
  yelp-tools
)
source=(
  "git+https://gitlab.gnome.org/GNOME/eog.git#tag=${pkgver/[a-z]/.&}"
)
b2sums=('25bc003422f201c96a9238103b807fc14c82ff5ac79149235ea279a870d16b2b13f3c3f864420ed758a0774a12a68fa140a2f4463460287a186a49bdc6c7af2a')

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D gtk_doc=true
    -D libportal=false
  )

  artix-meson eog build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_eog() {
  optdepends=('eog-plugins: Additional features')

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/gtk-doc
}

package_eog-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
