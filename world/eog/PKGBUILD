# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=eog
pkgname=(
  eog
  eog-docs
)
pkgver=47.0
pkgrel=4
pkgdesc="Eye of Gnome: An image viewing and cataloging program"
url="https://wiki.gnome.org/Apps/EyeOfGnome"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  exempi
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gnome-desktop
  gtk3
  hicolor-icon-theme
  lcms2
  libexif
  libgirepository
  libhandy
  libjpeg-turbo
  libpeas
  librsvg
  libx11
  zlib
)
makedepends=(
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  meson
  yelp-tools
)
source=("git+https://gitlab.gnome.org/GNOME/eog.git#tag=${pkgver/[a-z]/.&}"
        eog-prgname.patch)
b2sums=('b6c195c0590054abc21bd004b4f97b23d30cc689534e4e6feeb37c2123dd15eae9ab180898bdf37c2843ac427d119e827bf190e467d1462e969d3c8c3f07a69e'
        '789ff87c7c6975b3e5ed8ebb5af8d141d562810d2143ee5edf2851d563763b43abd26c7cf67ae4463f9205a353d94401ab262844cfeced50a96ac64eabd49cf7')

prepare() {
  cd $pkgname

  # Set prgname to application ID
  # https://gitlab.gnome.org/GNOME/eog/-/merge_requests/167
  git apply -3 ../eog-prgname.patch
}

build() {
  local meson_options=(
    -D gtk_doc=true
    -D libportal=false
  )

  artix-meson eog build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_eog() {
  optdepends=('eog-plugins: Additional features')

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/gtk-doc
}

package_eog-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
