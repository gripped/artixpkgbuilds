# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>

pkgname=matrix-synapse
pkgver=1.144.0
pkgrel=3
pkgdesc="Matrix reference homeserver"
url="https://github.com/element-hq/synapse"
arch=('x86_64')
license=('AGPL-3.0-or-later')
depends=('gcc-libs' 'glibc' 'libwebp' 'python' 'python-ijson' 'python-jsonschema' 'python-twisted'
         'python-pyopenssl' 'python-yaml' 'python-pyasn1' 'python-pynacl'
         'python-bcrypt' 'python-immutabledict'
         'python-pillow' 'python-pysaml2' 'python-pydantic'
         'python-unpaddedbase64' 'python-canonicaljson'
         'python-signedjson' 'python-pymacaroons'
         'python-service-identity' 'python-msgpack'
         'python-phonenumbers' 'python-prometheus_client'
         'python-attrs' 'python-netaddr' 'python-sortedcontainers'
         'python-treq' 'python-idna' 'python-jinja' 'python-matrix-common'
         'python-bleach' 'python-typing_extensions' 'python-python-multipart')
makedepends=(git python-build python-installer python-wheel python-poetry-core python-setuptools-rust)
checkdepends=('python-pip' 'python-authlib' 'python-pyjwt' 'python-lxml' 'python-parameterized'
              'python-txredisapi' 'python-hiredis' 'postgresql' 'python-pyicu')
optdepends=('perl: sync_room_to_group.pl'
            'python-psycopg2: PostgreSQL support'
            'python-lxml: URL previewing'
            'python-psutil: metrics'
            'python-pyjwt: jwt'
            'python-txredisapi: redis'
            'python-hiredis'
            'python-pyicu: Improve user search for international display names'
            'python-authlib: OpenID SSO support')
options=(!lto)
source=("$pkgname::git+https://github.com/element-hq/synapse.git#tag=v$pkgver"
        "$pkgname-fix-worker-test-race.patch"
        'generic_worker.yaml.example'
        'sysusers-synapse.conf'
        'tmpfiles-synapse.conf'
        )
sha256sums=('01eaa5afce4e4e0006206f7f754c53cdd1e6b67a6e599685c8ce5a4ecbecaca7'
            '751d0cb122b3c17cb4a2142461ac99bbaec798f1e4b357dd41ec4041e5623d35'
            'f67334856609997eac26939d77cfc520e78e98d3755543ab730d83a0f362a35e'
            '574175c27a4f07d4ce6a676d86b697c82c36b796442d0955881da588b6f4bb65'
            '65588c8c64dfb84cab831cd8d028a295d753cf7322dd63053e8488466047b45f')
backup=('etc/synapse/log_config.yaml')
install=synapse.install
validpgpkeys=('02450A9EDDFEE3E0C730B786A7E4A57880C3A4A9'
              '053191DFF4670330465227F7A542E4ED1B0FAC09'
              '283F86EA415D64E7D98E085BD5804497C6468FC1'
              '58C4E75BC67C92169A7FDD11FBCE0ACE0732186F'
              '9323BC4F687435CA8D0F03CB922F57ACB93AABF9'
              '93B2970FB2FD8855AD6E0229CB2B33F7C23D44C6'
              'D79D3CA0B61429A8A760525A903ECE108A39DEDD'
              'F124520CEEE062448FE1C8442D2EFA2F32FBE047'
              '177B595E4DFCB510C556750833FC58F6A7113048')

prepare() {
	cd $pkgname

	# Required to make synapse work with lxml 6.0
	git cherry-pick --no-commit 4555f61db5d6caf3cbbe03dfd3051e72c1b9c221

	# allow any poetry-core to be used
	sed 's/poetry-core>=2.0.0,<=2.1.3/poetry-core>=1.1.0/' -i pyproject.toml
	sed 's/setuptools_rust>=1.3,<=1.11.1/setuptools_rust>=1.3.0/' -i pyproject.toml

  patch -Np1 < ../$pkgname-fix-worker-test-race.patch
}

build() {
	cd $pkgname
	python -m build --wheel --no-isolation
}

check() {
	cd $pkgname
	local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
	python -m venv --system-site-packages test-env
	test-env/bin/python -m installer dist/*.whl
	pushd build/lib.linux-$CARCH-cpython-${python_version}
	ln -sv ../../tests .
	PYTHONPATH="$PWD" PATH="../../test-env/bin:$PATH" ../../test-env/bin/python -m twisted.trial -j$(nproc) tests
	rm -r tests _trial_temp
	popd
}

package() {
	cd $pkgname
	python -m installer --destdir="$pkgdir" dist/*.whl

	install -vdm755 -o 198 -g 198 "$pkgdir"/etc/synapse
	install -vDm644 contrib/systemd/log_config.yaml "$pkgdir"/etc/synapse/log_config.yaml
	install -vDm644 "$srcdir"/generic_worker.yaml.example "$pkgdir"/etc/synapse/workers/generic_worker.yaml.example

	install -vDm644 "$srcdir"/sysusers-synapse.conf "$pkgdir"/usr/lib/sysusers.d/synapse.conf
	install -vDm644 "$srcdir"/tmpfiles-synapse.conf "$pkgdir"/usr/lib/tmpfiles.d/synapse.conf
}
