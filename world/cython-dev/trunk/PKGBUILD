# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=cython-dev
pkgver=3.0.0rc2
pkgrel=1
pkgdesc='C-Extensions for Python (development version)'
arch=('x86_64')
url='https://cython.org'
license=('Apache')
depends=('python')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
provides=('cython')
conflicts=('cython')
_commit='8edd90a951029f7c3297e2e2beeab39166cb7760'
source=("$pkgname::git+https://github.com/cython/cython#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  for f in cygdb cython cythonize; do
    mv "$pkgdir"/usr/bin/$f "$pkgdir"/usr/bin/${f}3
    ln -s ${f}3 "$pkgdir"/usr/bin/$f
  done
}
