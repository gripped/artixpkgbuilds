# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=swell-foop
pkgver=48.1
pkgrel=2
pkgdesc="Clear the screen by removing groups of colored and shaped tiles"
url="https://gitlab.gnome.org/GNOME/swell-foop"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  libgee
  libgnome-games-support-2
  librsvg
  pango
)
makedepends=(
  appstream
  git
  meson
  vala
  yelp-tools
)
groups=(gnome-extra)
source=("git+$url.git#tag=${pkgver/[a-z]/.&}")
b2sums=('2a5d99370352e19a01b7ee69888490a4549bd237c90697f84be26eba531d160bc47d06f4e911c1eaa6bb5d88acd5ed44f33d8fced3f85a03f0a4ad0782af2390')

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
