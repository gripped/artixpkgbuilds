# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=swell-foop
pkgver=48.0
pkgrel=1
pkgdesc="Clear the screen by removing groups of colored and shaped tiles"
url="https://gitlab.gnome.org/GNOME/swell-foop"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  libgee
  libgnome-games-support-2
  librsvg
  pango
)
makedepends=(
  appstream
  git
  meson
  vala
  yelp-tools
)
source=("git+$url.git#tag=${pkgver/[a-z]/.&}")
b2sums=('e02c1a081338dee07e6ce3858e20a7047b5aa3daef3e7216f35504a9ce933c24d262c6ec5dd4ba89627adfdd0cca0fe688590794e579c9ed975862dbb3dc9ddc')

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
