# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=swell-foop
pkgver=46.0
pkgrel=2
pkgdesc="Clear the screen by removing groups of colored and shaped tiles"
url="https://gitlab.gnome.org/GNOME/swell-foop"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libgee
  libgnome-games-support-2
  librsvg
  pango
)
makedepends=(
  git
  meson
  vala
  yelp-tools
)
source=("git+$url.git#tag=${pkgver/[a-z]/.&}")
b2sums=('b455f01a5dd00d2a59bf4b617cf4fdc9af439efe761e7287ac079003d3ac6a98e77d0de98b0358c0b41a1e046857da6e09e46fb7aea6d4a0e25406fd580dc3be')

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
