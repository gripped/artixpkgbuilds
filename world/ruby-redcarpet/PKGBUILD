# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-redcarpet
pkgver=3.6.1
pkgrel=1
pkgdesc='A fast, safe and extensible Markdown to (X)HTML parser'
arch=(x86_64)
url='https://github.com/vmg/redcarpet'
license=(MIT)
depends=(
  ruby
)
makedepends=(
  git
  ruby-rake
  ruby-rake-compiler
  ruby-rdoc
  ruby-test-unit
)
checkdepends=(
  ruby-bundler
  tidy
)
options=(!emptydirs)
source=(git+https://github.com/vmg/redcarpet.git#tag=v$pkgver)
sha256sums=('cf924803dcc2571e4a31ad684a5a290ef5e29e46fdaf291fe6caeadcacc6c440')

prepare() {
  cd redcarpet
  sed -i 's|~>|>=|' redcarpet.gemspec
  sed -i -e "/task 'test:unit' => :compile/d" -e 's/=> :compile//' Rakefile
  echo gemspec > Gemfile
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd redcarpet
  gem build redcarpet.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    redcarpet-$pkgver.gem

  # remove unreproducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd redcarpet
  GEM_HOME="$PWD/tmp_install/$_gemdir" rake test
}

package() {
  cd redcarpet
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
