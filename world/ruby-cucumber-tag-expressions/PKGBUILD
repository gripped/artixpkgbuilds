# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bert Peters <bertptrs@archlinux.org>

pkgname=ruby-cucumber-tag-expressions
pkgver=10.0.0
pkgrel=1
pkgdesc="Cucumber tag expressions for ruby"
arch=(any)
url='https://github.com/cucumber/tag-expressions'
license=(MIT)
depends=(
  ruby
)
makedepends=(
  ruby-rake
  ruby-rspec
)
checkdepends=(
  ruby-bundler
)
options=(!emptydirs)
source=(https://github.com/cucumber/tag-expressions/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('1c2229ee082ab3b4d4df9507aeb6bf84a9c859c6aac4b4a141f700c78c6663c6')

build() {
  local _gemdir="$(gem env gemdir)"
  cd tag-expressions-$pkgver/ruby
  gem build cucumber-tag-expressions.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    cucumber-tag-expressions-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd tag-expressions-$pkgver/ruby
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd tag-expressions-$pkgver/ruby
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 ../LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
