# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=liburing
pkgver=2.4
_commit=298c083d75ecde5a8833366167b3b6abff0c8d39  # refs/tags/liburing-2.4
pkgrel=1
pkgdesc="Linux-native io_uring I/O access library"
arch=(x86_64)
url="https://git.kernel.dk/cgit/liburing/"
license=(
  LGPL2.1
  MIT
)
depends=(glibc)
makedepends=(git)
provides=(
  liburing.so
  liburing-ffi.so
)
source=(git+https://git.kernel.dk/liburing#tag=$_commit)
sha512sums=('SKIP')
validpgpkeys=(C0FC392DCADE26D975FA5E4AF7D358FB2971E0A6)  # Jens Axboe <axboe@kernel.dk>

build() {
  local configure_options=(
    --prefix=/usr
    --mandir=/usr/share/man
  )

  cd $pkgname
  ./configure "${configure_options[@]}"
  make
}

package() {
  make DESTDIR="$pkgdir" install -C $pkgname
  install -vDm 644 $pkgname/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
