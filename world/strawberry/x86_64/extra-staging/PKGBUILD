# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Fabio 'Lolix' Loli <lolix@disroot.org> -> https://github.com/FabioLolix

pkgname=strawberry
pkgver=1.0.17
pkgrel=4
pkgdesc='A music player aimed at audio enthusiasts and music collectors'
arch=(x86_64)
url='https://www.strawbs.org/'
license=(GPL3)
depends=(qt6-base chromaprint protobuf gst-plugins-base gst-plugins-good udisks2 libcdio libgpod libmtp fftw)
makedepends=(cmake boost qt6-tools)
optdepends=('gst-libav: additional codecs'
            'gst-plugins-bad: additional codecs'
            'gst-plugins-ugly: additional codecs')
source=(https://github.com/strawberrymusicplayer/strawberry/releases/download/$pkgver/$pkgname-$pkgver.tar.xz
        https://github.com/strawberrymusicplayer/strawberry/commit/58278ab1.patch
        https://github.com/strawberrymusicplayer/strawberry/commit/b52ffd09.patch)
sha256sums=('d5a4af4202813f5b5eb81c202ba632f964493cc381730e15ee3f6106321a714a'
            '3ea7459c53c7eb5c76d1cc00de42807adf8d1da04355dca17ff2b451eb4b6a6e'
            '94879d430714f6e89a2a8739de7d4bfb9eae8a524d00e7fa10d40e3db72eea53')

prepare() {
  patch -d $pkgname-$pkgver -p1 < 58278ab1.patch # Fix build with protobuf 23
  patch -d $pkgname-$pkgver -p1 < b52ffd09.patch # Fix build with protobuf 23
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_WITH_QT6=ON \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS -DQT_NO_DEBUG_OUTPUT"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
