# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>

pkgbase=podman
pkgname=(podman podman-docker)
pkgver=5.8.3
pkgrel=1
pkgdesc='Tool and library for running OCI-based containers in pods'
arch=(x86_64)
url='https://github.com/containers/podman'
license=(Apache-2.0)
makedepends=(
  apparmor
  btrfs-progs
  catatonit
  containers-common
  git
  go
  go-md2man
  gpgme
  libseccomp
  man-db
  shadow
  sqlite
  udev
)
# https://github.com/containers/podman/issues/13297
options=(!lto)
source=(
  "git+$url?signed#tag=v$pkgver"
)
# See the release-keys repository
# https://github.com/containers/release-keys
validpgpkeys=(
  B7DBDCA456F7335E91F1C25CD3624C551D0515C4  # Matthew Heon <mheon@redhat.com>
  7CE1E6F8C90CB53E7E4D8F2D502E08DB0BBF8EEE  # Ashley Cui <acui@redhat.com>
  9E33DD8704CC03E2DEB84D9A1C1EDD7CC7C3A0DD  # Lokesh Mandvekar <lsm5@redhat.com>
)
sha512sums=('294f91005d1a7f52f8696ed7fb613862e0c526749289f6257e82b7df0bf74267c2857ab0b94e5cfa58ac153517e175c1fed87860f249fafea1de395d296cebac')
b2sums=('c2cdeb522557855d19d8a6a90b644372acc25a1cc1c7f8b5939ec0f2facc847cc43363e01a149f24ad725d6bb7065340cb5c116830cf36e2e347aa1cdd73d018')

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie"
  export GOPATH="${srcdir}"

  make EXTRA_LDFLAGS='-compressdwarf=false -linkmode=external' PREFIX=/usr -C $pkgbase
  make docker-docs -C $pkgbase
}

check() {
  local container_libs_version="$(</usr/share/containers/container-libs.version)"
  local project_container_libs_version="$(sed --regexp-extended --quiet 's|.*go.podman.io/common v(.*)|\1|p' "$pkgbase/go.mod")"

  printf '%s %s (container-libs %s) - system container-libs %s\n' "$pkgbase" "$pkgver" "${project_container_libs_version%.*}" "${container_libs_version%.*}"

  if (( $(vercmp "${container_libs_version%.*}" "${project_container_libs_version%.*}") != 0 )); then
    printf '%s %s requires container-libs/common in version %s but we have version %s\n' "$pkgbase" "$pkgver" "${project_container_libs_version%.*}" "${container_libs_version%.*}"
    exit 1
  fi
}

package_podman() {
  depends=(
    catatonit
    conmon
    containers-common
    oci-runtime
    glibc
    iptables
    gpgme libgpgme.so
    libgcc
    libseccomp libseccomp.so
    passt
    shadow
    sqlite
  )
  optdepends=(
    'apparmor: for AppArmor support'
    'btrfs-progs: support btrfs backend devices'
    'fuse-overlayfs: for deprecated storage driver in rootless environment'
    'podlet:  Generate Podman Quadlet files from a Podman command, compose file, or existing object'
    'podman-compose: for docker-compose compatibility'
    'podman-desktop: GUI and tray to manage Podman containers (and Kubernetes pods)'
    'podman-docker: for Docker-compatible CLI'
    'slirp4netns: for alternative rootless network support'
  )

  make install install.completions DESTDIR="$pkgdir" PREFIX=/usr LIBEXECDIR=/usr/lib -C $pkgbase

  rm -r $pkgdir/usr/lib/systemd
}

package_podman-docker() {
  pkgdesc='Emulate Docker CLI using podman'
  depends=(
    podman
    sh
  )
  conflicts=(docker)
  provides=(docker)

  make install.docker-full DESTDIR="$pkgdir" PREFIX=/usr -C $pkgbase

  rm -r $pkgdir/usr/lib/systemd
}
