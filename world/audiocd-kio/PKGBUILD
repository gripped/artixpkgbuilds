# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=audiocd-kio
pkgver=25.12.1
pkgrel=1
pkgdesc='Kioslave for accessing audio CDs'
url='https://apps.kde.org/kio_audiocd/'
arch=(x86_64)
license=(GPL-2.0-or-later
         LGPL-2.0-or-later)
depends=(cdparanoia
         flac
         gcc-libs
         glibc
         kcmutils
         kconfig
         kconfigwidgets
         kcoreaddons
         ki18n
         kio
         kwidgetsaddons
         lame
         libkcddb
         libkcompactdisc
         libogg
         libvorbis
         qt6-base)
makedepends=(extra-cmake-modules
             kdoctools)
optdepends=('opus-tools: encoding to opus format')
groups=(kde-applications
        kde-multimedia)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('79601ed788c5739e5cf189eb47a68b0394975ed4441eedf96af03d0b7b7b3e18'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
