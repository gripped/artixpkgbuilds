# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org
# Contributor: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Pablo Arias <pabloariasal@gmail.com>
# Contributor: John Jenkins <twodopeshaggy@gmail.com>

pkgname=nnn
pkgver=5.1
pkgrel=1
pkgdesc="The fastest terminal file manager ever written."
arch=('x86_64')
depends=('bash' 'sed')
optdepends=(
    'atool: for more archive formats'
    'libarchive: for more archive formats'
    'zip: for zip archive format'
    'unzip: for zip archive format'
    'trash-cli: to trash files'
    'sshfs: mount remotes'
    'rclone: mount remotes'
    'fuse2: unmount remotes'
    'xdg-utils: desktop opener'
)
url="https://github.com/jarun/${pkgname}"
license=('BSD')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/jarun/nnn/releases/download/v$pkgver/nnn-v$pkgver.tar.gz"
        "${pkgname}-${pkgver}.tar.gz.sig::${url}/releases/download/v${pkgver}/nnn-${pkgver}.tar.gz.sig")
sha512sums=('b72775ef39dc483f5ec038cbc262e1dfc5fedd5db261cf146c38ea8a9951a9f54fe171e0e4b1089c50a6c32748ab691dd010c10c44baa22b5e7f39b4538cc27b'
            'SKIP')
validpgpkeys=('BBAD0B2F3093A7C3377A8F6BA75979F35C080412'
              'E0B84447CD884CBB13E3266E4A865183AF6C5631')

prepare() {
    sed -i 's/install: all/install:/' "${pkgname}-${pkgver}/Makefile"
}

build() {
    cd "${pkgname}-${pkgver}"
    make
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" PREFIX=/usr install
    make DESTDIR="${pkgdir}" PREFIX=/usr install-desktop

    install -Dm644 misc/auto-completion/fish/nnn.fish "${pkgdir}/usr/share/fish/vendor_completions.d/nnn.fish"
    install -Dm644 misc/auto-completion/bash/nnn-completion.bash "${pkgdir}/usr/share/bash-completion/completions/nnn"
    install -Dm644 misc/auto-completion/zsh/_nnn "${pkgdir}/usr/share/zsh/site-functions/_nnn"

    install -Dm644 -t "${pkgdir}/usr/share/nnn/quitcd/" misc/quitcd/*

    cp -a plugins "${pkgdir}/usr/share/nnn/plugins/"

    install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}/" LICENSE
}
