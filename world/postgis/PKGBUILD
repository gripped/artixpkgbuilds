# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Bruno Pagani <archange@archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=postgis
pkgver=3.5.1
pkgrel=1
pkgdesc="A spatial database extender for PostgreSQL"
arch=(x86_64)
url="https://postgis.net/"
license=(GPL-2.0-or-later)
depends=(
  bash
  gcc-libs
  gdal
  geos
  glibc
  json-c
  libxml2
  pcre2
  perl
  postgresql
  postgresql-libs
  proj
  protobuf-c
)
makedepends=(
  clang
  llvm
)
changelog=$pkgname.changelog
source=(https://download.osgeo.org/postgis/source/${pkgname}-${pkgver}.tar.gz)
sha256sums=('23706abc117fb1bab45a27a263b589f52fc08ebaff318c0bc0bdc940905306b5')
b2sums=('98c56eaab63f000bc74500c36b923196d54183237b726117add1f13f0aec37926506bcdc5233d87427d948a3edea081e84327ad9eeec3e6b87d5b5bbb20359b2')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -vfi
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
