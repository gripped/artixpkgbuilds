# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Bruno Pagani <archange@archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=postgis
pkgver=3.6.2
pkgrel=3
pkgdesc="A spatial database extender for PostgreSQL"
arch=(x86_64)
url="https://postgis.net/"
license=(GPL-2.0-or-later)
depends=(
  bash
  gcc-libs
  gdal
  geos
  glibc
  json-c
  libxml2
  pcre2
  perl
  postgresql
  postgresql-libs
  proj
  protobuf-c
)
makedepends=(
  clang
  llvm
)
changelog=$pkgname.changelog
source=(https://download.osgeo.org/postgis/source/${pkgname}-${pkgver}.tar.gz
        gdal-3.13.patch)
sha256sums=('d488089a64ee4060bce7dbba0291fcca358e7f7ac5230e2ee5b344e7485729c4'
            '55a9e65f97268ff0e6e0add937a25305a4834e991f6c7708a97e9f8058376be6')
b2sums=('66a998b1495bd4c03943fa718f08dee5abcd7ee9f16c3eec87574672f785359456f3d1913b1d26882968ce812bda8db86b5fc42c241ed82b1eb75a227034280c'
        '75636435fc476df4ccbf3420605a1eb1234048b831ec0f20e0886bdf73b49fd62f7221dd3ddb36277fabd0464399656c955a2929cddd407fc71ef3345a1fd766')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ../gdal-3.13.patch
  autoreconf -vfi
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
