# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Bruno Pagani <archange@archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=postgis
pkgver=3.6.3
pkgrel=1
pkgdesc="A spatial database extender for PostgreSQL"
arch=(x86_64)
url="https://postgis.net/"
license=(GPL-2.0-or-later)
depends=(
  bash
  gcc-libs
  gdal
  geos
  glibc
  json-c
  libxml2
  pcre2
  perl
  postgresql
  postgresql-libs
  proj
  protobuf-c
)
makedepends=(
  clang
  llvm
)
changelog=$pkgname.changelog
source=(https://download.osgeo.org/postgis/source/${pkgname}-${pkgver}.tar.gz)
sha256sums=('58ff19ae133e470280efb4949ef92e0364d4c2a66bef8c57e69477348d815ea3')
b2sums=('2d80c164dbcbdd462d0dfdf4890f372dda25eeeea370c9d8edf1c838ea07867bdc1456fb8f9e33f67ef9b10f45b0a32a541b0dfe2cbf91e1618d11ed7a7b40b3')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -vfi
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
