# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Ben <contrasutra@myrealbox.com>

pkgname=elinks
pkgver=0.20.0
pkgrel=1
pkgdesc='Advanced feature-rich text-mode web browser'
arch=(x86_64)
url='https://github.com/rkd77/elinks'
license=(GPL-2.0-only)
depends=(
  glibc
  libgcc
  libstdc++
  brotli
  bzip2
  gpm
  openssl
  lua
  libdom
  libcss
  libidn2
  xz
  libwapcaplet
  expat
  zlib
  curl
  tre
)
makedepends=(
  git
  meson
  dblatex
  xmlto
  libiconv
  gc
  cmake
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('acb99f97ea38338aa8546a8bebfcc836c3ec2f2a60f2f5bb52e8cd0af32b828fd3ba2be91791dda2a25cef16e1b42470ee2fb097b7daaf537ae18684df24c06e')
b2sums=('6e58c5b6935e1ac7dbaea3b320edfc15025b4ed495eb04614c8917708be78435df91c712c9ad09dc33b1ac86b4b7ba2bb36071c118143bc803fabbd119b23e22')

build() {
  local meson_opts=(
    "$pkgname"
    build
    -D cgi=true
    -D true-color=true
    -D html-highlight=true
    -D bzlib=true
    -D brotli=true
    -D lzma=true
    -D luapkg=lua55
    -D tre=true
    -D gemini=true
    -D reproducible=true
    -D source-date-epoch="$SOURCE_DATE_EPOCH"
  )
  artix-meson "${meson_opts[@]}"

  meson compile -C build
}

package() {
  meson install -C build --destdir="$pkgdir"
}
