# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgbase=avogadrolibs
pkgname=(avogadrolibs
         avogadrolibs-qt)
pkgver=1.102.1
pkgrel=6.1
pkgdesc='Libraries that provide 3D rendering, visualization, analysis and data processing useful in computational chemistry, molecular modeling, bioinformatics, materials science, and related areas'
arch=(x86_64)
url='https://two.avogadro.cc/'
license=(custom)
makedepends=(boost
             cmake
             eigen
             fast_float
             fmt
             git
             hdf5
             genxrdpattern
             glew
             jkqtplotter
             libmsym
             mmtf-cpp
             nlohmann-json
             openmpi
             pybind11
             python
             qt6-svg
             qt6-tools
             spglib
             tbb
             utf8cpp
             verdict
             vtk)
source=(git+https://github.com/OpenChemistry/avogadrolibs#tag=$pkgver)
sha256sums=('1e31404f5087dc0033203e3ae809a5f5c8e6aae0c6e8b11e710a5c6dc161f257')

prepare() {
  mkdir crystals fragments molecules # Dummy dirs to trick cmake, actually provided by avogadro-{crystals,fragments,molecules}
  git -C $pkgname cherry-pick -n 05c5c472518ee16e79d5cf0afc0dda0a4f927a74 # Find jkqtplotter in cmake config
}

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_C_FLAGS="$CFLAGS -ffat-lto-objects" \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS -ffat-lto-objects" \
    -DUSE_HDF5=ON \
    -DUSE_PYTHON=ON \
    -DUSE_VTK=ON \
    -DUSE_SYSTEM_GENXRDPATTERN=ON \
    -DUSE_EXTERNAL_NLOHMANN=ON \
    -DUSE_EXTERNAL_PUGIXML=ON \
    -DQT_VERSION=6
  cmake --build build
}

package_avogadrolibs() {
  depends=(avogadro-crystals
           avogadro-fragments
           avogadro-molecules
           gcc-libs
           glew
           glibc
           hdf5
           libglvnd
           pugixml
           spglib
           verdict)
  optdepends=('avogadrolibs-qt: For the VTK and Qt plugins')

  DESTDIR="$pkgdir" cmake --install build
  rm -r "$pkgdir"/usr/lib/libAvogadroQt* \
        "$pkgdir"/usr/lib/libAvogadroMoleQueue* \
        "$pkgdir"/usr/lib/libAvogadroVtk* \
        "$pkgdir"/usr/include/avogadro/{molequeue,qt*,vtk} \
        "$pkgdir"/usr/lib/avogadro2/{scripts,staticplugins}

  install -Dm644 $pkgname/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}

package_avogadrolibs-qt() {
  pkgdesc="Qt modules for Avogadro"
  depends=(avogadrolibs
           fmt # needed by VTK dependencies
           gcc-libs
           glew
           glibc
           jkqtplotter
           libarchive
           libglvnd
           libmsym
           libxcursor # needed by VTK dependencies
           openmpi # needed by VTK dependencies
           qt6-base
           qt6-svg
           vtk)
  conflicts=(avogadrolibs-qt5)

  DESTDIR="$pkgdir" cmake --install build/avogadro/molequeue
  DESTDIR="$pkgdir" cmake --install build/avogadro/qtgui
  DESTDIR="$pkgdir" cmake --install build/avogadro/qtopengl
  DESTDIR="$pkgdir" cmake --install build/avogadro/qtplugins
  DESTDIR="$pkgdir" cmake --install build/avogadro/vtk

  install -Dm644 $pkgbase/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
