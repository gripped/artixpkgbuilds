# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>

pkgname=gnome-boxes
pkgver=49.0
pkgrel=1
pkgdesc='Simple GNOME application to access virtual systems'
arch=(x86_64)
url='https://apps.gnome.org/Boxes/'
license=(LGPL-2.0-or-later)
depends=(
  cairo
  cdrtools
  dconf
  edk2-ovmf
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  libarchive
  libgudev
  libhandy
  libosinfo
  libportal
  libportal-gtk3
  libsoup3
  libusb
  libvirt-glib
  libxml2
  qemu-desktop
  spice-gtk
  webkit2gtk-4.1
)
makedepends=(
  appstream
  git
  gobject-introspection
  itstool
  meson
  vala
)
groups=(gnome-extra)
source=(
  # GNOME Boxes tags use SSH signatures which makepkg doesn't understand
  "git+https://gitlab.gnome.org/GNOME/gnome-boxes.git#tag=${pkgver/[a-z]/.&}"
  "git+https://gitlab.gnome.org/felipeborges/libovf-glib.git"
  0001-Use-fallback-dirs-when-user-special-dirs-don-t-exist.patch
  0002-Use-GApplication-to-parse-command-line-options.patch
  0003-Present-existing-window-on-activation.patch
  0004-Revert-spice-display-Only-downscale-displays.patch
  0005-spice-display-Set-zoom-level-based-on-scale-factor.patch
)
b2sums=('6cc984f8f37372069ba86f420e7b6098202a0a3d80f6c3955fe43b162943748e59e65bc922c88dd857aedc2af5312083abd24678bea153bce91fac36499e11c8'
        'SKIP'
        'd120296bd6a0507435662009f1c0e46cd1ec166298707c40d83a81e5aa0dbe7d4e6c2d24cf74480218466c926b38399cc404612c09b1c9798d7adb933f2ef82c'
        'f9931ac4f4e3e871b86c6ae0c202ac4abbdc8b3227e16eaf43a8f3cd28d92c7149f53c932c3f51167afe62fccbf43b7e261e45a22a68e1cc0577ba83b4115410'
        'bf914b770835513535177ed146cd5274fd8c4b38ae34afc26299b2578aac9822e7f0309bb160e92a58dc9b89a17bb724661141203b4f4560df6c78ec9cf93dc7'
        '1ae2e4e1826d9f7fd6da2bb4a2066a33b9bc90a128bdfb69ab2ab3027917802049e212ecd5be886ccc497adce149bc127d5d2d2097db6ef6ec71d8cc83b7c58b'
        '753c8cc5cd08922f7cc57b573ac0f4a19b0b3b0b63208b1425a7ed1c7f59ce625dae28fc419c532253220de4a1fd1f1f31017f66131aa39e0e20cfcd68136c6d')

prepare() {
  cd $pkgname

  git submodule init
  git submodule set-url subprojects/libovf-glib "$srcdir/libovf-glib"
  git -c protocol.file.allow=always -c protocol.allow=never submodule update

  # Use fallback dirs when user special dirs don't exist
  # https://gitlab.gnome.org/GNOME/gnome-boxes/-/merge_requests/634
  git apply -3 ../0001-Use-fallback-dirs-when-user-special-dirs-don-t-exist.patch

  # Use GApplication to parse command line options
  # https://gitlab.gnome.org/GNOME/gnome-boxes/-/merge_requests/677
  git apply -3 ../0002-Use-GApplication-to-parse-command-line-options.patch

  # Present existing window on activation
  # https://gitlab.gnome.org/GNOME/gnome-boxes/-/merge_requests/678
  git apply -3 ../0003-Present-existing-window-on-activation.patch

  # Set zoom level based on scale factor
  # https://gitlab.gnome.org/GNOME/gnome-boxes/-/merge_requests/684
  git apply -3 ../0004-Revert-spice-display-Only-downscale-displays.patch
  git apply -3 ../0005-spice-display-Set-zoom-level-based-on-scale-factor.patch
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
