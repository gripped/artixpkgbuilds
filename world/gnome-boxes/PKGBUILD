# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>

pkgname=gnome-boxes
pkgver=48.0
pkgrel=4
pkgdesc='Simple GNOME application to access virtual systems'
arch=(x86_64)
url='https://apps.gnome.org/Boxes/'
license=(LGPL-2.0-or-later)
depends=(
  cairo
  cdrtools
  dconf
  edk2-ovmf
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  libarchive
  libgudev
  libhandy
  libosinfo
  libportal
  libportal-gtk3
  libsoup3
  libusb
  libvirt-glib
  libxml2
  qemu-desktop
  spice-gtk
  webkit2gtk-4.1
)
makedepends=(
  appstream
  git
  gobject-introspection
  itstool
  meson
  vala
)
groups=(gnome-extra)
source=(
  # GNOME Boxes tags use SSH signatures which makepkg doesn't understand
  "git+https://gitlab.gnome.org/GNOME/gnome-boxes.git#tag=${pkgver/[a-z]/.&}"
  "git+https://gitlab.gnome.org/felipeborges/libovf-glib.git"
  gnome-boxes-fallback-dirs.patch
  gnome-boxes-parse-options.patch
  gnome-boxes-activate-window.patch
)
b2sums=('38dca4d850d3d7d54d05c46e281a5fad7da6d89b24d63be05ea467b81644e5a140600a7e019841eb6978bbb76707ca340ac6b99e24f41395851114e45e4fea03'
        'SKIP'
        'db647db5c9e3a1d1f0ad1e8821f37539879b1aab88ef1b9d08eaf1e680d3ef48fc89d52de9f0a2c99d87f3d6fb394cb77c2af6f71e7dd978516e61e3edde0884'
        'e26ced09f96c2da765c8765f8543983ade3b652c2c5555739a1783189c0bdadbb53ad7a19f7aa16ffe412e064982d93e41920d615ae0b36f3b5c00517becb542'
        '11af28770a58d529d4c2f983aa1e6352ccc096d2da8dc17b713865bb8e1ab67568d2297af74edcb7a2bb38c736dd4c2e198d30f1a79d8360b2f3e2cdd2b878dd')

prepare() {
  cd $pkgname

  git submodule init
  git submodule set-url subprojects/libovf-glib "$srcdir/libovf-glib"
  git -c protocol.file.allow=always -c protocol.allow=never submodule update

  # Use fallback dirs when user special dirs don't exist
  # https://gitlab.gnome.org/GNOME/gnome-boxes/-/merge_requests/634
  git apply -3 ../gnome-boxes-fallback-dirs.patch

  # Use GApplication to parse command line options
  # https://gitlab.gnome.org/GNOME/gnome-boxes/-/merge_requests/677
  git apply -3 ../gnome-boxes-parse-options.patch

  # Present existing window on activation
  # https://gitlab.gnome.org/GNOME/gnome-boxes/-/merge_requests/678
  git apply -3 ../gnome-boxes-activate-window.patch
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
