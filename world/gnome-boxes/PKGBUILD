# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>

pkgname=gnome-boxes
pkgver=46.0
pkgrel=1
pkgdesc='Simple GNOME application to access virtual systems'
arch=('x86_64')
url='https://wiki.gnome.org/Apps/Boxes'
license=('LGPL-2.0-or-later')
depends=(
  'cairo'
  'cdrtools'
  'dconf'
  'edk2-ovmf'
  'gdk-pixbuf2'
  'glib2'
  'gtk3'
  'hicolor-icon-theme'
  'libarchive'
  'libgudev'
  'libhandy'
  'libosinfo'
  'libportal'
  'libportal-gtk3'
  'libsecret'
  'libsoup3'
  'libusb'
  'libvirt-glib'
  'libxml2'
  'mtools'
  'qemu-desktop'
  'spice-gtk'
  'tracker3'
  'webkit2gtk-4.1'
  'xdg-user-dirs'
)
makedepends=(
  'appstream-glib'
  'gobject-introspection'
  'meson'
  'spice-protocol'
  'vala'
  'yelp-tools'
)
groups=('gnome-extra')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('67eeb5e989294958b25c397cfc5615ff65f44315c700010f0b9e336e6f3407cb')

build() {
  artix-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
