# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jan@archlinux.org>

pkgbase=gnome-desktop
pkgname=(
  gnome-desktop-common
  gnome-desktop
  gnome-desktop-4
  gnome-desktop-docs
)
pkgver=44.4
pkgrel=1
epoch=1
pkgdesc="Provides API shared by several apps on the GNOME desktop"
url="https://gitlab.gnome.org/GNOME/gnome-desktop"
arch=(x86_64)
license=(
  GPL-2.0-or-later
  LGPL-2.0-or-later
)
depends=(
  bubblewrap
  cairo
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gsettings-desktop-schemas
  iso-codes
  libseccomp
  libxkbcommon
  libelogind
  xkeyboard-config
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  gtk3
  gtk4
  meson
  elogind
  yelp-tools
)
checkdepends=(xorg-server-xvfb)
source=("git+https://gitlab.gnome.org/GNOME/gnome-desktop.git#tag=${pkgver/[a-z]/.&}")
b2sums=('1eff749ce5a00178c2186f01654299314aa2fa6084347f773e6c1778d3316e85cd9d0ddf64c77263ec102ddf19b22881df709bfad41482e14c7e5eb8fecbf3ad')

prepare() {
  cd $pkgbase
}

build() {
  local meson_options=(
    -D systemd=disabled
    -D debug_tools=false
    -D gtk_doc=true
  )

  artix-meson $pkgbase build "${meson_options[@]}"
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_gnome-desktop-common() {
  pkgdesc+=" (common files)"
  depends=()

  meson install -C build --destdir "$pkgdir"

  cd "$pkgdir"

  _pick gtk3 usr/include/gnome-desktop-3.0
  _pick gtk3 usr/lib/libgnome-desktop-3.so*
  _pick gtk3 usr/lib/pkgconfig/gnome-desktop-3.0.pc
  _pick gtk3 usr/lib/girepository-1.0/GnomeDesktop-3.0.typelib
  _pick gtk3 usr/share/gir-1.0/GnomeDesktop-3.0.gir

  _pick gtk4 usr/include/gnome-desktop-4.0
  _pick gtk4 usr/lib/libgnome-*-4.so*
  _pick gtk4 usr/lib/pkgconfig/gnome-*-4.pc
  _pick gtk4 usr/lib/girepository-1.0/Gnome*-4.0.typelib
  _pick gtk4 usr/share/gir-1.0/Gnome*-4.0.gir

  _pick docs usr/share/gtk-doc
}

package_gnome-desktop() {
  pkgdesc+=" using GTK 3"
  depends+=(
    "gnome-desktop-common=$epoch:$pkgver-$pkgrel"
    gtk3
  )
  provides=(libgnome-desktop-3.so)

  mv gtk3/* "$pkgdir"
}

package_gnome-desktop-4() {
  pkgdesc+=" using GTK 4"
  depends+=(
    "gnome-desktop-common=$epoch:$pkgver-$pkgrel"
    gtk4
  )
  provides=(libgnome-{bg,desktop,rr}-4.so)

  mv gtk4/* "$pkgdir"
}

package_gnome-desktop-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv docs/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
