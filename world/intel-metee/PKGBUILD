# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgbase=intel-metee
pkgname=(intel-metee intel-metee-doc)
pkgver=4.3.0
pkgrel=1
pkgdesc="Access library for Intel CSME HECI interface"
arch=(x86_64)
url="https://github.com/intel/metee/"
license=(Apache-2.0)
makedepends=(cmake doxygen graphviz)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('6652c3a1a3877a5912096a6e137a9c93230704132e17d31a3a91a730c8fda8fd')

prepare() {
    # remove obselete doxygen tags
    doxygen -u metee-${pkgver}/include/Doxyfile.in
}

build() {
  # fix error: "_FORTIFY_SOURCE" redefined [-Werror]
  # note: upstream forces _FORTIFY_SOURCE=2
  export CFLAGS="${CFLAGS/-Wp,-D_FORTIFY_SOURCE=?/}"
  export CXXFLAGS="${CXXFLAGS/-Wp,-D_FORTIFY_SOURCE=?/}"
  
  cmake -B build -S metee-${pkgver} \
    -G 'Unix Makefiles' \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_PREFIX_PATH=/usr \
    -DBUILD_DOCS=ON \
    -DBUILD_SHARED_LIBS=ON \
    -Wno-dev
  cmake --build build
}

package_intel-metee() {
  depends=(glibc)
  
  DESTDIR="${pkgdir}" cmake --install build
  mv "${pkgdir}"/usr/share/doc .
}

package_intel-metee-doc() {
  pkgdesc+=' (documentation)'
    
  install -dm755 "${pkgdir}"/usr/share
  mv doc "${pkgdir}"/usr/share
}
