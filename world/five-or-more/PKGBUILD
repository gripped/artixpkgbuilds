# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=five-or-more
pkgver=3.32.3
pkgrel=4
pkgdesc="Remove colored balls from the board by forming lines"
url="https://gitlab.gnome.org/GNOME/five-or-more"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  libgee
  libgnome-games-support
  librsvg
)
makedepends=(
  git
  gobject-introspection
  meson
  vala
  yelp-tools
)
source=(
  "git+$url.git#tag=${pkgver/[a-z]/.&}"
  0001-Reset-positions-when-the-board-is-changed.patch
)
b2sums=('4fa1320ffcc5fc8ca30d1bc81af90a4047805c368281b2d52ad4f63bed56bbeec338bc12ddbe73fa95739bfcc2a415bbca3ba43a3f9d493b0989d349786b34b6'
        '44e296624f17ac9069dc4bd87b99bac4b37c5c1390c5bcbc6d665e3316291de783dcb417a4b02c6dd8517ce15e96cce81b670fee7b62d0f22c30f8b3fb5ca58f')

prepare() {
  cd $pkgname

  # Reset positions when the board is changed
  # https://gitlab.gnome.org/GNOME/five-or-more/-/merge_requests/29
  git apply -3 ../0001-Reset-positions-when-the-board-is-changed.patch
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et
