# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=five-or-more
pkgver=48.0
pkgrel=1
pkgdesc="Remove colored balls from the board by forming lines"
url="https://gitlab.gnome.org/GNOME/five-or-more"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  libgee
  libgnome-games-support
  librsvg
)
makedepends=(
  git
  gobject-introspection
  meson
  vala
  yelp-tools
)
source=(
  "git+$url.git#tag=${pkgver/[a-z]/.&}"
)
b2sums=('df04fcb0a67ff9cf70b618bcdfcb2d028e47aa85d2022c9b6a74ffb79913499e631ab668f4636361eb0094e29b54a6c1713d4607a86d2f87c942aac804aa9620')

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et
