# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan-Erik Rediger <badboy at archlinux dot us>

pkgname=bats-assert
pkgver=2.2.4
pkgrel=1
pkgdesc="Common assertions for Bats"
url='https://github.com/bats-core/bats-assert'
arch=(any)
license=("custom:CC0")
depends=(
  bash
  bats
  bats-support
)
checkdepends=(
  coreutils
)
replaces=(bash-bats-assert)
source=(${url}/archive/refs/tags/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('ed826f9dfd07e66386584f16c5abc81321162b25ebe34ef1e9ac6524cf706b19db9d9e27d4008ad3b4edc17d2f9a8c1fe08947767129ec71be18c3d349408e92')
b2sums=('721907ee86b5f070abe5fae135ad053413b1dc6527b123d1683796566f1cc96bd142e84e090455df52ec7b4884aadca1b16e2d68e23e1e1da241b897115ef005')

check() {
  cd "${pkgname}-${pkgver}"
  bats --jobs "$(nproc)" --tap test
}

package() {
  cd "${pkgname}-${pkgver}"

  install -Dm 644 *.bash -t "${pkgdir}/usr/lib/bats/${pkgname}"
  install -Dm 644 src/*.bash -t "${pkgdir}/usr/lib/bats/${pkgname}/src"

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
