# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Konstantin Gizdov <arch at kge dot pw>
# Contributor: Torsten Ke√üler <tpkessler at archlinux dot org>

_pkgname='intel-oneapi-compiler-shared'
pkgbase="${_pkgname}-runtime"
pkgname=("${_pkgname}-runtime" "${_pkgname}-runtime-libs")
_pkgver=2025.0
pkgver=2025.0.4
_debpkgrel=1519
pkgrel=2
_pkgdesc="Intel oneAPI compiler runtime libraries"
arch=('x86_64')
url='https://software.intel.com/content/www/us/en/develop/tools/oneapi.html'
license=("LicenseRef-Intel-EULA-Developer-Tools")
depends=('level-zero-loader')
source=("https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
        "https://apt.repos.intel.com/oneapi/pool/main/${_pkgname}-common-${_pkgver}-${pkgver}-${_debpkgrel}_all.deb")
b2sums=('14569c598eec7b263a29cf619ea2fe3233b7eef080ba0a338afaea046351ed93885405fcb6ab5fec741689f2a64b626f8e0c22f4bec53a535c80e2c72fffde2b'
        '9f71a8538618d13aaf366309daf5b0d967f5041c8b6c651480403cde7ff19e814734294d21ea1120caf0473c814bb5361a9559747f22d7561a1630bc38e43b8c')

noextract=(
    "${pkgname}-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
    "${_pkgname}-common-${_pkgver}-${pkgver}-${_debpkgrel}_all.deb"
)
_lib_path='/opt/intel/oneapi/compiler'

prepare() {
    mkdir "${_pkgname}-${pkgver}"
    ar x "${srcdir}/${pkgname}-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
    tar xvf data.tar.xz -C "${_pkgname}-${pkgver}"
    rm data.tar.xz
    ar x "${srcdir}/${_pkgname}-common-${_pkgver}-${pkgver}-${_debpkgrel}_all.deb"
    tar xvf data.tar.xz -C "${_pkgname}-${pkgver}"
    rm data.tar.xz
}

build() {
    mkdir libs
    install -d "libs/${_lib_path}/${_pkgver}/bin"

    local _bin
    for _bin in aocl-ioc64 compiler icx-cc icx-cl ioc64 sycl-ls sycl-trace; do
        mv "${_pkgname}-${pkgver}/${_lib_path}/${_pkgver}/bin/${_bin}" \
           "libs/${_lib_path}/${_pkgver}/bin"
    done

    local _lib
    install -d "libs/${_lib_path}/${_pkgver}/lib"
    for _lib in libsvml.so libirng.so libimf.so libintlc.so libintlc.so.5; do
        mv "${_pkgname}-${pkgver}/${_lib_path}/${_pkgver}/lib/${_lib}" \
           "libs/${_lib_path}/${_pkgver}/lib"
    done

    mkdir most
    cp -a "${_pkgname}-${pkgver}"/* most
}

package_intel-oneapi-compiler-shared-runtime-libs() {
    depends=('intel-oneapi-common' 'intel-oneapi-openmp' 'glibc' 'gcc-libs'
             'zlib')
    conflicts=('intel-oneapi-basekit')
    pkgdesc="${_pkgdesc}: Minimal compiler libraries"

    cp -a libs/* "${pkgdir}"

    # add latest and common symlink
    ln -s "${_lib_path}/${_pkgver}" "${pkgdir}/${_lib_path}/latest"

    # allow libs to be found
    local _ldso_conf="${pkgdir}"/etc/ld.so.conf.d
    install -d "${_ldso_conf}"
    echo "${_lib_path}/latest/lib" >> "${_ldso_conf}/${pkgname}.conf"

    install -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
    ln -s /usr/share/licenses/intel-oneapi "${pkgdir}"/usr/share/licenses/"${pkgname}"/oneapi
}

package_intel-oneapi-compiler-shared-runtime() {
    depends=('intel-oneapi-openmp' 'intel-oneapi-common' 'intel-oneapi-compiler-shared-runtime-libs'
             'libxml2' 'glibc' 'gcc-libs' 'level-zero-loader' 'zlib')
    provides=("${_pkgname}-common" "opencl-driver")
    conflicts=('intel-oneapi-basekit')
    pkgdesc="${_pkgdesc}"

    cp -a most/* "${pkgdir}"

    # allow libs to be found
    local _ldso_conf="${pkgdir}"/etc/ld.so.conf.d
    install -d "${_ldso_conf}"
    echo "${_lib_path}/latest/linux/lib" > "${_ldso_conf}/${pkgname}.conf"
    echo "${_lib_path}/latest/linux/lib/x64" >> "${_ldso_conf}/${pkgname}.conf"

    echo '/opt/intel/oneapi/compiler/latest/lib/libintelocl.so' > 'intelocl.icd'
    install -Dm644 'intelocl.icd' "$pkgdir/etc/OpenCL/vendors/intelocl.icd"

    install -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
    ln -s /usr/share/licenses/intel-oneapi "${pkgdir}"/usr/share/licenses/"${pkgname}"/oneapi
}
