# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bert Peters <bertptrs@archlinux.org>

pkgname=ruby-cucumber-ci-environment
pkgver=10.0.1
pkgrel=1
pkgdesc='Detect CI Environment from environment variables'
arch=(any)
url='https://github.com/cucumber/ci-environment'
license=(MIT)
depends=(
  ruby
)
makedepends=(
  ruby-bundler
  ruby-rake
  ruby-rspec
)
options=(!emptydirs)
source=(https://github.com/cucumber/ci-environment/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('518cd579d63eb780e632f393d10f0af3d3dd489d1d1a6b04abb2db02b93d6eb2')

build() {
  local _gemdir="$(gem env gemdir)"
  cd ci-environment-$pkgver/ruby
  rake lib/cucumber/ci_environment/CiEnvironments.json
  gem build cucumber-ci-environment.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    cucumber-ci-environment-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd ci-environment-$pkgver/ruby
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd ci-environment-$pkgver/ruby
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
