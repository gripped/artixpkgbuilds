# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=ruby-benchmark-ips
_pkgname="${pkgname#ruby-}"
pkgver=2.14.0
pkgrel=3
pkgdesc='Provides iteration per second benchmarking for Ruby'
arch=('any')
url='https://github.com/evanphx/benchmark-ips'
license=('MIT')
depends=(
  ruby
)
makedepends=(
  git
  ruby-rake
  ruby-rdoc
)
checkdepends=(
  ruby-bundler
  ruby-minitest
)
options=('!emptydirs')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('1a1fb73ea84f7ea03f018f6578514a2b77e2e8985ee3766120f7587e767e2d3072f41c18c99c21ccc1379ee3fd7a26615efc164466fdb68d71f9a67fd130e3f3')
b2sums=('7ce7c4a4399c722d1178c03cc033c0ee67496a6de51f225e3544039a5a74b6e969ac24ae5d454663413f80491ff56d82f690861ed99ed221d202dc8eb4b4d2d2')

prepare() {
  cd "$pkgname"

  # we do not do version pinning
  sed --regexp-extended 's/~>/>=/g' --in-place *.gemspec Gemfile Rakefile
}

build() {
  cd "$pkgname"

  gem build "$_pkgname.gemspec"
}

check() {
  cd "$pkgname"

  rake
}

package() {
  cd "$pkgname"

  local _gemdir="$(gem env gemdir)"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$_pkgname-$pkgver.gem"

  # delete cache
  rm -rf "$pkgdir/$_gemdir/cache"

  # license
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  ln -sf "$_gemdir/gems/$_pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
