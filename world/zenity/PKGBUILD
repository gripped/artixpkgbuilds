# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=zenity
pkgver=4.2.1
pkgrel=1
pkgdesc="Display graphical dialog boxes from shell scripts"
url="https://gitlab.gnome.org/GNOME/zenity"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  pango
)
makedepends=(
  git
  glib2-devel
  help2man
  meson
  util-linux
  yelp-tools
)
source=("git+$url.git?signed#tag=$pkgver")
b2sums=('8c1bac960486e7b644d2b52bebbdda9949c87ea7d893aa56b55e4ae5e38404ca2902b557bb61d262f5b558237d485f5f359f489a01015c874e7515b71d8ed14c')
validpgpkeys=(
  98FF7532A4639119B8FB4F15E520923A38618D6C # Logan Rathbone (GNOME) <larathbone@gnome.org>
)

prepare() {
  cd zenity
}

build() {
  artix-meson zenity build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  hardlink -c "$pkgdir/usr/share/help"
}

# vim:set sw=2 sts=-1 et:
