# Maintainer: Tim Meusel <tim@bastelfreak.de>
# Contributor: Christian Hesse <mail@eworm.de>
# Contributor: Thomas S Hatch <thatch45 (at) Gmail (dot) com>

pkgname=virt-what
pkgver=1.26
pkgrel=1
license=('GPL')
arch=('x86_64')
depends=('dmidecode')
pkgdesc='A small program that prints out a list of facts about a virtual machine'
url='http://people.redhat.com/~rjones/virt-what/'
validpgpkeys=('F7774FB1AD074A7E8C8767EA91738F73E1B768A0')
source=("https://people.redhat.com/~rjones/virt-what/files/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha512sums=('1939e0f9c2ab9a17feb9f737e0823eb2f2950838e0724fb87119823213c1defc9c7229ed703e977540d6bfe0d681230e4131b4fcfe4db4864b784cba69254ee0'
            'SKIP')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure
  sed -i '/^sbindir/c sbindir = /usr/bin' Makefile
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make libexecdir=/usr/bin prefix=/usr DESTDIR=${pkgdir} install
}
