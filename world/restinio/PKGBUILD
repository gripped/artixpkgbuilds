# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Bruno Pagani <archange@archlinux.org>

pkgname=restinio
pkgver=0.7.9.1
pkgrel=1
pkgdesc="Header-only C++14 library that gives you an embedded HTTP/Websocket server"
arch=(any)
url="https://stiffstream.com/en/products/restinio.html"
license=(BSD)
depends=(asio llhttp fmt expected-lite)
makedepends=(cmake gcc openssl zlib pcre pcre2 boost)
source=(https://github.com/Stiffstream/restinio/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('4a0429e2ea4ece200228226f0c628ecf9ac111cb6a6b12ad857de84c0576bf80')

build() {
  cmake -B build -S ${pkgname}-${pkgver}/dev/ \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DRESTINIO_TEST=OFF \
      -DRESTINIO_SAMPLE=OFF \
      -DRESTINIO_WITH_SOBJECTIZER=OFF \
      -DRESTINIO_DEP_LLHTTP=system \
      -DRESTINIO_DEP_FMT=system \
      -DRESTINIO_DEP_EXPECTED_LITE=system \
      -DRESTINIO_DEP_STANDALONE_ASIO=system
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${pkgname}-${pkgver}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
