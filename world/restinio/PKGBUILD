# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Bruno Pagani <archange@archlinux.org>

pkgname=restinio
pkgver=0.7.7
pkgrel=1
pkgdesc="Header-only C++14 library that gives you an embedded HTTP/Websocket server"
arch=(any)
url="https://stiffstream.com/en/products/restinio.html"
license=(BSD)
depends=(asio llhttp fmt expected-lite)
makedepends=(cmake gcc openssl zlib pcre pcre2 boost)
source=(https://github.com/Stiffstream/restinio/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('d9b7a36f9587580b81c02078fc65a1b0091c4b5f4c528f70db2a4fb3c938a0e4')

build() {
  cmake -B build -S ${pkgname}-${pkgver}/dev/ \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DRESTINIO_TEST=OFF \
      -DRESTINIO_SAMPLE=OFF \
      -DRESTINIO_WITH_SOBJECTIZER=OFF \
      -DRESTINIO_DEP_LLHTTP=system \
      -DRESTINIO_DEP_FMT=system \
      -DRESTINIO_DEP_EXPECTED_LITE=system
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${pkgname}-${pkgver}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
