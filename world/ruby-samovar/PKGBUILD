# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-samovar
pkgver=2.4.2
pkgrel=1
pkgdesc='A flexible option parser excellent support for sub-commands and help documentation'
arch=(any)
url='https://github.com/ioquatix/samovar'
license=(MIT)
depends=(
  ruby
  ruby-console
)
makedepends=(
  git
  ruby-bundler
  ruby-rake
  ruby-rdoc
)
checkdepends=(
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-covered
  ruby-sus
)
options=(!emptydirs)
source=(git+https://github.com/ioquatix/samovar.git#tag=v$pkgver)
sha512sums=('2c350947f1cccdd31c0a09d0956bddb3f4cce82ba9db75dc22966809f19d483efb7a4ed978def8f8e4cab9bc5b1a2f6025f3f0b7a5c9195a231b3c0a8e17bd84')
b2sums=('9d896dd92e7d4b25cc845c4739607d2b94b4571860600dfa7d8a03d7788626032482c331e96e69dea70d5f05ec25c89109608fe6519fea7f3ab6e339518b3ee1')

prepare() {
  cd samovar

  sed -i '/release\.pem/d' samovar.gemspec
  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd samovar
  gem build --verbose samovar.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    samovar-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd samovar
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd samovar
  cp -a tmp_install/* "$pkgdir"/
}
