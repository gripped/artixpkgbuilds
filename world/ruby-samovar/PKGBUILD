# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-samovar
pkgver=2.5.0
pkgrel=1
pkgdesc='A flexible option parser excellent support for sub-commands and help documentation'
arch=(any)
url='https://github.com/ioquatix/samovar'
license=(MIT)
depends=(
  ruby
  ruby-console
)
makedepends=(
  git
  ruby-bundler
  ruby-rake
  ruby-rdoc
)
checkdepends=(
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-covered
  ruby-sus
)
options=(!emptydirs)
source=(git+https://github.com/ioquatix/samovar.git#tag=v$pkgver)
sha512sums=('422d35c83665c499c69b52974ffb335ba02e2548c0fc70d927dc9675d40eba374de81ac8a4d12350214b65e01541bc9ddfeea2e2c35b94d8c6db5de919fdad4a')
b2sums=('a642cb16df49856b1407d150e6af2ed318504c6f88f3c2533de2d3c1ce462461bc2a9e262ae98fb1de4f3fedd946497f7d50e6a4ac63de89c064ea834e650c1f')

prepare() {
  cd samovar

  sed -i '/release\.pem/d' samovar.gemspec
  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd samovar
  gem build --verbose samovar.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    samovar-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd samovar
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd samovar
  cp -a tmp_install/* "$pkgdir"/
}
