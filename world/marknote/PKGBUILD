# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=marknote
pkgver=1.5.1
pkgrel=1
pkgdesc='A simple markdown note management app'
url='https://apps.kde.org/marknote/'
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(breeze-icons
         gcc-libs
         glibc
         kcolorscheme
         kconfig
         kcoreaddons
         ki18n
         kiconthemes
         kirigami
         kirigami-addons
         kmime
         knotifications # QML
         kwindowsystem
         md4c
         qqc2-desktop-style
         qt6-base
         qt6-declarative)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('8179c5d202dd1d2f8a800dc6baf09dd191721b8713b429c49653d8d707264d06'
            'SKIP')
validpgpkeys=(39FFA93CAE9C6AFC212AD00202325448204E452A) # Carl Schwan <carl@carlschwan.eu>

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
