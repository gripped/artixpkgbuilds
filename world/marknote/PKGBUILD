# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=marknote
pkgver=1.4.1
pkgrel=1
pkgdesc='A simple markdown note management app'
url='https://apps.kde.org/marknote/'
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(breeze-icons
         gcc-libs
         glibc
         kcolorscheme
         kconfig
         kcoreaddons
         ki18n
         kiconthemes
         kirigami
         kirigami-addons
         kmime
         md4c
         qqc2-desktop-style
         qt6-base
         qt6-declarative)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/office/marknote/-/commit/25cf115f.patch)
sha256sums=('96c2f551c3d9a09cdbc2d6cc242e64b3a9c811df7f29410d5b85871ec2ad2799'
            'SKIP'
            'cbd6657562ae7416b5bf93099852e70db974aa5681117d18b86b8e770ea3a6df')
validpgpkeys=(39FFA93CAE9C6AFC212AD00202325448204E452A) # Carl Schwan <carl@carlschwan.eu>

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../25cf115f.patch # Drop unused kxmlgui dependency
}

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
