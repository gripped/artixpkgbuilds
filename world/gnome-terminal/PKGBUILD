# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-terminal
pkgver=3.54.1
pkgrel=1
pkgdesc="The GNOME Terminal Emulator"
url="https://wiki.gnome.org/Apps/Terminal"
arch=(x86_64)
license=(
  # Program
  GPL-3.0-or-later

  # Documentation
  CC-BY-SA-3.0
  GPL-3.0-only

  # Appstream-data
  GFDL-1.3-only
)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  gsettings-desktop-schemas
  gtk3
  hicolor-icon-theme
  libhandy
  libx11
  pango
  util-linux-libs
  vte3
)
makedepends=(
  docbook-xsl
  git
  glib2-devel
  gnome-shell
  libnautilus-extension
  meson
  yelp-tools
)
optdepends=(
  "libnautilus-extension: Nautilus integration"
)
groups=(gnome-extra)
source=("git+https://gitlab.gnome.org/GNOME/gnome-terminal.git#tag=$pkgver")
b2sums=('b328dfcf1d4d26492ef58f2e0cbbc7d505c1c6d91b522991f96f762b4cea15b76414a2059e37ccd34da37ea64520897056320e3c87a403549e05f770c99f5603')

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D b_lto=false
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
