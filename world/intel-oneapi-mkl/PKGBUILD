# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Konstantin Gizdov <arch at kge dot pw>
# Contributor: Torsten Ke√üler <tpkessler at archlinux dot org>
# Contributor: Martin Rodriguez Reboredo <yakoyoku@gmail.com>
# Contributor: huyz
# Contributor: Jingbei Li <i@jingbei.li>
pkgbase=intel-oneapi-mkl
pkgname=("${pkgbase}" "${pkgbase}-sycl")
_pkgver=2025.0
_debpkgrel=14
pkgver=2025.0.1
pkgrel=2
_pkgdesc="Intel oneAPI Math Kernel Library"
arch=('x86_64')
url='https://software.intel.com/content/www/us/en/develop/tools/oneapi.html'
license=("LicenseRef-Intel-Simplified")
makedepends=('dpkg')
source=(
        "https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-classic-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
        "https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-classic-include-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
        "https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-core-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
        "https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-core-devel-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
        "https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-cluster-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
        "https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-cluster-devel-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
        "https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-sycl-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
        "https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-sycl-blas-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
        "https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-sycl-lapack-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
        "https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-sycl-dft-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
        "https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-sycl-sparse-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
        "https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-sycl-data-fitting-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
        "${pkgname}.conf"
        "${pkgname}.sh")
options=('staticlibs' '!debug')
noextract=(
		   "${pkgbase}-classic-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
		   "${pkgbase}-classic-include-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
		   "${pkgbase}-core-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
		   "${pkgbase}-core-devel-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
		   "${pkgbase}-cluster-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
		   "${pkgbase}-cluster-devel-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
		   "${pkgbase}-sycl-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
		   "${pkgbase}-sycl-blas-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
		   "${pkgbase}-sycl-lapack-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
		   "${pkgbase}-sycl-dft-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
		   "${pkgbase}-sycl-sparse-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
		   "${pkgbase}-sycl-data-fitting-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
           )
sha256sums=('b1dae0ee0fd0622825146faf1ab6713ac69f90619867291f1aa3ca2b0c8ce4c5'
            '689223a1c616a56dae6c10b86eff48885c2a4fb488d37e9f93e7936307275874'
            '26d934b13c7e5bae019b5f45bfde5fda0a16beedf6f18f172007c53084784942'
            '77a6fe0d2e11593ab8a06a3510ef71bbcf63142be8530123dc77730ae2cd44fb'
            '904159bbbab7e1f9ec30ec9890216f39195d58820cd6c3928c21918afddb8c7a'
            '8cac9223c6e5c539799aa71672dafe9237ed32a21b4c9970b312071e2259319e'
            '27a8f4ef8cd8162ecb77d387a1583ae639fe9deef19469105b7f184bf60e0626'
            '192a730dedb0ce9e513735e56364f78276c835a917ed4054239b3e81f3b9e164'
            '52c0dfd47367a92ce49788a28bc043988f68dacfc146be09094b0b3309eccdd6'
            'b3ab0022eff79ddc52a78df3cddddce240e44f2d5509183989a8bea7597a6d34'
            'cc28d8334ea665dbf0cd22589eb54d47bd848ee5c84c9f99e4905633eeeb48ff'
            'cc6a702e1c40928ee18124361facfc182ac24f674082883ff46602b751950e06'
            'cd787d9843146ad5ccf28ec72311d76d95d6bb9e42974dd614d8b5aa76bb5bd9'
            '85a56750e03ad99a5b99e7296dcea411fb88c4a0a25e674c86ab48f5648d9ae9')

package_intel-oneapi-mkl() {
    pkgdesc="$_pkgdesc"
    depends=('intel-oneapi-common' 'intel-oneapi-tbb' 'intel-oneapi-openmp'
		     'glibc' 'gcc-libs' 'bash')
    replaces=('intel-mkl' 'intel-mkl-static')
    provides=('intel-mkl' 'intel-mkl-static')
    conflicts=('intel-mkl' 'intel-mkl-static' 'intel-oneapi-basekit')

    dpkg-deb -X ${pkgbase}-classic-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb "${pkgdir}"
    dpkg-deb -X ${pkgbase}-core-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb "${pkgdir}"
    dpkg-deb -X ${pkgbase}-cluster-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb "${pkgdir}"
    dpkg-deb -X ${pkgbase}-classic-include-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb "${pkgdir}"
    dpkg-deb -X ${pkgbase}-core-devel-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb "${pkgdir}"
    dpkg-deb -X ${pkgbase}-cluster-devel-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb "${pkgdir}"

    # symlink mkl version
    ln -s "$_pkgver" "${pkgdir}"/opt/intel/oneapi/mkl/latest

    install -Dm644 ${pkgbase}.conf "${pkgdir}"/etc/ld.so.conf.d/${pkgbase}.conf
    install -Dm644 ${pkgbase}.sh "${pkgdir}"/etc/profile.d/${pkgbase}.sh

    install -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
    ln -s /usr/share/licenses/intel-oneapi "${pkgdir}"/usr/share/licenses/"${pkgname}"/oneapi
    ln -s /opt/intel/oneapi/mkl/latest/licensing "${pkgdir}"/usr/share/licenses/"${pkgname}"/mkl

    # pkgconfig
    cd "${pkgdir}"/opt/intel/oneapi/mkl/"${_pkgver}"/lib/pkgconfig
    install -d "${pkgdir}"/usr/share/pkgconfig
    for _file in *.pc; do
        __file="$(basename ${_file})"
        ln -s /opt/intel/oneapi/mkl/"${_pkgver}"/lib/pkgconfig/${__file} "${pkgdir}"/usr/share/pkgconfig/${__file}
        sed -e 's@prefix=.*@prefix=/opt/intel/oneapi/mkl/latest@g' -i ${__file}
    done

    # provide old intel-mkl
    install -d "${pkgdir}"/usr/bin
    install -d "${pkgdir}"/opt/intel/mkl
    install -d "${pkgdir}"/opt/intel/mkl/lib
    ln -sf /opt/intel/oneapi/mkl/latest/bin/intel64 "${pkgdir}"/opt/intel/mkl/bin
    ln -sf /opt/intel/oneapi/mkl/latest/lib/intel64 "${pkgdir}"/opt/intel/mkl/lib/intel64
    ln -sf /opt/intel/oneapi/mkl/latest/lib/intel64 "${pkgdir}"/opt/intel/mkl/lib/intel64_lin
    ln -sf /opt/intel/oneapi/mkl/latest/include "${pkgdir}"/opt/intel/mkl/include
    ln -sf /opt/intel/mkl/bin/mkl_link_tool "${pkgdir}"/usr/bin/mkl_link_tool
}

package_intel-oneapi-mkl-sycl() {
    pkgdesc="$_pkgdesc (GPU offloading)"
    depends=('intel-oneapi-mkl-libs' 'intel-oneapi-compiler-dpcpp-cpp-runtime-libs'
             'intel-oneapi-common' 'intel-oneapi-compiler-shared-runtime-libs'
             'glibc' 'gcc-libs' 'bash')
    conflicts=('intel-oneapi-basekit')

    dpkg-deb -X ${pkgbase}-sycl-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb "${pkgdir}"
    dpkg-deb -X ${pkgbase}-sycl-blas-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb "${pkgdir}"
    dpkg-deb -X ${pkgbase}-sycl-lapack-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb "${pkgdir}"
    dpkg-deb -X ${pkgbase}-sycl-dft-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb "${pkgdir}"
    dpkg-deb -X ${pkgbase}-sycl-sparse-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb "${pkgdir}"
    dpkg-deb -X ${pkgbase}-sycl-data-fitting-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb "${pkgdir}"
}
