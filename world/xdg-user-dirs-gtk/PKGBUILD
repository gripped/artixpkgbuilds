# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=xdg-user-dirs-gtk
pkgver=0.14
pkgrel=1
pkgdesc="Creates user dirs and asks to relocalize them"
url="https://gitlab.gnome.org/GNOME/xdg-user-dirs-gtk"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  glib2
  glibc
  gtk3
  xdg-user-dirs
)
makedepends=(
  git
  meson
)
groups=(gnome)
source=(
  "git+https://gitlab.gnome.org/GNOME/xdg-user-dirs-gtk.git#tag=$pkgver"
  0001-desktop-Show-in-more-environments.patch
)
b2sums=('6d773bfdb7775df3716a13020d539e2aae3f5b131ca0d066c3ccabbf32f55d4deb8ea7746dee323dcdaa55196c5322549bb95e3edadc79bbbe62f7fe37f25994'
        '594de76b08508a177cb473b474ade75bd2646509c8782321f2fa2197cf208ceb16e414cd37cb47e7821d17b22a7cbb3714f86fbf802bb33dd76abc50822a05e6')

prepare() {
  cd $pkgname
  git apply -3 ../0001-desktop-Show-in-more-environments.patch
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
