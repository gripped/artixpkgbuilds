# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=iagno
pkgver=3.38.1+r137+g0d01085
pkgrel=1
pkgdesc="Dominate the board in a classic version of Reversi"
url="https://wiki.gnome.org/Apps/Reversi"
arch=(x86_64)
license=(GPL)
depends=(gtk3 gsound librsvg)
makedepends=(meson gobject-introspection yelp-tools appstream vala git)
_commit=0d0108571402e40761f95274cf1fad0b03fda8c9  # master
source=("git+https://gitlab.gnome.org/GNOME/iagno.git#commit=$_commit")
b2sums=('90be43ed4ac67181703f36d0c8c88257f1800c2721abf158c13b6988783f97909e5bf12d4bdc304abf866ddd2a9179275f4a6c662feb6e1ada1555de251a359f')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
