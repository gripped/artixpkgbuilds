# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=mbox-importer
pkgver=24.05.0
pkgrel=1
pkgdesc='Import mbox files to KMail'
arch=(x86_64)
url='https://kontact.kde.org'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kconfig
         kcoreaddons
         kcrash
         ki18n
         kidentitymanagement
         kwidgetsaddons
         libakonadi
         mailcommon
         mailimporter
         qt6-base)
makedepends=(extra-cmake-modules)
groups=(kde-applications
        kde-pim)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('7f167c9019ca99ff1c5a74c8164b336f3dca44e280f5cda0ee0eefc536a09149'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
