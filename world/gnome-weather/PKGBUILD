# Maintainer: Fabian Bornschein <fabiscafe-at-mailbox-dot-org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-weather
pkgver=47.0
pkgrel=1
pkgdesc="Access current weather conditions and forecasts"
url="https://apps.gnome.org/Weather"
arch=(any)
license=(GPL-2.0-or-later)
depends=(
  dconf
  geoclue
  gjs
  gtk4
  hicolor-icon-theme
  libadwaita
  libgweather-4
)
makedepends=(
  appstream-glib
  git
  gobject-introspection
  meson
)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/gnome-weather.git#tag=${pkgver/[a-z]/.&}")
b2sums=('2fc6461adb0e7df81e270ce17395cc5aebdff8a7ec51894f1f5e1e439a888de71f1a07cde1643636b2bcd9f6a6e32f32b57b5d1845e11ee7abba2e679f91d998')

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
