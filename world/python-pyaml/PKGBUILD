# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pyaml
pkgver=24.9.0
pkgrel=1
pkgdesc="PyYAML-based module to produce pretty and readable YAML-serialized data"
url="https://pypi.python.org/pypi/pyaml"
license=('WTFPL')
arch=('any')
depends=('python-yaml')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-unidecode')
optdepends=('python-unidecode: Needed if same-id objects or recursion is used within serialized data')
source=("https://pypi.io/packages/source/p/pyaml/pyaml-$pkgver.tar.gz")
sha512sums=('a10026d2e69c5bbfabbe5ed02814fecb8838c969761eec0137a93465478abc518ba83cfae4b9df643040aa5fe46a62fc9def1fd763830ae8d2cfdce3b3061365')

build() {
  cd pyaml-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd pyaml-$pkgver
  python -m unittest -v
}

package() {
  cd pyaml-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/

  # Remove tests
  python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  rm -r "$pkgdir"/usr/lib/python"$python_version"/site-packages/pyaml/tests
}
