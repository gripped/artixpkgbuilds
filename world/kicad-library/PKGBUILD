# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Filipe La√≠ns <lains@archlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Rachel Mant <aur@dragonmux.network>

pkgbase=kicad-library
pkgname=($pkgbase{,-3d})
pkgver=9.0.4
pkgrel=1
pkgdesc='Assorted libraries for KiCad'
arch=('any')
url='http://kicad.org/'
license=('CC-BY-SA-4.0')
makedepends=('git' 'cmake')
options=('!strip' '!debug')
source=(
  "git+https://gitlab.com/kicad/libraries/kicad-packages3D.git#tag=$pkgver"
  "git+https://gitlab.com/kicad/libraries/kicad-symbols.git#tag=$pkgver"
  "git+https://gitlab.com/kicad/libraries/kicad-templates.git#tag=$pkgver"
  "git+https://gitlab.com/kicad/libraries/kicad-footprints.git#tag=$pkgver"
)
sha512sums=('1ddf41e0ee3e5e9a4126eedd5b9d1ea116176e826a0b4bd6ab16ab682778622185c7041e9ce81362286426fccfb0d1d505b1cd9bcf2f9d10856f97168f9d3f45'
            '99d757a9f63b31a01c288001b2bfbe9d579ac24b7e9ae5c7141ee48adebfb84a808c2857555c509e4b11c0637b062dfed5e3a5fa2be5ad67ae2911902f8cd928'
            '46b8685d7104decb21569aacad2a7941bb3101b09428039e3e17068a1261fa3e26c5a0dd2767ca56264f3bb5bf25358587a0878d50e698776375ab2ad9d9ddd6'
            '9d986e7ea02355cd930af7fb07d80a132b6fd5c18434ebb496d739a1554b2ff7d9b08cd8518de14619ebcb98f155784b09e0f1ec8144f881a9b90efac807c44a')
b2sums=('02bc7a9bb35d6ebd2d771f7f17370e3f856d68012fd7a45a8218de6b687f07dd8e09f3f9c810846fd03779c9861360a76a4e62dae411c31cbefa9433e7377fa8'
        '0b83d6fa487a9e422688634a1ad17af6048f61bac856cc41b06565dfab1c88185a016c2752b3341535844ebbb067c7b98e61bcc0c186f58775a075b76e48e21d'
        'c54b437f06d78baa1123ad0e16028f12068082cbfb9a51c64abc7f44d62499761304bf3a5f97e0374f5f9cf3de6bcbf8aab3bdbc576747eb794cd669ec26fab6'
        'a0cf80c776c5254c15a4427ecd94800bf24794a18bbe21499ef6ca212c8d820be4602cd488b072221d7432574717fde97b4da88705befb97cc98ecc0bdf5b624')

build() {
  for package in symbols footprints packages3D templates; do
     cmake \
       -B "build-$package" \
       -S "kicad-$package" \
       -DCMAKE_INSTALL_PREFIX=/usr

     cmake --build "build-$package"
   done
}

package_kicad-library() {
  pkgdesc="KiCad symbol, footprint and template libraries"

  for package in symbols footprints templates; do
    DESTDIR="$pkgdir" cmake --install "build-$package"
  done
}

package_kicad-library-3d() {
  pkgdesc="KiCad 3D model libraries"

  DESTDIR="$pkgdir" cmake --install build-packages3D
}

# vim:set ts=2 sw=2 et:
