# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Filipe La√≠ns <lains@archlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Rachel Mant <aur@dragonmux.network>

pkgbase=kicad-library
pkgname=($pkgbase{,-3d})
pkgver=9.0.1
pkgrel=1
pkgdesc='Assorted libraries for KiCad'
arch=('any')
url='http://kicad.org/'
license=('CC-BY-SA-4.0')
makedepends=('git' 'cmake')
options=('!strip' '!debug')
source=(
  "git+https://gitlab.com/kicad/libraries/kicad-packages3D.git#tag=$pkgver"
  "git+https://gitlab.com/kicad/libraries/kicad-symbols.git#tag=$pkgver"
  "git+https://gitlab.com/kicad/libraries/kicad-templates.git#tag=$pkgver"
  "git+https://gitlab.com/kicad/libraries/kicad-footprints.git#tag=$pkgver"
)
sha512sums=('116e1338e1ecedcd91130140d85ea605536369c41d0929970bbcd3525adec4d676f17f5393d36b87bd8bf3de2ef7fd3933d64707e85f5a81bd8873a989fae194'
            '45d340f8858dd2e2e4660007678e69a5804c3b42cc1c39c38b6e2c709e9fcbfe44ba162b3ef3634e1be283378b59115cbaab081b9b7ef7c96fe6559692fe206d'
            '46b8685d7104decb21569aacad2a7941bb3101b09428039e3e17068a1261fa3e26c5a0dd2767ca56264f3bb5bf25358587a0878d50e698776375ab2ad9d9ddd6'
            '00512839a27371b61e3b4f0203e9e04a09395c5cb4dd1e57d759951c070d43a216e98bf2299ccfa1d9f40285ce6de87dfcb5f6fb00d6c016b1c717e14da64ed0')
b2sums=('844372dab2d4466966b9375843466cfc521ec3f3c246fff8ac2043e1d89e2f03a8993ae7137b11ffa8cd8075d77fe26f91d3d17e68a19f053fad87318f649952'
        'f9064ea03e2f1b166f4da04ae8b75008bc88211b7367f70e10b0263d75439dfe301f95d85393278e432000c11274bd1ec627b372b2ea89d7c6ce55c096858fc9'
        'c54b437f06d78baa1123ad0e16028f12068082cbfb9a51c64abc7f44d62499761304bf3a5f97e0374f5f9cf3de6bcbf8aab3bdbc576747eb794cd669ec26fab6'
        '64461c3ea23ef4cb03dff84e8971164b73ccafbceccac517b6dbfd9e23a72b00b976450e5862d592c39bd82d39c66f9e9d19564475c862f51d5609dcf2c02fb5')

build() {
  for package in symbols footprints packages3D templates; do
     cmake \
       -B "build-$package" \
       -S "kicad-$package" \
       -DCMAKE_INSTALL_PREFIX=/usr

     cmake --build "build-$package"
   done
}

package_kicad-library() {
  pkgdesc="KiCad symbol, footprint and template libraries"

  for package in symbols footprints templates; do
    DESTDIR="$pkgdir" cmake --install "build-$package"
  done
}

package_kicad-library-3d() {
  pkgdesc="KiCad 3D model libraries"

  DESTDIR="$pkgdir" cmake --install build-packages3D
}

# vim:set ts=2 sw=2 et:
