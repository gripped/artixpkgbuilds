# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Filipe La√≠ns <lains@archlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Rachel Mant <aur@dragonmux.network>

pkgbase=kicad-library
pkgname=($pkgbase{,-3d})
pkgver=9.0.0
pkgrel=1
pkgdesc='Assorted libraries for KiCad'
arch=('any')
url='http://kicad.org/'
license=('CC-BY-SA-4.0')
makedepends=('git' 'cmake')
options=('!strip' '!debug')
source=(
  "git+https://gitlab.com/kicad/libraries/kicad-packages3D.git#tag=$pkgver"
  "git+https://gitlab.com/kicad/libraries/kicad-symbols.git#tag=$pkgver"
  "git+https://gitlab.com/kicad/libraries/kicad-templates.git#tag=$pkgver"
  "git+https://gitlab.com/kicad/libraries/kicad-footprints.git#tag=$pkgver"
)
sha512sums=('06943bed8284640b663e22550856d094929f6eed121c6484afde1b3f940a16196ac1084727a3cd4d61deddaf5ec66e4c7ba2d749b30e96c09b8160a1e6003de3'
            '0360c95b514b12e6051a4f6b6d5bd008912006e69b19693583c685c69e87b6f26265b4ca0dccfac08943afea65e5a70b26c6be037f76e87ab8d895969b8fa2e0'
            '46b8685d7104decb21569aacad2a7941bb3101b09428039e3e17068a1261fa3e26c5a0dd2767ca56264f3bb5bf25358587a0878d50e698776375ab2ad9d9ddd6'
            '084c49dfc0d35e4af72c6cdd4207ad3db8a95a53569e56770a6aab19a53f126dc263b04b3c9e11391852472c7bf5d43c5d1397f704f1f388c757105c7f0f93c5')
b2sums=('c85247e2d2f57a4397c4388b0a0a61837f2872607d624f7cb15799cac01ac798ead9dd2932f930a29116c1ee5868788f3368fea3c9f27bca35fae9b4fea61296'
        '61e28f16aac3c50457869bdb75a2a9990810bc8f31e4af987f80f50044f123faec0cfe727bcc502cf11ea43b2f49931f7d496cdfcfeb2802110c3281bbda3204'
        'c54b437f06d78baa1123ad0e16028f12068082cbfb9a51c64abc7f44d62499761304bf3a5f97e0374f5f9cf3de6bcbf8aab3bdbc576747eb794cd669ec26fab6'
        '5d7f83bfc30944e18c01b8bff40fc027bdc476eefa08cade476b3bacd059a662a0f3db670370f7b457e8a4efdee2f2345f60e698d9bb10c908e2aa937de53bc8')

build() {
  for package in symbols footprints packages3D templates; do
     cmake \
       -B "build-$package" \
       -S "kicad-$package" \
       -DCMAKE_INSTALL_PREFIX=/usr

     cmake --build "build-$package"
   done
}

package_kicad-library() {
  pkgdesc="KiCad symbol, footprint and template libraries"

  for package in symbols footprints templates; do
    DESTDIR="$pkgdir" cmake --install "build-$package"
  done
}

package_kicad-library-3d() {
  pkgdesc="KiCad 3D model libraries"

  DESTDIR="$pkgdir" cmake --install build-packages3D
}

# vim:set ts=2 sw=2 et:
