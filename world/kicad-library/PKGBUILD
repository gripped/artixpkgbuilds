# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Filipe La√≠ns <lains@archlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Rachel Mant <aur@dragonmux.network>

pkgbase=kicad-library
pkgname=($pkgbase{,-3d})
pkgver=9.0.5
pkgrel=1
pkgdesc='Assorted libraries for KiCad'
arch=(any)
url='http://kicad.org/'
license=(CC-BY-SA-4.0)
makedepends=(git cmake)
options=(!strip !debug)
source=(
  "git+https://gitlab.com/kicad/libraries/kicad-packages3D.git#tag=$pkgver"
  "git+https://gitlab.com/kicad/libraries/kicad-symbols.git#tag=$pkgver"
  "git+https://gitlab.com/kicad/libraries/kicad-templates.git#tag=$pkgver"
  "git+https://gitlab.com/kicad/libraries/kicad-footprints.git#tag=$pkgver"
)
sha512sums=('978840e7b5aca7caaf1b76509b4dac03025fa265764febc11dc24ab13da73876b221cd04642878a185ba3fa0865266501694a90054a333187b076c3063633fad'
            '4f502037c48a182eebaec182455de4c84c8e0ba40090e2f232376ecfa2367269497e5e6e9d52f4d93daca80ff900e718e05ae79a87b5ee792840c1669d77c6e1'
            '46b8685d7104decb21569aacad2a7941bb3101b09428039e3e17068a1261fa3e26c5a0dd2767ca56264f3bb5bf25358587a0878d50e698776375ab2ad9d9ddd6'
            'dd0023c5faf1f4f401e8f349e0e04f1038925c00c9e02de13e1df09805ce875203e4c31633ef393b7411dcf3d29f501dbe49292eb52d5e224ff6a78d1460e2da')
b2sums=('4f70e2a910190a8b4316a589b780e8d89be58d7e60353fd8f8203d7290f9ee32dced4eead8f3caafa579669f48447fe9fbab731228316da9db52c713e8957f68'
        '9190f19a667f72ca56e4e127ccda6a0f0e88b0779fd10453ee992e25a27c5b97d57d0e7cefedc6f18618fd66fe3b9b377d52e519ab803d59dd0be8aa360178de'
        'c54b437f06d78baa1123ad0e16028f12068082cbfb9a51c64abc7f44d62499761304bf3a5f97e0374f5f9cf3de6bcbf8aab3bdbc576747eb794cd669ec26fab6'
        'c21fd624340e4fe2582a79b078c889bccd3491184030dd19e1f7c66ec8c5bf061f1da1539a41d4b90a922aaf9a1723acebccda9b0b4935743fcbe442e1a63191')

build() {
  for package in symbols footprints packages3D templates; do
     cmake \
       -B "build-$package" \
       -S "kicad-$package" \
       -DCMAKE_INSTALL_PREFIX=/usr

     cmake --build "build-$package"
   done
}

package_kicad-library() {
  pkgdesc="KiCad symbol, footprint and template libraries"

  for package in symbols footprints templates; do
    DESTDIR="$pkgdir" cmake --install "build-$package"
  done
}

package_kicad-library-3d() {
  pkgdesc="KiCad 3D model libraries"

  DESTDIR="$pkgdir" cmake --install build-packages3D
}

# vim:set ts=2 sw=2 et:
