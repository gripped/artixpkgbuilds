# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Filipe La√≠ns <lains@archlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Rachel Mant <aur@dragonmux.network>

pkgbase=kicad-library
pkgname=($pkgbase{,-3d})
pkgver=9.0.2
pkgrel=1
pkgdesc='Assorted libraries for KiCad'
arch=('any')
url='http://kicad.org/'
license=('CC-BY-SA-4.0')
makedepends=('git' 'cmake')
options=('!strip' '!debug')
source=(
  "git+https://gitlab.com/kicad/libraries/kicad-packages3D.git#tag=$pkgver"
  "git+https://gitlab.com/kicad/libraries/kicad-symbols.git#tag=$pkgver"
  "git+https://gitlab.com/kicad/libraries/kicad-templates.git#tag=$pkgver"
  "git+https://gitlab.com/kicad/libraries/kicad-footprints.git#tag=$pkgver"
)
sha512sums=('f9a620ea79e39bf0f106769e2f651565a8957b8a675d6219d6f16a8b2fd7e2375e0c26f78d17cad6e88f06bcf9c071e52d37ccdafc41cec22abd5c4161b10601'
            '4cdea15c0d1b53566592406fe2c7841339019b3685b823564b2137668df15ab0ff65cba58e7313d43a5c76e5032de772f82a7c3ec9791b5dbe426b259631f955'
            '46b8685d7104decb21569aacad2a7941bb3101b09428039e3e17068a1261fa3e26c5a0dd2767ca56264f3bb5bf25358587a0878d50e698776375ab2ad9d9ddd6'
            'fd65541d7ef4420b4142aece798bfb93fc4626f584236d0cb6ca8ca5c6fc47f5bcfb49cd03a81819d32558288af403447c3ead591812dd136de69aded8d16aab')
b2sums=('6e4751d3097c0726aaeef2f2e5eb78a8aebd37e50e35fa27ebc46d927c933f8d6599ba7b6902efab92b8ea1291a7e2e164ef60ec0c3d006211b247dab45fef0f'
        '86fe5ba30fe68c8e8d7f92dae32235e4bac7027bd6394f643a1304e4d4933758aeeccdf91112b9770f8646ef200fce69dd951cc040068ddd05f20745e75a5c19'
        'c54b437f06d78baa1123ad0e16028f12068082cbfb9a51c64abc7f44d62499761304bf3a5f97e0374f5f9cf3de6bcbf8aab3bdbc576747eb794cd669ec26fab6'
        '60f2e4a3e17504bd44e022ea2c600d04da34bd9952f2cbd0d34d0f113108d7b70df0b6b61929457520695601bd728e0ff3fdb623179e19e66fd86d9c011e8ea0')

build() {
  for package in symbols footprints packages3D templates; do
     cmake \
       -B "build-$package" \
       -S "kicad-$package" \
       -DCMAKE_INSTALL_PREFIX=/usr

     cmake --build "build-$package"
   done
}

package_kicad-library() {
  pkgdesc="KiCad symbol, footprint and template libraries"

  for package in symbols footprints templates; do
    DESTDIR="$pkgdir" cmake --install "build-$package"
  done
}

package_kicad-library-3d() {
  pkgdesc="KiCad 3D model libraries"

  DESTDIR="$pkgdir" cmake --install build-packages3D
}

# vim:set ts=2 sw=2 et:
