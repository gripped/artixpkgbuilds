# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: KokaKiwi <admin@kokaelkiwi.eu>
# Contributor: James Bulmer <nekinie@gmail.com>

pkgname=nanomsg
pkgver=1.2.5
pkgrel=1
pkgdesc='Simple high-performance implementation of several "scalability protocols"'
arch=(x86_64)
url='https://nanomsg.org/'
license=(MIT)
depends=(glibc)
makedepends=(cmake)
source=("https://github.com/nanomsg/nanomsg/archive/$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('a9a44da886c6e89ff393cecaeab72623c0a857f527ffb1ae741e863b999996602c8fdf60ea697b258ac911bdf8e5ba67b31951944a2bbe30533d07cc3c1deed6')

build() {
  cd $pkgname-$pkgver
  cmake -S . -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev
  cmake --build build
}

check() {
  cd $pkgname-$pkgver
  ctest --test-dir build --output-on-failure
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" COPYING
}
