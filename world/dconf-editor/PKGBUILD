# Maintainer: Fabian Bornschein <fabiscafe-at-mailbox-dot-org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=dconf-editor
pkgver=45.0.1
pkgrel=2
pkgdesc="GSettings editor for GNOME"
url="https://apps.gnome.org/DconfEditor/"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  dconf
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  libhandy
)
makedepends=(
  git
  meson
  vala
)
groups=(gnome-extra)
source=("git+https://gitlab.gnome.org/GNOME/dconf-editor.git#tag=${pkgver/[a-z]/.&}")
b2sums=('cd74f1f5736469c2799b6688fdd4669374354cbceaa25717aa4c4dd98f4c2ab631ff94a82ac2fdac41535372ae85c5fc70498594e44f5ece3830f0faf1e0a888')

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
