# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=cantarell-fonts
pkgver=0.311
pkgrel=1
epoch=1
pkgdesc="Humanist sans serif font"
url="https://gitlab.gnome.org/GNOME/cantarell-fonts"
arch=(any)
license=(OFL-1.1)
makedepends=(
  git
  meson
  python
  uv
)
source=(
  "git+$url.git#tag=${pkgver/[a-z]/.&}"
)
b2sums=('49bf549cd6fa44ec8fa852715d3ab6b5076897ab8cf9b50a86d901e60365bc3ef7535155b6e1917d0ad18023f61335e6453dacccb584f35c0ca025baca4323c8')

prepare() {
  cd $pkgname
  uv sync
}

build() (
  . $pkgname/.venv/bin/activate
  artix-meson $pkgname build
  meson compile -C build
)

package() (
  . $pkgname/.venv/bin/activate
  meson install -C build --destdir "$pkgdir"
  install -Dm644 $pkgname/COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
)

# vim:set sw=2 sts=-1 et:
