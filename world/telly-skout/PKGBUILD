# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=telly-skout
pkgver=24.02.1
pkgrel=1
pkgdesc='Convergent TV guide based on Kirigami'
url='https://apps.kde.org/telly-skout/'
arch=(x86_64)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kconfig5
         kcoreaddons5
         kcrash5
         ki18n5
         kirigami-addons5
         kirigami2
         qt5-base
         qt5-declarative
         qt5-quickcontrols2)
makedepends=(extra-cmake-modules)
groups=(kde-applications
        kde-utilities)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('58ab40b548234fa3ee7b7687f0229272cb1e6886b4213fc737f0b88b2fe7f4d8'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  artix-cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
