# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-bake-test-external
pkgver=0.4.0
pkgrel=1
pkgdesc='Run external test suites to check for breakage'
arch=(any)
url='https://github.com/ioquatix/bake-test-external'
license=(MIT)
depends=(
  ruby
  ruby-bake
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  git
  ruby-bake-modernize
  ruby-bake-test
  ruby-bundler
  ruby-rspec
  ruby-sus
)
options=(!emptydirs)
source=(git+https://github.com/ioquatix/bake-test-external.git#tag=v$pkgver)
sha256sums=('70777470aaf40349aff91bd0a378b8de57a9aab34de0d122170b3e214a7cd3b3')

prepare() {
  cd bake-test-external
  sed -e '/signing_key/d' -i bake-test-external.gemspec
  sed -i '/bake-gem/d' gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd bake-test-external
  gem build bake-test-external.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    bake-test-external-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd bake-test-external
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd bake-test-external
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 license.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
