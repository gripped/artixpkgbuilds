# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=openshadinglanguage
pkgver=1.14.5.1
pkgrel=1
pkgdesc="Advanced shading language for production GI renderers"
arch=('x86_64')
url="https://github.com/imageworks/OpenShadingLanguage"
license=('custom')
depends=('openimageio' 'imath' 'freetype2' 'libpng' 'libtiff' 'zlib'
         'ncurses' 'clang' 'qt6-base')
makedepends=('cmake' 'python' 'llvm' 'ninja' 'git')
optdepends=('python: the Python module')
source=($pkgname-$pkgver.tar.gz::https://github.com/AcademySoftwareFoundation/OpenShadingLanguage/archive/refs/tags/v${pkgver}.tar.gz
        osl-llvm20.patch)
sha512sums=('4dfa3659dacc78dc82b943f0d16685e9353924fab40127c69cb2fbdf6d0794970bf283f8711119c31369b9fe507c45e12137d2121f217678da9be86b44b4e2dc'
            '081827e53c756fd9c771720c7fedac94381b709db958203a61e9a0f9b52d679b79a6ea1ec049abda78b0bcbfdf05eeef0d3dac2c3f8a6c3973962c807fc10043')

prepare() {
  cd OpenShadingLanguage-$pkgver

  patch -Np1 -i "$srcdir"/osl-llvm20.patch
}

build() {
  cd OpenShadingLanguage-$pkgver

  cmake \
    -B build \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_CXX_STANDARD=17 \
    -DSTOP_ON_WARNING=OFF
  ninja -C build
}

package() {
  cd OpenShadingLanguage-$pkgver

  DESTDIR="$pkgdir/" ninja -C build install

  # Fixup an upstream fucky (See #1)
  rm "$pkgdir"/usr/build-scripts/serialize-bc.py
  rmdir "$pkgdir"/usr/build-scripts
  rm "$pkgdir"/usr/cmake/llvm_macros.cmake
  rmdir "$pkgdir"/usr/cmake

  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}

# vim:set ts=2 sw=2 et:
