# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=openshadinglanguage
pkgver=1.14.6.0
pkgrel=1
pkgdesc="Advanced shading language for production GI renderers"
arch=('x86_64')
url="https://github.com/imageworks/OpenShadingLanguage"
license=('custom')
depends=('openimageio' 'imath' 'freetype2' 'libpng' 'libtiff' 'zlib'
         'ncurses' 'clang' 'qt6-base')
makedepends=('cmake' 'python' 'llvm' 'ninja' 'git')
optdepends=('python: the Python module')
source=($pkgname-$pkgver.tar.gz::https://github.com/AcademySoftwareFoundation/OpenShadingLanguage/archive/refs/tags/v${pkgver}.tar.gz)
sha512sums=('3ac8fa5a2a595fa659e9982ac76556bcc4b68a6ed907cb3f300b9da69e72ec487a5f83cce61d671e8418d948b86945e2ee8471dfc3dc4cf00cbe596bf07725f9')

build() {
  cd OpenShadingLanguage-$pkgver

  cmake \
    -B build \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_CXX_STANDARD=17 \
    -DSTOP_ON_WARNING=OFF
  ninja -C build
}

package() {
  cd OpenShadingLanguage-$pkgver

  DESTDIR="$pkgdir/" ninja -C build install

  # Fixup an upstream fucky (See #1)
  rm "$pkgdir"/usr/build-scripts/serialize-bc.py
  rmdir "$pkgdir"/usr/build-scripts
  rm "$pkgdir"/usr/cmake/llvm_macros.cmake
  rmdir "$pkgdir"/usr/cmake

  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}

# vim:set ts=2 sw=2 et:
