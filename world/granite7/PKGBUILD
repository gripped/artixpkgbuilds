# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=granite7
pkgver=7.8.0
pkgrel=1
pkgdesc='Library that extends Gtk+'
arch=(x86_64)
url=https://github.com/elementary/granite
license=(LGPL3)
groups=(pantheon)
depends=(
  cairo
  gdk-pixbuf2
  glib2
  gtk4
  libgee
  pango
)
makedepends=(
  cmake
  git
  gobject-introspection
  libshumate
  meson
  sassc
  vala
)
provides=(libgranite-7.so)
_tag=9d59c4a348de7f69c312ce9eabc154dcdc6c550f
source=(git+https://github.com/elementary/granite.git#tag=${_tag})
sha256sums=('d11bb8e9646acfcbfb27c323f167c51b9e4c8e607af61885ec623d1ac11b2389')

pkgver() {
  cd granite
  git describe --tags
}

build() {
  artix-meson granite build \
    -D b_pie=false
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
