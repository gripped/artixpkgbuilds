# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=granite7
pkgver=7.7.0
pkgrel=1
pkgdesc='Library that extends Gtk+'
arch=(x86_64)
url=https://github.com/elementary/granite
license=(LGPL3)
groups=(pantheon)
depends=(
  cairo
  gdk-pixbuf2
  glib2
  gtk4
  libgee
  pango
)
makedepends=(
  cmake
  git
  gobject-introspection
  libshumate
  meson
  sassc
  vala
)
provides=(libgranite-7.so)
_tag=ae94e9123bbf1dea8db38bedd8a925dbd5a8f812
source=(git+https://github.com/elementary/granite.git#tag=${_tag})
sha256sums=('8dd5f6584ac62f1d3b86de76f4eb13c21f59e19b7e6e3e30f88ddf1a89fbba10')

pkgver() {
  cd granite
  git describe --tags
}

build() {
  artix-meson granite build \
    -D b_pie=false
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
