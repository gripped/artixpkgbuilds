# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=kmail
pkgver=25.04.2
pkgrel=1
pkgdesc='KDE mail client'
arch=(x86_64)
url='https://apps.kde.org/kmail2/'
license=(GPL-2.0-or-later)
depends=(akonadi-contacts
         akonadi-import-wizard
         akonadi-mime
         akonadi-search
         gcc-libs
         glibc
         gpgmepp
         kbookmarks
         kcalendarcore
         kcalutils
         kcmutils
         kcodecs
         kcolorscheme
         kcompletion
         kconfig
         kconfigwidgets
         kcontacts
         kcoreaddons
         kcrash
         kdbusaddons
         kdepim-runtime
         kguiaddons
         ki18n
         kiconthemes
         kidentitymanagement
         kio
         kitemmodels
         kitemviews
         kjobwidgets
         kldap
         kmail-account-wizard
         kmailtransport
         kmime
         knotifications
         knotifyconfig
         kontactinterface
         kparts
         kpimtextedit
         kservice
         kstatusnotifieritem
         ktextaddons
         ktnef
         kuserfeedback
         kwidgetsaddons
         kwindowsystem
         kxmlgui
         libakonadi
         libgpg-error
         libgravatar
         libkdepim
         libkleo
         libksieve
         mailcommon
         mbox-importer
         messagelib
         pim-data-exporter
         pim-sieve-editor
         pimcommon
         qgpgme
         qt6-base
         qt6-webengine
         qtkeychain-qt6
         sonnet)
makedepends=(extra-cmake-modules
             kdoctools)
optdepends=('kdepim-addons: additional plugins'
            'kleopatra: mail encryption support')
groups=(kde-applications
        kde-pim)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('ccb6d5998c64959454879c1b059c7fe2cd1dc05b88f698fc2c60e61deee19433'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
