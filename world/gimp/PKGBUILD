# Maintainer: Christian Hesse <mail@eworm.de>
# Maintainer: Christian Heusel <gromit@archlinux.org>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Isenmann <daniel@archlinux.org>

pkgname=gimp
_pkgver=3.0.0
pkgver=3.0.0
pkgrel=1
pkgdesc='GNU Image Manipulation Program'
url='https://www.gimp.org/'
arch=('x86_64')
license=('GPL-3.0-or-later')
depends=(
  # Core deps
  'appstream-glib'
  'babl'
  'cairo'
  'fontconfig'
  'freetype2'
  'gcc-libs'
  'gdk-pixbuf2'
  'gegl'
  'glib2'
  'glibc'
  'gtk3'
  'harfbuzz'
  'hicolor-icon-theme'
  'iso-codes'
  'json-glib'
  'lcms2'
  'libgexiv2'
  'libmypaint'
  'libunwind'
  'mypaint-brushes1'
  'pango'
  'python-gobject'
  'zlib'

  # Plugins deps
  'aalib'
  'bzip2'
  'libgudev'
  'libheif'
  'libjpeg-turbo'
  'libjxl'
  'libmng'
  'libpng'
  'librsvg'
  'libtiff'
  'libwebp'
  'libwmf'
  'libx11'
  'libxcursor'
  'libxext'
  'libxfixes'
  'libxmu'
  'libxpm'
  'openexr'
  'openjpeg2'
  'poppler-data'
  'poppler-glib'
  'xz'
)
makedepends=(
  'alsa-lib'
  'appstream'
  'cfitsio'
  'ghostscript'
  'gi-docgen'
  'gjs'
  'glib2-devel'
  'gobject-introspection'
  'gtk-doc'
  'gvfs'
  'intltool'
  'meson'
  'vala'
)
optdepends=(
  'alsa-lib: for MIDI event controller module'
  'cfitsio: for FITS support'
  'ghostscript: for PostScript support'
  'gjs: for JavaScript scripting support'
  'gutenprint: for sophisticated printing only as gimp has built-in cups print support'
  'gvfs: for HTTP/S support (and many other schemes)'
)
install=gimp.install
source=(
  # https://download.gimp.org/pub/gimp/v${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2
  https://download.gimp.org/gimp/v${pkgver%.*}/${pkgname}-${_pkgver}.tar.xz
  0001-no-check-update.patch
  0002-fix-detection-of-libheif-1.15.0.patch
  linux.gpl
)
sha256sums=('93f1ca3d9d1bd8cac0e52c49fb886cbbe4b28222ee835bf1319e3287901d2d20'
            'ac3e8b44cf391f4ab3050652f2cc1f146f451fb25178d5a596d905f5bad13fcf'
            '24814e981121830242f0a9b7d1da99e7282b247b87b482e2b394cff75b4675ef'
            '1003bbf5fc292d0d63be44562f46506f7b2ca5729770da9d38d3bb2e8a2f36b3')

prepare() {
  cd ${pkgname}-${_pkgver}

  # TODO: check if those patches are still needed
  # if yes they need to be rebased
  # patch -Np1 < ../0001-no-check-update.patch
  # patch -Np1 < ../0002-fix-detection-of-libheif-1.15.0.patch
}

build() {
  local meson_options=(
    --buildtype=plain
    --prefix=/usr
    --sysconfdir=/etc
    --libexecdir=/usr/bin \
    -Dopenmp=enabled
    -Dcheck-update=no
    -Dbug-report-url='https://gitlab.archlinux.org/archlinux/packaging/packages/gimp/-/issues'
    -Dopenexr=enabled
    -Dilbm=disabled
    -Dheadless-tests=disabled # enabled by default, depends on xorg-server-xvfb
  )

  artix-meson ${pkgname}-${_pkgver} build "${meson_options[@]}"
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
