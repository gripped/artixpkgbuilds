# Maintainer: Christian Hesse <mail@eworm.de>
# Maintainer: Christian Heusel <gromit@archlinux.org>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Isenmann <daniel@archlinux.org>

pkgname=gimp
pkgver=3.0.8
pkgrel=2
pkgdesc='GNU Image Manipulation Program'
url='https://www.gimp.org/'
arch=('x86_64')
license=('GPL-3.0-or-later')
depends=(
  # Core deps
  'appstream'
  'babl'
  'cairo'
  'fontconfig'
  'freetype2'
  'gcc-libs'
  'gdk-pixbuf2'
  'gegl'
  'glib2'
  'glibc'
  'gtk3'
  'harfbuzz'
  'hicolor-icon-theme'
  'iso-codes'
  'json-glib'
  'lcms2'
  'libgexiv2'
  'libmypaint'
  'libunwind'
  'mypaint-brushes1'
  'pango'
  'python-gobject'
  'zlib'

  # Plugins deps
  'aalib'
  'bzip2'
  'libgudev'
  'libheif'
  'libjpeg-turbo'
  'libjxl'
  'libmng'
  'libpng'
  'librsvg'
  'libtiff'
  'libwebp'
  'libwmf'
  'libx11'
  'libxcursor'
  'libxext'
  'libxfixes'
  'libxmu'
  'libxpm'
  'openexr'
  'openjpeg2'
  'poppler-data'
  'poppler-glib'
  'xz'
)
makedepends=(
  'alsa-lib'
  'bash-completion'
  'cfitsio'
  'ghostscript'
  'gi-docgen'
  'git'
  'gjs'
  'glib2-devel'
  'gobject-introspection'
  'gtk-doc'
  'gvfs'
  'intltool'
  'meson'
  'qoi'
  'vala'
)
optdepends=(
  'alsa-lib: for MIDI event controller module'
  'bash-completion: for bash completion support'
  'cfitsio: for FITS support'
  'ghostscript: for PostScript support'
  'gjs: for JavaScript scripting support'
  'gutenprint: for sophisticated printing only as gimp has built-in cups print support'
  'gvfs: for HTTP/S support (and many other schemes)'
)
install=gimp.install
source=(
  git+https://gitlab.gnome.org/GNOME/gimp.git#tag=GIMP_${pkgver//./_}
  git+https://gitlab.gnome.org/GNOME/gimp-data.git
)
sha256sums=('7737db8e51e7a95a43734cfe8c01cdf8a8391c9d19262348c3f5bee42a0bda04'
            'SKIP')

prepare() {
  cd $pkgname
  git submodule init
  git config submodule.gimp-data.url "$srcdir/gimp-data"
  git -c protocol.file.allow=always submodule update

  # https://gitlab.archlinux.org/archlinux/packaging/packages/gimp/-/issues/12
  git cherry-pick -n 1685c86af5d6253151d0056a9677ba469ea10164
}

build() {
  local meson_options=(
    --sysconfdir=/etc
    --libexecdir=/usr/bin \
    -Dopenmp=enabled
    -Dcheck-update=no
    -Dbug-report-url='https://gitlab.archlinux.org/archlinux/packaging/packages/gimp/-/issues'
    -Dopenexr=enabled
    -Dilbm=disabled
    -Dheadless-tests=disabled # enabled by default, depends on xorg-server-xvfb
  )

  artix-meson ${pkgname} build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
