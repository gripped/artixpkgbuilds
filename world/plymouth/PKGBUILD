# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Taijian <taijian@posteo.de>
# Contributor: Sebastian Lau <lauseb644@gmail.com>
# Contributor: Damian01w <damian01w@gmail.com>
# Contributor: Padfoot <padfoot@exemail.com.au>

pkgname=plymouth
pkgver=26.134.222
pkgrel=3
pkgdesc='Graphical boot splash screen'
arch=(x86_64)
url='https://www.freedesktop.org/wiki/Software/Plymouth/'
license=(GPL-2.0-or-later)
depends=(
  adwaita-fonts
  bash
  cairo
  filesystem
  fontconfig
  freetype2
  glib2
  glibc
  libdrm
  libevdev
  libpng
  libx11
  libxkbcommon
  pango
  xkeyboard-config
)
makedepends=(
  docbook-xsl
  git
  gtk3
  meson
)
optdepends=('gtk3: x11 renderer')
backup=(
  etc/logrotate.d/bootlog
  etc/plymouth/plymouthd.conf
)
install='plymouth.install'
source=(
  "git+https://gitlab.freedesktop.org/plymouth/$pkgname.git#tag=$pkgver"
  plymouth-font-file.c
  plymouth.initcpio_hook
  plymouth.initcpio_install
  plymouth-shutdown.initcpio_install
  50-plymouth.conf
)
b2sums=('5130dbb16d4634915b7f68542b6c29d664bd8c26f4508adf536833e93f2c78aee287208e88dc3bcf8298979c6f9811657ab5a30c1563b1e5307b7e886b6bffa2'
        'b5033c46af2492d18a8308292542c4e551148ef00e98a79eb36d694922417d51173b15ada869185a6c76c4adfa85691d5ed0540e8a9fb6426a40d802027f8ef8'
        'afb2449b542aa3e971eab6b953c907347fdf4e499b4140a5e6736a7c99557c0d8d2fed28dbee56d84c8c619335c59bd382457d85e51145884ad0616e9095f232'
        '4df5509bda11da7c0524afd5a7768630634500cabc1cb2c65260b38c8af41d8c1f60ff0161a68a36bf0d4dcf04dec3aff97ef15a7a068193bcd080e4f6907e1c'
        '15b3dd628ac05659f0ea7128b873a00263aa45ef8964a2bd985c3c9bcd8df86dfcd204fd572a68cfae0d2087b4ae46224a2799e23dee1cb25fc59dbebfae22ee'
        '5ab47942d222c311b495d056f24bdd9d08438162fef1ca6e51982fc00018acbf0c31e4c61164b40950fe2511e17ba2a4635894d145d83a9b349e5a8a83f8c026')

prepare() {
  cd $pkgname

  # Fix default XKB keymap fallback overriding KEYMAP
  # https://gitlab.freedesktop.org/plymouth/plymouth/-/merge_requests/388
  git cherry-pick -n cea90be39d263e388003a07d125b469c0803d55c

  # Use mkinitcpio to update initrd
  sed -i 's/^dracut -f$/mkinitcpio -P/' scripts/plymouth-update-initrd

  # Change default theme
  sed -i 's/^Theme=spinner$/Theme=bgrt/' src/plymouthd.defaults

  # Switch from Cantarell to Adwaita Sans; set monospace font to Adwaita Mono
  sed -i 's/Cantarell/Adwaita Sans/
          /TitleFont=Adwaita/a MonospaceFont=Adwaita Mono 12' scripts/keymap-render.py themes/*/*.plymouth.desktop
}

build() {
  artix-meson $pkgname build \
    -D logo=/usr/share/pixmaps/artixlinux-logo.png -Dsystemd-integration=false
  meson compile -C build

  # Compile font helper script
  gcc -o plymouth-font-file plymouth-font-file.c $CFLAGS $LDFLAGS $(pkg-config --cflags --libs pangoft2)

  # Convert logo for the spinner theme
  rsvg-convert '/usr/share/pixmaps/artixlinux-logo-text-dark.svg' -o artixlinux-logo-text-dark.png
}

package() {
  meson install -C build --destdir "$pkgdir"
  rm -r "$pkgdir/run"

  # Install font helper script
  install -Dm755 plymouth-font-file "$pkgdir/usr/lib/plymouth/plymouth-font-file"

  # Install mkinitcpio hook
  install -Dm644 plymouth.initcpio_hook "$pkgdir/usr/lib/initcpio/hooks/$pkgname"
  install -Dm644 plymouth.initcpio_install "$pkgdir/usr/lib/initcpio/install/$pkgname"

  install -Dm644 plymouth-shutdown.initcpio_install "$pkgdir/usr/lib/initcpio/install/$pkgname-shutdown"
  install -Dm644 50-plymouth.conf "$pkgdir/usr/lib/tmpfiles.d/50-plymouth.conf"
  
  # Install logo for the spinner theme
  install -Dm644 artixlinux-logo-text-dark.png "$pkgdir/usr/share/$pkgname/themes/spinner/watermark.png"
}
