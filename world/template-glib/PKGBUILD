# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=template-glib
pkgname=(
  template-glib
  template-glib-docs
)
pkgver=3.38.0
pkgrel=2
pkgdesc="Templating library for GLib"
url="https://gitlab.gnome.org/GNOME/template-glib"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  gcc-libs
  glib2
  glibc
)
makedepends=(
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  meson
  vala
)
source=("git+https://gitlab.gnome.org/GNOME/template-glib.git#tag=$pkgver")
b2sums=('16621a53ba2d07631e371007d897e38f9f0d726d25c039dcbd6a94cc5b7671aec8a2e0ffb0c3109c4af11aab23defb8301cd5fc9498ad6317e2281ebef658cfe')

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D docs=true
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_template-glib() {
  provides=(libtemplate_glib-1.0.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_template-glib-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
