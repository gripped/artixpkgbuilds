# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=template-glib
pkgver=3.36.3
pkgrel=1
pkgdesc="A templating library for GLib"
url="https://gitlab.gnome.org/GNOME/template-glib"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  libgirepository
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  meson
  vala
)
provides=(libtemplate_glib-1.0.so)
source=("git+https://gitlab.gnome.org/GNOME/template-glib.git#tag=$pkgver")
b2sums=('bd32bd950c3e7ce64165e860b3519ccf78ab1d7d41a58a995a2f995a18a3ffc20f40b23ee27af4e2c45f19a6eed9aa38c33e1318c78d1bfc87c1f731ab7b0f02')

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
