# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Ronald van Haren <ronald@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=digikam
_pkgver=8.7.0
pkgver=${_pkgver//-/} # for beta versions
pkgrel=3
pkgdesc='An advanced digital photo management application'
arch=(x86_64)
license=(GPL-2.0-or-later)
url='https://www.digikam.org/'
depends=(akonadi-contacts
         exiv2
         expat
         ffmpeg
         gcc-libs
         glib2
         glibc
         imagemagick
         jasper
         kcalendarcore
         kcompletion
         kconfig
         kconfigwidgets
         kcontacts
         kcoreaddons
         kfilemetadata
         ki18n
         kiconthemes
         kio
         knotifications
         knotifyconfig
         kservice
         kwidgetsaddons
         kxmlgui
         lcms2
         lensfun
         libglvnd
         libgphoto2
         libheif
         libjpeg-turbo
         libjxl
         libksane
         libpng
         libtiff
         libx11
         libxml2
         libxslt
         opencv
         perl
         perl-image-exiftool
         qt6-base
         qt6-multimedia
         qt6-networkauth
         qt6-scxml
         qt6-svg
         qt6-webengine
         sh
         solid
         sonnet
         threadweaver
         x265
         zlib)
makedepends=(boost
             doxygen
             eigen
             extra-cmake-modules
             kdoctools)
optdepends=('darktable: RAW import'
            'hugin: panorama tool'
            'qt6-imageformats: support for additional image formats (WEBP, TIFF)'
            'rawtherapee: RAW import')
source=(https://download.kde.org/stable/$pkgname/${_pkgver%-*}/digiKam-$_pkgver.tar.xz{,.sig}
        https://invent.kde.org/graphics/digikam/-/commit/afe5772b.patch)
sha256sums=('79e35ec20a43934cf8322d47c9c0540b0b8b727fa7d9aceb90f710fcfe9da02f'
            'SKIP'
            'e147c37ebd5b05eae52cc81f30789e505529c3d618869c0858b642ef554b0ed8')
validpgpkeys=(D1CF2444A7858C5F2FB095B74A77747BC2386E50) # digiKam.org (digiKam project) <digikamdeveloper@gmail.com>

prepare() {
  patch -d $pkgname-$_pkgver -p1 < afe5772b.patch # Fix face recognition with openCV 4.12
}

build() {
  cmake -B build -S $pkgname-$_pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_WITH_QT6=ON \
    -DENABLE_KFILEMETADATASUPPORT=ON \
    -DENABLE_MEDIAPLAYER=ON \
    -DENABLE_AKONADICONTACTSUPPORT=ON \
    -DENABLE_MYSQLSUPPORT=ON \
    -DENABLE_APPSTYLES=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
