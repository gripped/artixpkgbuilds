# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Kevin <kevin@archlinux.org>
# Contributor: criminy <criminy@gmail.com>

pkgbase=cairomm
pkgname=(
  cairomm
  cairomm-docs
)
pkgver=1.14.5
pkgrel=2
pkgdesc="C++ bindings for Cairo"
url="https://www.cairographics.org/cairomm/"
arch=(x86_64)
license=(LGPL-2.0-or-later)
depends=(
  cairo
  gcc-libs
  glibc
  libsigc++
)
makedepends=(
  git
  libsigc++-docs
  meson
  mm-common
)
options=(!emptydirs)
source=("git+https://gitlab.freedesktop.org/cairo/cairomm.git#tag=$pkgver")
b2sums=('3df2ce4165371900d3609a6046f13496cf2b85071a89fc6acbd648482ea8a4555adfdd0657f926a1c500c1bec0141c16d653ca43675e5fbd9687e7c350772fd0')

prepare() {
  cd cairomm
}

build() {
  local meson_options=(
    -D maintainer-mode=true
  )

  artix-meson cairomm build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_cairomm() {
  depends+=(libsigc-2.0.so)
  provides=(libcairomm-1.0.so)

  meson install -C build --destdir "$pkgdir"

  # Split -docs
  mkdir -p docs/usr/share
  mv -t docs/usr/share "$pkgdir"/usr/share/{devhelp,doc}
}

package_cairomm-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv -t "$pkgdir" docs/*
}

# vim:set sw=2 sts=-1 et:
