# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=falkon
pkgver=23.08.2
pkgrel=1
pkgdesc='Cross-platform QtWebEngine browser'
arch=(x86_64)
url='https://www.falkon.org/'
license=(GPL3)
depends=(qt5-webengine qt5-x11extras qt5-svg karchive5)
makedepends=(extra-cmake-modules qt5-tools kio5 purpose5 libgnome-keyring pyside2 shiboken2)
optdepends=('kio5: KDE integration'
            'purpose5: KDE integration'
            'libgnome-keyring: gnome keyring integration'
            'pyside2: Python plugins')
groups=(kde-applications kde-network)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('57975a9be20e5c19eb9a0cc41b7eec53652e60a89b9dbc32b4d9fc1bbf4c7f6a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  artix-cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
