# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=falkon
pkgver=25.12.0
pkgrel=1
pkgdesc='Cross-platform QtWebEngine browser'
arch=(x86_64)
url='https://www.falkon.org/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         karchive
         libxcb
         openssl
         qt6-base
         qt6-declarative
         qt6-webchannel
         qt6-webengine)
makedepends=(extra-cmake-modules
             kio
             libgnome-keyring
             purpose
             pyside6
             qt6-tools
             shiboken6)
optdepends=('kcoreaddons: KDE integration'
            'kcrash: KDE integration'
            'kio: KDE integration'
            'kjobwidgets: KDE integration'
            'kwallet: KDE integration'
            'libgnome-keyring: gnome keyring integration'
            'purpose: KDE integration'
            'pyside6: Python plugins')
groups=(kde-applications
        kde-network)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('6d9b4dba1bde0d04570736ed7bb9bc1288cd7fb94b4ffe934a990912b9b7a959'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
