# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=falkon
pkgver=23.08.5
pkgrel=1
pkgdesc='Cross-platform QtWebEngine browser'
arch=(x86_64)
url='https://www.falkon.org/'
license=(GPL3)
depends=(qt5-webengine qt5-x11extras qt5-svg karchive5)
makedepends=(extra-cmake-modules qt5-tools kio5 purpose5 libgnome-keyring pyside2 shiboken2)
optdepends=('kio5: KDE integration'
            'purpose5: KDE integration'
            'libgnome-keyring: gnome keyring integration'
            'pyside2: Python plugins')
groups=(kde-applications kde-network)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/network/falkon/-/commit/27c59ed.patch)
sha256sums=('800a5f1432a6e50f64265eb4bbd8982f445ddd436b5e072f3e49bb4e7183b077'
            'SKIP'
            '1e751930e29d21074ea4629bc670bbe313697fdff7766b1026b9758819eb00fb')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  cd $pkgname-$pkgver
  # Fix form detection for password managers
  patch -Np1 -i ../27c59ed.patch
}

build() {
  artix-cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
