# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kgraphviewer
pkgver=24.08.0
pkgrel=1
pkgdesc='A Graphviz dot graph file viewer'
arch=(x86_64)
url='https://apps.kde.org/kgraphviewer/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         graphviz
         kconfig
         kconfigwidgets
         kcoreaddons
         ki18n
         kparts
         kwidgetsaddons
         kxmlgui
         qt6-5compat
         qt6-base
         qt6-svg)
makedepends=(boost
             extra-cmake-modules
             kdoctools)
groups=(kde-applications
        kde-graphics)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('f2efe1591be2794df6eb299ed6ab32629bf91208c097a0e2a5fe89c8ce5c8cd1'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build(){
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
