# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='sqlite3'
pkgname="ruby-${_gemname}"
pkgver=2.9.2
pkgrel=1
pkgdesc='Ruby bindings for the SQLite3 embedded database'
url='https://github.com/luislavena/sqlite3-ruby'
arch=('x86_64')
license=('BSD-3-Clause')
options=('!emptydirs')
depends=(
  ruby
  ruby-mini_portile2
  sqlcipher
  sqlite
)
makedepends=(
  ruby-bundler
  ruby-rake
  ruby-rake-compiler
  ruby-rake-compiler-dock
  ruby-rdoc
)
checkdepends=(
  ruby-minitest
)
source=("https://github.com/sparklemotion/sqlite3-ruby/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha512sums=('135412c42fc0e2ba1f89ff657743e7f9c2a82eafbf76c195d3191f924d563700bd73ee6a25d2e6fddeabf6c2d41694e83c4b5654dd281335020ef67d98282b02')
b2sums=('890e8f8151741b5149e182d98bfeb3cb26e12e4d63226670977df70101c4dcdcb46b644df888982fea3ba8046b7fb49ca1f066ec4ab485aa8e7741742d91dba1')

prepare() {
  cd "${_gemname}-ruby-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended \
    --expression 's|~>|>=|g' \
    "${_gemname}.gemspec"

  # remove rubocop
  rm --verbose rakelib/format.rake

  sed --in-place --regexp-extended \
    --expression 's|:rubocop, ||g' \
    Rakefile

  sed --in-place --regexp-extended \
    --expression '/rubocop/d' \
    Gemfile
}

build() {
  cd "${_gemname}-ruby-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  rake compile -- --enable-system-libraries --with-sqlcipher
  rake build

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --build-root "tmp_install" \
    "pkg/${_gemname}-${pkgver}.gem"
}

check() {
  cd "${_gemname}-ruby-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rake test
}

package() {
  cd "${_gemname}-ruby-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
