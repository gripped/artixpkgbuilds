# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-photos
pkgver=44.0
pkgrel=5
epoch=1
pkgdesc="Access, organize, and share your photos on GNOME"
url="https://wiki.gnome.org/Apps/Photos"
arch=(x86_64)
license=(GPL)
depends=(
  babl
  dleyna
  gegl
  geocode-glib-2
  gnome-online-accounts
  gsettings-desktop-schemas
  gtk3
  libdazzle
  libgexiv2
  libhandy
  libportal-gtk3
  librsvg
  tracker3-miners
)
makedepends=(
  docbook-xsl
  git
  glib2-devel
  meson
  python
  yelp-tools
)
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-photos.git#tag=$pkgver"
  "git+https://gitlab.gnome.org/GNOME/libgd.git"
)
b2sums=('a85d6f4f5c8cce703b44d1de1f319db16d71ce2f5bc1fa02017eafafaa0deedc07e6178d7c1c955470e2e88a187a42f5680df0584c1833c548ec32c8d675c64d'
        'SKIP')

prepare() {
  cd $pkgname

  git submodule init
  git submodule set-url subprojects/libgd "$srcdir/libgd"
  git -c protocol.file.allow=always submodule update
}

build() {
  local meson_options=(
    -D manuals=true
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
