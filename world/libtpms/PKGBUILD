# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Jonas Witschel <diabonas@archlinux.org>

pkgname=libtpms
pkgver=0.10.2
pkgrel=1
pkgdesc='Library providing a software emulation of a Trusted Platform Module (TPM 1.2 and TPM 2.0)'
arch=('x86_64')
url='https://github.com/stefanberger/libtpms'
license=('BSD-3-Clause')
depends=(
  'glibc'
  'openssl'
)
makedepends=('git')
source=("git+$url.git?signed#tag=v$pkgver")
sha512sums=('5c809f685b91e34a61c74f8bf9ee74533fbd47a6138e1b2343f93c8d496eb71e237963ab263728a8c204371117221978730b6459e2cde401dff3939c3b2b0777')
validpgpkeys=('B818B9CADF9089C2D5CEC66B75AD65802A0B4211') # Stefan Berger <stefanb@linux.vnet.ibm.com>

prepare() {
	cd "$pkgname"
	autoreconf --install --force
}

build() {
	cd "$pkgname"
  ./configure \
    --prefix=/usr \
    --with-openssl \
    --with-tpm2
	make
}

check() {
	cd "$pkgname"
	make check
}

package() {
	cd "$pkgname"
	make DESTDIR="$pkgdir" install
	install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
