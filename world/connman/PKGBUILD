# Maintainer: Christian Rebischke <Chris.Rebischke[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Lucas De Marchi <lucas.de.marchi@gmail.com>

pkgname=connman
pkgver=1.43
pkgrel=3
pkgdesc="Intel's modular network connection manager"
url="https://01.org/connman"
arch=('x86_64')
license=('GPL-2.0-only')
depends=(
  'dbus'
  'gcc-libs'
  'glib2'
  'glibc'
  'gnutls'
  'iptables'
  'libmnl'
  'readline'
)
makedepends=(
  'bluez'
  'iwd'
  'openconnect'
  'openvpn'
  'ppp'
  'wpa_supplicant'
)
optdepends=(
  'bluez: Support for Bluetooth devices'
  'iwd: for WiFi devices'
  'openconnect: for VPN Support'
  'openvpn: for VPN Support'
  'pptpclient: for ppp support'
  'wpa_supplicant: for WiFi devices'
)
backup=('etc/connman/main.conf')
source=(
  "$pkgname-$pkgver.tar.xz::https://www.kernel.org/pub/linux/network/$pkgname/$pkgname-$pkgver.tar.xz"
  "$pkgname-$pkgver.tar.sign::https://www.kernel.org/pub/linux/network/$pkgname/$pkgname-$pkgver.tar.sign"
  "allow_group_network.diff"
)
sha512sums=('3289525cf764313700c520d3159431cdf719029096a1001d60ebdbda6406888699bc86336e7de890bf2c34a0624f3ba3081b0b57838a962b3c90bb5cd3484b38'
            'SKIP'
            '06dd5867d460f1c3cf6c359e650ca2ef24495493a99cd03dbd17f23e587e9066d9bc98758d85d5c690d1ae21fa77ad8da5e2fa83d0b52c95d7a535784c5c4964')
validpgpkeys=('E932D120BC2AEC444E558F0106CA9F5D1DCF2659') # Marcel Holtmann <marcel@holtmann.org>

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i "$srcdir/allow_group_network.diff"
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --runstatedir=/run \
    --bindir=/usr/bin \
    --sbindir=/usr/bin \
    --with-systemdunitdir=no \
    --with-tmpfilesdir=/usr/lib/tmpfiles.d \
    --enable-pptp \
    --enable-openconnect \
    --enable-vpnc \
    --enable-openvpn \
    --enable-polkit \
    --enable-client \
    --enable-nmcompat \
    --enable-test \
    --enable-pie \
    --enable-iwd
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -vDm755 -t "$pkgdir/usr/bin" client/connmanctl
  install -vDm644 -t "$pkgdir/etc/connman" src/main.conf
  rm -rvf "$pkgdir/usr/lib/systemd/"
}
