# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgname=libmirage
pkgver=3.3.2
pkgrel=1
pkgdesc="CD-ROM image (B6T/C2D/CCD/CDI/CIF/CUE/ISO/MDS/MDX/NRG/TOC) access library"
url="https://cdemu.sourceforge.io/"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  bzip2
  glib2
  glibc
  libgcrypt
  libsamplerate
  libsndfile
  xz
  zlib
)
makedepends=(
  cmake
  git
  gobject-introspection
  gtk-doc
  intltool
  ninja
  vala
)
source=("cdemu-code::git+https://git.code.sf.net/p/cdemu/code#tag=$pkgname-$pkgver")
b2sums=('1d5e5f44025144161fa31d71991150091ab989327360bba47309eaadc855b18116d8bde56e28c9d3eb8339f919a71d5795045dc9a4eae15c0cace0ef86ad72ac')

prepare() {
  cd cdemu-code/$pkgname
}

build() {
  local cmake_options=(
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_LIBDIR=lib
    -D CMAKE_INSTALL_PREFIX=/usr
    -D POST_INSTALL_HOOKS=OFF
  )

  cmake -S cdemu-code/$pkgname -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

# vim:set sw=2 sts=-1 et:
