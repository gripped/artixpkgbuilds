# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=glycin1
pkgname=(
  glycin1
  glycin1-gtk4
)
pkgver=1.2.4
pkgrel=2
pkgdesc="Sandboxed and extendable image decoding (legacy version)"
arch=(x86_64)
url="https://gitlab.gnome.org/GNOME/glycin"
license=('MPL-2.0 OR LGPL-2.1-or-later')
depends=(
  bubblewrap
  cairo
  fontconfig
  gcc-libs
  glib2
  glibc
  lcms2
  libjxl
  librsvg
  libseccomp
)
makedepends=(
  clang
  git
  gtk4
  gobject-introspection
  libheif
  meson
  rust
  vala
)
checkdepends=(
  dav1d
  ffmpeg
  rav1e
  svt-av1
)
source=(
  "git+$url.git#tag=${pkgver/[a-z]/.&}"
  "git+https://gitlab.gnome.org/sophie-h/test-images.git"
)
b2sums=('5fbc359b393361749b1712289f7ececd3ac705f5c5afb6e4d1ef48147b765a91e7fee1b77fbb64b09eef9abaf04d2ff5c46e013264819611aba7a2eedfed6c7b'
        'SKIP')

# Use debug
export CARGO_PROFILE_RELEASE_DEBUG=2 CARGO_PROFILE_RELEASE_STRIP=false

# Use LTO
export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

prepare() {
  cd glycin

  git submodule init
  git submodule set-url tests/test-images "$srcdir/test-images"
  git -c protocol.file.allow=always -c protocol.allow=never submodule update

  # Match cargo_home in meson.build
  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  local meson_options=(
    -D test_skip_install=true
  )

  artix-meson glycin build "${meson_options[@]}"
  meson compile -C build
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_glycin1() {
  optdepends=('libheif: heif/heic file support')
  provides=(libglycin-1.so)
  conflicts=('glycin<2')

  meson install -C build --destdir "$pkgdir" --no-rebuild

  cd "$pkgdir"
  _pick gtk4 usr/include/glycin-gtk4-*
  _pick gtk4 usr/lib/girepository-1.0/GlyGtk4-*
  _pick gtk4 usr/lib/libglycin-gtk4-*
  _pick gtk4 usr/lib/pkgconfig/glycin-gtk4-*
  _pick gtk4 usr/share/gir-1.0/GlyGtk4-*
  _pick gtk4 usr/share/vala/vapi/glycin-gtk4-*
}

package_glycin1-gtk4() {
  pkgdesc+=" - GTK4 integration"
  depends=(
    gcc-libs
    glib2
    glibc
    glycin1
    gtk4
    lcms2
    libseccomp
  )
  provides=(libglycin-gtk4-1.so)

  mv gtk4/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
