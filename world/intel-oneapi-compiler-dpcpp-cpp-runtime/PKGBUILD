# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Konstantin Gizdov <arch at kge dot pw>
# Contributor: Torsten Ke√üler <tpkessler at archlinux dot org>

_pkgbase='intel-oneapi-compiler-dpcpp'
_pkgname="${_pkgbase}-cpp"
pkgbase="${_pkgname}-runtime"
pkgname=("${_pkgname}-runtime" "${_pkgname}-runtime-libs")
_pkgver=2025.0
pkgver=2025.0.4
_debpkgrel=1519
pkgrel=1
_pkgdesc="Intel oneAPI Data Parallel C++ compiler"
arch=('x86_64')
url='https://software.intel.com/content/www/us/en/develop/tools/oneapi.html'
license=("custom")
source=("https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
        "https://apt.repos.intel.com/oneapi/pool/main/${_pkgname}-common-${_pkgver}-${pkgver}-${_debpkgrel}_all.deb"
        "https://apt.repos.intel.com/oneapi/pool/main/${_pkgbase}-eclipse-cfg-${_pkgver}-${pkgver}-${_debpkgrel}_all.deb"
        "https://apt.repos.intel.com/oneapi/pool/main/intel-oneapi-icc-eclipse-plugin-cpp-${_pkgver}-${pkgver}-${_debpkgrel}_all.deb")
b2sums=('f53c6a0cb362ba75441fa592c730d4d31d8dc9d34693acb31ae42eff410e6ef3707c18c618a0f8ef1f871daddc307242e7d27cae29ce152785cbbf77fc84c69c'
        '08fb588e91451314931cd65ecab3c64b3553e0eb580ce904b08499cc7f9ac6ce201647f43c3038aee032c929f86785be77cd6a12de90ee78a01bb49e3540a556'
        'b62356139b870e95914ba5c1f16070ee5381a377719f6a26427ed4c147688f2999200c79169c9dde91b22f4327f46fdd4ff5436ad08e1649d1151144c1129ee4'
        'fde30ddaeca190ebb23ff8afd9ac7be6eae7049b7adf2fc44970ca558d293f44b90addb9e074930ac6ecd3284b4cd12976e1f0a6ef97cabdecb6b688ff547daa')
noextract=(
    "${pkgname}-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
    "${_pkgname}-common-${_pkgver}-${pkgver}-${_debpkgrel}_all.deb"
    "${_pkgbase}-eclipse-cfg-${_pkgver}-${_debpkgrel}_all.deb"
    "intel-oneapi-icc-eclipse-plugin-cpp-${_pkgver}-${pkgver}-${_debpkgrel}_all.deb"
)

build() {
    mkdir "${_pkgbase}-${pkgver}"
    ar x "${srcdir}/${pkgname}-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
	tar xvf data.tar.xz -C "${_pkgbase}-${pkgver}"
    rm data.tar.xz

    ar x "${srcdir}/${_pkgname}-common-${_pkgver}-${pkgver}-${_debpkgrel}_all.deb"
    tar xvf data.tar.xz -C "${_pkgbase}-${pkgver}"
    rm data.tar.xz

    ar x "${srcdir}/${_pkgbase}-eclipse-cfg-${_pkgver}-${pkgver}-${_debpkgrel}_all.deb"
    tar xvf data.tar.xz -C "${_pkgbase}-${pkgver}"
    rm data.tar.xz

    ar x "${srcdir}/intel-oneapi-icc-eclipse-plugin-cpp-${_pkgver}-${pkgver}-${_debpkgrel}_all.deb"
    tar xvf data.tar.xz -C "${_pkgbase}-${pkgver}"
    rm data.tar.xz

    mkdir "${_pkgbase}-${pkgver}-libs"
    local _lib_path="/opt/intel/oneapi/compiler/${_pkgver}"
    install -d "${_pkgbase}-${pkgver}-libs/${_lib_path}/lib"
    cd "${_pkgbase}-${pkgver}/${_lib_path}/lib"
    local _lib
    for _lib in libur_* libsycl*; do
        mv "${_lib}" "${srcdir}/${_pkgbase}-${pkgver}-libs/${_lib_path}/lib"
    done
}

package_intel-oneapi-compiler-dpcpp-cpp-runtime-libs() {
    depends=(
      'intel-oneapi-compiler-shared-runtime-libs'
      'intel-oneapi-tbb'
      'intel-oneapi-common'
    )
    conflicts=('intel-oneapi-basekit')
    pkgdesc="${_pkgdesc}: Minimal runtime libraries"

    cp -a "${srcdir}/${_pkgbase}-${pkgver}-libs/opt" "${pkgdir}"

    # allow libs to be found
    local _lib_path='/opt/intel/oneapi/compiler'
    local _ldso_conf="${pkgdir}"/etc/ld.so.conf.d
    install -d "${_ldso_conf}"
    echo "${_lib_path}/latest/linux/lib" > "${_ldso_conf}/${pkgname}.conf"

    install -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
    ln -s /usr/share/licenses/intel-oneapi "${pkgdir}"/usr/share/licenses/"${pkgname}"/oneapi
}

package_intel-oneapi-compiler-dpcpp-cpp-runtime() {
    depends=(
      'intel-oneapi-compiler-shared-runtime'
      'intel-oneapi-compiler-dpcpp-cpp-runtime-libs'
      'intel-oneapi-common'
    )
    provides=(
      'intel-oneapi-compiler-dpcpp-cpp-common'
      'intel-oneapi-compiler-dpcpp-eclipse-cfg'
      'intel-oneapi-icc-eclipse-plugin-cpp'
    )
    conflicts=('intel-oneapi-basekit')
    pkgdesc="${_pkgdesc}: Full runtime libraries and header files"

    cp -a "${srcdir}/${_pkgbase}-${pkgver}/opt" "${pkgdir}"

    # allow libs to be found
    local _lib_path='/opt/intel/oneapi/compiler'
    local _ldso_conf="${pkgdir}"/etc/ld.so.conf.d
    install -d "${_ldso_conf}"
    echo "${_lib_path}/latest/linux/lib/x64" >> "${_ldso_conf}/${pkgname}.conf"

    install -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
    ln -s /usr/share/licenses/intel-oneapi "${pkgdir}"/usr/share/licenses/"${pkgname}"/oneapi
}
