# Maintainer: Morten Linderud <foxboron@archlinux.org> 
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Alad Wenter <https://wiki.archlinux.org/index.php/Special:EmailUser/Alad>

pkgbase=font-awesome
_pkgbase=Font-Awesome
pkgname=('woff2-font-awesome' 'otf-font-awesome')
pkgver=7.0.0
pkgrel=4
pkgdesc="Iconic font designed for Bootstrap"
url="https://fontawesome.com/"
license=('LicenseRef-OFL')
arch=('any')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/FortAwesome/Font-Awesome/archive/${pkgver}.tar.gz")
sha256sums=('ac6013cd8c7c49f3f388506bf2ef4c280edea38cc0267d83b445cf1f8a3c1e4b')

package_woff2-font-awesome() {
pkgdesc="${pkgdesc} - woff2 format"
provides=('ttf-font-awesome')
replaces=('ttf-font-awesome')
install="${pkgname}.install"

	cd "${_pkgbase}-${pkgver}"
	install -d "${pkgdir}/usr/share/fonts/WOFF2"
	install -m 644 ./webfonts/*.woff2 "${pkgdir}/usr/share/fonts/WOFF2"
	install -Dm 644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_otf-font-awesome() {
pkgdesc="${pkgdesc} - otf format"

	cd "${_pkgbase}-${pkgver}"
	install -d "${pkgdir}/usr/share/fonts/OTF"
	install -m 644 ./otfs/*.otf "${pkgdir}/usr/share/fonts/OTF"
	install -Dm 644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
