# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=pango
pkgname=(
  pango
  pango-docs
)
pkgver=1.58.1
pkgrel=1
epoch=1
pkgdesc="A library for layout and rendering of text"
url="https://www.pango.org/"
arch=(x86_64)
license=(LGPL-2.0-or-later)
depends=(
  cairo
  fontconfig
  freetype2
  fribidi
  glib2
  glibc
  harfbuzz
  libsysprof-capture
  libthai
  libx11
  libxft
  libxrender
)
makedepends=(
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  help2man
  meson
  python-docutils
)
source=("git+https://gitlab.gnome.org/GNOME/pango.git#tag=$pkgver")
b2sums=('15e1bd79984c10e2919a95998d8895ed7bea78ef4abe0de938590f011575a01194a59182247b0444f1737ed96afde207594ac203f30447a03e76aae51bdd5ffe')

prepare() {
  cd pango
}

build() {
  local meson_options=(
    -D documentation=true
    -D man-pages=true
    -D sysprof=enabled
  )

  artix-meson pango build "${meson_options[@]}"
  meson compile -C build
}

# not running checks: validation breaks when deps
# (especially harfbuzz) don't match upstream CI

package_pango() {
  provides=(libpango{,cairo,ft2,xft}-1.0.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_pango-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
