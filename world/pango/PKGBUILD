# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=pango
pkgname=(
  pango
  pango-docs
)
pkgver=1.56.4
pkgrel=2
epoch=1
pkgdesc="A library for layout and rendering of text"
url="https://www.pango.org/"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  cairo
  fontconfig
  freetype2
  fribidi
  glib2
  glibc
  harfbuzz
  libsysprof-capture
  libthai
  libx11
  libxft
  libxrender
)
makedepends=(
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  help2man
  meson
  python-docutils
)
source=("git+https://gitlab.gnome.org/GNOME/pango.git#tag=$pkgver")
b2sums=('2396a29025fca9e843b4aad11369aa89141f65d55ee92bea633fcac714d881616b4d5405687375afddbcf3453b46fbf4ad6e41ba1d85d6d763e5c7493f305771')

prepare() {
  cd pango

  # Fix a crash
  # https://gitlab.archlinux.org/archlinux/packaging/packages/pango/-/issues/3
  git cherry-pick -n c7dcffdc86a2ac4fe8e176aac3a6b71cb79adbe4
}

build() {
  local meson_options=(
    -D documentation=true
    -D man-pages=true
    -D sysprof=enabled
  )

  artix-meson pango build "${meson_options[@]}"
  meson compile -C build
}

# not running checks: validation breaks when deps
# (especially harfbuzz) don't match upstream CI

package_pango() {
  provides=(libpango{,cairo,ft2,xft}-1.0.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_pango-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
