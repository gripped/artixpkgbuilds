# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=kclock
pkgver=24.12.3
pkgrel=2
pkgdesc='Clock app for Plasma Mobile'
url='https://apps.kde.org/kclock/'
arch=(x86_64)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kcmutils
         kconfig
         kcoreaddons
         kcrash
         kdbusaddons
         ki18n
         kirigami
         kirigami-addons
         knotifications
         kstatusnotifieritem
         ksvg
         libplasma
         qqc2-desktop-style
         qt6-base
         qt6-declarative
         qt6-multimedia
         qt6-svg)
makedepends=(extra-cmake-modules)
groups=(kde-applications
        kde-utilities)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/utilities/kclock/-/commit/7926d90a.patch)
sha256sums=('43d326dffd1d0ed9b9efad3e7499690c745d486783e0551db6cc3c0952df15ab'
            'SKIP'
            'f820d200575ee2c3a19af214d748bfc7e05d1625decb5625d01a2d5c7282f6f2')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < 7926d90a.patch # Backport fixes for kirigami 6.12
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
