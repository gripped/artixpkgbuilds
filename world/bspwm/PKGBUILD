# Maintainer: kenobi <kenobi@artixlinux.org>
# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Bastien Dejean <nihilhill@gmail.com>

pkgname=bspwm
pkgver=0.9.11
pkgrel=1
pkgdesc='Tiling window manager based on binary space partitioning'
arch=(x86_64)
url='https://github.com/baskerville/bspwm'
license=(BSD-2-Clause)
makedepends=(git)
depends=(glibc libxcb sh xcb-util xcb-util-keysyms xcb-util-wm)
optdepends=('sxhkd: to define keyboard and pointer bindings'
            'xdo: for the example panel')
source=("git+$url#tag=$pkgver")
b2sums=('ed4f3b02f01736cb10084094dfe9ddbb2ce2d6f309d0b0434665cd7422a57351ae7fc1b93784c2d90ad7ffbd4ad68c8abf52c72b529b2820c83549ce60f5c4f8')

build() {
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  make -C $pkgname PREFIX=/usr
}

package() {
  cd $pkgname
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
