# Maintainer: kenobi <kenobi@artixlinux.org>
# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Bastien Dejean <nihilhill@gmail.com>

pkgname=bspwm
pkgver=0.9.11
pkgrel=2
pkgdesc='Tiling window manager based on binary space partitioning'
arch=(x86_64)
url='https://github.com/baskerville/bspwm'
license=(BSD-2-Clause)
makedepends=(git)
depends=(glibc libxcb sh xcb-util xcb-util-keysyms xcb-util-wm)
optdepends=('sxhkd: to define keyboard and pointer bindings'
            'xdo: for the example panel')
#source=("git+$url#tag=$pkgver")
source=("git+$url#commit=76d0f10dac6f6e3a1321a816e15a3263e20a42dc") # version 0.9.11 + one commit that fixes a bug
b2sums=('158e2ff8af596807adbd1ad738482690f0e044ad7defdf0c6a0a277d8bf9353907531ff65f3e7c303519f993f8835609cff804041c8f714c3b7fb880d091ea75')

build() {
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  make -C $pkgname PREFIX=/usr
}

package() {
  cd $pkgname
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
