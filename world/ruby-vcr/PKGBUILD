# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-vcr
pkgver=6.3.0
pkgrel=1
pkgdesc="Record your test suite's HTTP interactions and replay them during future test runs for fast, deterministic, accurate tests."
arch=(any)
url='https://benoittgt.github.io/vcr'
license=(MIT)
depends=('ruby' 'ruby-base64')
makedepends=('git' 'ruby-rspec' 'ruby-test-unit' 'ruby-rake' 'ruby-pry')
options=(!emptydirs)
source=("git+https://github.com/vcr/vcr.git#tag=v$pkgver")
sha256sums=('a91d51345e898c493a918eebc4f84136ae468211cdacb6bd6c7d3f07a39453f3')

prepare() {
  cd vcr
  sed -i 's|~>|>=|' -i vcr.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd vcr
  gem build vcr.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    vcr-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

# Disabled: revisit after a lot more gems are packaged
# check() {
#   local _gemdir="$(gem env gemdir)"
#   cd vcr-$pkgver
#   GEM_HOME="tmp_install/$_gemdir" rake
# }

package() {
  cd vcr
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
