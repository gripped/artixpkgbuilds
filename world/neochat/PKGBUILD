# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=neochat
pkgver=24.12.0
pkgrel=2
pkgdesc='A client for matrix, the decentralized communication protocol'
url='https://apps.kde.org/neochat/'
arch=(x86_64)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(cmark
         gcc-libs
         glibc
         icu
         kcolorscheme
         kconfig
         kcoreaddons
         kcrash
         kdbusaddons
         ki18n
         kio
         kirigami
         kirigami-addons
         kitemmodels
         knotifications
         kquickcharts
         kquickimageeditor
         kservice
         kstatusnotifieritem
         kunifiedpush
         kwindowsystem
         libquotient
         prison
         purpose
         qcoro
         qqc2-desktop-style
         qt6-base
         qt6-declarative
         qt6-location
         qt6-multimedia
         qt6-positioning
         qt6-webview
         qtkeychain-qt6
         sonnet
         syntax-highlighting)
makedepends=(extra-cmake-modules
             kdoctools
             python)
groups=(kde-applications
        kde-network)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/network/neochat/-/commit/0d286db0.patch)
sha256sums=('c88f8292575b7298cad7f426dec26aef33cd3da6dd1be65484436507b461eeb6'
            'SKIP'
            '9d9c0ef42f9ba0fe57d5a223761d6e9490ebc48513bb3a9996d6bf8ec77a9f4a')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < 0d286db0.patch # Fix crash when sending messages
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
