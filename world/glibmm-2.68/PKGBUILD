# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgbase=glibmm-2.68
pkgname=(
  glibmm-2.68
  glibmm-2.68-docs
)
pkgver=2.80.1
pkgrel=1
pkgdesc="C++ bindings for GLib"
url="https://www.gtkmm.org/"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  libsigc++-3.0
)
makedepends=(
  clang
  git
  meson
  mm-common
  perl-xml-parser
)
checkdepends=(glib-networking)
options=(!emptydirs)
source=("git+https://gitlab.gnome.org/GNOME/glibmm.git#tag=$pkgver")
b2sums=('4cf2c5a3ee258581f34440fa415cf07d4ca033dd38647d408b828427df55cbaa484197b45f64cf4cb877831a989e34c7686f09e4de2167a0f77bbde437be263d')

prepare() {
  cd glibmm
}

build() {
  local meson_options=(
    -D maintainer-mode=true
  )

  artix-meson glibmm build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_glibmm-2.68() {
  depends+=(libsigc-3.0.so)
  provides=(libglibmm-2.68.so)

  meson install -C build --destdir "$pkgdir"

  # Split -docs
  mkdir -p docs/usr/share
  mv -t docs/usr/share "$pkgdir"/usr/share/{devhelp,doc}
}

package_glibmm-2.68-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv -t "$pkgdir" docs/*
}

# vim:set sw=2 sts=-1 et:
