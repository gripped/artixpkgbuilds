# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Alberto Redondo <albertomost at gmail dot com>
# Contributor: Caltlgin Stsodaat <contact@fossdaily.xyz>
# Contributor: Chih-Hsuan Yen <yan12125@gmail.com>
# Contributor: Alexander 'z33ky' Hirsch <1zeeky@gmail.com>

_pkgname=requests-futures
pkgname=python-requests-futures
pkgver=1.1.0
pkgrel=1
pkgdesc='Asynchronous Python HTTP Requests for Humans using Futures'
arch=('any')
url='https://github.com/ross/requests-futures'
license=('Apache-2.0')
depends=('python-requests')
makedepends=('git' 'python-build' 'python-installer' 'python-wheel' 'python-setuptools')
checkdepends=('python-pytest' 'python-pytest-httpbin')
source=("git+https://github.com/ross/requests-futures.git#tag=v$pkgver")
sha256sums=('9bcb4dfb71c7e8eaee90e3b8b0028b91cb1b5c900945aa217cc75f9cd049c47f')

build() {
  cd requests-futures
  python -m build --wheel --no-isolation
}

check() {
  cd requests-futures
  pytest
}

package() {
  cd requests-futures
  python -m installer --destdir="$pkgdir" dist/*.whl
}
