# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: sebikul <sebikul@gmail.com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=geary
pkgver=46.0
pkgrel=6
epoch=1
pkgdesc='A lightweight email client for the GNOME desktop'
arch=(x86_64)
url=https://wiki.gnome.org/Apps/Geary
license=(
  CC-BY-3.0
  CC-BY-SA-3.0
  LGPL-2.1-or-later
)
depends=(
  at-spi2-core
  cairo
  dconf
  enchant
  folks
  gcr
  gdk-pixbuf2
  glib2
  glibc
  gmime3
  gnome-online-accounts
  gsound
  gspell
  gtk3
  hicolor-icon-theme
  icu
  iso-codes
  json-glib
  libgee
  libgoa
  libhandy
  libpeas
  libsecret
  libsoup3
  libstemmer
  libunwind
  libxml2
  libytnef
  org.freedesktop.secrets
  pango
  sqlite
  webkit2gtk-4.1
)
makedepends=(
  appstream-glib
  cmake
  git
  gobject-introspection
  meson
  vala
  yelp-tools
)
source=("git+https://gitlab.gnome.org/GNOME/geary.git#tag=${pkgver/[a-z]/.&}")
b2sums=('7e424cc2f5964390881aefdc6becf527a1e7e05f4edaac373149a73a20b22605d907bb465a0da7456431b2bd6dbb4e1dfe85dccbb5fc8a50e2cee2ea3f335e32')
validpgpkeys=(
  3A2EF7F138557A145F2866E99FAB18747A8FC649 # Michael James Gratton <mike@vee.net>
  71BFA565F77720034F44E0B00CF8493F76A7AE36 # CÃ©dric Bellegarde <cedric.bellegarde@adishatz.org>
)

prepare() {
  cd geary

  # Set prgname to application ID
  # https://gitlab.gnome.org/GNOME/geary/-/merge_requests/851
  git cherry-pick -n e91606b25b052df747d123be11f9ca90a813e4bf
}

build() {
  local meson_options=(
    -D profile=release
  )

  artix-meson geary build "${meson_options[@]}"
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim:set sw=2 sts=-1 et:
