# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Emanuele Rampichini <emanuele.rampichini@gmail.com>

pkgname=smb4k
pkgver=4.0.2
pkgrel=1
pkgdesc='A KDE program that browses samba shares'
arch=(x86_64)
url='https://smb4k.sourceforge.net/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kauth
         kcompletion
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         kdbusaddons
         kdnssd
         kdsoap-qt6
         kdsoap-ws-discovery-client
         ki18n
         kiconthemes
         kio
         kjobwidgets
         knotifications
         kstatusnotifieritem
         kwallet
         kwidgetsaddons
         kwindowsystem
         kxmlgui
         qt6-base
         qt6-declarative
         qtkeychain-qt6
         smbclient
         solid)
makedepends=(extra-cmake-modules
             kdoctools
             kirigami
             libplasma)
source=(https://downloads.sourceforge.net/project/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz)
sha256sums=('ae459052d4d3dcd82cabc270021f4fafc517f54811dffbdb3b652976d9985da1')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DSMB4K_WITH_WS_DISCOVERY=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
