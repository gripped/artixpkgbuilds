# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Emanuele Rampichini <emanuele.rampichini@gmail.com>

pkgname=smb4k
pkgver=3.2.4
pkgrel=1
pkgdesc='A KDE program that browses samba shares'
arch=(x86_64)
url='https://smb4k.sourceforge.net/'
license=(GPL)
depends=(gcc-libs
         glibc
         kauth5
         kcompletion5
         kconfig5
         kconfigwidgets5
         kcoreaddons5
         kcrash5
         kdbusaddons5
         kdnssd5
         ki18n5
         kiconthemes5
         kio5
         kjobwidgets5
         knotifications5
         kparts5
         kwallet5
         kwidgetsaddons5
         kwindowsystem5
         kxmlgui5
         qt5-base
         qt5-declarative
         smbclient
         solid5)
makedepends=(extra-cmake-modules
             kdoctools5
             plasma-framework5)
source=(https://downloads.sourceforge.net/project/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz)
sha256sums=('a4ee6cd6b5b34a4b28edc501faba6e9cdae512a1c6115537addf763533219c75')

build() {
  artix-cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
