# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Severen Redwood <me@severen.dev>
# Contributor: Tomasz Jakub Rup <tomasz.rup@gmail.com>

_bootstrap=0
pkgname=pnpm
pkgver=12.9.0
pkgrel=1
pkgdesc='Fast, disk space efficient package manager'
arch=(any)
url=https://pnpm.io
license=(MIT)
depends=(node-gyp)
makedepends=(git rust)
(( _bootstrap == 0 )) && makedepends+=(pnpm npm python)
source=("git+https://github.com/$pkgname/$pkgname.git#tag=v$pkgver?signed")
if (( _bootstrap == 1 )); then
  source+=("pnpm-linux-x64-v$pkgver.tar.gz::https://github.com/pnpm/pnpm/releases/download/v$pkgver/pnpm-linux-x64.tar.gz")
  noextract=("pnpm-linux-x64-v$pkgver.tar.gz")
fi
b2sums=('99cdddeeb0270cb2e0b7ba6feee7fd9b98a7851f9a85874c1cd51dec1adc8d031ea88ddd65043fb835a492e9a89b89e7ff4b4d1a7da4073a66420c7852f16337')
validpgpkeys=(7B74D1299568B586BA9962B5649E4D4AF74E7DEC) # Zoltan Kochan <z@kochan.io>

prepare() {
  if (( _bootstrap == 1 )); then
    mkdir tmp
    bsdtar xf pnpm-linux-x64-v$pkgver.tar.gz -C tmp
    ln -sr tmp/pn{pm,}
    # pnx/pnpx are just `pnpm dlx`; wrap them so nothing falls through to the system pnpm
    for bin in pnx pnpx; do
      printf '#!/bin/sh\nexec "%s/tmp/pnpm" dlx "$@"\n' "$srcdir" > tmp/$bin
      chmod +x tmp/$bin
    done
    export PATH="$srcdir/tmp:$PATH"
  fi

  cd $pkgname/${pkgname}11/$pkgname
  pnpm install --frozen-lockfile

}

build() {
  if (( _bootstrap == 1 )); then
    export PATH="$srcdir/tmp:$PATH"
  fi

  cd $pkgname/${pkgname}11/$pkgname
  pnpm run compile
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname

  install -d "$pkgdir"/{usr/bin,$mod_dir/dist}
  ln -s $mod_dir/bin/$pkgname.mjs "$pkgdir"/usr/bin/$pkgname
  ln -s $mod_dir/bin/$pkgname.mjs "$pkgdir"/usr/bin/pn
  ln -s $mod_dir/bin/pnpx.mjs "$pkgdir"/usr/bin/pnpx
  ln -s $mod_dir/bin/pnpx.mjs "$pkgdir"/usr/bin/pnx

  cd $pkgname/${pkgname}11/$pkgname
  cp -r bin package.json "$pkgdir"/$mod_dir
  install -Dt "$pkgdir"/usr/share/licenses/$pkgname LICENSE
  cd dist
  cp -r $pkgname.mjs pnpmrc templates worker.js "$pkgdir"/$mod_dir/dist
}
