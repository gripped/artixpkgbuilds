# Maintainer: Torr <torr@artixlinux.org>
pkgname=step-cli
pkgver=0.26.1
pkgrel=2
pkgdesc="Zero trust swiss army knife for working with X509, OAuth, JWT, OATH OTP, etc."
arch=("x86_64")
url="https://smallstep.com/cli"
## File: https://github.com/smallstep/cli/blob/master/CHANGELOG.md
changelog=Changelog.txt
license=("Apacha2" "MIT")
depends=("glibc")
makedepends=("go" "gcc")
source=(
	"https://github.com/smallstep/cli/archive/refs/tags/v$pkgver.tar.gz"
)
sha256sums=(
	"98350ab87a7ba0b3abef077f9228413a0b00afbd1eb7ccab794f166d701a93d1"
)


build() {
	cd "cli-$pkgver"
	export CGO_CFLAGS="$CFLAGS" CGO_LDFLAGS="$LDFLAGS" \
		GOPATH=/tmp/go GOCACHE=/tmp/go \
		GOTOOLCHAIN=local
	test ! -d bin && mkdir bin
	go build \
		-buildmode pie \
		-trimpath \
		-ldflags "-linkmode=external" \
		-mod readonly \
		-modcacherw \
		-buildvcs=false \
		-o bin/step ./cmd/step
}

package() {
	cd "cli-$pkgver"
	install -Dm 755 bin/step -t "$pkgdir/usr/bin"
	install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/step"
	install -Dm 644 autocomplete/zsh_autocomplete "$pkgdir/usr/share/bash-completion/completions/step"
	install -Dm 644 autocomplete/bash_autocomplete "$pkgdir/usr/share/zsh/site-functions/_step"
}
