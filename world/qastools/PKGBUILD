# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Esclapion <esclapion at gmail.com>

pkgname=qastools
pkgver=1.1.0
pkgrel=1
pkgdesc="A collection of desktop applications for the Linux sound system ALSA."
arch=(x86_64)
url="https://gitlab.com/sebholt/qastools"
license=(MIT)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-base
  qt6-svg
)
makedepends=(
  alsa-lib
  cmake
  qt6-tools
  udev
)
source=($url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('cc39c2cb802f85a7b73aec5a944100672b7d541cdac71f2000e6967b2e03ff55d1104c4f8685dc599bef72d0936d80b8a2f7ed362251a6f2be0001dbc6009ccf')
b2sums=('0c46c36961dbbb02b3acab5e59b66c7cb8c4a7913ad1180085939bad70108e156f0f2bc27c7353b2ff35043d93cb60d80109e98b00ce6dab37464632c5b2619e')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-v$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  depends+=(
    alsa-lib libasound.so
    systemd-libs libudev.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-v$pkgver/{CHANGELOG,README.md,TODO} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 $pkgname-v$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
}

