# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Esclapion <esclapion at gmail.com>

pkgname=qastools
pkgver=1.5.0
pkgrel=1
pkgdesc="A collection of desktop applications for the Linux sound system ALSA."
arch=(x86_64)
url="https://gitlab.com/sebholt/qastools"
license=(MIT)
groups=(pro-audio)
depends=(
  glibc
  hicolor-icon-theme
  libgcc
  libstdc++
  qt6-base
  qt6-svg
)
makedepends=(
  alsa-lib
  cmake
  qt6-tools
  libudev
)
source=($url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('cd34dc931e109c3bb1e5c103c1e15191f013cddd74ae7c72aee141de394c72908b34081639b3cae833a3c5724f6b619b4be5b201f50720a54a4b51fe7ad629db')
b2sums=('bc95ad666b5fa976c3bd3a47842e6dfc17f49821a47ffe27564becc95c423812dae38c167745551915ff4a6c8a1e6252fb9abce57d4f782fdd87e68e63aef227')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-v$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  depends+=(
    alsa-lib libasound.so
    libudev libudev.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-v$pkgver/{CHANGELOG,README.md,TODO} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 $pkgname-v$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
}

