# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Esclapion <esclapion at gmail.com>

pkgname=qastools
pkgver=1.0.0
pkgrel=1.1
pkgdesc="A collection of desktop applications for the Linux sound system ALSA."
arch=(x86_64)
url="https://gitlab.com/sebholt/qastools"
license=(MIT)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-base
  qt6-svg
)
makedepends=(
  alsa-lib
  cmake
  qt6-tools
  udev
)
source=($url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('a00a3d9d6ba854e3ff90bb4405968a9328a02e3d720edb37b4f46c93b54ef50a68794ee17d6df55c1e8117df610efdf0a1f388ce5849e3f216db38cef736bdd0')
b2sums=('0344b7314c7d59278b527c1afe99c78acc0a9ede5ab43c34bd5a5af24ad4dde58aea41c998ccf8e8509e96b291983040e63577e87b953b30ad7dcc2ea6a5021f')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-v$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  depends+=(
    alsa-lib libasound.so
    udev libudev.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-v$pkgver/{CHANGELOG,README.md,TODO} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 $pkgname-v$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
}

