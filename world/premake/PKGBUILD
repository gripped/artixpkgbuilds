# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: revel <revelΘmuub·net>
# Contributor: Marcos J. S. Magalhaes <mjsmagalhaes ^dot^ insc _at_ gmail ~dot~ com>

pkgname=premake
pkgver=5.0beta8
_pkgver=5.0.0-beta8
pkgrel=1
pkgdesc="A simple build configuration and project generation tool"
arch=('x86_64')
url="https://premake.github.io/"
license=('BSD-3-Clause')
depends=('glibc' 'util-linux-libs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/premake/premake-core/archive/refs/tags/v${_pkgver}.tar.gz")
sha512sums=('15b22030f252dd9a1f8cfa1318aca2657a11c64490b21583dabcde0ea390b6e4825f75ba707de4fb821f3e96520ccc774c08f9dbf64bf988d6c7026083c2e862')

build() {
  cd "premake-core-$_pkgver"

  make -f Bootstrap.mak linux
}

package() {
  cd "premake-core-$_pkgver"

  install -Dm755 "bin/release/premake5" "${pkgdir}/usr/bin/premake5"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
