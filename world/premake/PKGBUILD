# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: revel <revelΘmuub·net>
# Contributor: Marcos J. S. Magalhaes <mjsmagalhaes ^dot^ insc _at_ gmail ~dot~ com>

pkgname=premake
pkgver=5.0beta7
_pkgver=5.0.0-beta7
pkgrel=1
pkgdesc="A simple build configuration and project generation tool"
arch=('x86_64')
url="https://premake.github.io/"
license=('BSD-3-Clause')
depends=('glibc' 'util-linux-libs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/premake/premake-core/archive/refs/tags/v${_pkgver}.tar.gz")
sha512sums=('b6ed480c85db82d749b22b1b97537da525d8eb062514766393c1ff4fc8077289c0037200b7bc260754f320fd5d9c109cb56adbaeec59d485cd95728564927499')

build() {
  cd "premake-core-$_pkgver"

  make -f Bootstrap.mak linux
}

package() {
  cd "premake-core-$_pkgver"

  install -Dm755 "bin/release/premake5" "${pkgdir}/usr/bin/premake5"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
