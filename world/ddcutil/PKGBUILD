# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Deon Spengler <deon@spengler.co.za>

pkgname=ddcutil
pkgver=3.0.1
pkgrel=1
pkgdesc='Query and change Linux monitor settings using DDC/CI and USB.'
url='https://www.ddcutil.com/'
arch=('x86_64')
license=('GPL-2.0-or-later')
depends=('glibc' 'glib2' 'i2c-tools' 'libusb' 'libdrm' 'jansson' 'libx11' 'libxext' 'libxrandr' 'libudev' 'acl' 'dbus')
makedepends=('git')
source=(git+https://github.com/rockowitz/ddcutil.git#tag=v$pkgver)
sha512sums=('94d571bd15bbfbe2b699669bff8b18476a2d17b3830273d2b4b99245cc3ff7acdaaac96bd28011c6bbedb411f672532c2c7539e26f563f51eca33931fcc36d55')

prepare() {
  cd ddcutil
  autoreconf -fiv
}

build() {
  cd ddcutil
  ./configure --prefix=/usr --disable-build-timestamp
  make
}

package() {
  cd ddcutil
  make DESTDIR="$pkgdir" install
}

# vim: ft=sh ts=2 sw=2 et
