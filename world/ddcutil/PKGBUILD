# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Deon Spengler <deon@spengler.co.za>

pkgname=ddcutil
pkgver=2.1.0
pkgrel=2
pkgdesc='Query and change Linux monitor settings using DDC/CI and USB.'
url='http://ddcutil.com/'
arch=('x86_64')
license=('GPL2')
depends=('glib2' 'i2c-tools' 'libusb' 'libdrm' 'jansson')
makedepends=('udev')
source=(https://github.com/rockowitz/ddcutil/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('d48f87427050caf8e38f6a4b0dd71c1639101762444a712d7a7d97302ebc0dc4fd7c4a5cb188b251c6a6a369b298a43dbc7d91e0eca8cdb6f51c1ae6ae7f1f4b')

prepare() {
  cd $pkgname-$pkgver
  sed -e 's|"x"|"x\$\"\""|' -i configure.ac # Fix version suffix so that -Werror is disabled
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
# Workaround https://github.com/rockowitz/ddcutil/issues/365
  CFLAGS+=" -DNDEBUG" \
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim: ft=sh ts=2 sw=2 et
