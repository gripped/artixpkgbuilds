# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Deon Spengler <deon@spengler.co.za>

pkgname=ddcutil
pkgver=2.2.6
pkgrel=1
pkgdesc='Query and change Linux monitor settings using DDC/CI and USB.'
url='http://ddcutil.com/'
arch=('x86_64')
license=('GPL-2.0-or-later')
depends=('glibc' 'glib2' 'i2c-tools' 'libusb' 'libdrm' 'jansson' 'libgcc' 'libx11' 'libxext' 'libxrandr' 'libudev')
makedepends=('git' 'udev')
source=(git+https://github.com/rockowitz/ddcutil.git#tag=v$pkgver)
sha512sums=('c1014dbe0da28addc2a12e264d3a44a266fce9de95c51c20c6c98f2b1096e1f0f4393b1e9aff2d3f1bc05c8a439fc15d87104956617b6bac946a44afcdf463e1')

prepare() {
  cd ddcutil
  autoreconf -fiv
}

build() {
  cd ddcutil
  ./configure --prefix=/usr --disable-build-timestamp
  make
}

package() {
  cd ddcutil
  make DESTDIR="$pkgdir" install
}

# vim: ft=sh ts=2 sw=2 et
