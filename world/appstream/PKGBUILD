# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>
# Contributor: Tim Jester-Pfadt <t.jp<at>gmx.de>

pkgbase=appstream
pkgname=(appstream
         appstream-qt)
pkgver=1.1.4
pkgrel=2
pkgdesc='Provides a standard for creating app stores across distributions'
arch=(x86_64)
url='https://distributions.freedesktop.org/wiki/AppStream'
license=(LGPL-2.1-or-later)
depends=(cairo
         curl
         fontconfig
         freetype2
         gdk-pixbuf2
         glib2
         glibc
         libblake3
         libfyaml
         libgcc
         librsvg
         libstemmer
         libxml2
         libxmlb
         pango
         zstd)
makedepends=(bash-completion
             gi-docgen
             git
             glib2-devel
             gobject-introspection
             gperf
             itstool
             meson
             qt6-tools
             vala
             xmlto)
source=(git+https://github.com/ximion/appstream#tag=v$pkgver?signed
        update-appstream-cache.hook)
sha256sums=('445e541f679bb0106a872f3817804339922c6efda2d7395767b28aa7e4329f49'
            'edc632e4a76ebe5efc76a56fe5f797e5c981cca6f2f0111c7ce0170d1330c788')
validpgpkeys=(D33A3F0CA16B0ACC51A60738494C8A5FBF4DECEB) # Matthias Klumpp <matthias@tenstral.net>

prepare() {
  git -C appstream cherry-pick -n 28cb9d3401086614412508d8a67d64752df8e37e # Restore ABI
}

build() {
  meson build appstream \
    --prefix=/usr \
    --libexecdir=lib \
    -Dqt=true \
    -Dsystemd=false \
    -Dvapi=true \
    -Dcompose=true
  meson compile -C build
}

package_appstream() {
  meson install --destdir "$pkgdir" -C build

# provided by -qt subpackage
  rm -r "$pkgdir"/usr/{include/AppStreamQt,lib/cmake,lib/libAppStreamQt.*}

  install -Dm644 update-appstream-cache.hook "$pkgdir"/usr/share/libalpm/hooks/90-update-appstream-cache.hook
}

package_appstream-qt() {
  pkgdesc='Qt6 interface for AppStream'
  depends=(appstream
           glib2
           glibc
           libgcc
           libstdc++
           qt6-base)

  meson install --destdir "$pkgdir" -C build

# provided by appstream
  rm -r "$pkgdir"/usr/{bin,include/appstream{,-compose},lib/{appstreamcli-compose,girepository-1.0,libappstream*,pkgconfig},share}
}
