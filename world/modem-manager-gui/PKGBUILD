# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Ilya Medvedev <medved55rus [at] gmail [dot] com>

pkgname=modem-manager-gui
pkgver=0.0.20
pkgrel=4
pkgdesc='Frontend for ModemManager daemon able to control specific modem functions'
arch=(x86_64)
url='https://sourceforge.net/projects/modem-manager-gui/'
license=(GPL-3.0-or-later)
depends=(
  cairo
  gdbm
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  gtkspell3
  hicolor-icon-theme
  libappindicator
  libnotify
  modemmanager
)
makedepends=(
  itstool
  meson
  po4a
)
optdepends=('networkmanager: monitor network traffic')
source=(
  "https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz"
  meson0.61.patch
  Move-the-NetworkManager-dispatcher-script-out-of-etc.patch
  fix_notification_segfault.patch
  fix_segfault_on_DNS_entries.patch
  fix-tray-icon.patch
)
b2sums=('5c26c11555351e76e5341e415e367f1fcd40366fa5eab8139e12685d68f6d59f3ec9236d367b2413f2538f98fb068c8c231027565f39ba7b7d6c8f1205774f38'
        '8ca7277beeb883e8e4e394ab5ee179324cf69ea74b4a87832c3619ec877b7381e55ed9985d728a45c7ea194262ce8cfcdbbc95e26e42d5304faee1de1db0439b'
        'a660bc8e2168d2d53ce708972020efd87ecd2f0461559bb54d8afb39823f854b59a5f85627a593e2b8ac0fab8e3ba277ecf114d91c884dce92aa901714d21912'
        '8f6045c63c2dc47e337f25971ce0ecc01ab85715f53f004ec6e622d82732a3652b2838c963edd580e12f50030f40af8546d9ca19be93044b39923d0c7fb0496e'
        '179b6925c4ec73c0ffde4be402572c6f9187ccac91664b5e142f69463c437f2440c3b3a2fdb7ef1f316d2de1355afc24113f74987d7fd98df69125a0af5265eb'
        '3c32707e2bedc49277e691f0323fbd22244ece45792cb9df06fe5373964aad0565a5fb3f987d37b950e2859a7beaf0ce1701f13365e0661b4760e89545fb2451')

prepare() {
  cd $pkgname

  # Drop positional arguments to fix build with meson 0.61
  patch -Np1 -i ../meson0.61.patch

  # Move the NetworkManager dispatcher script out of /etc
  patch -Np1 -i ../Move-the-NetworkManager-dispatcher-script-out-of-etc.patch

  # Fix SIGSEGV when initializing libnotify
  patch -Np1 -i ../fix_notification_segfault.patch

  # Fix segfault on DNS entries
  patch -Np1 -i ../fix_segfault_on_DNS_entries.patch

  # Fix tray icon
  patch -Np1 -i ../fix-tray-icon.patch

  # Use libappindicator
  sed -i 's/ayatana-appindicator/appindicator/' src/main.h meson.build
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
