# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=isa-l
pkgver=2.32.1
pkgrel=1
pkgdesc="Intelligent Storage Acceleration Library"
arch=(x86_64)
url="https://github.com/intel/isa-l"
license=('BSD-3-Clause')
depends=(glibc)
makedepends=(
  help2man
  nasm
)
provides=(libisal.so)
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('d6a7b0a7269b53905cec04c8b15eb1cc783449f182c3b5c344d21adef7b4bf15f9ad6f9aae4b4ad9aebd55cc90ebd56e755842114e40ee0be9c4274235569fea')

prepare() {
  cd $pkgname-$pkgver
  ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --enable-programs
  make
}

check() {
  cd $pkgname-$pkgver
  make check
  make tests
}

package() {
  cd $pkgname-$pkgver
  make install DESTDIR="$pkgdir"
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
