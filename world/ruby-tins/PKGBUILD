# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Contributor: Hyacinthe Cartiaux <hyacinthe.cartiaux@free.fr>

pkgname=ruby-tins
_pkgname="${pkgname#ruby-}"
pkgver=1.59.0
pkgrel=1
pkgdesc='All the stuff that is not good/big enough for a real library'
arch=(any)
url='https://github.com/flori/tins'
license=(MIT)
depends=(ruby ruby-bigdecimal ruby-sync ruby-mize)
makedepends=(git ruby-rake)
options=(!emptydirs)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('0d2f907bb9940877208f15bfe708ed1d45ee58050d51ec4a610155ce963bdb8ad1ad4dfb4ef7096ed7f2b29026a5fc68e7d93df07433025a08e8d1865c569232')
b2sums=('80d35df4626ba0fb12779f1a1041d3280cd660c4e6bdbfb17b3e63105ccf6ce0b7e24127fe7729eab4d0e41cfcfa03dab852c40af231069ae3a689cca204384a')

prepare() {
  cd "$pkgname"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' tins.gemspec
}

build() {
  cd "$pkgname"

  gem build tins.gemspec
}

package() {
  cd "$pkgname"

  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$_pkgname-$pkgver.gem"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
