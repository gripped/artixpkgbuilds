# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Contributor: Hyacinthe Cartiaux <hyacinthe.cartiaux@free.fr>

pkgname=ruby-tins
_pkgname="${pkgname#ruby-}"
pkgver=1.46.0
pkgrel=1
pkgdesc='All the stuff that is not good/big enough for a real library'
arch=(any)
url='https://github.com/flori/tins'
license=(MIT)
depends=(ruby ruby-bigdecimal ruby-sync ruby-mize)
makedepends=(git ruby-rake)
options=(!emptydirs)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('d672afc0d992a195bebf96573ec36486962bd404abf73fd2cfaea71cf91f76b8cff96b10beacbafea438ff11f4b1729063f0609efeb9a085950b93d824dbc99d')
b2sums=('c99eb8108f28897471e711b8bc8e617a874db70e43304e4f7c58ff1cfc1907054e5993afe172469707320fd9b456421e6209d1f2037568fe032f890040bc2a12')

build() {
  cd "$pkgname"

  gem build tins.gemspec
}

package() {
  cd "$pkgname"

  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$_pkgname-$pkgver.gem"

  # delete cache
  rm -rf "$pkgdir/$_gemdir/cache"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
