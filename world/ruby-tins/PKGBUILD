# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Contributor: Hyacinthe Cartiaux <hyacinthe.cartiaux@free.fr>

pkgname=ruby-tins
_pkgname="${pkgname#ruby-}"
pkgver=1.39.1
pkgrel=1
pkgdesc='All the stuff that is not good/big enough for a real library'
arch=('any')
url='https://github.com/flori/tins'
license=('MIT')
depends=('ruby' 'ruby-bigdecimal' 'ruby-sync')
makedepends=('git' 'ruby-rake')
options=('!emptydirs')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('96981daaaa6f3f654b896780138793fb4f4dfadffbdabfc7504ba12bfbd82f01f749a2d631770813e83f80ca3917240ab2bdf6e5f1c4846a07e2d174817a02ee')
b2sums=('3cd358e1fd7cc11b7079003ae557721268b88972fbafe434ef9999014ac1bddffc659431bfffa9a0acd8bec83e2a7be56f3f50153777474396850140e8abf3c5')

build() {
  cd "$pkgname"

  gem build tins.gemspec
}

package() {
  cd "$pkgname"

  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$_pkgname-$pkgver.gem"

  # delete cache
  rm -rf "$pkgdir/$_gemdir/cache"

  # license
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  ln -sf "$_gemdir/gems/$_pkgname-$pkgver/COPYING" "$pkgdir/usr/share/licenses/$pkgname"
}
