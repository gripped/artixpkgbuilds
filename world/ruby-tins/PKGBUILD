# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Contributor: Hyacinthe Cartiaux <hyacinthe.cartiaux@free.fr>

pkgname=ruby-tins
_pkgname="${pkgname#ruby-}"
pkgver=1.38.0
pkgrel=1
pkgdesc='All the stuff that is not good/big enough for a real library'
arch=('any')
url='https://github.com/flori/tins'
license=('MIT')
depends=('ruby' 'ruby-bigdecimal' 'ruby-sync')
makedepends=('git' 'ruby-rake')
options=('!emptydirs')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('5e8012710477fa6a2538fd116274fc3c0066392f9646bb1021242348610e5e22cdd4e5165219a4e81a916049740787e39705da671fece8ee028e2fcb92d5a762')
b2sums=('9ad117286291a628f90209fd015e731c9d30869dd06b255ed7b7a7e18dd1b6df5ae726c5d9ee57bd0642318fe5b1964a8a2ecaf851ac0018626d9d3b283192e0')

build() {
  cd "$pkgname"

  gem build tins.gemspec
}

package() {
  cd "$pkgname"

  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$_pkgname-$pkgver.gem"

  # delete cache
  rm -rf "$pkgdir/$_gemdir/cache"

  # license
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  ln -sf "$_gemdir/gems/$_pkgname-$pkgver/COPYING" "$pkgdir/usr/share/licenses/$pkgname"
}
