# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Contributor: Hyacinthe Cartiaux <hyacinthe.cartiaux@free.fr>

pkgname=ruby-tins
_pkgname="${pkgname#ruby-}"
pkgver=1.47.0
pkgrel=1
pkgdesc='All the stuff that is not good/big enough for a real library'
arch=(any)
url='https://github.com/flori/tins'
license=(MIT)
depends=(ruby ruby-bigdecimal ruby-sync ruby-mize)
makedepends=(git ruby-rake)
options=(!emptydirs)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('a45c48a6aa7d90104cc2481f10dcb48e0b8733cae2d5915004e8b977170eb2f47d1919c8d7cd6a8d5ee651ea4a14d03386033167c14276a77777a102b4f4e0a7')
b2sums=('1c04cdb9759d3a6d1ac02f4cb7c1ea4fe0754a2711baa1c2a03ebabc3653beaa3a7869f876b728ecd96c443af7f2ccec3befe5c9a89168912e572e5f520d8aa2')

build() {
  cd "$pkgname"

  gem build tins.gemspec
}

package() {
  cd "$pkgname"

  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$_pkgname-$pkgver.gem"

  # delete cache
  rm -rf "$pkgdir/$_gemdir/cache"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
