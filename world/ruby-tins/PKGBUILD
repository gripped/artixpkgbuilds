# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Contributor: Hyacinthe Cartiaux <hyacinthe.cartiaux@free.fr>

pkgname=ruby-tins
_pkgname="${pkgname#ruby-}"
pkgver=1.37.1
pkgrel=3
pkgdesc='All the stuff that is not good/big enough for a real library'
arch=('any')
url='https://github.com/flori/tins'
license=('MIT')
depends=('ruby' 'ruby-sync')
makedepends=('git' 'ruby-rake')
options=('!emptydirs')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('04838ba35236acaf24492984782b3f4a66d906b3c537badfd2db9660afb3d8715265db601346ebd2b468c63dfba020405e6b5173383d1a7c666b5545a9ac2f71')
b2sums=('806c720414401fc8f27a18193e7bb81c17abadc8e2b2ae570df90ffe062ede3f0359b9fb8910680b7a904b88d790e4cdac62f1e2818c09e63ab9799a8f5a8133')

build() {
  cd "$pkgname"

  gem build tins.gemspec
}

package() {
  cd "$pkgname"

  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$_pkgname-$pkgver.gem"

  # delete cache
  rm -rf "$pkgdir/$_gemdir/cache"

  # license
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  ln -sf "$_gemdir/gems/$_pkgname-$pkgver/COPYING" "$pkgdir/usr/share/licenses/$pkgname"
}
