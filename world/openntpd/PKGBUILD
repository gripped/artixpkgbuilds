# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Mark Rosenstand <mark@borkware.net>
# Contributor: Giorgio Lando <patroclo7@gmail.com> (adjtimex patch)
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=openntpd
pkgver=6.8p1
pkgrel=9
pkgdesc='Free, easy to use implementation of the Network Time Protocol'
url='https://www.openntpd.org/'
arch=('x86_64')
license=('BSD')
depends=('glibc' 'libretls' 'esysusers')
conflicts=('ntp')
options=(emptydirs)
backup=('etc/ntpd.conf')
source=(https://cdn.openbsd.org/pub/OpenBSD/OpenNTPD/${pkgname}-${pkgver}.tar.gz{,.asc}
        openntpd.sysusers)
sha512sums=('200056bedb9c757aae1ce5d3f6655175ec058cb479429fe4704955f3a3fa15e8a9cb578ae4898ddb4cfc08c9742bbab6a7c92b5e569f06a148e40c448360b58f'
            'SKIP'
            '2d709ce7ce83a799125158dc55b4ebb319e04d5ab6c133f0d8f7dd93503d670ff50e492059ad208e0272fdde5de407c685e18c6d7a328a2ce5901ec4e72108ed')
validpgpkeys=('A1EB079B8D3EB92B4EBD3139663AF51BD5E4D8D5') # Brent Cook <bcook@openbsd.org>

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
  sed -i 's|pool.ntp.org|2.arch.pool.ntp.org|' ntpd.conf
}

build() {
  cd ${pkgname}-${pkgver}
    ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --with-privsep-user=ntp \
    --localstatedir=/var
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  rmdir "${pkgdir}/var/run"
  install -d "${pkgdir}/var/lib/ntp"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 "${srcdir}/openntpd.sysusers" "${pkgdir}/usr/lib/sysusers.d/openntpd.conf"
}

# vim: ts=2 sw=2 et:
