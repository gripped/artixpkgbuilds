# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Mark Rosenstand <mark@borkware.net>
# Contributor: Giorgio Lando <patroclo7@gmail.com> (adjtimex patch)
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=openntpd
pkgver=6.8p1
pkgrel=10
pkgdesc='Free, easy to use implementation of the Network Time Protocol'
url='https://www.openntpd.org/'
arch=('x86_64')
license=('BSD')
depends=('glibc' 'libretls' 'esysusers')
makedepends=('git')
conflicts=('ntp')
options=(emptydirs)
backup=('etc/ntpd.conf')
source=(openntpd::git+https://github.com/openntpd-portable/openntpd-portable.git#tag=${pkgver}
        openbsd::git+https://github.com/openntpd-portable/openntpd-openbsd.git 
        openntpd.sysusers)
sha512sums=('488cae440fecb7f33f68e46792b0a7d5ae86f5da6bfb1034ad79c19030d377376b532aa09e416526afb769531bd4826359e3fd451a24d604d412d89652c210a9'
            'SKIP'
            '2d709ce7ce83a799125158dc55b4ebb319e04d5ab6c133f0d8f7dd93503d670ff50e492059ad208e0272fdde5de407c685e18c6d7a328a2ce5901ec4e72108ed')
validpgpkeys=('A1EB079B8D3EB92B4EBD3139663AF51BD5E4D8D5') # Brent Cook <bcook@openbsd.org>

prepare() {
  cd ${pkgname}
  sh autogen.sh
}

build() {
  cd ${pkgname}
    ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --with-privsep-user=ntp \
    --localstatedir=/var
  make
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install

  sed -i 's|pool.ntp.org|2.arch.pool.ntp.org|' "${pkgdir}/etc/ntpd.conf"

  rmdir "${pkgdir}/var/run"
  install -d "${pkgdir}/var/lib/ntp"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 "${srcdir}/openntpd.sysusers" "${pkgdir}/usr/lib/sysusers.d/openntpd.conf"
}

# vim: ts=2 sw=2 et:
