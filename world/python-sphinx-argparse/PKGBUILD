# Maintainer: David Runge <dvzrv@archlinux.org>

_name=sphinx-argparse
pkgname=python-sphinx-argparse
pkgver=0.6.1
pkgrel=1
pkgdesc="Sphinx extension that automatically documents argparse commands and options"
arch=(any)
url="https://github.com/sphinx-doc/sphinx-argparse/"
license=(MIT)
depends=(
  python
  python-docutils
  python-sphinx
)
makedepends=(
  python-build
  python-flit-core
  python-installer
  python-wheel
)
checkdepends=(
  python-lxml
  python-pytest
)
optdepends=('python-commonmark: markdown support')
source=(
  $pkgname-$pkgver.tar.gz::${url}archive/refs/tags/v$pkgver.tar.gz
  python-sphinx-argparse-0.6.0-drop-the-upper-limit-for-flit-core.patch
)
sha512sums=('c9a9fff0a0b25cf619a90021c763cae4e273d6b58b0f3f774841d1a0eaa754dc9dd92c98bad2b4004f8621d4cff7e6309edb62484a6426d745ad3006174bf19f'
            'fdd706d14b2b2066af70a9e8094dae5045c6dcb560215855ec408b49cbb4fe1e5888297d27ce7e7dec3db8ce1eaa953680978049f0d5e493311934d0a1d7eb0b')
b2sums=('13c44b43c8dc16c94322b67f8245efed60c148871e8c5c08a80921528ad1bedd15a82efd66b7990e389e14be0ddd2e7a4d219eed54fff4ca9e6750b977dc4adf'
        'ca749ca05576b9e3502b1e9cc14745467b98878b8487e263789d8389d9a1605734169e0383b76eac5f2e9baeec325100bdd518e41c2245cd0ad18259cacabd35')

prepare() {
  # Remove the version upper-limit for flit_core.
  # Backport of https://github.com/sphinx-doc/sphinx-argparse/pull/102
  patch -Np 1 -d $_name-$pkgver -i ../python-sphinx-argparse-0.6.0-drop-the-upper-limit-for-flit-core.patch
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE.rst -t "$pkgdir/usr/share/licenses/$pkgname/"
}
# vim:set ts=2 sw=2 et:
