# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=ruby-set
_pkgname="${pkgname#ruby-}"
pkgver=1.1.0
pkgrel=1
pkgdesc='This library provides the Set class'
arch=('any')
url='https://github.com/ruby/set'
license=('BSD-2-Clause')
depends=('ruby')
makedepends=('git')
checkdepends=('ruby-rake' 'ruby-minitest' 'ruby-test-unit')
options=('!emptydirs')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('cbe5455a6fcb07c08661456ccda6a83ed06691ed6dfa53029e50e3e73b2fee3edef2d7beee6904d7795d18189cfef0426dbd9d9be569d7ae3875c1a9e1462a19')
b2sums=('d638371acbfc34fe5cc902b0951bb45e00e654e7b803970a3531c2d7d40402c85482f769be91118e9268b14e57bc9cbe9e5be5946341b8497ddaa06b8bd9d31c')

prepare() {
  cd "$pkgname"

  # we do not do version pinning
  sed --regexp-extended 's/~>/>=/g' --in-place *.gemspec Gemfile Rakefile
}

build() {
  cd "$pkgname"

  gem build "$_pkgname.gemspec"
}

check() {
  cd "$pkgname"

  rake test
}

package() {
  cd "$pkgname"

  local _gemdir="$(gem env gemdir)"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$_pkgname-$pkgver.gem"

  # delete cache
  rm -rf "$pkgdir/$_gemdir/cache"

  # license
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  ln -sf "$_gemdir/gems/$_pkgname-$pkgver/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname"
}
