# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=mm-common
pkgver=1.0.6
pkgrel=3
pkgdesc="Common build files of the C++ bindings"
url="https://www.gtkmm.org/"
arch=(any)
license=(GPL-2.0-or-later)
depends=(
  bash
  doxygen
  graphviz
  gtk-doc
  libsigc++-docs
  perl-xml-parser
  python
)
makedepends=(
  git
  libsigc++
  meson
)
_gccver=15.1.0
source=(
  "git+https://gitlab.gnome.org/GNOME/mm-common.git#tag=$pkgver"
  "libstdc++-$_gccver.tag::https://gcc.gnu.org/onlinedocs/gcc-$_gccver/libstdc++/api/libstdc++.tag"
)
b2sums=('e3e6857edcb7a03b3a959a9dfba381b0cd77c06231a1cdd3784f42888e6494321db8a6378c797e87e95a90dcde1350e4c5b3616b91e4d9f02fdddab9b56c4091'
        '5be30b8ca19464aa11b76bce23e09c28fbf9b949b25b119dc2a6ade69273dcfd9a2bd1e098ab63e7d056e0497e70704b90a2372839b99e3561c506da8aeacdb4')

prepare() {
  cd $pkgname

  # Use more stable libstdc++ tags
  cp ../libstdc++-$_gccver.tag doctags/libstdc++.tag
  sed -e "/^htmlrefpub=/s|=.*|=https://gcc.gnu.org/onlinedocs/gcc-$_gccver/libstdc++/api/|" \
      -i doctags/*.pc.in
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
