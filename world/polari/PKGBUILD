# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=polari
pkgver=49.0
pkgrel=2
pkgdesc="An IRC Client for GNOME"
url="https://apps.gnome.org/Polari/"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  dconf
  gcc-libs
  gdk-pixbuf2
  gjs
  glib2
  glibc
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
  libgirepository
  libsecret
  libsoup3
  org.freedesktop.secrets
  pango
  telepathy-glib
  telepathy-idle
  telepathy-mission-control
  tinysparql
)
makedepends=(
  appstream
  git
  gobject-introspection
  meson
  yelp-tools
)
optdepends=('webkit2gtk-4.1: URL preview feature')
groups=(gnome-circle)
source=(
  # Polari tags use SSH signatures which makepkg doesn't understand
  "git+https://gitlab.gnome.org/GNOME/polari.git#tag=${pkgver/[a-z]/.&}"
  0001-joinDialog-Fix-detecting-pointer-devices.patch
  0002-mainWindow-Disconnect-event-handler-on-destroy.patch
  0003-Add-option-to-disable-URL-preview-feature.patch
)
b2sums=('d210c0e21096bc12924a0f0b34f5ba0f04ce77eeeaad90575b1af52205fa5462b8a8bd43ff9dce02b4ac6edf5a7c703c8ad819955644dff72754bfb1a4efb447'
        'bc1e8802e74c0fb1c2436998440a885cf3b717b3000af81be73f330a734b925ed48a9f9cb7afd1a3024bfe00862e8ec90b43e5a0f3ab1539ae3548f46554a474'
        'f97c86ca46a443a57f8a03849e8d45e8d6fc56403bac26f6cdc74bf55407ecd3ea9177d8763e9e15c0c3cca93c75355f84abbfaee9ab8fc7f23291cca5276191'
        '1c317c21f9742b4d48110637871f8fc8b1e8adb92f283446bb945ac0da28fd71d7f14a79d763cafc68c7444f86b169ba1e7f4979405ccee99bc9cb2bb836fc89')

prepare() {
  cd polari

  # joinDialog: Fix detecting pointer devices
  # https://gitlab.gnome.org/GNOME/polari/-/merge_requests/320
  git apply -3 ../0001-joinDialog-Fix-detecting-pointer-devices.patch

  # mainWindow: Disconnect event handler on destroy
  # https://gitlab.gnome.org/GNOME/polari/-/merge_requests/329
  git apply -3 ../0002-mainWindow-Disconnect-event-handler-on-destroy.patch

  # Add option to disable URL preview feature
  # https://gitlab.gnome.org/GNOME/polari/-/merge_requests/330
  git apply -3 ../0003-Add-option-to-disable-URL-preview-feature.patch
}

build() {
  artix-meson polari build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
