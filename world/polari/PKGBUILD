# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=polari
pkgver=50.0
pkgrel=1
pkgdesc="An IRC Client for GNOME"
url="https://apps.gnome.org/Polari/"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  dconf
  gdk-pixbuf2
  gjs
  glib2
  glibc
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
  libgcc
  libgirepository
  libsecret
  libsoup3
  org.freedesktop.secrets
  pango
  telepathy-glib
  telepathy-idle
  telepathy-mission-control
  tinysparql
)
makedepends=(
  appstream
  git
  gobject-introspection
  meson
  yelp-tools
)
optdepends=('webkitgtk-6.0: URL preview feature')
groups=(gnome-circle)
source=(
  # Polari tags use SSH signatures which makepkg doesn't understand
  "git+https://gitlab.gnome.org/GNOME/polari.git#tag=${pkgver/[a-z]/.&}"
  0001-joinDialog-Fix-detecting-pointer-devices.patch
  0002-mainWindow-Disconnect-event-handler-on-destroy.patch
  0003-Add-option-to-disable-URL-preview-feature.patch
)
b2sums=('5318887907a01d0594424159c7af046ffb358f2f29063dfe7832ad9d74755a1c8e9444167fa9e9724fb8dc1af0b69e0db69dd0bca0802edf74c61d556de2968f'
        'bc1e8802e74c0fb1c2436998440a885cf3b717b3000af81be73f330a734b925ed48a9f9cb7afd1a3024bfe00862e8ec90b43e5a0f3ab1539ae3548f46554a474'
        'f97c86ca46a443a57f8a03849e8d45e8d6fc56403bac26f6cdc74bf55407ecd3ea9177d8763e9e15c0c3cca93c75355f84abbfaee9ab8fc7f23291cca5276191'
        'ad7ec19ee761a753734846e066d084035f6f5e0378fd5120e611acb5bda58ea72cd6ec0c4360ec6f570490aced2aad4828ca75637552946f4df0f01aeec14529')

prepare() {
  cd polari

  # joinDialog: Fix detecting pointer devices
  # https://gitlab.gnome.org/GNOME/polari/-/merge_requests/320
  git apply -3 ../0001-joinDialog-Fix-detecting-pointer-devices.patch

  # mainWindow: Disconnect event handler on destroy
  # https://gitlab.gnome.org/GNOME/polari/-/merge_requests/329
  git apply -3 ../0002-mainWindow-Disconnect-event-handler-on-destroy.patch

  # Add option to disable URL preview feature
  # https://gitlab.gnome.org/GNOME/polari/-/merge_requests/330
  git apply -3 ../0003-Add-option-to-disable-URL-preview-feature.patch
}

build() {
  artix-meson polari build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
