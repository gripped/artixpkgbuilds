# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=polari
pkgver=46.0
pkgrel=5
pkgdesc="An IRC Client for GNOME"
url="https://apps.gnome.org/Polari/"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  dconf
  gcc-libs
  gjs
  glib2
  glibc
  gnome-keyring
  gtk4
  hicolor-icon-theme
  libadwaita
  libgirepository
  libsoup3
  telepathy-glib
  telepathy-idle
  telepathy-logger
  telepathy-mission-control
  tracker3
)
makedepends=(
  appstream
  git
  gobject-introspection
  meson
  yelp-tools
)
optdepends=('webkit2gtk-4.1: URL preview feature')
groups=(gnome-circle)
source=(
  # Polari tags use SSH signatures which makepkg doesn't understand
  "git+https://gitlab.gnome.org/GNOME/polari.git#tag=${pkgver/[a-z]/.&}"
  0001-joinDialog-Fix-detecting-pointer-devices.patch
  0002-mainWindow-Disconnect-event-handler-on-destroy.patch
  0003-Add-option-to-disable-URL-preview-feature.patch
)
b2sums=('4e8ebd9ab847cd67b0427e69e41fa1a102f00799309767efdb3b1e109a2737155224bf1558808f610639200e817dd64cef6d657cfcefbca183c8e779d36dad16'
        '9d25b66c9bdfd3f2463b27a2b402e0f529ca269b0e384ca2f79aa01c47326a330ca46c95b24b05bb7c66cf73c5396af63ec958abdb03b147d786f8635aec74c7'
        '7823af94b463184cde4ae1dbd50d71fa640a6be10bdea3c4a34ae9d6d2e988226a30538c121566f9b42d8044bc3e556032fd121b6ff1611aedcfbc90fef5d745'
        'aa8c85064184acde7b37bf1b8e3d79b61173c39a1c114fcf7272504fc912f35c0cd15298e08f8f0a9b75507c73e8369483a7d7062e9f2e6772c310f47f286929')

prepare() {
  cd polari

  # Fix running with gjs 1.86.0
  # https://gitlab.archlinux.org/archlinux/packaging/packages/polari/-/issues/1
  git cherry-pick -n d7946c7fe39f112cd3f751bb95b170446022980d

  # joinDialog: Fix closing the dialog
  # https://gitlab.gnome.org/GNOME/polari/-/merge_requests/319
  git cherry-pick -n 56544fe9c036c8a0e6f4990054730627c32bb561

  # joinDialog: Fix detecting pointer devices
  # https://gitlab.gnome.org/GNOME/polari/-/merge_requests/320
  git apply -3 ../0001-joinDialog-Fix-detecting-pointer-devices.patch

  # mainWindow: Disconnect event handler on destroy
  # https://gitlab.gnome.org/GNOME/polari/-/merge_requests/329
  git apply -3 ../0002-mainWindow-Disconnect-event-handler-on-destroy.patch

  # Add option to disable URL preview feature
  # https://gitlab.gnome.org/GNOME/polari/-/merge_requests/330
  git apply -3 ../0003-Add-option-to-disable-URL-preview-feature.patch
}

build() {
  artix-meson polari build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
