# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Stefan Kirrmann <stefan.kirrmann at gmail dot com>

pkgname=open-iscsi
pkgver=2.1.11
pkgrel=1
pkgdesc='iSCSI userland tools'
arch=('x86_64')
url='https://www.open-iscsi.com/'
license=('GPL')
makedepends=('meson' 'udev')
depends=('libudev' 'util-linux-libs' 'kmod' 'openssl' 'open-isns')
install=$pkgname.install
backup=('etc/iscsi/iscsid.conf'
	'etc/iscsi/initiatorname.iscsi')
options=('docs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/open-iscsi/open-iscsi/archive/$pkgver.tar.gz")
sha256sums=('f288d1823b15782432608e5f53723159562e2c44e9a72b40fe15a5ca064ac86a')

build() {
  local _meson_options=(
    -Discsi_sbindir=/usr/bin/
    -Drulesdir=/usr/lib/udev/rules.d/
    -Dno_systemd=true
  )

  artix-meson "${pkgname}-${pkgver}" build "${_meson_options[@]}"

  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build

  # no initiatorname in package, initialized in install script
  echo -n > "${pkgdir}"/etc/iscsi/initiatorname.iscsi

  # install docs
  install -D -m0644 -t "${pkgdir}"/usr/share/doc/${pkgname}/ "${pkgname}-${pkgver}"/{Changelog,README}
}
