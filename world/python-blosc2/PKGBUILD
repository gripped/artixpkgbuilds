# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-blosc2
pkgver=2.7.0
pkgrel=3
pkgdesc='Wrapper for the blosc2 compressor'
arch=('x86_64')
url='https://github.com/Blosc/python-blosc2'
license=('BSD-3-Clause')
depends=(
  'blosc2'
  'glibc'
  'python'
  'python-msgpack'
  'python-ndindex'
  'python-numexpr'
  'python-numpy'
  'python-py-cpuinfo'
)
makedepends=(
  'git'
  'cmake'
  'cython'
  'ninja'
  'python-build'
  'python-installer'
  'python-scikit-build'
  'python-setuptools'
)
checkdepends=(
  'python-psutil'
  'python-pytest'
  'python-pytorch'
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('29b05656c121320dc11dd03855d430efc75ab1ea495d68134c57be403941ce0f1fc7ac13755136e4131c48c4fe093a2883a5fc717a2c2092a9ef48ce2da10aba')
b2sums=('439c4548fe938f869d66b5dbd6c7609f88d3cf31124d0ca43175877e24dc540165975153a57f6d6d5b9de9cb3a092c0116154ce1792480922e77cc1582ce1c05')

build() {
  cd "$pkgname"

  export CMAKE_ARGS="-DUSE_SYSTEM_BLOSC2=ON"

  # skip unnecessary dependencies
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd "$pkgname"

  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
