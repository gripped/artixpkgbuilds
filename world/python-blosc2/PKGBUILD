# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-blosc2
pkgver=3.0.0
pkgrel=2
pkgdesc='Wrapper for the blosc2 compressor'
arch=('x86_64')
url='https://github.com/Blosc/python-blosc2'
license=('BSD-3-Clause')
depends=(
  'blosc2'
  'glibc'
  'python'
  'python-httpx'
  'python-msgpack'
  'python-ndindex'
  'python-numexpr'
  'python-numpy'
  'python-py-cpuinfo'
)
makedepends=(
  'cmake'
  'cython'
  'git'
  'ninja'
  'python-build'
  'python-installer'
  'python-scikit-build-core'
  'python-setuptools'
)
checkdepends=(
  'python-psutil'
  'python-pytest'
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('5df824a72b2a38e4ed63e623a8292d0fb8f3c02091313c817768cefec4df76b21d2bd2043dc5185f5549ae994ca69f4ed201f92628690d84419f28480e551127')
b2sums=('205369d718af21862ddf73e41a4d2c044178552e02504cce93cc752ed4fa0f26c611d7a17a6c870eea2d12a7a5b6fbe122943d2a7dcf04ae3c19351fc15adab8')

build() {
  cd $pkgname
  export CMAKE_ARGS="-DUSE_SYSTEM_BLOSC2=ON"
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname
  python -m venv venv-test --system-site-packages
  ./venv-test/bin/python -m installer dist/*.whl
  ./venv-test/bin/python -m pytest
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
