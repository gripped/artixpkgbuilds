# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-blosc2
pkgver=3.5.0
pkgrel=1
pkgdesc='Wrapper for the blosc2 compressor'
arch=('x86_64')
url='https://github.com/Blosc/python-blosc2'
license=('BSD-3-Clause')
depends=(
  'blosc2'
  'glibc'
  'python'
  'python-msgpack'
  'python-ndindex'
  'python-numexpr'
  'python-numpy'
  'python-platformdirs'
  'python-py-cpuinfo'
  'python-requests'
)
makedepends=(
  'cmake'
  'cython'
  'git'
  'ninja'
  'python-build'
  'python-installer'
  'python-scikit-build-core'
  'python-setuptools'
)
checkdepends=(
  'python-psutil'
  'python-pytest'
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('59d849d646779c9992cf2f993bb55aba224d7c2481a19842c7956eaadb0e02a2b1b8e61716ad5947d96e7a164bd2b4c390bbe55bd8330230cc137f652dda791f')
b2sums=('2bf87f1a723f1e0b3446dbb758602d35cd4bb2b87f89d9c4f962490f6c3169182ea14fc1b6457b56c4705338a42ce6339c509e1b782e68708bd0b780d3c7898c')

build() {
  cd $pkgname
  export CMAKE_ARGS="-DUSE_SYSTEM_BLOSC2=ON"
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname
  python -m venv venv-test --system-site-packages
  ./venv-test/bin/python -m installer dist/*.whl
  # Deselect tests failing since v3.4.0, not sure why
  ./venv-test/bin/python -m pytest \
    --deselect tests/ndarray/test_lazyexpr.py::test_broadcasting \
    --deselect tests/ndarray/test_lazyexpr.py::test_chain_expressions \
    --deselect tests/ndarray/test_reductions.py::test_broadcast_params \
    --deselect tests/ndarray/test_reductions.py::test_fast_path \
    --deselect tests/ndarray/test_reductions.py::test_save_version1 \
    --deselect tests/ndarray/test_reductions.py::test_save_version2 \
    --deselect tests/ndarray/test_reductions.py::test_save_version3 \
    --deselect tests/ndarray/test_reductions.py::test_save_version4 \
    --deselect tests/ndarray/test_lazyexpr.py::test_chain_persistentexpressions
    # this last one is Artix CI specific
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
