# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-blosc2
pkgver=4.8.1
pkgrel=1
pkgdesc='Wrapper for the blosc2 compressor'
arch=(x86_64)
url='https://github.com/Blosc/python-blosc2'
license=(BSD-3-Clause)
depends=(
  blosc2
  glibc
  python
  python-msgpack
  python-ndindex
  python-numexpr
  python-numpy
  python-httpx
  python-h2
  python-pydantic
  python-rich
  python-threadpoolctl
)
makedepends=(
  cmake
  cython
  git
  ninja
  python-build
  python-installer
  python-scikit-build-core
  python-setuptools
)
checkdepends=(
  python-psutil
  python-pytest
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('cac91b4be43e6d50bd14332db5e32c7c2b0bf3819d122c2ce5eaa448b3fe6b6289839f5d69e91ef5768d560d28539c6f4f203b088a8810218ace7445611d3935')
b2sums=('85797cc0c3a7c11ccf40a822de92f7a5e55f78517d71576f5ad4f9f29582c90e13f18e8eb7522528256ce983a73b493dbd81602cef79d36c254cb89e6e3800ce')

build() {
  cd $pkgname
  export CMAKE_ARGS="-DUSE_SYSTEM_BLOSC2=ON"
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname
  python -m venv venv-test --system-site-packages
  ./venv-test/bin/python -m installer dist/*.whl
  # Deselect tests failing since v3.4.0, not sure why
  # test_expand_dims: sys.getrefcount() behavior changed in Python 3.14
  ./venv-test/bin/python -m pytest \
    --deselect tests/ndarray/test_resize.py::test_expand_dims \
    --deselect tests/ndarray/test_lazyexpr.py::test_broadcasting \
    --deselect tests/ndarray/test_lazyexpr.py::test_chain_expressions \
    --deselect tests/ndarray/test_lazyexpr.py::test_chain_persistentexpressions \
    --deselect tests/ndarray/test_reductions.py::test_broadcast_params \
    --deselect tests/ndarray/test_reductions.py::test_fast_path \
    --deselect tests/ndarray/test_reductions.py::test_save_version1 \
    --deselect tests/ndarray/test_reductions.py::test_save_version2 \
    --deselect tests/ndarray/test_reductions.py::test_save_version3 \
    --deselect tests/ndarray/test_reductions.py::test_save_version4
}

package() {
  cd $pkgname

  python -m installer --destdir="$pkgdir" dist/*.whl

  # why are these files there?
  (
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  cd "$pkgdir$site_packages"
  rm -vrf include lib share
  )

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
