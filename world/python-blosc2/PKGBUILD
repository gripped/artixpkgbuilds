# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-blosc2
pkgver=2.7.1
pkgrel=2
pkgdesc='Wrapper for the blosc2 compressor'
arch=('x86_64')
url='https://github.com/Blosc/python-blosc2'
license=('BSD-3-Clause')
depends=(
  'blosc2'
  'glibc'
  'python'
  'python-msgpack'
  'python-ndindex'
  'python-numexpr'
  'python-numpy'
  'python-py-cpuinfo'
)
makedepends=(
  'git'
  'cmake'
  'cython'
  'ninja'
  'python-build'
  'python-installer'
  'python-scikit-build'
  'python-setuptools'
)
checkdepends=(
  'python-psutil'
  'python-pytest'
# 'python-pytorch'
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('add4e22a48d575d0d4b106428fcfe0f1085bccfafec3fc94da0e939718f5827a12a769af92ea06b9140082f3d0f43f1ef2332d5a2b2919ab11615f6fa60f3ef0')
b2sums=('3e72d5608c338f3488cb1312a8da6c839b599558e7bb91709852823614031dc9e1ea3f09d7fce74b1864a93269b5c865c8ac277065eb90d026b6aac7565c77c5')

build() {
  cd $pkgname
  export CMAKE_ARGS="-DUSE_SYSTEM_BLOSC2=ON"
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd $pkgname
  python -m installer --destdir=tmp_install dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  cp -r tests "$PWD/tmp_install/$site_packages"
  (cd "$PWD/tmp_install/$site_packages" && pytest)
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
