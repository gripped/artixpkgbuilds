# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-blosc2
pkgver=4.13.1
pkgrel=1
pkgdesc='Wrapper for the blosc2 compressor'
arch=(x86_64)
url='https://github.com/Blosc/python-blosc2'
license=(BSD-3-Clause)
depends=(
  blosc2
  glibc
  python
  python-msgpack
  python-ndindex
  python-numexpr
  python-numpy
  python-httpx
  python-h2
  python-pydantic
  python-rich
  python-threadpoolctl
)
makedepends=(
  cmake
  cython
  git
  ninja
  python-build
  python-installer
  python-scikit-build-core
  python-setuptools
)
checkdepends=(
  python-psutil
  python-pytest
  python-pytest-asyncio
  python-requests
  python-aiohttp
)
source=(
  "$pkgname::git+$url#tag=v$pkgver"
  argh.patch
)
sha512sums=('a30054d917062f337d40d1c732e3103a4c4c33b46ecf0dffb3e4fd96a210b79fc88e577e501dabb43e4342e47e149d589548b6a4ee4b9a92c267d3fe7b915395'
            '88f486cd6385055da9bad586bc885ea852c4deb745fd429dc63e104cb579190115746932664ebad1b435df83cc1b6134b42ee5107db3dc92fb67cf7a3fd7acb0')
b2sums=('313851713e7420111362ef8e0903d90a43d5e0370045f4a0c2c62e0ef5f3fd77876867b92cee67441fe68b39bebd6ddf9960063a7027e1e694f58e47512110c6'
        '1595af3fe29e7410996a180d0456d276abae8f243eb6ec9497cb98979ccf03456f4c94e825375f70c2377987cc7d554e0df794e75c66830a05ecc7f4beb27864')

prepare() {
  cd "$pkgname"

  patch -p1 -i "$srcdir/argh.patch"
}

build() {
  cd $pkgname
  export CMAKE_ARGS="-DUSE_SYSTEM_BLOSC2=ON"
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname
  python -m venv venv-test --system-site-packages
  ./venv-test/bin/python -m installer dist/*.whl
  # Deselect tests failing since v3.4.0, not sure why
  # test_expand_dims: sys.getrefcount() behavior changed in Python 3.14
  ./venv-test/bin/python -m pytest \
    --deselect tests/ndarray/test_resize.py::test_expand_dims \
    --deselect tests/ndarray/test_lazyexpr.py::test_broadcasting \
    --deselect tests/ndarray/test_lazyexpr.py::test_chain_expressions \
    --deselect tests/ndarray/test_lazyexpr.py::test_chain_persistentexpressions \
    --deselect tests/ndarray/test_reductions.py::test_broadcast_params \
    --deselect tests/ndarray/test_reductions.py::test_fast_path \
    --deselect tests/ndarray/test_reductions.py::test_save_version1 \
    --deselect tests/ndarray/test_reductions.py::test_save_version2 \
    --deselect tests/ndarray/test_reductions.py::test_save_version3 \
    --deselect tests/ndarray/test_reductions.py::test_save_version4
}

package() {
  cd $pkgname

  python -m installer --destdir="$pkgdir" dist/*.whl

  # why are these files there?
  (
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  cd "$pkgdir$site_packages"
  rm -vrf include lib share
  )

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
