# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-blosc2
pkgver=4.9.1
pkgrel=1
pkgdesc='Wrapper for the blosc2 compressor'
arch=(x86_64)
url='https://github.com/Blosc/python-blosc2'
license=(BSD-3-Clause)
depends=(
  blosc2
  glibc
  python
  python-msgpack
  python-ndindex
  python-numexpr
  python-numpy
  python-httpx
  python-h2
  python-pydantic
  python-rich
  python-threadpoolctl
)
makedepends=(
  cmake
  cython
  git
  ninja
  python-build
  python-installer
  python-scikit-build-core
  python-setuptools
)
checkdepends=(
  python-psutil
  python-pytest
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('39af225cc2054e2e10895082baf7366e23c8e93e2294a400944a50f01ffea57c911cae627c171a1bf159fc1fba0cbc01570df338617f6344a7d30400744b9fd3')
b2sums=('f63c146bf44064d998d2abb114b0fbfb05b9062c1dd988e7e41993b1d69920b6dc6457b3d7b05f4cb46793b2e3550d25edc1c33464178d70c05c32960047d73e')

build() {
  cd $pkgname
  export CMAKE_ARGS="-DUSE_SYSTEM_BLOSC2=ON"
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname
  python -m venv venv-test --system-site-packages
  ./venv-test/bin/python -m installer dist/*.whl
  # Deselect tests failing since v3.4.0, not sure why
  # test_expand_dims: sys.getrefcount() behavior changed in Python 3.14
  ./venv-test/bin/python -m pytest \
    --deselect tests/ndarray/test_resize.py::test_expand_dims \
    --deselect tests/ndarray/test_lazyexpr.py::test_broadcasting \
    --deselect tests/ndarray/test_lazyexpr.py::test_chain_expressions \
    --deselect tests/ndarray/test_lazyexpr.py::test_chain_persistentexpressions \
    --deselect tests/ndarray/test_reductions.py::test_broadcast_params \
    --deselect tests/ndarray/test_reductions.py::test_fast_path \
    --deselect tests/ndarray/test_reductions.py::test_save_version1 \
    --deselect tests/ndarray/test_reductions.py::test_save_version2 \
    --deselect tests/ndarray/test_reductions.py::test_save_version3 \
    --deselect tests/ndarray/test_reductions.py::test_save_version4
}

package() {
  cd $pkgname

  python -m installer --destdir="$pkgdir" dist/*.whl

  # why are these files there?
  (
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  cd "$pkgdir$site_packages"
  rm -vrf include lib share
  )

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
