# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=xdg-desktop-portal-hyprland
pkgver=1.2.1
_protocolver=4d29e48433270a2af06b8bc711ca1fe5109746cd
pkgrel=1
pkgdesc='xdg-desktop-portal backend for hyprland'
url="https://github.com/hyprwm/$pkgname"
arch=(x86_64)
license=(MIT)
provides=(xdg-desktop-portal-impl)
depends=(libinih
         libuuid.so
         libelogind
         pipewire
         qt6-base
         qt6-wayland
         sdbus-cpp
         xdg-desktop-portal)
makedepends=(cmake
             meson
             scdoc
             wayland
             wayland-protocols)
optdepends=('grim: required for the screenshot portal to function'
            'slurp: support for interactive mode for the screenshot portal')
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/v$pkgver.tar.gz"
        "https://github.com/hyprwm/hyprland-protocols/archive/$_protocolver.tar.gz"
        hyprland-portals.conf)
sha256sums=('7f902a954bfc60ea081a40b482be837fad558b14aa751546b1f51ac9e8daef17'
            '5f6be4d870e94314f05ec7ff9c9c1f028748230ff80a7f89aeaf66c08e5c70e0'
            '33b80d588498992c1d4387c32775cab673a9a8dab2d917c0b4b20b59cebaa308')

prepare() {
    cd "$_archive/subprojects"
    rm -rf hyprland-protocols sdbus-cpp
    ln -sfT "$srcdir/hyprland-protocols-$_protocolver" hyprland-protocols
}

build() {
    cd "$_archive"
    artix-meson -Dsystemd=disabled build
    ninja -C build
    make -C hyprland-share-picker all
}

package() {
    cd "$_archive"
    DESTDIR="$pkgdir" ninja -C build install
    install -Dm0755 -t "$pkgdir/usr/bin/" hyprland-share-picker/build/hyprland-share-picker
    install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE

    # fix detection with x-d-p 1.18
    install -Dm644 ${srcdir}/hyprland-portals.conf  ${pkgdir}/usr/share/xdg-desktop-portal/hyprland-portals.conf
}
