# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=kontact
pkgver=25.12.0
pkgrel=1
pkgdesc='KDE Personal Information Manager'
arch=(x86_64)
url='https://apps.kde.org/kontact/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         grantleetheme
         kcmutils
         kcolorscheme
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         ki18n
         kiconthemes
         kio
         kjobwidgets
         kontactinterface
         kmail-account-wizard
         kparts
         kservice
         kwidgetsaddons
         kxmlgui
         libkdepim
         pimcommon
         qt6-base
         qt6-webengine)
makedepends=(extra-cmake-modules
             kdoctools)
optdepends=(akregator
            kaddressbook
            kmail
            korganizer
            zanshin)
groups=(kde-applications
        kde-pim)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('c46916b6917d29621d29978df53e3af7f2b19a668f4eec91bc1f6d6c8a4ff92f'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
