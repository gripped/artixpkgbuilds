# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-protocol-websocket
pkgver=0.21.0
pkgrel=1
pkgdesc='A low level implementation of the WebSocket protocol'
arch=(any)
url='https://github.com/socketry/protocol-websocket'
license=(MIT)
depends=(
  ruby
  ruby-protocol-http
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-async-http
  ruby-async-websocket
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-decode
  ruby-falcon
  ruby-sus
  ruby-sus-fixtures-async
  ruby-sus-fixtures-async-http
)
options=(!emptydirs)
source=("git+${url}.git#tag=v${pkgver}")
sha512sums=('e462f3d7d4bf738449f71ae4521522f1d24e6644b3fb2203505fd22e81ec18e49c04c974e844b95ed4e99187b00109995930240e6bf3696590465f8a6a5270dd')
b2sums=('d057e5ce7559eb8aba2350f4066b91a199009bbb55e4f4c346f61c0bda0a2f93fc7011aa5e32f825d3be7a7e091ad9c86f4d907edaf4f51b42f9e945ee42e19d')

prepare() {
  cd protocol-websocket
  sed -r -e 's|~>|>=|g' -e '/signing_key/d' -i protocol-websocket.gemspec
  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd protocol-websocket
  gem build protocol-websocket.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    protocol-websocket-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd protocol-websocket
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd protocol-websocket
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 license.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
