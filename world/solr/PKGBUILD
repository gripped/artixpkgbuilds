# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=solr
pkgver=9.10.1
pkgrel=1
pkgdesc="Open source enterprise search platform built on Apache Lucene"
arch=(any)
url="https://lucene.apache.org/solr/"
license=(Apache)
depends=(bash 'java-runtime=11')
makedepends=(gradle git groovy 'java-environment=11' npm)
backup=(
  etc/$pkgname/server/jetty-http.xml
  etc/$pkgname/server/jetty-https.xml
  etc/$pkgname/server/jetty-ssl.xml
  etc/$pkgname/server/jetty.xml
  etc/$pkgname/server/webdefault.xml
  etc/$pkgname/$pkgname.in.sh
  etc/$pkgname/$pkgname.xml
  etc/$pkgname/zoo.cfg
)
source=(
  https://archive.apache.org/dist/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver-src.tgz
  $pkgname.sysusers
  $pkgname.tmpfiles
  support-reproducible-builds.patch
  skip-failing-test.patch
  skip-checks-for-git-repo.patch
  allow-using-system-gradle.patch
)
sha512sums=('14e786eacc4964bb7c41a5596d9eea930f66b6c27efa3da4a2941d2962d6ba70bde34a22b5909cc80b8953ff505cdea327e2a04222e4dfb2058d52903b4bef36'
            '4a92e55709bcaffeab9456b2c3a7e76b38ab91f0bcf8a714f7da0691c46dfb28ef39741827ab90a1e7a26d716acfdc050af5cd4d043137bff51c07e114819fc2'
            '97252d1ba1e4e211a6b5a038981cbc9d6663a0d7a980b23484f838eebb2f8194571a3f34ea6b92ed0efd4b7f862e020b5e3e8478ad1de1c6832e232d0b20ff6d'
            'cda14703a9144e0add93d6f7a32691a80e2cadbb738c0c752122e6e27bbf1441a48a1a2ee146f10dd4f0ba4e5f5e38a46b45c1e01ff8c79bd477f3030a7c265d'
            'cdf46ddc62f3f8c7b258a806cf1b2765b156077becb77a97dbe3dae8ee05942d5fcf34e40ff25da5c4022fe3fb47bf378f51e58fd18005f5b0f525099ae22353'
            '1ad7eb67116fc58e7ad38926a696e76f9bc10447101e95ddfb0da914cb7a9916aeab47ad498fec3ff18285827f21d8855c1854778ce09c8e70dc9ce1576b57d1'
            '5e45fcd63be50484dbdd7b909f187d4d90a89639b80571bd93c432599b2064498344d4168dd06dfc45368ce043a873ae6be804d3ff1a3c42746b045c215cab92')
b2sums=('4fb5d7485188e5cbd239909758c55c55de485d25041906787d53825480849e576a9a7087070774d2de8d33a9a1578dec60e3652123e09d913b7d5fc67aee8ecd'
        'bd7624d92430c461063faa63dc00f5b546f86937c01161d3f2027406a37e8e3b7bcd7b3ca4b268e2ccd6c96c3f49addcfea4a86844145822ed70eaecdd3fd4bc'
        '37ab5d9af1da1178fcc58ce39654fdecb842b24d4e2264a2eec95c10223e7d003f27a6b2957a267a17ce0fa72258d96642d5b909963576a5a7d13e4e05c47a51'
        '60b0b2124ff8e4e2faa9323a0156fa02ea606c5779f7b80e42a05de78fa7e67546e33a444c5b1b4d61aaf2dcd067430022091449da386c310b8a99ce75eb1d3c'
        '8120846e77a55611c07ba27ce526820b0a71ddc749caf18e9e936eef6e033cc9836dcf449aebe04090b36afb603ef1d15ac08d60d965c5af46c21fc8aadc0134'
        '3e7d4e76994752bd62aa6e519bbfecc9db2ad81d3f086ea1920f5bc78bb93c68aab6ca01225860e46e59666af450f3a826fe91edf7a820396be342d431002b94'
        'ef52cfc76c484b78bacfee8a6e6321d80d3a03bdf11f81ab15e8328dd680cc87010e34a3f59423325707f60c87fe03fb2e77a082140e364d61ebaf5582101f59')
# list of trusted signing keys: https://downloads.apache.org/solr/KEYS
validpgpkeys=(
  2085660D9C1FCCACC4A479A3BF160FF14992A24C  # Ishan Chattopadhyaya <ishan@apache.org>
  E58A6F4D5B2B48AC66D5E53BD4F181881A42F9E6  # Ignacio Vera (CODE SIGNING KEY) <ivera@apache.org>
  81D3EB0408B4E1EB10AF443BA4F4C886B29BC2F4  # Alan Woodward (CODE SIGNING KEY) <romseygeek@apache.org>
  86EDB9C33B8517228E88A8F93E48C0C6EF362B9E  # Mike Drob (CODE SIGNING KEY) <mdrob@apache.org>
  38DA0C3CE8181703A08E4D57377C3BA26AD29C0A  # Bruno Roustant <broustant@apache.org>
  50E3EE1C91C7E0CB4DFB007B369424FC98F3F6EC  # Houston Paul Putman IV (CODE SIGNING KEY) <houston@apache.org>
  902CC51935C140BF820230961FD5295281436075  # Jason Gerlowski (CODE SIGNING KEY) <gerlowskija@apache.org>
  7D8D90F8F64F23077AC87CF7CB77CB79928BB0EC  # Atri Sharma <atri@apache.org>
  CFCE5FBB920C3C745CEEE084C38FF5EC3FCFDB3E  # Noble Paul (CODE SIGNING KEY) <noble@apache.org>
  FBC25D7E1712025294FE66590A6AA179B9BBF45E  # Timothy Potter (CODE SIGNING KEY) <thelabdude@apache.org>
  9722F25F650057E26C803B60A6D064D833B3A969  # Mayya Sharipova (CODE SIGNING KEY) <mayya@apache.org>
  E6E21FFCDCEA14C95910EA65051A0FAF76BC6507  # Adrien Grand (CODE SIGNING KEY) <jpountz@apache.org>
  3558857D1F5754B78C7F8B5A71A45A3D0D8D0B93  # Jan HÃ¸ydahl (CODE SIGNING KEY) <janhoy@apache.org>
  C3E7CBD9B9FE2B419EB14B89612B39A5BC981763  # Michael A. Gibney <magibney@apache.org>
  FDB3D11D716BB32ACF8C93AB919B21537AA80271  # Justin Sweeney (CODE SIGNING KEY) <jsweeney@apache.org>
  2289AC4180E130507D7A82F479C211E0AEFCA72E  # Alex Deparvu <stillalex@apache.org>
  515EA995ED1DD799FA1422E5CA7514D8385D790B  # David Smiley (CODE SIGNING KEY) <dsmiley@apache.org>
  E05FDF113D89E7FB4A2DF4B2684D544160392455  # Jason Gerlowski (Release Signing Key) <gerlowskija@apache.org>
  1CF0DAD1470504C4EA95C697140BC45803B03F7F  # Patrick Gustav Heck (CODE SIGNING KEY) <gus@apache.org>
  EDF961FF03E647F9CA8A9C2C758051CCA3A13A7F  # Anshum Gupta <anshumg@apple.com>
)

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i "$srcdir"/skip-checks-for-git-repo.patch
  patch -Np1 -i "$srcdir"/support-reproducible-builds.patch
  patch -Np1 -i "$srcdir"/skip-failing-test.patch
  # patch -Np1 -i "$srcdir"/allow-using-system-gradle.patch
  sed -i 's/download = true/download = false/' gradle/node.gradle
}

build() {
  cd $pkgname-$pkgver

  # override adding -SNAPSHOT on version
  # skip signing of jars
  # skip generation of reference guide
  # 
  ./gradlew \
    -Dversion.release=$pkgver \
    assembleRelease \
    -Psign=false -x signJarsPublication \
    -Prefguide.include=false \
    --parallel \
    --daemon

  install -vdm 755 "$srcdir/$pkgname-$pkgver/dist"
  tar -xf "$pkgname/distribution/build/release/$pkgname-$pkgver.tgz" --strip 1 -C "$srcdir/$pkgname-$pkgver/dist"
}


package() {
  local config

  cd $pkgname-$pkgver

  # remove unneeded and OS specific scripts
  rm -rvf dist/bin/{install_solr_service.sh,solr.cmd,solr.in.cmd}

  # make target script executable
  chmod 755 dist/bin/${pkgname}
  # symlink script into PATH
  install -vdm 755 "$pkgdir/usr/bin/"
  ln -sv /usr/share/$pkgname/bin/$pkgname "$pkgdir/usr/bin/$pkgname"

  # configuration
  install -vDm 644 dist/bin/$pkgname.in.sh -t "${pkgdir}/etc/$pkgname"
  install -vDm 644 dist/server/etc/*.xml -t "${pkgdir}/etc/$pkgname/server"
  install -vDm 644 dist/server/$pkgname/$pkgname.xml -t "$pkgdir/etc/$pkgname/"
  install -vDm 644 dist/server/$pkgname/zoo.cfg -t "$pkgdir/etc/$pkgname/"

  # copy application
  install -vdm 755 "$pkgdir/usr/share/$pkgname/"
  cp -dr --no-preserve='ownership' dist/{bin,docs,example,licenses,modules,server} "$pkgdir/usr/share/$pkgname/"

  # symlink configuration into place
  ln -svf /etc/$pkgname/$pkgname.in.sh "$pkgdir/usr/share/$pkgname/"

  install -vdm 750 "$pkgdir/var/lib/$pkgname"
  ln -svf /etc/$pkgname/$pkgname.xml "$pkgdir/var/lib/$pkgname/"
  ln -svf /etc/$pkgname/zoo.cfg "$pkgdir/var/lib/$pkgname/"
  for config in {jetty,jetty-{http,https,ssl},webdefault}.xml; do
    ln -svf /etc/$pkgname/server/$config "$pkgdir/usr/share/$pkgname/server/etc/$config"
  done

  # logs directory
  install -vdm 750 "$pkgdir/var/log/$pkgname/"
  # docs
  install -vDm 644 dist/NOTICE.txt -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 ../$pkgname.sysusers "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  install -vDm 644 ../$pkgname.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
}
