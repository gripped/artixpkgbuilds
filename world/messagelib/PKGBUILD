# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=messagelib
pkgver=24.05.1
pkgrel=1
pkgdesc='KDE PIM messaging library'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL-2.0-or-later)
depends=(akonadi-contacts
         akonadi-mime
         akonadi-search
         gcc-libs
         glibc
         gpgme
         grantleetheme
         karchive
         kcalendarcore
         kcodecs
         kcolorscheme
         kcompletion
         kconfig
         kconfigwidgets
         kcontacts
         kcoreaddons
         kguiaddons
         ki18n
         kiconthemes
         kitemviews
         kmailtransport
         kmime
         kidentitymanagement
         kio
         kitemmodels
         kjobwidgets
         kmbox
         kpimtextedit
         kservice
         ktextaddons
         ktexttemplate
         ktextwidgets
         kwidgetsaddons
         kxmlgui
         libakonadi
         libgravatar
         libkdepim
         libkleo
         pimcommon
         qca-qt6
         qgpgme-qt6
         qt6-base
         qt6-webengine
         sonnet
         syntax-highlighting)
makedepends=(doxygen
             extra-cmake-modules
             qt6-doc
             qt6-tools)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('da2091616ddc3b8002031942b2e87a6bb4aabaa33b17d5298f5558f3c14f3646'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
