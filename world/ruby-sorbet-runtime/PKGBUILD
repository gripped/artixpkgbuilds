# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='sorbet-runtime'
pkgname="ruby-${_gemname}"
pkgver=0.6.12897
_version_hash='20260122164912-011041135'
pkgrel=1
pkgdesc="Sorbet's runtime type checking component"
arch=('any')
url="https://github.com/sorbet/sorbet"
license=('Apache-2.0')
depends=(
  ruby
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-concurrent
  ruby-minitest
  ruby-mocha
  ruby-parser
  ruby-rake
  ruby-subprocess
)
options=('!emptydirs')
source=(
  "${url}/archive/${pkgver}.${_version_hash}/${pkgname}-${pkgver}.${_version_hash}.tar.gz"
  "${pkgname}_fix_tests.patch"
)
sha512sums=('761bd523791d6398d52a43da0f55147f8dfa177bff2f1732242d6469884a14de8b0a405abffdde0b2bc50575f95cba6d8f28ec102585924cf276b2254e897d55'
            'a39f61f680da5c2d2dc83aec4e5358686eaa3834f76eaa68c85e0f7d7d32ad9bf97953395ab805ca5669e7d98c144469b68e19a9028e5ff90f4c917dd059538b')
b2sums=('4ff57fbd22e5f9b0168c466634469434e44eed85941a9220e2f9389bf07f1edaf7cadf29cdba9fc1dbb2a8fd3b6a4f269c6ae67a86c26533d49bbf0fbbd76f63'
        'a9c2e83f4e04509db40b21f827208eb0ec03ebaae7f3971ddd2dcd0253fe00baf74b905a7a81079d23ac6f939a2e092bba3d5db7c415b1f70283ad1b65de715b')

prepare() {
  cd "sorbet-${pkgver}.${_version_hash}"

  patch --verbose --strip=1 --input="../${pkgname}_fix_tests.patch"

  cd "gems/${_gemname}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended \
    --expression 's|~>|>=|g' \
    --expression "s|s\.version     = '0.0.0'|s.version     = '${pkgver}'|g" \
    "${_gemname}.gemspec"
}

build() {
  cd "sorbet-${pkgver}.${_version_hash}/gems/${_gemname}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unreproducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "sorbet-${pkgver}.${_version_hash}/gems/${_gemname}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rake test
}

package() {
  cd "sorbet-${pkgver}.${_version_hash}/gems/${_gemname}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 ../../LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
