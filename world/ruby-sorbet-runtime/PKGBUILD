# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='sorbet-runtime'
pkgname="ruby-${_gemname}"
pkgver=0.6.13342
_version_hash='20260716135343-aafce23c1'
pkgrel=1
pkgdesc="Sorbet's runtime type checking component"
arch=('any')
url="https://github.com/sorbet/sorbet"
license=('Apache-2.0')
depends=(
  ruby
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-concurrent
  ruby-minitest
  ruby-mocha
  ruby-parser
  ruby-rake
  ruby-subprocess
)
options=('!emptydirs')
source=(
  "${url}/archive/${pkgver}.${_version_hash}/${pkgname}-${pkgver}.${_version_hash}.tar.gz"
  "${pkgname}_fix_tests.patch"
)
sha512sums=('edd45d26f172738998f37278c384fbbf31f9f53f17db46f89c7b569748599c7833da404464254758604671ea9276d7df34e542cc8cb6f6a6d9a3be581fb4bfc6'
            'a39f61f680da5c2d2dc83aec4e5358686eaa3834f76eaa68c85e0f7d7d32ad9bf97953395ab805ca5669e7d98c144469b68e19a9028e5ff90f4c917dd059538b')
b2sums=('4c1a68a34dde3261e7c665f747b1b0be1fc7999fd168433eaaebab9876556c2701b3fc4ec4e1bdac7fcb474c70ba5aa6fd2fc1a38c88618b847b918f3657539a'
        'a9c2e83f4e04509db40b21f827208eb0ec03ebaae7f3971ddd2dcd0253fe00baf74b905a7a81079d23ac6f939a2e092bba3d5db7c415b1f70283ad1b65de715b')

prepare() {
  cd "sorbet-${pkgver}.${_version_hash}"

  patch --verbose --strip=1 --input="../${pkgname}_fix_tests.patch"

  cd "gems/${_gemname}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended \
    --expression 's|~>|>=|g' \
    --expression "s|s\.version     = '0.0.0'|s.version     = '${pkgver}'|g" \
    "${_gemname}.gemspec"
}

build() {
  cd "sorbet-${pkgver}.${_version_hash}/gems/${_gemname}"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --build-root "tmp_install" \
    "${_gemname}-${pkgver}.gem"
}

check() {
  cd "sorbet-${pkgver}.${_version_hash}/gems/${_gemname}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rake test
}

package() {
  cd "sorbet-${pkgver}.${_version_hash}/gems/${_gemname}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 ../../LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
