# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.br>


# TODO: Switch to Pyro5: https://github.com/irmen/Pyro5
pkgname=python-pyro
pkgver=4.82
pkgrel=6
pkgdesc='Distributed object middleware (RPC)'
arch=(any)
url='https://github.com/irmen/Pyro4'
license=(MIT)
depends=(
  python
  python-serpent
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('5c09f2f651e5fcd3545c5b377246160cfa4f744163cac0856832d063c223eb42b9fa25fe3cdbc1fd1dd75b4a23260718c79f437e53ec151ea681ea7645e8a03a')
b2sums=('3d4c903a6b4515851c949f8a59419990f801ac0eb21c449a5581aeec0c96af7b0ce463229d9af1a16fc78d702ec5a0bedce7cbaaeffbf2f5971aedc8b2db5920')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
 
