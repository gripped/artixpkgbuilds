# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: pandada8 <pandada8@gmail.com>

pkgname=v2ray
pkgver=5.52.0
pkgrel=2.1
pkgdesc="A platform for building proxies to bypass network restrictions"
arch=('x86_64')
url="https://github.com/v2fly/v2ray-core"
license=('MIT')
depends=('glibc' 'v2ray-domain-list-community' 'v2ray-geoip')
makedepends=('go' 'git')
backup=(etc/v2ray/config.json)
source=("git+https://github.com/v2fly/v2ray-core.git#tag=v$pkgver")
sha512sums=('dd145d4556e16e557541e3726a57f903362a68ba8753bc16be3190e1bd0cb5711876a5b089cd700e7ec68f87072860d9a21dfa14802a1fc9188017b485fd743e')

prepare() {
  cd v2ray-core

  export GOPATH="${srcdir}"
  go mod download -modcacherw -x
}

build() {
  cd v2ray-core
  export GOPATH="${srcdir}"
  export GOFLAGS="-buildmode=pie -mod=readonly -modcacherw"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  go build -ldflags "-compressdwarf=false -linkmode external" -o v2ray ./main
}

check() {
  cd v2ray-core
  go test -tags json -v -timeout 30m ./...
}

package() {
  cd v2ray-core
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/v2ray/
  install -Dm644 release/config/*.json -t "$pkgdir"/etc/v2ray/
  install -Dm755 v2ray -t "$pkgdir"/usr/bin/
}
