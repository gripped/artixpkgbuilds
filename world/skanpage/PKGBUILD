# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=skanpage
pkgver=23.08.5
pkgrel=1
pkgdesc='Utility to scan images and multi-page documents'
arch=(x86_64)
url='https://apps.kde.org/skanpage/'
license=(GPL)
depends=(ksanecore kirigami2 purpose5 tesseract kquickimageeditor5)
makedepends=(extra-cmake-modules)
groups=(kde-applications kde-utilities)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('38e2300e1466c43bb531989c29d11221ebcaa2e3e8bb062a814c1f03270b72bb'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  artix-cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
