# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Marcin (CTRL) Wieczorek <marcin@marcin.co>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=umockdev
pkgver=0.18.4
pkgrel=1
pkgdesc="Record/mock hardware devices for bug reports and regression tests"
arch=('x86_64')
url="https://github.com/martinpitt/umockdev/"
license=('LGPL')
depends=('udev' 'libgudev' 'libpcap')
makedepends=('meson' 'gobject-introspection' 'gtk-doc' 'vala')
source=(https://github.com/martinpitt/umockdev/releases/download/$pkgver/$pkgname-$pkgver.tar.xz)
sha256sums=('115306f17be78f8c99e20652e2deccdd48df38736bf00eb4f43fefc3809a319c')

build() {
  cd $pkgname-$pkgver

  artix-meson -Dgtk_doc=true ../build
  ninja -v -C ../build
}

check() {
  cd $pkgname-$pkgver

  meson test -C ../build --print-errorlogs
}

package() {
  cd $pkgname-$pkgver

  DESTDIR="${pkgdir}" meson install -C ../build
}
