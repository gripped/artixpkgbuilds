# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=xdg-dbus-proxy
pkgver=0.1.6
pkgrel=1
pkgdesc="Filtering proxy for D-Bus connections"
url="https://github.com/flatpak/xdg-dbus-proxy"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  dbus
  gcc-libs
  glib2
  glibc
)
makedepends=(
  docbook-xsl
  git
  meson
)
source=("git+$url#tag=$pkgver")
b2sums=('fca110423a5657ddc4c197024935ea6794ea2d028678feb5ac0aefc5241b634149e32b230e5eeaee62a8a3d235fd1dfecf803a0bcdfaab7072372ee9141fc945')

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
