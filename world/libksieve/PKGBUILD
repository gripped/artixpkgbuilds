# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=libksieve
pkgver=25.04.3
pkgrel=1
pkgdesc='KDE PIM library for managing sieves'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kcolorscheme
         kcompletion
         kconfig
         kcoreaddons
         ki18n
         kiconthemes
         kidentitymanagement
         kio
         kmime
         ktextaddons
         kwidgetsaddons
         libkdepim
         libsasl
         pimcommon
         qt6-base
         qt6-webengine
         sonnet
         syntax-highlighting)
makedepends=(doxygen
             extra-cmake-modules
             kdoctools
             kmailtransport
             qt6-doc
             qt6-tools)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('07f6d97362e408dabcfb191b0bfd728edfd5996ee37a84074e928cb2e9c13a9d'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
