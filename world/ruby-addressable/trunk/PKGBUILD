# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Mario Finelli <mario at finel dot li>
# Contributor: Artem Vorotnikov <artem at vorotnikov dot me>

_gemname=addressable
pkgname=ruby-addressable
pkgver=2.8.3
pkgrel=1
pkgdesc='URI Implementation'
arch=('any')
url='https://github.com/sporkmonger/addressable'
license=('Apache')
depends=('ruby' 'ruby-public_suffix')
makedepends=('ruby-rdoc')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/sporkmonger/addressable/archive/addressable-${pkgver}.tar.gz)
sha256sums=('b7c882e6a3be343f0a4695edc6fe27218b41bfeb9969e657f12fee9d0e08f499')
sha512sums=('4751fc304f1baefc6c57ef2cc1aec21a2b3ee58c5c3a159aa762f2eeb8df08276aeb18efaccefa9d887d2a4a03eeb9d1270a92f1d99df3cfa5cfe6eaee17ebdc')

prepare() {
  cd ${_gemname}-${_gemname}-${pkgver}
  sed -E 's|~>|>=|g' -i ${_gemname}.gemspec
  sed -E 's|"< [0-9.]+", ||g' -i ${_gemname}.gemspec
}

build() {
  cd ${_gemname}-${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
