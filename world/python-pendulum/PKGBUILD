# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pendulum
pkgname=python-pendulum
pkgver=3.2.0
pkgrel=1
pkgdesc="Python datetimes made easy"
arch=(x86_64)
url="https://pendulum.eustace.io/"
_url="https://github.com/python-pendulum/pendulum"
license=(MIT)
depends=(
  gcc-libs
  glibc
  python
  python-dateutil
  python-time-machine
)
makedepends=(
  python-build
  python-installer
  python-maturin
  python-wheel
)
checkdepends=(
  python-babel
  python-freezegun
  python-pytest
  python-pytz
)
source=(
  $pkgname-$pkgver.tar.gz::$_url/archive/$pkgver.tar.gz
)
sha512sums=('9f935499ec8bc7ba0f5f8be293ac292c0538bfd01d15cea760f849dd154d04de700bdef08bde874dc2da5c472deb8fd176476a3fbf2cb3d0100eabf368cebc34')
b2sums=('3dcf3ddf664aa87fc8860c070f7ac2f895901a204845fbc87d9569b18aa937f7600bfce213d520c1ad7972be4966f25775c61b50678097d2658f1cae42ca6e83')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
  )
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$site_packages:$PYTHONPATH"
  pytest "${pytest_options[@]}"
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {CHANGELOG.md,README.rst} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
