# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-parser
pkgver=3.3.11.1
pkgrel=1
pkgdesc='A production-ready Ruby parser written in pure Ruby'
arch=(any)
url='https://github.com/whitequark/parser'
license=(MIT)
depends=(
  ruby
  ruby-ast
  ruby-racc
)
makedepends=(
  git
  ragel6
  ruby-bundler
  ruby-rake
  ruby-rdoc
)
checkdepends=(
  ruby-erb
  ruby-minitest
  ruby-simplecov
)
options=(!emptydirs)
source=(git+https://github.com/whitequark/parser.git#tag=v$pkgver)
sha512sums=('5e496ac62607028cbd530b701100839c8017d2c6c9e65cee2880f8b216abc61201beeedceb404160de598e547f60efde29a728793bf9be995cef95446dd7566d')
b2sums=('2f7578fdf6c65d8e852c21a8f5e1e3f96397e42c3d9b3fad00e61dc7d62418a8b786fafe566ac89c47fb9bf12be10a46a075c63692c54a951c34dff4c60ff026')

prepare() {
  cd parser
  sed 's/~>/>=/' -i parser.gemspec
  sed -i '/[cC]liver/d' Rakefile
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd parser
  rake generate
  gem build parser.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    parser-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd parser
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd parser
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
