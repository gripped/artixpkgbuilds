# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-parser
pkgver=3.3.4.0
pkgrel=1
pkgdesc='A production-ready Ruby parser written in pure Ruby'
arch=(any)
url='https://github.com/whitequark/parser'
license=(MIT)
depends=(
  ruby
  ruby-ast
  ruby-racc
)
makedepends=(
  ragel
  ruby-bundler
  ruby-rake
  ruby-rdoc
)
checkdepends=(
  ruby-erb
  ruby-minitest
  ruby-simplecov
)
options=(!emptydirs)
source=(https://github.com/whitequark/parser/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('882330a6e0de62f19a452e587a32eb16732ffb4233e6664d5c90e3030857d4baab33b838489b4a3d122b48b21849ccf2ab291b0cafea6b728423f328a4ec9fe2')
b2sums=('fbac9486cdf08701a0fba0f24f35c42347e29d74ea203764445fe973e65c93ea801fd986a28e448c080c92a1b104e2692f3d5ed21d76543423cb0fbe5726a6bf')

prepare() {
  cd parser-$pkgver
  sed 's/~>/>=/' -i parser.gemspec
  sed -i '/[cC]liver/d' Rakefile
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd parser-$pkgver
  rake generate
  gem build parser.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    parser-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd parser-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd parser-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
