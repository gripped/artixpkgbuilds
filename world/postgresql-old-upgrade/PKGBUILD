# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Dan McGee <dan@archlinux.org>

pkgbase=postgresql-old-upgrade
pkgname=(
  postgresql-old-upgrade
)
pkgver=17.7
pkgrel=2
pkgdesc="Older PostgreSQL for migrating major versions with pg_upgrade"
url="https://www.postgresql.org/"
arch=(x86_64)
license=(PostgreSQL)
depends=(
  gcc-libs
  glibc
  icu
  krb5
  libldap
  libxml2
  llvm-libs
  lz4
  openssl
  pam
  util-linux-libs
  zlib
  zstd
)
makedepends=(
  clang
  llvm
  perl
  python
  tcl
  util-linux
)
source=(
  https://ftp.postgresql.org/pub/source/v${pkgver}/postgresql-${pkgver}.tar.bz2
)
b2sums=('245f866c7375566ca772171b89eb2277a17175b599973e740ce599605292a5847e68b857f83b208a24d0ab6938740faf00ab26758d72c7e119131745f8ab63cb')

# PostgreSQL releases are unsigned and only provide these sums
md5sums=('a4fa04d16e511e068736d154ca74752d')
sha256sums=('ef9e343302eccd33112f1b2f0247be493cb5768313adeb558b02de8797a2e9b5')

prepare() {
  cd postgresql-${pkgver}
}

build() {
  local configure_options=(
    --without-systemd
    --prefix=/opt/pgsql-${pkgver%%.*}
    --disable-rpath
    --disable-nls
    --disable-tap-tests
    --with-gssapi
    --with-icu
    --with-ldap
    --with-libxml
    --without-libxslt
    --with-llvm
    --with-lz4
    --with-openssl
    --with-pam
    --with-perl
    --with-python
    --without-readline
    --with-system-tzdata=/usr/share/zoneinfo
    --with-tcl
    --with-uuid=e2fs
    --with-zstd
  )

  # use fat LTO objects for static libraries
  CFLAGS+=" -ffat-lto-objects"
  CXXFLAGS+=" -ffat-lto-objects"

  mkdir -p build; cd build
  ../postgresql-${pkgver}/configure "${configure_options[@]}"
  make world-bin
}

package_postgresql-old-upgrade() {
  depends+=("postgresql-libs>=${pkgver}")
  optdepends=(
    'perl: for PL/Perl support'
    'python: for PL/Python 3 support'
    'tcl: for PL/Tcl support'
  )

  make -C build DESTDIR="${pkgdir}" install-world-bin


  install -Dm644 postgresql-${pkgver}/COPYRIGHT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set sw=2 sts=-1 et:
