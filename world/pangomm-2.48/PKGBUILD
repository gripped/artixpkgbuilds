# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=pangomm-2.48
pkgname=(
  pangomm-2.48
  pangomm-2.48-docs
)
pkgver=2.58.0
pkgrel=1
pkgdesc="C++ bindings for Pango"
url="https://www.gtkmm.org/"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  cairomm-1.16
  glib2
  glibc
  glibmm-2.68
  libgcc
  libsigc++-3.0
  libstdc++
  pango
)
makedepends=(
  cairomm-1.16-docs
  git
  glibmm-2.68-docs
  libsigc++-3.0-docs
  meson
  mm-common
)
options=(!emptydirs)
source=("git+https://gitlab.gnome.org/GNOME/pangomm.git#tag=$pkgver")
b2sums=('b6ffa68ee3c7c66385ef66089b213d5c9fec76a4efc816beee5c9ff5344c78390533df926946c3345301fe14d36ac1f5a198f092ef27e9738e310539e8520024')

prepare() {
  cd pangomm
}

build() {
  local meson_options=(
    -D maintainer-mode=true
  )

  artix-meson pangomm build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_pangomm-2.48() {
  depends+=(
    libcairomm-1.16.so
    libglibmm-2.68.so
    libsigc-3.0.so
  )
  provides=(libpangomm-2.48.so)

  meson install -C build --destdir "$pkgdir"

  # Split -docs
  mkdir -p docs/usr/share
  mv -t docs/usr/share "$pkgdir"/usr/share/{devhelp,doc}
}

package_pangomm-2.48-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv -t "$pkgdir" docs/*
}

# vim:set sw=2 sts=-1 et:
