# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=d-spy
pkgver=49.1
pkgrel=1
pkgdesc="D-Bus debugger for GNOME"
url="https://apps.gnome.org/Dspy/"
arch=(x86_64)
license=(
  GPL-3.0-or-later
  LGPL-3.0-or-later
)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  libdex
)
makedepends=(
  git
  meson
)
groups=(gnome-extra)
source=("git+https://gitlab.gnome.org/GNOME/d-spy.git#tag=${pkgver/[a-z]/.&}")
b2sums=('dc4d57e41612aa7cc428e1f4196c2e641d4545ce401a8b1f35a8229c74dc91d6f287ef100f3a0a3aebea962eee8bfe8181c3756fa775543a583951b46d12ecca')

build() {
  artix-meson d-spy build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
