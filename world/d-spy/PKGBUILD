# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=d-spy
pkgver=49.2
pkgrel=1
pkgdesc="D-Bus debugger for GNOME"
url="https://apps.gnome.org/Dspy/"
arch=(x86_64)
license=(
  GPL-3.0-or-later
  LGPL-3.0-or-later
)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  libdex
)
makedepends=(
  git
  meson
)
groups=(gnome-extra)
source=("git+https://gitlab.gnome.org/GNOME/d-spy.git#tag=${pkgver/[a-z]/.&}")
b2sums=('8273cb74448672b6aeb611d7f47271e8dad884d8152e74f933d90d4f7bdcf82f7173bd1555dce91c5fcf4fa6aed3b6788ba7f576466e9343414cee5105d5008a')

build() {
  artix-meson d-spy build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
