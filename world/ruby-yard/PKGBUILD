# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Jerome Leclanche <jerome@leclan.ch>

_gemname=yard
pkgname=ruby-${_gemname}
pkgver=0.9.35
pkgrel=1
pkgdesc='YARD is a Ruby Documentation tool. The Y stands for "Yay!"'
url='https://yardoc.org/'
arch=('any')
license=('MIT')
depends=(
  ruby
  ruby-rdoc
  ruby-webrick
)
makedepends=(
  ruby-irb
  ruby-rake
  ruby-samus
)
options=('!emptydirs')
source=(https://github.com/lsegal/yard/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('83251d52b8f4ec00411cb223386b9b3763da530c37db7a99a9a3747139b97f52d4e978e7556c3d50896537ad60a89d88b67affc630d4f7617062c33e6ec223ac')
b2sums=('27e8c0e576f5150d1adcfd63afdfcb33f437319ae646347c8b0ab24e6b093b4d3de3f0703cc0683a420e93f8d6261c2221d0af7c3ed6dfe463d2f296bdfeda49')

build() {
  cd ${_gemname}-${pkgver}
  rake gem
}

check() {
  cd ${_gemname}-${pkgver}
  rake spec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "${pkgdir}/${_gemdir}" \
    --bindir "${pkgdir}/usr/bin" \
    ${_gemname}-${pkgver}.gem

  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -a docs/* "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"

  cd "${pkgdir}/${_gemdir}"
  rm -rf cache gems/${_gemname}-${pkgver}/{ext,lib/*/*.so,man} \
    extensions/*/*/${_gemname}-${pkgver}/{mkmf.log,gem_make.out}
}

# vim: ts=2 sw=2 et:
