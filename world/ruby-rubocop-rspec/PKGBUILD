# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='rubocop-rspec'
pkgname="ruby-${_gemname}"
pkgver=3.6.0
pkgrel=2
pkgdesc='Code style checking for RSpec files. A plugin for the RuboCop code style enforcing & linting tool'
arch=('any')
url="https://github.com/rubocop/${_gemname}"
license=('MIT')
depends=(
  ruby
  rubocop
  ruby-lint_roller
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bundler
  ruby-parallel
  ruby-rack
  ruby-rake
  ruby-rspec
  ruby-rubocop-performance
  ruby-rubocop-rake
  ruby-yard
)
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('184933da287d2c1f06331a6c2019a8bec69831f13f062d837dc6e9a524d7b0fc7d5ba8e4668ec11141943efe72da6cbb61fff775fc2bee4bb5b56e21390e3a4a')
b2sums=('60a0efbe7e2c59db76d886f8150c462897288f9212cbf8c2192a6f0be721496eceb47ac3e09608717561d5fbf289749914fc2153e5721c2902d0ccbab2a3b4ed')

prepare() {
  cd "${_gemname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"

  sed --in-place --regexp-extended \
    --expression '/bump/d' \
    --expression '/simplecov/d' \
    Gemfile

  rm tasks/cut_release.rake

  # remove some broken tests for now
  sed --in-place --regexp-extended \
    --expression '/ignores example groups defined inside methods/,+22d' \
    spec/rubocop/cop/rspec/empty_example_group_spec.rb
  sed --in-place --regexp-extended \
    --expression '/ignores when inside define method/,+15d' \
    --expression '/ignores when inside define singleton method/,+13d' \
    spec/rubocop/cop/rspec/focus_spec.rb
}

build() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install/${_gemdir}/cache/" \
    "tmp_install/${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install/${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install/${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install/${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  NO_COVERAGE=true GEM_HOME="tmp_install/${_gemdir}" rake spec
}

package() {
  cd "${_gemname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 MIT-LICENSE.md --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}
