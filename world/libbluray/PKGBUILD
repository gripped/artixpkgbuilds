# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andrew Cook <ariscop@gmail.com>

pkgname=libbluray
pkgver=1.5.0
pkgrel=1
pkgdesc='Library to access Blu-Ray disks for video playback'
arch=(x86_64)
url=https://www.videolan.org/developers/libbluray.html
license=(LGPL-2.1-only)
depends=(
  fontconfig
  glibc
  libfreetype.so
  libxml2
)
makedepends=(
  apache-ant
  git
  java-environment=17
  meson
  ninja
)
optdepends=('java-runtime: BD-J library')
provides=(libbluray.so)
source=(
  git+https://code.videolan.org/videolan/libbluray.git#tag=${pkgver}
  git+https://code.videolan.org/videolan/libudfread.git
)
b2sums=('dfc9b0bc0656a115a27599f3df1fddb6a485762fc27624bf4ad35e0930ad2f2c13db2ee9c4b6cdb2ac9fc69d77e69bb2b1dab9af0266080deed54366751a4bfb'
        'SKIP')

prepare() {
  cd libbluray
  for submodule in contrib/libudfread; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../${submodule#*/}
    git -c protocol.file.allow=always submodule update ${submodule}
  done
}

build() {
  artix-meson libbluray build
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}
