# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-tweaks
pkgver=46.0
pkgrel=3
pkgdesc="Graphical interface for advanced GNOME 3 settings (Tweak Tool)"
url="https://wiki.gnome.org/Apps/Tweaks"
arch=(any)
license=(GPL-3.0-or-later)
depends=(
  dconf
  glib2
  gnome-desktop-4
  gsettings-desktop-schemas
  gtk4
  hicolor-icon-theme
  libadwaita
  libgudev
  libnotify
  pango
  python
  python-gobject
)
makedepends=(
  git
  meson
)
groups=(gnome-extra)
source=(
  # Key that signed 46.0 was revoked
  "git+https://gitlab.gnome.org/GNOME/gnome-tweaks.git#tag=$pkgver"
)
b2sums=('f6de88d985864a2eedc3e7dce9030379c952826ef4d4295b665d47d36d67d001bf3574b68c71596d9edd93204cf13359a559663340b694800f8409c3159e0465')
validpgpkeys=(
  A3C5EBBF43FBA5F829F1A2548D66762250C07E85 # Evan Welsh <ewlsh@gnome.org>
)

prepare() {
  cd $pkgname

  # https://gitlab.archlinux.org/archlinux/packaging/packages/gnome-tweaks/-/issues/1
  # https://gitlab.gnome.org/GNOME/gnome-tweaks/-/merge_requests/145
  git cherry-pick -n abfb4692483a9b2a9207f13f136d57fac4ac0e87
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
