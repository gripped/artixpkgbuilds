# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org> 
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=umbrello
pkgver=25.12.1
pkgrel=1
pkgdesc='UML modeller'
arch=(x86_64)
url='https://apps.kde.org/umbrello/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         karchive
         kcompletion
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         ki18n
         kiconthemes
         kio
         ktexteditor
         kwidgetsaddons
         kxmlgui
         libxml2
         libxslt
         qt6-base
         qt6-svg)
makedepends=(extra-cmake-modules
             kdoctools)
groups=(kde-applications
        kde-sdk)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('13146ca7d51029196b5ff706a37183815ec9eef5b07938dd127c498824fb2408'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() { 
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_WITH_QT6=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
