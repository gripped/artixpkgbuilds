# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gedit
pkgver=48.0
pkgrel=1
pkgdesc="Easy-to-use general-purpose text editor"
url="https://gedit-technology.github.io/apps/gedit/"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  bash
  cairo
  dconf
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gobject-introspection-runtime
  gsettings-desktop-schemas
  gspell
  gtk3
  hicolor-icon-theme
  libgedit-amtk
  libgedit-gfls
  libgedit-gtksourceview
  libgedit-tepl
  libgirepository
  libpeas
  pango
  python
  python-gobject
)
makedepends=(
  appstream
  appstream-glib
  desktop-file-utils
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  meson
  vala
  yelp-tools
)
optdepends=('gedit-plugins: Additional features')
conflicts=('gedit-code-assistance<=3.16.0+4+gd19b879-1')
source=(
  "git+https://gitlab.gnome.org/GNOME/gedit.git#tag=${pkgver/[a-z]/.&}"
  "git+https://gitlab.gnome.org/GNOME/libgd.git"
)
b2sums=('67a57a85088496c614f3c0c8d8a1074ba005328e2a38efe3aa530ede65bd51f28c11dabb25854c2e04a58a11aeb33743d057f25d37aa796334701d5d459e557d'
        'SKIP')

prepare() {
  cd gedit

  git submodule init 
  git submodule set-url subprojects/libgd "$srcdir/libgd"
  git -c protocol.file.allow=always submodule update
}

build() {
  local meson_options=(
    -D gtk_doc=true
    -D require_all_tests=true
  )

  artix-meson gedit build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
