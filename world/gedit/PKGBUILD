# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gedit
pkgver=49.0
pkgrel=2
pkgdesc='Easy-to-use general-purpose text editor'
arch=(x86_64)
url='https://gedit-text-editor.org/'
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gsettings-desktop-schemas
  gspell
  gtk3
  hicolor-icon-theme
  libgedit-amtk
  libgedit-gfls
  libgedit-gtksourceview
  libgedit-tepl
  libpeas
  pango
)
makedepends=(
  appstream
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  itstool
  meson
)
optdepends=('gedit-plugins: Additional features')
source=(
  "git+https://gitlab.gnome.org/GNOME/gedit.git#tag=${pkgver/[a-z]/.&}"
  git+https://gitlab.gnome.org/GNOME/libgd.git
  gedit-new-tab.patch
  gedit-prgname.patch
)
b2sums=(
  bbd269332f6167d5b6b51837131d29ecb0f588645ec5d5a06729760d7ba415f91125ae017ddc824ca74eb973c261610fb8e5ae47ca232d88ab5f8aa075fb8aa4
  SKIP
  3f30dde7f7141f2992f750fb01f215823f48c384c2e24d66d94d0471b99362024ed76512092e31e1703890d0e4e94384ab599be042405248807b5183ae1e6c1f
  7a7a00699f32fdc6f3e7d64411c0e9158ae92cf1874de7d32cff0249e16368282e147a625897ae02f2ecf153db809619a87fa29beb7c0a4e4d16540053e2af80
)
validpgpkeys=(08C0A6B1A08310A45363B328426F014A261FD87F) # Sébastien Wilmet <swilmet@mailfence.com>

prepare() {
  cd $pkgname

  git submodule init
  git submodule set-url subprojects/libgd "$srcdir/libgd"
  git -c protocol.file.allow=always submodule update

  # Don't create new tab unless requested
  git apply -3 ../gedit-new-tab.patch

  # Set prgname to application ID
  git apply -3 ../gedit-prgname.patch
}

build() {
  artix-meson $pkgname build \
    -D gtk_doc=true \
    -D require_all_tests=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
