# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gedit
pkgver=48.1
pkgrel=3
pkgdesc="Easy-to-use general-purpose text editor"
url="https://gedit-text-editor.org/"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  bash
  cairo
  dconf
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gobject-introspection-runtime
  gsettings-desktop-schemas
  gspell
  gtk3
  hicolor-icon-theme
  libgedit-amtk
  libgedit-gfls
  libgedit-gtksourceview
  libgedit-tepl
  libgirepository
  libpeas
  pango
)
makedepends=(
  appstream
  appstream-glib
  desktop-file-utils
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  meson
  python-gobject
  vala
  yelp-tools
)
optdepends=('gedit-plugins: Additional features'
            'python-gobject: Python extension support')
conflicts=('gedit-code-assistance<=3.16.0+4+gd19b879-1')
source=(
  "git+https://gitlab.gnome.org/GNOME/gedit.git#tag=${pkgver/[a-z]/.&}"
  "git+https://gitlab.gnome.org/GNOME/libgd.git"
)
b2sums=('ed42c57aeac7bfd5d6f3b5ae17af8060698d515c5f2fbfc815a8eb530e27b8bc506e0090025250945aa425b27921a71f7b7635bb09a6d1884287f324c9a0efd4'
        'SKIP')

prepare() {
  cd gedit

  git submodule init
  git submodule set-url subprojects/libgd "$srcdir/libgd"
  git -c protocol.file.allow=always submodule update
}

build() {
  local meson_options=(
    -D gtk_doc=true
    -D require_all_tests=true
  )

  artix-meson gedit build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
