# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Cilyan Olowen <gaknar@gmail.com>
# Contributor: fill <mailbox@fillo.ru>
# Contributor: Anton Shestakov <engored*ya.ru>

pkgname=flashrom
pkgver=1.5.1
pkgrel=2
pkgdesc="Utility for reading, writing, erasing and verifying flash ROM chips"
arch=(x86_64)
url="https://www.flashrom.org/"
license=(GPL-2.0-or-later)
depends=(
  libcrypto.so
  libftdi
  libusb
  pciutils
)
makedepends=(
  cmocka
  git
  meson
  python-sphinx
)
optdepends=('dmidecode: for SMBIOS/DMI table decoder support')
source=(git+https://github.com/flashrom/flashrom.git#tag=v${pkgver})
sha256sums=('4a755be0c279d95e68f5d8c8c0ea478c9cb2a372cbf211bc2dc67055400e32b8')

prepare() {
  sed -i 's#GROUP="plugdev"#TAG+="uaccess"#g' flashrom/util/flashrom_udev.rules
}

build() {
  artix-meson ${pkgname} build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
  install -Dm644 flashrom/util/flashrom_udev.rules "${pkgdir}"/usr/lib/udev/rules.d/70-flashrom.rules
}
