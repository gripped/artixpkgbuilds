# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Cilyan Olowen <gaknar@gmail.com>
# Contributor: fill <mailbox@fillo.ru>
# Contributor: Anton Shestakov <engored*ya.ru>

pkgname=flashrom
pkgver=1.5.0
pkgrel=1
pkgdesc="Utility for reading, writing, erasing and verifying flash ROM chips"
arch=(x86_64)
url="https://www.flashrom.org/"
license=(GPL-2.0-or-later)
depends=(
  libftdi
  libusb
  pciutils
)
makedepends=(
  cmocka
  git
  meson
  python-sphinx
)
optdepends=('dmidecode: for SMBIOS/DMI table decoder support')
source=(git+https://github.com/flashrom/flashrom.git#tag=v${pkgver})
sha256sums=('c9f8899bd8f0e8c21f503212e627089fb5c9267e7afca636a9f43eb1fad47693')

build() {
  artix-meson ${pkgname} build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"

  cd ${pkgname}
  sed -i 's#GROUP="plugdev"#TAG+="uaccess"#g' util/flashrom_udev.rules
  install -Dm644 util/flashrom_udev.rules "${pkgdir}"/usr/lib/udev/rules.d/70-flashrom.rules
}
