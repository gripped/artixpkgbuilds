# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=tang
pkgver=15
pkgrel=1
pkgdesc='Server for binding data to network presence'
arch=('x86_64')
url='https://github.com/latchset/tang'
license=('GPL3')
depends=('http-parser' 'jose')
makedepends=('git' 'asciidoc' 'meson')
source=("git+$url.git#tag=v$pkgver"
        'tang.sysusers.conf'
        'tang.tmpfiles.conf')
sha512sums=('ee157ed9e1d87a14e01696117219e2420c9c11da1bee264779db5c866b4a70f56f7c333bc0d92c3ab6bcb9d6cd59cdeab1b15d934b8573383dccb7c9d5fdc740'
            '08b5abb5ff5195a96c1196e7336eefeabad36a82ef8862881689e3c9cfa8aebc5679e14c4aacc3fa80793fe42ffa591e6ec0ec07bf98f6e0d783e23b2f06218a'
            '48c3026b37ce3fe180633facb99a194e1fcb0ff860f6dff33541b6216b441572c1de0620102ce25e36c06f3c769b3149cb595f5aa4e355416c680b1c6f73e957')
validpgpkeys=('7CE2CB3D2AF59FE1EA22F551D0D219ED1F7E762C') # Sergio Correia <scorreia@redhat.com>

build() {
	cd "$pkgname"
	meson --libexecdir=/usr/lib --buildtype=plain build
	meson compile -C build
}

check() {
	cd "$pkgname"
#       fails on build server, test locally
#	meson test -C build
}

package() {
	cd "$pkgname"
	DESTDIR="$pkgdir" meson install -C build
	mkdir -pm700 "$pkgdir/var/db/tang"
	install -Dm644 "$srcdir/tang.sysusers.conf" "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
	install -Dm644 "$srcdir/tang.tmpfiles.conf" "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"

	rm -r $pkgdir/usr/lib/systemd
}
