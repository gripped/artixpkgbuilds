# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Andreas 'Segaja' Schleifer <archlinux at segaja dot de>

_gemname='puma'
pkgname="ruby-$_gemname"
pkgver=6.4.2
pkgrel=3
pkgdesc='A Ruby/Rack web server built for concurrency'
arch=('x86_64')
url='https://puma.io/'
license=('BSD-3-CLAUSE')
options=(!emptydirs)
depends=(
  ruby
  ruby-nio4r
  ruby-sd_notify
)
makedepends=(
  git
  ruby-bundler
  ruby-minitest
  ruby-minitest-proveit
  ruby-minitest-retry
  ruby-minitest-stub-const
  ruby-rack
  ruby-rackup
  ruby-rake
  ruby-rake-compiler
  ruby-rdoc
)
source=(
  "https://github.com/puma/puma/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
  "${pkgname}_fix_tests.patch"
  # see https://github.com/puma/puma/issues/3489
  "https://github.com/puma/puma/commit/11e5d6dcbf0f6bfb170f38c5713d5851399a55dd.patch"
  "https://github.com/puma/puma/commit/0b6b91080db31e7c51b2a430efee2c2827b050f8.patch"
  "https://github.com/puma/puma/commit/9b3a89b4cb068809f7840aeb8f045d0079f1266d.patch"
  "https://github.com/puma/puma/commit/6db7c28b069710419cc0a78dbc93c351b222a8e4.patch"
  "https://github.com/puma/puma/commit/aadf9e25c38c2a9a3c3aa36b20f37f340696da3d.patch"
)
sha512sums=('95f1aa43b019f14160c638ac04bc7648a9f49b5ad418319bcbab90fae7da0e94f122701ce71da864d27c7cc3fe5a2ff5a2ac6b88b8582ea5ce2201d54784af67'
            '45c327aedbaa9bdaeffcb64b02dc09810d58aee92b6054ba3a06baa01f8c65f5ff57c50917ec1cc93041e79ee54b152486e22a2d7332731012dde8d0b947cb06'
            '0d02475ca92272326d06f0a6a6822108b1a0ab5a3f4f65d3ee881e278903a21d9bb833fff3b36c1bb40d5b28d9889f6a7f8a833e1a2b8d986a2dde9331b26c47'
            '7a13faef06a5d70a937fa41eddac139f18b5200475e500e4b32ed7b4def8cc2557f1286de96bd5c82a2f11ef62fd785169024f25e0fac541ac66fc079cf003f6'
            '7a1b602ff012659fd3140d8aefc5a38db176923fc755728c0fea3e9d76cd693ac0533286c17deec48d2421bb924c03b7ffeb4723b1a1bae219e0fd0ee8e0c33d'
            '3ef6c6f5c2c9d900ee9383cf8c3be44b09db98be1e8bbfa1192cf6c69490a4719de45737dec8ba52145723f3c232c3aee0f5896a34a3b5dcea1c0e716b3b527f'
            'eb1b2ea7adedb62da75192b2488618a6f77dcc13088ad36eae991345f99fe6d582cdf6db71e35e8778bbe7c890582b5b1c14f9b1fd77c14e827a307a49a1cf47')
b2sums=('5d5ee194c2247eb5cf49b81bc2d32c37a52e82eefe94520a952c217663f111ca5dacc54362c522772127882a7c39da8b9ebc6851604cc719db4e170c59559784'
        '348ba36808641101242923ca8056548465374faa8caabfe2d013a2fd66f55296be657076099b980d30beed2d8c81df04bb122199f4ffe82276e8b9e43c195ce8'
        '7e87841ee8c3c983dca25970ae7b5f7a28cc37a957cd9268a7020dba5eafff8cc935f3c5e15771c6a6c4cf11dd896e33d6bab7ca8e5c824add743a8e99450a11'
        '4effff2e5346fdb8ca85d454c64ae9fb8736f7b5eb01280c734661a5a577f80bd8c6c7d5442c0fcf19e3129f6229f45e12cf3fda60dcecb219852a61b18af485'
        'e9cb943e993af3417a91b9661dc03715cc20bfac122f44eee089c8aafb89ecb184292d4cef8f9d62f80a1f5ac0d23d934ba2c51f39a39e426d14909deb42c0da'
        'a1aeae19cd7fc870d94f4f5d09a7cb2b0766d11d6a2807d5d4b8547b3c0d21eb20fe6f9935d21c909dd81db65a31fcf30f143f5944925fdfd1a303750ad22208'
        '1a5f60b4f9f63ad4d7995211176c85beef6639e27587d684f5c7798f6e6ae628ac1ab07772c474224454735f4063c70407967b7b6f1dcb1a7f85fc3f9d2be040')

prepare() {
  cd "${_gemname}-${pkgver}"

  git apply -p1 ../11e5d6dcbf0f6bfb170f38c5713d5851399a55dd.patch
  git apply -p1 ../0b6b91080db31e7c51b2a430efee2c2827b050f8.patch
  git apply -p1 ../9b3a89b4cb068809f7840aeb8f045d0079f1266d.patch
  git apply -p1 ../6db7c28b069710419cc0a78dbc93c351b222a8e4.patch
  patch -Np1 -i ../aadf9e25c38c2a9a3c3aa36b20f37f340696da3d.patch

  # we built based on a tar archive, not a git repo
  sed --in-place 's/git ls-files/find/' "${_gemname}.gemspec"

  # update gemspec/Gemfile to allow newer version of the deps
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec" Gemfile

  # disable m. Only required for debugging during development
  sed --in-place "/'m'/d" Gemfile

  # disable localhost. Only required for self-signed certificates during development
  sed --in-place "/localhost/d" Gemfile

  # remove some tests and fix others
  patch --verbose --strip=1 --input="../${pkgname}_fix_tests.patch"

  rm --verbose \
    test/config/ssl_self_signed_config.rb \
    test/test_preserve_bundler_env.rb \
    test/test_puma_localhost_authority.rb \
    test/test_worker_gem_independence.rb
}

build() {
  cd "${_gemname}-${pkgver}"

  export PUMA_NO_RUBOCOP="true"

  rake compile
  rake build
}

# tests can be flaky with failures in TestIntegrationCluster#test_hot_restart_does_not_drop_connections_threads [test/helpers/integration.rb:490]
# if so then just re-run the tests
# see https://github.com/puma/puma/issues/3114
check() {
  cd "${_gemname}-${pkgver}"

  # https://github.com/puma/puma/blob/master/CONTRIBUTING.md#file-limits
  ulimit -Sn 4096

  export PUMA_NO_RUBOCOP="true"

  rake test:all TESTOPTS="--seed=16538"
}

package() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "${pkgdir}/${_gemdir}" \
    --bindir "${pkgdir}/usr/bin" \
    "pkg/${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "${pkgdir}/${_gemdir}/cache/" \
    "${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "${pkgdir}/${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "${pkgdir}/${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "${pkgdir}/${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete

  install -D --mode=644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -D --mode=644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}
