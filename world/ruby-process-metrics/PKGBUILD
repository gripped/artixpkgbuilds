# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='process-metrics'
pkgname="ruby-${_gemname}"
pkgver=0.13.0
pkgrel=1
pkgdesc='Provide detailed OS-specific process metrics'
arch=('any')
url='https://github.com/socketry/process-metrics'
license=('MIT')
depends=(
  procps-ng
  ruby
  ruby-console
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-sus
)
options=('!emptydirs')
source=("git+https://github.com/socketry/process-metrics.git#tag=v${pkgver}")
sha512sums=('a616c75620ef8124ba607a48e7ce8488989935726fec11cb11ce27b8fe074d886c4fb47a8ac01abd9e39697fbaee61a77433bdaa931f2dffe6b4e88479752dc2')
b2sums=('70d011f0d8046bdf48723a7e029f84c3109211bf2a36d9fcda96bd8185d0359d2957163cf3328352dce5270544046adb1cada54154e43412d7b40f8cfcb3f3c8')

prepare() {
  cd "${_gemname}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended \
    --expression 's|~>|>=|g' \
    --expression '/signing_key/d' \
    "${_gemname}.gemspec"

  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  cd "${_gemname}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --build-root "tmp_install" \
    "${_gemname}-${pkgver}.gem"
}

check() {
  cd "${_gemname}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" bake test
}

package() {
  cd "${_gemname}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 license* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
