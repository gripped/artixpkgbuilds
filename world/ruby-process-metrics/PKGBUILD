# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='process-metrics'
pkgname="ruby-${_gemname}"
pkgver=0.12.0
pkgrel=1
pkgdesc='Provide detailed OS-specific process metrics'
arch=('any')
url='https://github.com/socketry/process-metrics'
license=('MIT')
depends=(
  procps-ng
  ruby
  ruby-console
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-sus
)
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('829bb9cd870b2ddedd252cd5a4b8d822ea4c99d8932e29c5dfe1ac87ffed6689e81e1003a05cf173c9b35706e853bd54925619717189b6dcc34d2bdf51c28f9e')
b2sums=('127116ce51235fa1cff36e3c6ddaf79985fa3f4285e1f6cd2b880cabcdd3539f5cfc7a8ab8ea3f7c2f08995973e745f449f53845ae87627ed97f20152c599425')

prepare() {
  cd "${_gemname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended \
    --expression 's|~>|>=|g' \
    --expression '/signing_key/d' \
    "${_gemname}.gemspec"

  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --build-root "tmp_install" \
    "${_gemname}-${pkgver}.gem"
}

check() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" bake test
}

package() {
  cd "${_gemname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 license* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
