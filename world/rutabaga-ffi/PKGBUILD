# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: envolution
# Contributor: David Garfias <dgarfiasme at gmail dot com>
# shellcheck shell=bash disable=SC2034,SC2154
pkgname=rutabaga-ffi
pkgver=0.1.75
pkgrel=1
pkgdesc="Cross-platform, open-source, Rust-based GPU paravirtualization"
arch=("x86_64")
url="https://github.com/magma-gpu/rutabaga_gfx"
license=(BSD-3-Clause)
source=("$pkgname-$pkgver.tar.gz::https://github.com/magma-gpu/rutabaga_gfx/archive/refs/tags/v${pkgver}.tar.gz")
depends=(glibc gcc-libs virglrenderer gfxstream)
makedepends=(rust meson git cmake rust-bindgen)
sha256sums=('361a433d3a0f7ad1fbde922882fd4dd91483915e20c7e89980feabf65ed5e51f')

# Use debug
export CARGO_PROFILE_RELEASE_DEBUG=2 CARGO_PROFILE_RELEASE_STRIP=false

# Use LTO
export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

build() {
  cd rutabaga_gfx-$pkgver
  artix-meson . build -Dfeatures=gfxstream -Dffi=true --buildtype=release --wrap-mode=forcefallback
  meson compile -C build
}

package() {
  cd rutabaga_gfx-$pkgver
  meson install -C build --destdir "$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
# vim:set ts=2 sw=2 et:
