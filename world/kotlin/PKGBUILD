# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Romain Gautier <romain.gautier@nimamoh.com>

pkgname=kotlin
pkgver=2.2.21
pkgrel=1
pkgdesc='Statically typed programming language with multiplatform support'
arch=(any)
url='https://kotlinlang.org/'
license=(Apache-2.0 custom)
makedepends=(setconf)
depends=(java-runtime-headless)
source=("https://github.com/JetBrains/kotlin/releases/download/v${pkgver/_/-}/kotlin-compiler-${pkgver/_/-}.zip")
b2sums=('cd28a7e43811ce2f42594d604759f217c29b959c7e848b921d2a114b79d62d968aa6a9972e3c53f4a9a7828148766d1a5586fddd9c54c120bc8ba6fcf4548598')

prepare() {
  cd ${pkgname}c/bin

  setconf kotlin      DIR         /usr/bin
  setconf kotlinc-js  DIR         /usr/bin
  setconf kotlinc-jvm DIR         /usr/bin
  setconf kotlinc     KOTLIN_HOME /usr/share/kotlin
}

package() {
  cd ${pkgname}c

  # jar files
  install -d "$pkgdir/usr/share/$pkgname/lib"
  for jar in lib/*.jar; do
    install -Dm644 "$jar" "$pkgdir/usr/share/$pkgname/lib"
  done

  # build.txt must be installed for "-version" to work
  install -Dm644 build.txt "$pkgdir/usr/share/$pkgname"

  # executables
  install -Dm755 -t "$pkgdir/usr/bin" bin/kotlin
  install -Dm755 -t "$pkgdir/usr/bin" bin/kotlinc-js
  install -Dm755 -t "$pkgdir/usr/bin" bin/kotlinc-jvm
  install -Dm755 -t "$pkgdir/usr/bin" bin/kotlinc

  # licenses
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  cd "$srcdir/${pkgname}c/license"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 NOTICE.txt "$pkgdir/usr/share/licenses/$pkgname"
  for txt in third_party/*.txt; do
    install -Dm644 "$txt" "$pkgdir/usr/share/licenses/$pkgname"
  done
}
