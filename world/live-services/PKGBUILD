# Maintainer: artoo <artoo@artixlinux.org>
# Contributor: nous <nous@artixlinux.org>
# Contributor: dudemanguy <dudemanguy@artixlinux.org>

pkgbase=live-services
pkgname=('artix-live-base'
        'artix-live-openrc'
        'artix-live-runit'
        'artix-live-s6'
        'artix-live-dinit')
pkgver=0.13.9
pkgrel=2
pkgdesc='Artix live session'
arch=('any')
url="https://gitea.artixlinux.org/artix/live-services"
license=('GPL')
makedepends=('git')
# Build a commit point
_commit=241eaff2612a8ca0dbfc37b067d6a75da95f7942 # 0.13.9 + git
source=("git+$url.git#commit=$_commit")
# source=("${pkgbase}-${pkgver}.tar.gz::$url/archive/${pkgver}.tar.gz")
sha256sums=('SKIP')

build() {
    make -C "${pkgbase}"
}

package_artix-live-base() {
    pkgdesc='Artix live base scripts'
    depends=('artools-base')

    make -C "${pkgbase}" DESTDIR=${pkgdir} install_base install_xdg
}

package_artix-live-openrc() {
    pkgdesc='Artix live openrc init scripts'
    depends=('openrc' 'artix-live-base')

    make -C "${pkgbase}" DESTDIR=${pkgdir} install_rc

    install -d "${pkgdir}"/etc/runlevels/default
    ln -sf /etc/init.d/pacman-init "${pkgdir}"/etc/runlevels/default/pacman-init
    ln -sf /etc/init.d/artix-live "${pkgdir}"/etc/runlevels/default/artix-live
}

package_artix-live-runit() {
    pkgdesc='Artix live runit init scripts'
    depends=('runit' 'artix-live-base')

    make -C "${pkgbase}" DESTDIR=${pkgdir} install_runit

    install -d "${pkgdir}"/etc/runit/runsvdir/default
    ln -sf /etc/runit/sv/pacman-init "${pkgdir}"/etc/runit/runsvdir/default/pacman-init
}

package_artix-live-s6() {
    pkgdesc='Artix live s6 init scripts'
    depends=('s6-base' 'artix-live-base')

    make -C "${pkgbase}" DESTDIR=${pkgdir} install_s6
}

package_artix-live-dinit() {
    pkgdesc='Artix live dinit init scripts'
    depends=('dinit' 'artix-live-base')

    make -C "${pkgbase}" DESTDIR=${pkgdir} install_dinit
}
