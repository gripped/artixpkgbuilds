# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=gnome-autoar
pkgname=(
  gnome-autoar
  gnome-autoar-docs
)
pkgver=0.5.1
pkgrel=1
pkgdesc="Automatic archives creating and extracting library"
url="https://gnome.pages.gitlab.gnome.org/gnome-autoar/"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  glib2
  glibc
  libarchive
  libgcc
)
makedepends=(
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  meson
  vala
)
source=("git+https://gitlab.gnome.org/GNOME/gnome-autoar.git#tag=$pkgver")
b2sums=('2759c402756ec3e58c93f6a41a8991fb2713f6026c5fde8937efc672ef7a4161c04fa612a81c3c45b5fbf568e4dfd55490f6d96f301aa3919172e27fb82ec2be')

prepare() {
  cd $pkgbase
}

build() {
  local meson_options=(
    -D docs=true
    -D tests=true
    -D vapi=true
  )

  artix-meson $pkgbase build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_gnome-autoar() {
  provides=(libgnome-autoar-${pkgver%%.*}.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_gnome-autoar-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
