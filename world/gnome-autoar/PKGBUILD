# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-autoar
pkgver=0.4.5
pkgrel=1
pkgdesc="Automatic archives creating and extracting library"
url="https://wiki.gnome.org/TingweiLan/GSoC2013Final"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  gtk3
  libarchive
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  meson
  vala
)
provides=(libgnome-autoar{,-gtk}-${pkgver%%.*}.so)
source=("git+https://gitlab.gnome.org/GNOME/gnome-autoar.git#tag=$pkgver")
b2sums=('089fc1f3c409f12fc660433f6884f21bb09b05bda242279b65b083474da926fcaa7cb4d7f4f4b8ab8e3703f645d85ff6dcf6c3db12d1a2a7a8551fa5101d7ca4')

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D gtk_doc=true
    -D tests=true
    -D vapi=true
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
