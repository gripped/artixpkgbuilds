# Maintainer: Filipe La√≠ns <lains@archlinux.org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>

pkgname=fwupd-efi
pkgver=1.7
pkgrel=1
pkgdesc='EFI Application used by uefi-capsule plugin in fwupd'
arch=('any')
url='https://github.com/fwupd/fwupd-efi'
license=('LGPL-2.1-or-later')
makedepends=(
  'git'
  'gnu-efi'
  'meson'
  'python-pefile'
)
source=(git+${url}.git#tag=${pkgver}?signed)
sha256sums=('72ea065678d333e1db8e63e563b6151571b12de721b14896294ac25107c93514')
validpgpkeys=('163EB50119225DB3DF8F49EA17ACBA8DFA970E17'  # Richard Hughes <richard@hughsie.com>
              '0B0B6E494E9D5EFB39180D9A2D192CA624770276') # Mario Limonciello <superm1@gmail.com>

build() {
  artix-meson ${pkgname} build  \
    -D efi_sbat_distro_id='artix' \
    -D efi_sbat_distro_summary='Artix Linux' \
    -D efi_sbat_distro_pkgname=${pkgname} \
    -D efi_sbat_distro_version=${pkgver} \
    -D efi_sbat_distro_url="https://archlinux.org/packages/community/x86_64/${pkgname}/" \
    -D genpeimg=disabled
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}
