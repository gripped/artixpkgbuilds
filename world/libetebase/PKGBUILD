# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=libetebase
pkgver=0.5.8
pkgrel=1
pkgdesc='Etebase C library'
arch=(x86_64)
url='https://github.com/etesync/libetebase'
license=(BSD-3-Clause)
depends=(gcc-libs
         glibc
         libsodium
         openssl)
makedepends=(git
             rust)
source=(git+https://github.com/etesync/libetebase#tag=v$pkgver)
sha256sums=('9c73782d81b9f459cf96b33d35787340dd266d397e70eca3bc31fd9dd2d86f07')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  env CARGO_INCREMENTAL=0 SODIUM_USE_PKG_CONFIG= cargo build --release --locked --offline
  make pkgconfig
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
