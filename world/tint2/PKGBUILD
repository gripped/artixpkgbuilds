# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Robin Candau <antiz@archlinux.org>
# Contributor: Blue Peppers <bluepeppers@archlinux.us>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Yannick LM <LMyannicklm1337@gmail.com>

pkgname=tint2
pkgver=17.0.2
pkgrel=7
pkgdesc='Basic, good-looking task manager for WMs'
arch=(x86_64)
url='https://gitlab.com/o9000/tint2'
license=(GPL-2.0-only)
depends=(gtk3 imlib2 startup-notification)
makedepends=(cmake git setconf)
source=("git+$url.git#tag=$pkgver"
        fix_segfault.patch
        fix_fortify_source_crash.patch
        tint2.patch)
b2sums=('5bd28d0799822b3d93d6a0526cc829bcb0555590d842acef1569c862076cff5a858f49f29300e70a7eb89172f4f7aa31f38bf098488f1676f1c689ad5c96bad7'
        '545be728efbd92565f064659b57ae797834271fb45e3908aee6fff26c7ce2bc0db734c2e907fbc92f1827d2ed1719b46cb830141d5e8756ba1c8ee62da2c33bb'
        '8314d3eff52dcf9884cbf57c3a0e333c6ab8adeff42f235b59e19193fc6717f64b0f642edc0e69c716124cd5cce13388ea6b1d3bac461c45b084d8c979246892'
        '2bf2340d012eb96da3fcef51750c367f5529276a6b0fcefba001f57cb23f15cbb645015d81e48f51a3c7505186c6b40a3fa8bcb8102530454f782a852834e29d')

prepare() {
  cd $pkgname

  setconf get_version.sh VERSION="$pkgver"

  # Patch to fix segfault issue when opening some apps like conky, mpv or steam
  # See https://gitlab.archlinux.org/archlinux/packaging/packages/tint2/-/issues/1
  patch -Np1 -i ../fix_segfault.patch
  # Patch to fix crashes on startup
  patch -Np1 -i ../fix_fortify_source_crash.patch
  # Patch to fix crashes on certain systray events
  patch -Np1 -i ../tint2.patch
}

build() {
  mkdir -p build
  cd build
  cmake ../$pkgname \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_POLICY_VERSION_MINIMUM=3.5 \
    -D ENABLE_TINT2CONF=1
  make
}

package() {
  DESTDIR="$pkgdir" make -C build install
}
