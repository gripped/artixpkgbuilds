# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Caleb Maclennan <caleb@alerque.com>
# Contributor: tobias <tobias@arhlinux.org>

pkgname=exiv2
pkgver=0.28.6
pkgrel=2
pkgdesc="Exif, Iptc and XMP metadata manipulation library and tools"
url="https://exiv2.org"
arch=('x86_64')
license=('GPL2')
depends=(brotli libbrotlidec.so
         curl
         expat libexpat.so
         gcc-libs # libgcc_s.so libstdc++.so
         gettext
         glibc # libc.so libm.so
         libcurl.so
         libinih libINIReader.so
         zlib libz.so)
makedepends=('git' 'cmake' 'gtest' 'ninja')
checkdepends=('python')
provides=('libexiv2.so')
source=(git+https://github.com/Exiv2/exiv2#tag=v$pkgver)
sha512sums=('c4a61ab6fd2bb50ea189200c038580e9640492075d0417a8e463225de7fc0cf9407e4a16745aed6dcbee88ebe2ad24b9fff1142f227d002107e08c7d706a83b8')
b2sums=('176f5d9be25eb476b740f2fd2be9ec6fac1315bf38234a59e6710fe75493c60973a218d69015545871c181a1c7c7a0a84a944926fff664381730ebb524dafb00')

prepare() {
  cd $pkgname
# Fix ABI break https://github.com/Exiv2/exiv2/issues/3376
  git revert -n eceaa0790a2b4d204dd2b75032d00ca2b4283cdc \
                e5bf22e0cebeabeb2ffd40678344467a271be12d
}

build() {
  cd ${pkgname}
  cmake -B build \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DEXIV2_BUILD_SAMPLES=ON \
    -DEXIV2_BUILD_UNIT_TESTS=ON \
    -DEXIV2_ENABLE_VIDEO=ON \
    -DEXIV2_ENABLE_NLS=ON \
    -DEXIV2_ENABLE_XMP=ON \
    -DEXIV2_ENABLE_CURL=ON \
    -DEXIV2_ENABLE_WEBREADY=ON \
    -DEXIV2_ENABLE_BMFF=ON
  ninja -C build
}

package() {
  cd ${pkgname}
  # remove samples instal which are only needed for unit tests
  sed '/samples\/cmake_install.cmake/d' -i build/cmake_install.cmake
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
