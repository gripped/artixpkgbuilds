# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=ruby-unicode-emoji
_pkgname="${pkgname#ruby-}"
pkgver=4.0.4
pkgrel=3
pkgdesc='Provides Unicode Emoji data and regexes, incorporating the latest standards'
arch=('any')
url='https://github.com/janlelis/unicode-emoji'
license=('MIT')
depends=(
  ruby
  ruby-unicode-version
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-rake
  ruby-minitest
)
options=('!emptydirs')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('d733a18bec0ac3838f53b21518c98cb843a37e11f7f4dcb49a597ab14f66a04356da1925ff73073f6eb5fddca3b8c4f7e883db7a5b6c228a58af3890992098fd')
b2sums=('8cd1b24581c5af4ea6a35e5b540d3bdc4cb39818fdfb8fd3d64e82587c7ff580953dd22f2fe7a5c6d99d871d8b693bf89cab29e75ea8d714565e3f63828042d9')

build() {
  cd "$pkgname"

  gem build "$_pkgname.gemspec"
}

check() {
  cd "$pkgname"

  rake spec
}

package() {
  cd "$pkgname"

  local _gemdir="$(gem env gemdir)"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$_pkgname-$pkgver.gem"

  # delete cache
  rm -rf "$pkgdir/$_gemdir/cache"

  # license
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  ln -sf "$_gemdir/gems/$_pkgname-$pkgver/MIT-LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname"
}
