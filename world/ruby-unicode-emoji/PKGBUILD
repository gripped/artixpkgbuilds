# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=ruby-unicode-emoji
_pkgname="${pkgname#ruby-}"
pkgver=4.2.0
pkgrel=1
pkgdesc='Provides Unicode Emoji data and regexes, incorporating the latest standards'
arch=(any)
url='https://github.com/janlelis/unicode-emoji'
license=(MIT)
depends=(
  ruby
  ruby-unicode-version
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-rake
  ruby-minitest
)
options=(!emptydirs)
source=(
  "$pkgname::git+$url#tag=v$pkgver"
  "$pkgname-$pkgver-emoji-test.txt::https://www.unicode.org/Public/emoji/latest/emoji-test.txt"
)
sha512sums=('cb35151d15313f7a3b37698e3ea57741c36d01b16a0656ecd2696fc0c32f24690d124edf68c48ac353e2954075ffd3752562c4e39cc3d24a14bae5179f7062e0'
            '25f72b7e66ac8d96df68cc86b1f059552073cbd022e289e6bbcef0a09368fcbc5b43de6f97ed8ed10d30cf8524f019677105f2120f097b585480b4477aaa49dc')
b2sums=('09d887724fa8513a23ba9d0d79cc556335906b82d4dfc8243720a60f4695948e6938362743c2f7c1a65f64f5df823e0319f256717ad2179cabef9ae5eb95d6d2'
        'addd7e2aba8c0ee3db27fe496ff78c43ef5cacd05d58afc438a9574893ba63d6c6a0ed209766ac9ec3509f7ffdbe9202ccbb84bf75a8b132d60fd19da3f823d9')

prepare() {
  cp "$pkgname-$pkgver-emoji-test.txt" "$pkgname/spec/data/emoji-test.txt"
}

build() {
  cd "$pkgname"

  gem build "$_pkgname.gemspec"
}

check() {
  cd "$pkgname"

  rake spec
}

package() {
  cd "$pkgname"

  local _gemdir="$(gem env gemdir)"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$_pkgname-$pkgver.gem"

  # delete cache
  rm -rf "$pkgdir/$_gemdir/cache"

  # license
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  ln -sf "$_gemdir/gems/$_pkgname-$pkgver/MIT-LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname"
}
