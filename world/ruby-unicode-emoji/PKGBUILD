# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=ruby-unicode-emoji
_pkgname="${pkgname#ruby-}"
pkgver=4.1.0
pkgrel=1
pkgdesc='Provides Unicode Emoji data and regexes, incorporating the latest standards'
arch=(any)
url='https://github.com/janlelis/unicode-emoji'
license=(MIT)
depends=(
  ruby
  ruby-unicode-version
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-rake
  ruby-minitest
)
options=(!emptydirs)
source=(
  "$pkgname::git+$url#tag=v$pkgver"
  "$pkgname-$pkgver-emoji-test.txt::https://www.unicode.org/Public/emoji/latest/emoji-test.txt"
)
sha512sums=('9d65c774030f3760a57690a6fd5180aecf7c656e12c7919953f7545c0b2a12cea819c14974a8b09a20203b5565971dabc669266a5f9074b840b1244a1f0159e3'
            '25f72b7e66ac8d96df68cc86b1f059552073cbd022e289e6bbcef0a09368fcbc5b43de6f97ed8ed10d30cf8524f019677105f2120f097b585480b4477aaa49dc')
b2sums=('fe54c1f9c9a24deaef532cc09df53e17c991fbbecd30f803d0dd305aca15d0524bc626dabc857b45fb1f0c4fe3d36d5ca392c6fdd538b50096594c8a5ea333a2'
        'addd7e2aba8c0ee3db27fe496ff78c43ef5cacd05d58afc438a9574893ba63d6c6a0ed209766ac9ec3509f7ffdbe9202ccbb84bf75a8b132d60fd19da3f823d9')

prepare() {
  cp "$pkgname-$pkgver-emoji-test.txt" "$pkgname/spec/data/emoji-test.txt"
}

build() {
  cd "$pkgname"

  gem build "$_pkgname.gemspec"
}

check() {
  cd "$pkgname"

  rake spec
}

package() {
  cd "$pkgname"

  local _gemdir="$(gem env gemdir)"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$_pkgname-$pkgver.gem"

  # delete cache
  rm -rf "$pkgdir/$_gemdir/cache"

  # license
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  ln -sf "$_gemdir/gems/$_pkgname-$pkgver/MIT-LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname"
}
