# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Adrian Sampson <adrian@radbox.org>

pkgname=python-musicbrainzngs
pkgver=0.7.1
pkgrel=10
pkgdesc='Python bindings for Musicbrainz NGS webservice'
arch=(any)
url='https://github.com/alastair/python-musicbrainzngs'
license=(BSD-2-Clause MIT)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('7f2d225c5724c1f5312cd4191ba6a91118f749cfcb367a04168245bea34c7291882b5de3be97bc9bbfa3e088714ff48edfc338ddc79812da5be4b9053eab0074')
b2sums=('3432fc3b8a8b7f63510db3b426b25fb95fe5de66381b3e96efd7867de10f53e7978b96c2420dcc9624a40e6c083b163642f8026a1f05d065a7638cba0735247e')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" COPYING
}
# vim: ts=2 sw=2 et:
 
