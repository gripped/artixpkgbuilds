# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=libblastrampoline
pkgver=5.13.1
pkgrel=1
pkgdesc='Using PLT trampolines to provide a BLAS and LAPACK demuxing library'
arch=(x86_64)
url='https://github.com/JuliaLinearAlgebra/libblastrampoline'
license=(MIT)
depends=(glibc)
makedepends=(git)
source=(git+https://github.com/JuliaLinearAlgebra/libblastrampoline#tag=v$pkgver)
sha256sums=('4c128ac01a7cce9a8b4bab4b0b7beeebeda133412e4293e25887a194948a064f')
options=(!lto)

build() {
  cd $pkgname/src
  make\
    COMPLEX_RETSTYLE_AUTODETECTION=0 \
    F2C_AUTODETECTION=0 \
    LBT_CFLAGS="$CFLAGS -fPIC -D_GNU_SOURCE -DARCH_x86_64" \
    LBT_LDFLAGS="-shared $LDFLAGS"
}

package() {
  cd $pkgname/src
  make F2C_AUTODETECTION=0 COMPLEX_RETSTYLE_AUTODETECTION=0 prefix="$pkgdir"/usr install
  install -Dm644 ../LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname
}
