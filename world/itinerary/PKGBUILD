# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=itinerary
pkgver=23.08.5
pkgrel=1
pkgdesc='Itinerary and boarding pass management application'
arch=(x86_64)
url='https://apps.kde.org/itinerary/'
license=(LGPL BSD)
depends=(kitinerary kio5 networkmanager-qt5 kosmindoormap khealthcertificate qt5-location qqc2-desktop-style5 prison5
         kitemmodels5 kunitconversion5 kfilemetadata5 kirigami-addons5 libquotient)
makedepends=(extra-cmake-modules)
groups=(kde-applications kde-pim)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('42e38c9b0e147eb5223212470055c7dd3283ae7052c8537b565435a2889927a7'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  artix-cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgname-$pkgver/LICENSES/* -t "$pkgdir"/usr/share/licenses/$pkgname
}
