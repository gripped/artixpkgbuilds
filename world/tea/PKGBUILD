# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: John K. Luebs <jkl at johnluebs dot tld>

pkgname=tea
pkgver=0.10.1
pkgrel=1
pkgdesc='A command line tool to interact with Gitea servers'
arch=('x86_64')
url='https://gitea.com/gitea/tea'
license=('MIT')
depends=('glibc')
makedepends=('git' 'go')
replaces=('gitea-tea')
options=('!lto')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('8b32be890e1a9fe022459ed52e61f328e0ab1955e4f4566142e75ec452fd4f49904e8a2fbcf825048b70c7a3081479ba1b37ef6925211efd5258c31bd01b9b84')
b2sums=('8b50335c5fc35a3f6ae23085e07ce83b146cc8c23a56ab8178ccf783ffd47179a453adea1041fbc9854c6d74249038376c4df69640bc3c846fe1019302a032e1')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # fix zsh completion
  sed -i "s/\$PROG/tea/" contrib/autocomplete.zsh

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}
build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"
  local TAGS=""

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X main.Version=${pkgver} \
    -X main.Tags=${TAGS}" \
    -o build \
    .
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/tea

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # completions
  install -vDm644 contrib/autocomplete.sh "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -vDm644 contrib/autocomplete.zsh "$pkgdir/usr/share/zsh/site-functions/_tea"
}
