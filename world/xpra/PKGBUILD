# Maintainer: artist for Artix Linux

pkgname=xpra
pkgver=6.1.3
pkgrel=1
pkgdesc="multi-platform screen and application forwarding system screen for X11"
arch=('x86_64')
url='https://www.xpra.org'
license=('GPL2')
depends=('gtk3' 'python' 'libxtst' 'python-cairo' 'python-gobject' 'python-pillow' 'python-lz4'
         'ffmpeg' 'libvpx' 'xf86-video-dummy' 'libxkbfile' 'libxres'
         'python-netifaces' 'python-rencode' 'python-opengl'
         'python-lz4')
optdepends=('x264: Codec' 'python-dbus: dbus features'
            'python-pycups: Printing support'
            'python-cryptography: Cryptography'
            'python-numpy: GPU accelerated capture and compression for NVIDIA cards'
            'gst-python: Sound Forwarding'
            'opencv: Webcam Forwarding')
makedepends=('pkgconf' 'pygobject-devel' 'python-setuptools' 'cython' 'uglify-js' 'pandoc')
backup=('etc/xpra/xpra.conf' 'etc/xpra/xorg.conf'
        'etc/xpra/conf.d/05_features.conf'
        'etc/xpra/conf.d/10_network.conf'
        'etc/xpra/conf.d/12_ssl.conf'
        'etc/xpra/conf.d/15_file_transfers.conf'
        'etc/xpra/conf.d/16_printing.conf'
        'etc/xpra/conf.d/20_audio.conf'
        'etc/xpra/conf.d/30_picture.conf'
        'etc/xpra/conf.d/35_webcam.conf'
        'etc/xpra/conf.d/40_client.conf'
        'etc/xpra/conf.d/42_client_keyboard.conf'
        'etc/xpra/conf.d/50_server_network.conf'
        'etc/xpra/conf.d/55_server_x11.conf'
        'etc/xpra/conf.d/60_server.conf'
        'etc/xpra/conf.d/65_proxy.conf'
        'etc/pam.d/xpra')
source=($pkgname-$pkgver.tar.xz::$url/src/$pkgname-$pkgver.tar.xz
        $pkgname-$pkgver.tar.xz.asc::$url/src/$pkgname-$pkgver.tar.xz.gpg)

md5sums=('9b110981868e14d794535812c93cfebe'
         'SKIP')
sha1sums=('d6997b219b4579f3c513d48276f7be8d1875680d'
          'SKIP')
sha256sums=('d0f76421af910c28dc88ed500e50c79ecf8f26ffb4eebde45037de0b3ea419f5'
            'SKIP')
validpgpkeys=('B4993B57323148E37977E5D873254CAD17978FAF') # Xpra <xpra@xpra.org>

build() {
  cd "${srcdir}/$pkgname-$pkgver"
  python setup.py build --without-cuda_kernels --without-cuda_rebuild
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"
  #python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  python setup.py install --root="${pkgdir}" --skip-build
  sed -i 's|/build/xpra/pkg/xpra/etc/xpra/xorg.conf|/etc/xpra/xorg.conf|' "${pkgdir}"/etc/xpra/conf.d/55_server_x11.conf
  mkdir -p "${pkgdir}"/usr/share/dbus-1
  #Move D-BUS Policy
  mv "${pkgdir}"/{etc,usr/share}/dbus-1/system.d
  mv "${pkgdir}/usr/libexec/xpra" "${pkgdir}/usr/lib/"
  rmdir "${pkgdir}/usr/libexec"
  #Fix for FS#69804
  chmod +x "${pkgdir}"/usr/bin/*
}
