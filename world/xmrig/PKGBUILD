# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Konstantin Shalygin <k0ste@k0ste.ru>

pkgname=xmrig
pkgver=6.25.0
pkgrel=1
pkgdesc='RandomX, KawPow, CryptoNight, AstroBWT and GhostRider unified CPU/GPU miner and RandomX benchmark'
url='https://github.com/xmrig/xmrig'
arch=('x86_64')
license=('GPL-3.0-only')
depends=(
  'glibc'
  'hwloc'
  'libssl.so'
  'libuv'
)
makedepends=('cmake')
backup=("etc/${pkgname}/${pkgname}.conf")
source=(https://github.com/xmrig/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        xmrig.sysusers)
sha256sums=('55e415792f2a4ef2430cb26829b0b7d96fc52f51c6274831e9fe76b8aef28593'
            'd8f499302fb2b642fe02586c81c410a299e0a6e133aef1cc1c783bcdcb3f44f6')

prepare() {
  cd "${pkgname}-${pkgver}"
  mkdir -p build
}

build() {
  cd "${pkgname}-${pkgver}/build"
  cmake .. -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm775 "build/${pkgname}" -t "${pkgdir}/usr/bin"
  install -Dm644 "src/config.json" "${pkgdir}/etc/${pkgname}/xmrig.conf"
  install -Dm0644 "${srcdir}/${pkgname}.sysusers" "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
}

# vim: ts=2 sw=2 et:
