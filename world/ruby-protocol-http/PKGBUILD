# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-protocol-http
pkgver=0.54.0
pkgrel=1
pkgdesc='Provides abstractions to handle HTTP protocols'
arch=(any)
url='https://github.com/socketry/protocol-http'
license=(MIT)
depends=(
  ruby
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-decode
  ruby-sus
  ruby-sus-fixtures-async
)
options=(!emptydirs)
source=("git+${url}.git#tag=v${pkgver}")
sha512sums=('073255b91c888b108e923b2af0037a2a40a790c2599d7f0bc2b5c5ddb6efd21c77be031125be36969391b7d2b1d147cdf4b704c5795427b7ac23323d14da2277')
b2sums=('796fd83cfa6238560c64620b833eba719ffe37ac5de5674c74282d5a755a5b65afb8be9942efc4979511426fab144703a8816d0101d86484895e531911a30d6a')

prepare() {
  cd protocol-http

  sed -r \
    -e 's|~>|>=|g' \
    -e '/signing_key/d' \
    -i protocol-http.gemspec

  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd protocol-http
  gem build protocol-http.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    protocol-http-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd protocol-http
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd protocol-http
  cp -a tmp_install/* "$pkgdir"/
}
