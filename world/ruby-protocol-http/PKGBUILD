# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-protocol-http
pkgver=0.60.0
pkgrel=1
pkgdesc='Provides abstractions to handle HTTP protocols'
arch=(any)
url='https://github.com/socketry/protocol-http'
license=(MIT)
depends=(
  ruby
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-decode
  ruby-sus
  ruby-sus-fixtures-async
  ruby-sus-fixtures-benchmark
)
options=(!emptydirs)
source=("git+${url}.git#tag=v${pkgver}")
sha512sums=('2043544cbcd56ad321f6a01fd170de2d28dfaf94d3fc5cbc45b1d5ff32383b6ab9a0e72a4fd9a6a6b63ed91dd4c25239983a65a943025316ff3697ba0ac54d55')
b2sums=('e5bc57aed1f633a14856b1ea70253379ab59909d8a04c54e76b21994a70e94afd078b249e363685fb6ef6379fa2bac75409daadaf83445b2e0ccad242b2e3145')

prepare() {
  cd protocol-http

  sed -r \
    -e 's|~>|>=|g' \
    -e '/signing_key/d' \
    -i protocol-http.gemspec

  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd protocol-http
  gem build protocol-http.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    protocol-http-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd protocol-http
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd protocol-http
  cp -a tmp_install/* "$pkgdir"/
}
