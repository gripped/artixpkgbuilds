# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Robin Candau <antiz@archlinux.org>
# Contributor: Daniel LÃ¶bl <dloebl.2000@gmail.com>

pkgname=libcgif
pkgver=0.5.1
pkgrel=1
pkgdesc="A fast and lightweight GIF encoding library"
url="https://github.com/dloebl/cgif"
arch=('x86_64')
license=('MIT')
depends=('glibc')
makedepends=('meson')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('211e3dfba7138e4cbc1272999aa735be52fc14cab8cb000d9d6aa9d294423034')

build() {
	meson build "cgif-${pkgver}" --prefix=/usr
	meson compile -C build
}

check() {
	meson test -C build
}

package() {
	meson install -C build --destdir "${pkgdir}"
	install -Dm 644 "cgif-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
