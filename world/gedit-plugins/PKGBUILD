# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Sergej Chodarev <sergejx@centrum.sk>
# Contributor: zhuqin <zhuqin83@gmail.com>

pkgname=gedit-plugins
pkgver=47.0
pkgrel=3
pkgdesc="Collection of plugins for the gedit Text Editor"
url="https://gitlab.gnome.org/GNOME/gedit-plugins"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  dconf
  gedit
  glib2
  glibc
  gtk3
  libgedit-gtksourceview
  libgedit-tepl
  libpeas
  pango
  python
  python-cairo
  python-dbus
  python-gobject
)
makedepends=(
  appstream-glib
  git
  gobject-introspection
  gucharmap
  libgit2-glib
  meson
  vte3
  yelp-tools
)
optdepends=(
  'gucharmap: for charmap plugin'
  'libgit2-glib: for git plugin'
  'vte3: for embedded terminal'
)
source=("git+https://gitlab.gnome.org/GNOME/gedit-plugins.git#tag=${pkgver/[a-z]/.&}")
b2sums=('7394eb59977d471b8b2ac44f09c02d8f76976fe60351305c707d866aa89037d5e1ea9d11951d8f34bbf4533673eead58bffabef43820bf20a800b6f33c2d23ca')


prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  python -m compileall -d /usr "$pkgdir/usr"
  python -O -m compileall -d /usr "$pkgdir/usr"
}

# vim:set sw=2 sts=-1 et:
