# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Sergej Chodarev <sergejx@centrum.sk>
# Contributor: zhuqin <zhuqin83@gmail.com>

pkgname=gedit-plugins
pkgver=50.0
pkgrel=2
pkgdesc='Collection of plugins for the gedit Text Editor'
arch=(x86_64)
url='https://gitlab.gnome.org/World/gedit/gedit-plugins'
license=(GPL-2.0-or-later)
depends=(
  dconf
  gedit
  glib2
  glibc
  gtk3
  libgedit-gtksourceview
  libgedit-tepl
  libpeas
)
makedepends=(
  git
  itstool
  meson
)
source=("git+https://gitlab.gnome.org/World/gedit/gedit-plugins.git#tag=${pkgver/[a-z]/.&}")
b2sums=(beaffaa0e81f1e55204e932eba48497b0d22e365152f7c372a6fd6b5d5a5ff839ef8c6503490b818f02af87cdd5c2c9e894119aac2561e464fc54e045df18eab)
validpgpkeys=(08C0A6B1A08310A45363B328426F014A261FD87F) # Sébastien Wilmet <swilmet@mailfence.com>

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
