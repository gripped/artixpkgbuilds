# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Sergej Chodarev <sergejx@centrum.sk>
# Contributor: zhuqin <zhuqin83@gmail.com>

pkgname=gedit-plugins
pkgver=48.0
pkgrel=2
pkgdesc="Collection of plugins for the gedit Text Editor"
url="https://gitlab.gnome.org/World/gedit/gedit-plugins"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  dconf
  gedit
  glib2
  glibc
  gtk3
  libgedit-gtksourceview
  libgedit-tepl
  libpeas
  pango
  python
  python-cairo
  python-dbus
  python-gobject
)
makedepends=(
  appstream-glib
  git
  gobject-introspection
  gucharmap
  meson
  vte3
  yelp-tools
)
optdepends=(
  'gucharmap: for charmap plugin'
  'vte3: for embedded terminal'
)
source=("git+https://gitlab.gnome.org/World/gedit/gedit-plugins.git#tag=${pkgver/[a-z]/.&}")
b2sums=('d311965f561d56f2c761a62039f24d46c95004fdacaf7e4554787f57f8f1f72ebc76904e2048633782b1115edd117b58b0b68e747af7d38092fd80a1838b33b4')


prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  # Plugins not handled by meson's python.bytecompile
  python -m compileall -d "/usr/lib/gedit" "$pkgdir/usr/lib/gedit"
  python -O -m compileall -d "/usr/lib/gedit" "$pkgdir/usr/lib/gedit"
}

# vim:set sw=2 sts=-1 et:
