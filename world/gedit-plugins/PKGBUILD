# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Sergej Chodarev <sergejx@centrum.sk>
# Contributor: zhuqin <zhuqin83@gmail.com>

pkgname=gedit-plugins
pkgver=49.0
pkgrel=1
pkgdesc='Collection of plugins for the gedit Text Editor'
arch=(x86_64)
url='https://gitlab.gnome.org/World/gedit/gedit-plugins'
license=(GPL-2.0-or-later)
depends=(
  dconf
  gedit
  glib2
  glibc
  gtk3
  libgedit-gtksourceview
  libgedit-tepl
  libpeas
)
makedepends=(
  git
  itstool
  meson
)
source=("git+https://gitlab.gnome.org/World/gedit/gedit-plugins.git#tag=${pkgver/[a-z]/.&}")
b2sums=(0378b433319a2f1c4195251f75d8903ba2b1ef2f3f2dbd56869b11f821cdadd5adb9543357de5d069ae84459c379dcb2a545a01707651a2ad8a94cf22ce30560)
validpgpkeys=(08C0A6B1A08310A45363B328426F014A261FD87F) # Sébastien Wilmet <swilmet@mailfence.com>

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
