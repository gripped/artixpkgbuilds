# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=gnome-bluetooth-3.0
pkgver=47.1
pkgrel=1
pkgdesc="GNOME Bluetooth Subsystem"
url="https://wiki.gnome.org/Projects/GnomeBluetooth"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  bluez
  gcc-libs
  glib2
  glibc
  gsound
  gtk4
  libadwaita
  libnotify
  upower
)
makedepends=(
  docbook-xsl
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  meson
  python
)
checkdepends=(python-dbusmock)
provides=(libgnome-bluetooth{,-ui}-3.0.so)
source=("git+https://gitlab.gnome.org/GNOME/gnome-bluetooth.git#tag=${pkgver/[a-z]/.&}")
b2sums=('f769aa01ce530f6cc69e87477e9ebd86d4e5a984b2b785d57da1ade3a5728e87aaef2c6480d4a8378b0cb5894ac82efba482c07d8f6b743c4bfd4b39711b7043')

prepare() {
  cd gnome-bluetooth
}

build() {
  local meson_options=(
    -D gtk_doc=true
  )

  artix-meson gnome-bluetooth build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
