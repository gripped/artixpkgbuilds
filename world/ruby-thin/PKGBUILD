# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname=thin
pkgname=ruby-${_gemname}
pkgver=2.0.1
pkgrel=1
pkgdesc='Thin and fast Ruby web server'
url='https://github.com/macournoyer/thin'
arch=('x86_64')
license=('RUBY')
depends=(
  glibc
  ruby
  ruby-daemons
  ruby-eventmachine
  ruby-rack
)
makedepends=(
  ruby-rake
)
checkdepends=(
  ruby-rake-compiler
  ruby-rspec
)
options=('!emptydirs')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha512sums=('2a32c37116f8753fb29210b4c48b0179dc18d80c0328bfabfae5d3507790befc0d088bf898e49dd1c660598df956268b6d98ff694fe3f673bf9e929fd1c19596')
b2sums=('3ce422b4e88f1635954363204b8b7fec77d8e63c7ed01b1932be200b27d8d01fe6374a5b0853d8e3c8bb093d81ebedc34f6d4a8bdaec75c921c3f320d393c407')

prepare() {
  cd ${_gemname}-${pkgver}

  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec

  sed --in-place --regexp-extended \
    --expression '/daemonizing_spec/d' \
    rakelib/spec.rake

  rm --verbose spec/daemonizing_spec.rb
}

build() {
  cd ${_gemname}-${pkgver}

  rake compile
  rake build
}

check() {
  cd "${_gemname}-${pkgver}"

  rake spec
}

package() {
  cd ${_gemname}-${pkgver}

  local _gemdir="$(gem env gemdir)"

  gem install \
    --ignore-dependencies \
    --no-user-install \
    -i "${pkgdir}${_gemdir}" \
    -n "${pkgdir}/usr/bin" \
    ${_gemname}-${pkgver}.gem

  install -Dm 644 README.md CHANGELOG -t "${pkgdir}/usr/share/doc/${pkgname}"

  cp -a example "${pkgdir}/usr/share/doc/${pkgname}"

  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem" \
    "${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/ext/thin_parser/Makefile"
  rm -rf "${pkgdir}/${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "${pkgdir}/${_gemdir}" \( -name 'mkmf.log' -or -name 'gem_make.out' \) -delete
}

# vim: ts=2 sw=2 et:
