# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=srt
pkgver=1.5.3
pkgrel=2
pkgdesc="Secure Reliable Transport library"
url="https://www.srtalliance.org/"
arch=(x86_64)
license=(MPL2)
depends=(
  gcc-libs
  openssl
)
makedepends=(
  cmake
  git
  ninja
)
_commit=09f35c0f1743e23f514cb41444504a7faeacf89e  # tags/v1.5.3
source=("git+https://github.com/Haivision/srt#commit=$_commit")
b2sums=('44aed3d87f693244dbaa145f3987ccf5d3294145b1eedaafccf9d88eecdea628e1abd19fbec4588cef0b1390e18aa51dc606cbc9b8c7f7cf09405eea1d6c8f95')

pkgver() {
  cd srt
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd srt
}

build() {
  local cmake_options=(
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_BUILD_TYPE=None
    -DENABLE_STATIC=ON
    -DENABLE_TESTING=ON
  )

  cmake -S srt -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

check() {
  cd build
  ./uriparser-test
  ./utility-test
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

# vim:set sw=2 sts=-1 et:
