# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Gerhard Brauer <gerbra@archlinux.de>

pkgname=nbd
pkgver=3.27.1
pkgrel=2
pkgdesc='tools for network block devices, allowing you to use remote block devices over TCP/IP'
arch=('x86_64')
url='https://github.com/NetworkBlockDevice/nbd/'
license=('GPL-2.0-or-later')
depends=('glib2' 'gnutls' 'libnl')
makedepends=('git' 'docbook-utils' 'docbook-sgml' 'perl-sgmls' 'autoconf-archive')
backup=('etc/nbd-server/config')
source=("git+https://github.com/NetworkBlockDevice/nbd.git#tag=nbd-${pkgver}"
        '0000-dummy.keep'
        '0001-nbd-client-support-device-with-and-without-dev-prefi.patch'
        'config'
        'sysusers.conf')
sha256sums=('6b8d0a8d9751b539ada025c925b35b34557b1b33ae507324b684f44353a02737'
            'd31f32b819c769a94843fa0efa1d2bae0342c5e7975ad422502e24401636c43a'
            'f81f0e4d3a994f79341561129126334d6833eeb38ce2e3a6a014ca57fcf53cfa'
            'ee2e9fbbeb8a8b9b71d16b6f32eb41788f6def9d00cc4a47897ed3cb97cdde7c'
            'c2259eba02b7e959476e4f7032f273c1972947dfdeb9019106e70ce8798b912d')

prepare() {
  cd "${srcdir}/${pkgname}"

  # https://github.com/NetworkBlockDevice/nbd/issues/182
  git revert -n \
    'da5e07c057abbee8cc4d2beef03952c7a44fd9eb'

  patch -Np1 < ../0001-nbd-client-support-device-with-and-without-dev-prefi.patch

  autoreconf -fi
}

build() {
  cd "${srcdir}/${pkgname}"

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --enable-syslog

  make
}

# checks still fail...
#check() {
#  cd "${srcdir}/${pkgname}"
#
#  make check
#}

package() {
  cd "${srcdir}/${pkgname}"

  make DESTDIR="${pkgdir}" install

  install -D -m0644 "${srcdir}"/config "${pkgdir}"/etc/nbd-server/config
  install -D -m0644 "${srcdir}"/sysusers.conf "${pkgdir}"/usr/lib/sysusers.d/nbd.conf

  install -D -m0644 doc/README "${pkgdir}"/usr/share/doc/nbd/README
}
