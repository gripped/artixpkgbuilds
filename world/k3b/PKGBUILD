# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=k3b
pkgver=23.08.5
pkgrel=1
epoch=1
pkgdesc='Feature-rich and easy to handle CD burning application'
arch=(x86_64)
url='https://apps.kde.org/k3b/'
license=(GPL)
depends=(libkcddb5 kcmutils5 knotifyconfig5 libmad kfilemetadata5 knewstuff5
         libmpcdec libdvdread libburn libsamplerate qt5-webengine)
makedepends=(extra-cmake-modules kdoctools5)
optdepends=('cdrtools: for CD burning with cdrecord'
            'dvd+rw-tools: for DVD burning support'
            'vcdimager: for VCD burning support'
            'emovix: for bootable multimedia CD/DVD support'
            'cdrdao: for disk-at-once (DAO) mode support'
            'cdparanoia: for CD ripping support')
groups=(kde-applications kde-multimedia)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('c789518b45c8505710fd2b66706c4826f251c5487d9b381df76cc96ffb59309a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  artix-cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
