# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=k3b
pkgver=25.08.1
pkgrel=1
epoch=1
pkgdesc='Feature-rich and easy to handle CD burning application'
arch=(x86_64)
url='https://apps.kde.org/k3b/'
license=(GPL-2.0-or-later)
depends=(ffmpeg
         flac
         gcc-libs
         glibc
         karchive
         kauth
         kbookmarks
         kcmutils
         kcolorscheme
         kcompletion
         kconfig
         kconfigwidgets
         kcoreaddons
         kfilemetadata
         ki18n
         kiconthemes
         kio
         knewstuff
         knotifications
         knotifyconfig
         kwidgetsaddons
         kxmlgui
         lame
         libburn # cdrskin
         libdvdread
         libkcddb
         libmad
         libmpcdec
         libogg
         libsamplerate
         libsndfile
         libvorbis
         qt6-base
         qt6-webengine
         solid
         taglib)
makedepends=(extra-cmake-modules
             kdoctools)
optdepends=('cdparanoia: for CD ripping support' 
            'cdrdao: for disk-at-once (DAO) mode support'
            'cdrtools: for CD burning with cdrecord'
            'dvd+rw-tools: for DVD burning support'
            'emovix: for bootable multimedia CD/DVD support'
            'vcdimager: for VCD burning support')
groups=(kde-applications
        kde-multimedia)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('87cce056225f0a1ad33c2648c93a0d1af3510de9f6dad06357c1d97fd7721536'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
