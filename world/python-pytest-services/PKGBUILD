# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pytest-services
pkgname=python-pytest-services
pkgver=2.2.2
pkgrel=1
pkgdesc="Services plugin for pytest testing framework"
arch=(any)
url="https://github.com/pytest-dev/pytest-services"
license=(MIT)
depends=(
  python
  python-pytest
  python-requests
  python-psutil
  python-zc.lockfile
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-setuptools-scm
  python-wheel
)
checkdepends=(
  memcached
  python-astroid
  python-mysqlclient
  python-pylibmc
  xorg-server-xvfb
)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('d59c27e9b2041204425e1bbffceb6fa7ad5d19d3af062e24d67f5ddb464008a572ff539614405fa8134b5b4cd4605a067b20b912dcbdede9eca1dc5a544181e6')
b2sums=('f17e4f544a2a72c597290a94adbd86acf6b5110de126e3d43f99b96e924978893249fb90a83e23f814377cd86126bbdcd395f3cce0da935f783cee1cb473eece')

build() {
  cd $_name-$pkgver
  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir="tmp_install" dist/*.whl
  export PYTHONPATH="$PWD/tmp_install/$_site_packages/:$PYTHONPATH:$PWD/tests"
  pytest -vv -k 'not test_mysql'
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {AUTHORS,CHANGES,README}.rst -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
