# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Contributor: nfnty

pkgbase=kea
pkgname=("${pkgbase}" "${pkgbase}-docs")
pkgver=3.2.0
pkgrel=1
epoch=1
pkgdesc="High-performance, extensible DHCP server engine from ISC, supporting both DHCPv4 and DHCPv6"
url="https://kea.isc.org"
arch=('x86_64')
license=('MPL-2.0')
makedepends=('git' 'boost' 'boost-libs' 'mariadb' 'meson' 'postgresql' 'python' 'python-sphinx' 'python-sphinx_rtd_theme' 'doxygen' 'graphviz' 'gtest'
             'log4cplus' 'mariadb-libs' 'openssl' 'postgresql-libs' 'texlive-bibtexextra' 'texlive-basic' 'texlive-fontsextra'
             'texlive-formatsextra' 'texlive-games' 'texlive-humanities' 'texlive-latexextra' 'texlive-music' 'texlive-pictures'
             'texlive-pstricks' 'texlive-publishers' 'texlive-science')
checkdepends=('procps-ng')
install="${pkgbase}.install"
source=("git+https://gitlab.isc.org/isc-projects/kea.git#tag=Kea-${pkgver}?signed"
        'kea.sysusers'
        'kea.tmpfiles'
        'fix-build-with-boost-1.89.patch'
        'fix-build-with-boost-1.90.patch')
b2sums=('33f590ffd62706317b501b1d2a0fb7ea1fdc9e5fc905cd9bbb46b7fb961a795def010233b36cc8e6b4cefc0a877bf3d6867905a4df224cd3eedf0a12da144ecb'
        '630310bb2b544a00276a0fa0fff8b7bb93f6bf63e3e5f8ed38f2e1fd2d9747ea4b4cbaa7c0023eec1baddf5e8fe1966b71c6941b3a3cd2e7705e67b15543f2c7'
        'd62c9181b55956441c43414bc2a5a8cff143281931bd57fe584ed03e6035a87c610da2530c10233189fa5926e98e47d05c120b5218e77c9b842131668c9be1e9'
        '4cfe04b61d7884ed492c3f7f06cf3ea68ec231063a8ba868221bbeb17ab94315d80e05243db89a12b29d0b2b1e35e1c7a86f33a42874ddc85661420ea9347e07'
        '1258a72d82eedd1d428b65079cbf66e1efb1bd7fbe26c0029151c9264afd4d526e11f6f7a648fe4165cab52107bfb40577f59e50178e6c82b98efaab328f7e3c')
validpgpkeys=('BE0E9748B718253A28BB89FFF1B11BF05CF02E57'  # Internet Systems Consortium, Inc. (Signing key, 2017-2018) <codesign@isc.org>
              'AE3FAC796711EC59FC007AA474BB6B9A4CBB3D38'  # Internet Systems Consortium, Inc. (Signing key, 2019-2020) <codesign@isc.org>
              '7E1C91AC8030A5A59D1EFAB9750F3C87723E4012'  # Internet Systems Consortium, Inc. (Signing key, 2021-2022) <codesign@isc.org>
              '090A2A07923F925B5767803A42E5DF78C83271DB'  # Marcin Godzina (Code-Signing Key) <mgodzina@isc.org>
              'DA6A3508E672A49DD382AFD95B8F4D91B88ED909'  # Andrei Pavel (Code-Signing Key) <andrei@isc.org>
              '0259A33B5F5A3A4466CF345C7A5E084CACA51884') # Wlodek Wencel (Code-Signing Key) <wlodek@isc.org>

build() {
	artix-meson "${pkgbase}" build -D netconf=disabled -D tests=enabled --install-umask 0022 -D runstatedir=/run
	meson compile -C build
	meson compile -C build devel doc
}

check() {
	# Ignore tests that require live DBs
	meson test -C build $(meson test -C build --list | \
		grep -v 'kea:kea-mysql-tests' | \
		grep -v 'kea:kea-pgsql-tests' | \
		grep -v 'kea:kea-dhcp-tests' | \
		grep -v 'kea:dhcp-mysql-lib-tests' | \
		grep -v 'kea:dhcp-pgsql-lib-tests' | \
		grep -v 'kea:dhcp-forensic-log-libloadtests' | \
		grep -v 'kea:dhcp-lease-query-tests' | \
		grep -v 'kea:kea-dhcp4-tests' | \
		grep -v 'kea:kea-dhcp6-tests' | \
		grep -v 'shell-tests - kea:kea_admin_mysql_tests.sh' | \
		grep -v 'shell-tests - kea:kea_admin_pgsql_tests.sh' | \
		grep -v 'kea-config-tests'  | \
		grep -v 'dhcp-forensic-log-libload-tests' | \
		awk '{print $NF}')
}

package_kea() {
	depends=('log4cplus' 'mariadb-libs' 'openssl' 'postgresql-libs')
	optdepends=('mariadb: lease information database'
	            'postgresql: lease information database'
	            'python: to use kea-shell'
	            'krb5: Kerberos support'
	            'kea-docs: user and developer documentation')
	backup=('etc/kea/kea-dhcp4.conf'
	        'etc/kea/kea-dhcp6.conf'
	        'etc/kea/kea-dhcp-ddns.conf'
	        'etc/kea/keactrl.conf'
	        'etc/kea/kea-ctrl-agent.conf')

	meson install -C build --destdir "${pkgdir}"

	install -Dm 644 kea.sysusers "${pkgdir}/usr/lib/sysusers.d/${pkgbase}.conf"
	install -Dm 644 kea.tmpfiles "${pkgdir}/usr/lib/tmpfiles.d/${pkgbase}.conf"

	# Do not package /run & /var/run (/run is handled via systemd-tmpfiles, /var/run is a symlink to /run)
	rm -rf "${pkgdir}"{/run,/var/run}

	# Update /etc/kea/{,radius} and /var/{log,lib}/kea directories permissions to match the ones set by systemd-tmpfiles
	# This is to avoid unecessary "directory permissions differ" warnings from pacman during upgrades
	chmod 750 "${pkgdir}/etc/kea/"{,radius} "${pkgdir}/var/"{log,lib}/kea

	# Split docs in a separate package
	rm -rf "${pkgdir}/usr/share/"{doc,man}
}

package_kea-docs() {
	pkgdesc="${pkgdesc} (user and developer documentation)"
	provides=('kea-devel-docs')
	replaces=('kea-devel-docs')

	install -d "${pkgdir}/usr/share/doc/${pkgbase}/"

	cp -r build/doc/devel/html "${pkgdir}/usr/share/doc/${pkgbase}/devel"
	cp -r build/doc/sphinx/_build/html "${pkgdir}/usr/share/doc/${pkgbase}/html"
	cp -r "${pkgbase}/doc/examples" "${pkgdir}/usr/share/doc/${pkgbase}/examples"
	rm -f "${pkgdir}/usr/share/doc/kea/devel/doxygen"{,-error}.log

	install -Dm 644 build/doc/sphinx/_build/man/*.1 -t "${pkgdir}/usr/share/man/man1/"
	install -Dm 644 build/doc/sphinx/_build/man/*.8 -t "${pkgdir}/usr/share/man/man8/"
}
