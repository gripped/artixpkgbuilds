# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=mandown
pkgver=1.1.1
pkgrel=1
arch=(x86_64)
license=('Apache-2.0')
url="https://gitlab.com/kornelski/mandown"
pkgdesc="Create man pages from markdown markup"
depends=(
  glibc
  libgcc
)
makedepends=(rust cargo)
source=(https://gitlab.com/kornelski/mandown/-/archive/v${pkgver}/mandown-v${pkgver}.tar.gz)
sha512sums=('b041bc038a4714385673c4a49dedb0d3ed3a049e658e7373d98fb62f0f3af06160aa4be8be9cf0ff79c87d63b89dabdda1c173b64c9a0014511b4777d891d1da')

prepare() {
  cd "$pkgname-v$pkgver"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname-v$pkgver"
  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname-v$pkgver"
  cargo test --frozen --all-features
}

package() {
  cd "$pkgname-v$pkgver"
  install -vDm 755 target/release/${pkgname} -t "${pkgdir}/usr/bin"
}
