# Maintainer: Laurent Carlier <lordheavym@archlinux.org>

pkgname=directx-shader-compiler
pkgdesc="A compiler for HLSL to DXIL (DirectX Intermediate Language)."
pkgver=1.8.2407
pkgrel=1
arch=('x86_64')
url="https://github.com/microsoft/DirectXShaderCompiler"
license=('custom')
depends=('ncurses')
makedepends=('git' 'cmake' 'ninja' 'python')
options=(!lto)
source=("${pkgname}::git+https://github.com/microsoft/DirectXShaderCompiler.git#tag=v${pkgver}"
        "${pkgname}-DirectX-Headers::git+https://github.com/microsoft/DirectX-Headers.git"
        "${pkgname}-SPIRV-Headers::git+https://github.com/KhronosGroup/SPIRV-Headers"
        "${pkgname}-SPIRV-Tools::git+https://github.com/KhronosGroup/SPIRV-Tools"
        "${pkgname}-effcee::git+https://github.com/google/effcee"
        "${pkgname}-googletest::git+https://github.com/google/googletest"
        "${pkgname}-re2::git+https://github.com/google/re2")
sha256sums=('36606d88de4b7d278a1387276395aa4ec0e37d539a48072bdf8fd96a5547d296'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd "${pkgname}"

  git submodule init

  git config submodule."external/DirectX-Headers".url "${srcdir}/${pkgname}"-DirectX-Headers
  git config submodule."external/SPIRV-Headers".url "${srcdir}/${pkgname}"-SPIRV-Headers
  git config submodule."external/SPIRV-Tools".url "${srcdir}/${pkgname}"-SPIRV-Tools
  git config submodule."external/effcee".url "${srcdir}/${pkgname}"-effcee
  git config submodule."external/googletest".url "${srcdir}/${pkgname}"-googletest
  git config submodule."external/re2".url "${srcdir}/${pkgname}"-re2

  git -c protocol.file.allow=always submodule update
}

build() {
  cd "${pkgname}"

  cmake -B build -S . -G Ninja \
    -C cmake/caches/PredefinedParams.cmake \
    -DCMAKE_BUILD_TYPE=Debug \
    -DLLVM_ENABLE_LTO=False

  cmake --build build
}

package() {
  cd "${pkgname}"

  install -m755 -d "${pkgdir}"/usr/bin
  install -m755 -d "${pkgdir}"/usr/lib
  install -m755 -d "${pkgdir}"/usr/include
  install -m755 -d "${pkgdir}"/usr/share/licenses/${pkgname}

  install build/bin/dxc "${pkgdir}"/usr/bin/
  cp build/lib/libdxcompiler.so* "${pkgdir}"/usr/lib/
  cp -r include/dxc "${pkgdir}"/usr/include/

  install LICENSE.TXT "${pkgdir}"/usr/share/licenses/${pkgname}/
  install ThirdPartyNotices.txt "${pkgdir}"/usr/share/licenses/${pkgname}
}
