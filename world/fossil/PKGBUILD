# Maintainer: Anatol Pomozov <anatol pomozov at gmail> 
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Daniel YC Lin <dlin.tw at gmail>
# Contributor: Konstantin Plotnikov <kostyapl at gmail dot com>

pkgname=fossil
pkgver=2.28
pkgrel=1
pkgdesc='Simple, high-reliability, distributed software configuration management'
arch=(x86_64)
url='https://www.fossil-scm.org'
license=(BSD-2-Clause)
depends=(
  fuse2
  glibc
  openssl
  sqlite
  zlib
)
makedepends=(tcl)
optdepends=('tcl: diff --tk support')
source=(
  "https://fossil-scm.org/home/tarball/version-$pkgver/fossil-src-$pkgver.tar.gz"
  "$pkgname-fix-tests.patch"
)
b2sums=('81008e2b85fe55ddb1f4e0b88337d6263e290957cdbba90266159fb12cc696cd8474f30b62c89fada56317775732dd2d8f6c538ac377204250e8164278e12f3c'
        'c08d0f6cf864b10f81af6e27cd1e43c8a6d570085b8da114adbda048d7926e403ee7d208d39a62f24ced6acf9f89b7e91a05d02dcb43a6f9f557238a699b0068'
        '9dc1b1bf1425dd2b865cb5e0114df401a66d36583aa40cad9ee8ca1875952c094b6439f4decd0748633fd064bad7c8ba469d21477a72794a17cec38ca4ca9b43'
        'f88ca1638c5b03d5d97b2fad944d08d0f43be415f93829baf52ee7d1800a49fc2b8f108b366e3dc74d0135e8ab0499b295ae7cfa9322dbfbd4eded563e9416bc')

prepare() {
  cd fossil-src-$pkgver
  patch -Np1 < ../$pkgname-fix-tests.patch
}

build() {
  cd fossil-src-$pkgver
  ./configure --prefix=/usr \
    --json \
    --with-sqlite=/usr \
    --with-tcl=/usr \
    --with-tcl-private-stubs=1
  make
}

check() {
  cd fossil-src-$pkgver
  tclsh test/tester.tcl fossil
}

package() {
  cd fossil-src-$pkgver
  install -vDm755 -t "$pkgdir/usr/bin" fossil
  install -vDm644 tools/fossil-autocomplete.bash "$pkgdir/usr/share/bash-completion/completions/fossil"
  install -vDm644 tools/fossil-autocomplete.zsh "$pkgdir/usr/share/zsh/site-functions/_fossil"
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" COPYRIGHT-BSD2.txt
}
