# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Matt Monaco <cx monaco matt>

pkgname=lmdb
pkgver=0.9.34
pkgrel=1
pkgdesc="Symas Lightning Memory-Mapped Database"
arch=('x86_64')
url="https://symas.com/lmdb"
license=('custom:OpenLDAP')
depends=('glibc')
makedepends=('git')
options=('!emptydirs')
source=("git+https://git.openldap.org/openldap/openldap.git#tag=LMDB_$pkgver"
        lmdb.pc)
sha512sums=('e233d9b3924c45a27b74ccb927308d685ac64bea7b44877aa1da864e00a970da86406956896054e1dcf09cd8219ce54ecb9041405c324e557aa4324fac2e0857'
            '0349d4b08a1f93fe338d8f8e3e5a83e24211a46f999fe529bc1ac49c8c4975559d95a548c203d9427e3f82d62e934ba3cd1be6f734f9b9405b2a26477be4ed33')

prepare() {
  sed -e "s|@PKGVER@|$pkgver|" -i lmdb.pc
}

build() {
  cd openldap/libraries/liblmdb
  make CFLAGS="$CFLAGS" CPPFLAGS="$CPPFLAGS" LDFLAGS="$LDFLAGS" prefix=/usr
}

check() {
  cd openldap/libraries/liblmdb
  make test
}

package() {
  cd openldap/libraries/liblmdb
  install -dm755 "$pkgdir"/usr/{bin,lib,include,man/man1,share}
  make DESTDIR="$pkgdir" prefix=/usr install

  mv "$pkgdir"/usr/man "$pkgdir"/usr/share/man

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  install -Dm644 "$srcdir"/lmdb.pc -t "$pkgdir"/usr/lib/pkgconfig/
}
