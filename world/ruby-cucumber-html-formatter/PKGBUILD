# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bert Peters <bertptrs@archlinux.org>

pkgname=ruby-cucumber-html-formatter
pkgver=24.1.0
pkgrel=1
pkgdesc="HTML formatter for Cucumber"
arch=(any)
url='https://github.com/cucumber/html-formatter/tree/main/ruby'
license=(MIT)
depends=(
  ruby
  ruby-cucumber-messages
)
makedepends=(
  git
  npm
  ruby-bundler
  ruby-rake
  ruby-rspec
)
options=(!emptydirs)
source=(
  "git+https://github.com/cucumber/html-formatter.git#tag=v$pkgver"
)
sha512sums=('7ef51e03b3c759c6a447e354173271c33caa8dae34e4d07cb5973bae7426aa226bf28a2014e0a09b6e09bd99c3cf92eb5e585a4b34002a99592f51dc257b07a5')
b2sums=('e9e0dc9a3a3dc95b65e01c68249c2c142f03c381c5b06a9e933d12329be9eb46a54c897c6383774d83ceba55ac4025521ebcbea3b25cc8f29b713071eaa02913')

prepare() {
  cd html-formatter
  sed -r -e 's|~>|>=|g' -e "s/, '< 35'//" -i ruby/cucumber-html-formatter.gemspec

  # Arch package builds disallow npm git dependencies; this config is lint-only.
  sed -i '/"@cucumber\/biome-config":/d' javascript/package.json javascript/package-lock.json
  sed -i '/"node_modules\/@cucumber\/biome-config": {/,/    },/d' javascript/package-lock.json
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd html-formatter
  make prepare
  cd ruby
  gem build cucumber-html-formatter.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    cucumber-html-formatter-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd html-formatter/ruby
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd html-formatter/ruby
  cp -a tmp_install/* "$pkgdir"/
}
