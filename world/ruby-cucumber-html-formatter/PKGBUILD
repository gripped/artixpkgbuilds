# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bert Peters <bertptrs@archlinux.org>

pkgname=ruby-cucumber-html-formatter
pkgver=23.1.0
pkgrel=1
pkgdesc="HTML formatter for Cucumber"
arch=(any)
url='https://github.com/cucumber/html-formatter/tree/main/ruby'
license=(MIT)
depends=(
  ruby
  ruby-cucumber-messages
)
makedepends=(
  git
  npm
  ruby-bundler
  ruby-rake
  ruby-rspec
)
options=(!emptydirs)
source=(
  "git+https://github.com/cucumber/html-formatter.git#tag=v$pkgver"
)
sha512sums=('270f634915a946543948d6da9bc917dcb99c143ef7e087c623c25e73a28fa53c645d9a70970626b92bfe2719f23158c3f15e606eab54e1779297ddafd88b0c6a')
b2sums=('5b492ed4341ed7d5a6cfbff0a59c5ff7dea96f5455168d9568892a48ee482173ba6faf408fa64d68a3df89e1239d736fe69fd069c8bb717424ebfb04f1ec53e5')

prepare() {
  cd html-formatter
  sed -r -e 's|~>|>=|g' -e "s/, '< 33'//" -i ruby/cucumber-html-formatter.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd html-formatter
  make prepare
  cd ruby
  gem build cucumber-html-formatter.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    cucumber-html-formatter-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd html-formatter/ruby
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd html-formatter/ruby
  cp -a tmp_install/* "$pkgdir"/
}
