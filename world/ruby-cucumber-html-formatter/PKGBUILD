# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bert Peters <bertptrs@archlinux.org>

pkgname=ruby-cucumber-html-formatter
pkgver=21.13.0
pkgrel=1
pkgdesc="HTML formatter for Cucumber"
arch=(any)
url='https://github.com/cucumber/html-formatter/tree/main/ruby'
license=(MIT)
depends=(
  ruby
  ruby-cucumber-messages
)
makedepends=(
  npm
  ruby-bundler
  ruby-rake
  ruby-rspec
  ruby-cucumber-compatibility-kit
)
options=(!emptydirs)
source=(
  "https://github.com/cucumber/html-formatter/archive/v$pkgver/cucumber-html-formatter-$pkgver.tar.gz"
)
sha512sums=('81723ed0c1c8b2a0f78114290758aecdcc8903671ab6e4040fb7b77f1e764413bf6f12a68eff2a94b2faef37fc77636d7025dd1d5bbe6e541545275e3a61dd3e')
b2sums=('55b794e28819511e65427fb8a1554bc51faf7778657243001412cff3f3fc1a9f4522f0f364522729985d04ebd1c4e65b1148c7221949b9895e3f12b1c5ecea5c')

prepare() {
  cd html-formatter-$pkgver/ruby
  sed -r -e 's|~>|>=|g' -e "s/, '< 25'//" -i cucumber-html-formatter.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd html-formatter-$pkgver
  make prepare
  cd ruby
  gem build cucumber-html-formatter.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    cucumber-html-formatter-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd html-formatter-$pkgver/ruby
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd html-formatter-$pkgver/ruby
  cp -a tmp_install/* "$pkgdir"/
}
