# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bert Peters <bertptrs@archlinux.org>

pkgname=ruby-cucumber-html-formatter
pkgver=24.0.0
pkgrel=1
pkgdesc="HTML formatter for Cucumber"
arch=(any)
url='https://github.com/cucumber/html-formatter/tree/main/ruby'
license=(MIT)
depends=(
  ruby
  ruby-cucumber-messages
)
makedepends=(
  git
  npm
  ruby-bundler
  ruby-rake
  ruby-rspec
)
options=(!emptydirs)
source=(
  "git+https://github.com/cucumber/html-formatter.git#tag=v$pkgver"
)
sha512sums=('aad7609133e8f2e64ab55bca21ee0d109eac44c51a7889c1f6bc52bdf27c6d94f3fdd198bd2ca383af577098a1368d68376a6cb5ebe00ad6d23b0b331c4c5674')
b2sums=('b424c3f96eac43d59d07a8a3dffc871c604ace0bd013e72b962911c61bdb448772701701c82de0f7a5ef2fca8e0787a1cb654050ff663ff9f82a274573cf344d')

prepare() {
  cd html-formatter
  sed -r -e 's|~>|>=|g' -e "s/, '< 33'//" -i ruby/cucumber-html-formatter.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd html-formatter
  make prepare
  cd ruby
  gem build cucumber-html-formatter.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    cucumber-html-formatter-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd html-formatter/ruby
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd html-formatter/ruby
  cp -a tmp_install/* "$pkgdir"/
}
