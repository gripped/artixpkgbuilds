# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bert Peters <bertptrs@archlinux.org>

pkgname=ruby-cucumber-html-formatter
pkgver=21.14.0
pkgrel=1
pkgdesc="HTML formatter for Cucumber"
arch=(any)
url='https://github.com/cucumber/html-formatter/tree/main/ruby'
license=(MIT)
depends=(
  ruby
  ruby-cucumber-messages
)
makedepends=(
  npm
  ruby-bundler
  ruby-rake
  ruby-rspec
)
options=(!emptydirs)
source=(
  "https://github.com/cucumber/html-formatter/archive/v$pkgver/cucumber-html-formatter-$pkgver.tar.gz"
  "html-formatter-cck-17.patch::https://github.com/cucumber/html-formatter/pull/415.patch"
)
sha512sums=('7265be10e168fb6146d723179f59c612878a26ed29ca22c733d9a5604721a344f7cfc7eb96a10d8c56c6a758d52615bd3f85f5e457eea49b5f0f5fb5d2c80944'
            '5700fa28af096f0e5df67a498fcb739eecf64c86567048e364e1513ee7d7868efc4f540970fc5dd239f67679be31ddb3afe3a10616386228300468dccbdb2852')
b2sums=('c4043569eb11e23bb89b8c63af719861e82fa2d7b40e8b4a1096d3e0498a8de6c25d00effa54140b54530fa9ff2ef591e5a95a23fc1d5165bf4003780e7b5a68'
        'd410b123481f4015cc7c9154df4f52cceebf3f882a1afe752531f7ac0ab19f63a3d7f4f7e05acb61a894fe935d4b409d4774a33e31697c1544475da3473de25b')

prepare() {
  cd html-formatter-$pkgver
  patch --strip=1 --verbose --input="$srcdir/html-formatter-cck-17.patch"
  sed -r -e 's|~>|>=|g' -e "s/, '< 25'//" -i ruby/cucumber-html-formatter.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd html-formatter-$pkgver
  make prepare
  cd ruby
  gem build cucumber-html-formatter.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    cucumber-html-formatter-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd html-formatter-$pkgver/ruby
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd html-formatter-$pkgver/ruby
  cp -a tmp_install/* "$pkgdir"/
}
