# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bert Peters <bertptrs@archlinux.org>

pkgname=ruby-cucumber-html-formatter
pkgver=24.1.1
pkgrel=1
pkgdesc="HTML formatter for Cucumber"
arch=(any)
url='https://github.com/cucumber/html-formatter/tree/main/ruby'
license=(MIT)
depends=(
  ruby
  ruby-cucumber-messages
)
makedepends=(
  git
  npm
  ruby-bundler
  ruby-rake
  ruby-rspec
)
options=(!emptydirs)
source=(
  "git+https://github.com/cucumber/html-formatter.git#tag=v$pkgver"
)
sha512sums=('fcc4aec33b46b6b1d1b533f27eb4c5b346c384d81c9a5e948a3b50b15649395d8836c315b082b583302b8610c2ac4150c29e0a0a9b51a0537e54d04b76f92bac')
b2sums=('9a9ae72f2615c4cc2d4832fa20629a059841e28c2669fabcfa6195a6d09494fa45d3b8a025ea875a9d179b9f551e0a669d8481765aecd80007f46a1ba2fa5bb5')

prepare() {
  cd html-formatter
  sed -r -e 's|~>|>=|g' -e "s/, '< 35'//" -i ruby/cucumber-html-formatter.gemspec

  # Arch package builds disallow npm git dependencies; this config is lint-only.
  sed -i '/"@cucumber\/biome-config":/d' javascript/package.json javascript/package-lock.json
  sed -i '/"node_modules\/@cucumber\/biome-config": {/,/    },/d' javascript/package-lock.json
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd html-formatter
  make prepare
  cd ruby
  gem build cucumber-html-formatter.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    cucumber-html-formatter-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd html-formatter/ruby
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd html-formatter/ruby
  cp -a tmp_install/* "$pkgdir"/
}
