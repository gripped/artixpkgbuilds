# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bert Peters <bertptrs@archlinux.org>

pkgname=ruby-cucumber-html-formatter
pkgver=22.1.0
pkgrel=1
pkgdesc="HTML formatter for Cucumber"
arch=(any)
url='https://github.com/cucumber/html-formatter/tree/main/ruby'
license=(MIT)
depends=(
  ruby
  ruby-cucumber-messages
)
makedepends=(
  npm
  ruby-bundler
  ruby-rake
  ruby-rspec
)
options=(!emptydirs)
source=(
  "https://github.com/cucumber/html-formatter/archive/v$pkgver/cucumber-html-formatter-$pkgver.tar.gz"
)
sha512sums=('d11b1b5969de9b86ab2de905de76ea4edf41015f9a189d4d7d1dbd5370e521ba173c889eaebf02e0a2b02732d016500bb858e37dc676e97fa04d7b59ebe85dfd')
b2sums=('f94458b77e3e6bddad3fcf6167ee3dd35a694cbf908e913c2ee4788eef87bc89ec4d7bbc4d77efc46df37c0a3bed1cbc69772ba1268aa9daf6fabe7dfded74e2')

prepare() {
  cd html-formatter-$pkgver
  sed -r -e 's|~>|>=|g' -e "s/, '< 25'//" -i ruby/cucumber-html-formatter.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd html-formatter-$pkgver
  make prepare
  cd ruby
  gem build cucumber-html-formatter.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    cucumber-html-formatter-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd html-formatter-$pkgver/ruby
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd html-formatter-$pkgver/ruby
  cp -a tmp_install/* "$pkgdir"/
}
