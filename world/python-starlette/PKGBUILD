# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>

_name=starlette
pkgname=python-$_name
pkgver=0.47.2
pkgrel=1
pkgdesc='The little ASGI framework that shines'
arch=(any)
url="https://github.com/encode/starlette"
license=(BSD-3-Clause)
depends=(
  python
  python-anyio
)
makedepends=(
  python-build
  python-installer
  python-hatchling
  python-wheel
)
checkdepends=(
  python-aiosqlite
  python-databases
  python-pytest
  python-trio

  # optdepends
  python-itsdangerous
  python-jinja
  python-python-multipart
  python-pyyaml
  python-httpx

  # not specified,but required
  python-sqlalchemy
)
optdepends=(
  'python-itsdangerous: for session middleware support'
  'python-jinja: for jinja templates'
  'python-python-multipart: for form parsing'
  'python-pyyaml: for schema generator'
  'python-httpx: for test client'
)
source=($_name-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz)
sha512sums=('812ac9d3eadd41a729c1b0d3ac1d6b0f369122fed450962280de252c0e8fab73fc5944991a7453ef48f920171849bfcb797d15f64302ee3645a34ae49004ee73')
b2sums=('dbb7dbbd8e5b7092c37d5c169d9a82c5739335bcc6766e04f0b639559b1c3b6dd12bebf6feb62fb75f16af4550864e211c9f1fe3982ca115ca8e7d6cd9fbeca0')

prepare() {
  cd $_name-$pkgver
  # Ignore new warnings from python-anyio 4.4.0 making check() fails
  sed -i '/"error"/d' pyproject.toml
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    --deselect 'tests/test_routing.py::test_lifespan_with_on_events[asyncio]'
    --deselect 'tests/test_routing.py::test_lifespan_with_on_events[trio]'
    --deselect 'tests/middleware/test_wsgi.py::test_wsgi_post[asyncio]'
    --deselect 'tests/middleware/test_wsgi.py::test_wsgi_post[trio]'
    --deselect 'tests/test_requests.py::test_request_body[asyncio]'
    --deselect 'tests/test_requests.py::test_request_stream[asyncio]'
    --deselect 'tests/test_requests.py::test_request_body[trio]'
    --deselect 'tests/test_requests.py::test_request_stream[trio]'

  )

  cd $_name-$pkgver
  pytest "${pytest_options[@]}"
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname/"
}
