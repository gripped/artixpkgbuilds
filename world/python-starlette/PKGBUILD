# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>

_name=starlette
pkgname=python-$_name
pkgver=0.42.0
pkgrel=2
pkgdesc='The little ASGI framework that shines'
arch=(any)
url="https://github.com/encode/starlette"
license=(BSD-3-Clause)
depends=(
  python
  python-anyio
)
makedepends=(
  python-build
  python-installer
  python-hatchling
  python-wheel
)
checkdepends=(
  python-aiosqlite
  python-databases
  python-pytest
  python-trio

  # optdepends
  python-itsdangerous
  python-jinja
  python-python-multipart
  python-pyyaml
  python-httpx

  # not specified,but required
  python-sqlalchemy
)
optdepends=(
  'python-itsdangerous: for session middleware support'
  'python-jinja: for jinja templates'
  'python-python-multipart: for form parsing'
  'python-pyyaml: for schema generator'
  'python-httpx: for test client'
)
source=($_name-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz
        fix_tests_with_httpx_0.27.patch)
sha512sums=('d7b0da5333930d6ce9315387cc7e23fd22b4ed379d37c8f143cf6e6a07b5cb2d207d4c349616788633387fa54c6fc377feb28df6ccd1216b824e66a47c950f2b'
            '673f1e3f714b2d7c962111e279d9086ee2868fab93e537f6363749c2043c92711db403460d5adfc8520cc8253417a47f55a2da4a1df4aa4abc0e662dad25875a')
b2sums=('8c8d85288ad7a42b9771431377e5b6ecd98f6c6088fa913bddf496db2de1c7c1e471d1b9a0a6a17885acfe96e503391e066d1b8c6f612329afe69d8ca8743935'
        'c3a2a1f340c912940a78ada3925729ee635ee58dde3b79391dc87cb266c72434fdcb67f04d1b3f77f7f2388dbcd9f1bf323bf819dcc3b7f5b3ff50fa0265f533')

prepare() {
  cd $_name-$pkgver
  # Ignore new warnings from python-anyio 4.4.0 making check() fails
  sed -i '/"error"/d' pyproject.toml
  # Temporary test to fix tests with python-httpx < 0.28
  # Reverts https://github.com/encode/starlette/pull/2773
  patch -Np1 < $srcdir/fix_tests_with_httpx_0.27.patch
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    --deselect 'tests/test_routing.py::test_lifespan_with_on_events[asyncio]'
    --deselect 'tests/test_routing.py::test_lifespan_with_on_events[trio]'
  )

  cd $_name-$pkgver
  pytest "${pytest_options[@]}"
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname/"
}
