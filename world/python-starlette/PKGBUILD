# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>

_name=starlette
pkgname=python-$_name
pkgver=0.47.1
pkgrel=1
pkgdesc='The little ASGI framework that shines'
arch=(any)
url="https://github.com/encode/starlette"
license=(BSD-3-Clause)
depends=(
  python
  python-anyio
)
makedepends=(
  python-build
  python-installer
  python-hatchling
  python-wheel
)
checkdepends=(
  python-aiosqlite
  python-databases
  python-pytest
  python-trio

  # optdepends
  python-itsdangerous
  python-jinja
  python-python-multipart
  python-pyyaml
  python-httpx

  # not specified,but required
  python-sqlalchemy
)
optdepends=(
  'python-itsdangerous: for session middleware support'
  'python-jinja: for jinja templates'
  'python-python-multipart: for form parsing'
  'python-pyyaml: for schema generator'
  'python-httpx: for test client'
)
source=($_name-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz)
sha512sums=('22744f3697448c9327bdb1a6e71f4ea7fd6c1b868bdce1eaaa797e9a8be5dbd81513ae086c7b9d23e8fbf9c57373a43990f4aacf0bebef5c4c6fb626b16d8ab2')
b2sums=('e27b59a89a937cb70cbd4df50f2f55df6c07fab318772e3afb1b3143fd57fa61c07a48df6563b61682832852ea4db5f1df3c0cbebd2fd135f8d2d16d23fe3aa9')

prepare() {
  cd $_name-$pkgver
  # Ignore new warnings from python-anyio 4.4.0 making check() fails
  sed -i '/"error"/d' pyproject.toml
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    --deselect 'tests/test_routing.py::test_lifespan_with_on_events[asyncio]'
    --deselect 'tests/test_routing.py::test_lifespan_with_on_events[trio]'
    --deselect 'tests/middleware/test_wsgi.py::test_wsgi_post[asyncio]'
    --deselect 'tests/middleware/test_wsgi.py::test_wsgi_post[trio]'
    --deselect 'tests/test_requests.py::test_request_body[asyncio]'
    --deselect 'tests/test_requests.py::test_request_stream[asyncio]'
    --deselect 'tests/test_requests.py::test_request_body[trio]'
    --deselect 'tests/test_requests.py::test_request_stream[trio]'

  )

  cd $_name-$pkgver
  pytest "${pytest_options[@]}"
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname/"
}
