# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Simon 'ALSimon' Gilliot <simon@gilliot.fr>
# Contributor: Olivier Biesmans <olivier at biesmans dot fr>
# Contributor: Dobroslaw Kijowski

pkgname=mitmproxy
pkgver=12.2.0
pkgrel=3
pkgdesc='SSL-capable man-in-the-middle HTTP proxy'
arch=(any)
url='https://mitmproxy.org'
license=(MIT)
depends=(
  python
  python-aioquic
  python-argon2-cffi
  python-asgiref
  python-brotli
  python-certifi
  python-cryptography
  python-flask
  python-h11
  python-h2
  python-hyperframe
  python-kaitaistruct
  python-ldap3
  python-mitmproxy-rs
  python-msgpack
  python-publicsuffix2
  python-pyopenssl
  python-pyparsing
  python-pyperclip
  python-requests
  python-ruamel-yaml
  python-sortedcontainers
  python-tornado
  python-typing_extensions
  python-urwid
  python-wsproto
  python-zstandard
  python-bcrypt
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-hypothesis
  python-parver
  python-pytest-asyncio
  python-pytest-timeout
)
provides=(pathod)
conflicts=(pathod)
replaces=(pathod)
source=("$pkgname::git+https://github.com/mitmproxy/mitmproxy#tag=v$pkgver")
sha512sums=('5465a27e46bc83adc9d9e04d83d116947d501157366acee9a340332efd97cb6af068803bdf9eba6ec6f7557b16ed5c2bccf3345a3aef337ac81335bd028c90d7')
b2sums=('99e3a92ac89423900eb205a6fa73194146e1fba4c5be16612951cd5a30c8d7fe117a0e14d953885ffec67861cd97b5ed2760f7d9ec5a131d2376dc82b3e4a8ae')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  local pytest_opts=(
  	--override-ini="addopts="
    --deselect test/mitmproxy/addons/test_dns_resolver.py
  )

  python -m installer --destdir=test_dir dist/*.whl
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  PATH="test_dir/usr/bin:$PATH" PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH" pytest -vv "${pytest_opts[@]}"
}


package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim:set ts=2 sw=2 et:
