# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>
# Contributor: Simon 'ALSimon' Gilliot <simon@gilliot.fr>
# Contributor: Olivier Biesmans <olivier at biesmans dot fr>
# Contributor: Dobroslaw Kijowski

pkgname=mitmproxy
pkgver=12.2.3
pkgrel=1
pkgdesc='SSL-capable man-in-the-middle HTTP proxy'
arch=(any)
url='https://mitmproxy.org'
license=(MIT)
depends=(
  python
  python-aioquic
  python-argon2-cffi
  python-asgiref
  python-brotli
  python-certifi
  python-cryptography
  python-flask
  python-h11
  python-h2
  python-hyperframe
  python-kaitaistruct
  python-ldap3
  python-mitmproxy-rs
  python-msgpack
  python-publicsuffix2
  python-pyopenssl
  python-pyparsing
  python-pyperclip
  python-requests
  python-ruamel-yaml
  python-sortedcontainers
  python-tornado
  python-typing_extensions
  python-urwid
  python-wsproto
  python-zstandard
  python-bcrypt
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-hypothesis
  python-parver
  python-pytest-asyncio
  python-pytest-timeout
  python-pytest-xdist
)
provides=(pathod)
conflicts=(pathod)
replaces=(pathod)
source=(
  "$pkgname::git+https://github.com/mitmproxy/mitmproxy#tag=v$pkgver"
  fix-pytest-opts.patch
)
sha512sums=('6432a61b6d500714f9a7007824d3f426080b2ea4be5d4151c6c315788159bd6128b7688e6b9716ccb4836e752946b9f9547a8e797775d31482643c5ac67ba7b5'
            'ededd8c815f2d69089111c86436f2b2a3607feb2aa82694b2a772fb952ddd77d52a6cb37af914ac38d628ff4dda5e2c50b9672edb2b52560ff5446fde348d842')
b2sums=('6c588d736a42c5008dcae2634d18cc4713b4fd495c08d97c0d1cc415fc00164866f22640f4d5e3b58ed33f046ee1e72202548459c810d3d00277ca2b203286aa'
        'b225be51896f8b42ce88ee19e09bbe8cd56bd6455c1ecbb6c914f35ed8c47e5242bfe6af665e3ca783e5a6362be3610929a2baff7e6f0c0a3cf804adf0e50660')

prepare() {
  cd "$pkgname"

  # ERROR: pyproject.toml: Cannot use both [tool.pytest] (native TOML types)
  # and [tool.pytest.ini_options] (string-based INI format) simultaneously.
  patch -p1 -i "$srcdir/fix-pytest-opts.patch"
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  # temporarily install wheel
  python -m installer --destdir=test_dir dist/*.whl
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  export PATH="test_dir/usr/bin:$PATH"
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"

  # http3 related tests failing
  local deselected_tests=(
    --deselect 'test/mitmproxy/addons/test_proxyserver.py::test_reverse_http3_and_quic_stream[http3]'
    --deselect 'test/mitmproxy/proxy/layers/http/test_http3.py::test_ignore_push'
    --deselect 'test/mitmproxy/proxy/layers/http/test_http3.py::test_fail_without_header'
    --deselect 'test/mitmproxy/proxy/layers/http/test_http3.py::test_invalid_header'
    --deselect 'test/mitmproxy/proxy/layers/http/test_http3.py::test_simple'
    --deselect 'test/mitmproxy/proxy/layers/http/test_http3.py::test_response_trailers[stream]'
    --deselect 'test/mitmproxy/proxy/layers/http/test_http3.py::test_response_trailers[]'
    --deselect 'test/mitmproxy/proxy/layers/http/test_http3.py::test_request_trailers[stream]'
    --deselect 'test/mitmproxy/proxy/layers/http/test_http3.py::test_request_trailers[]'
    --deselect 'test/mitmproxy/proxy/layers/http/test_http3.py::test_upstream_error'
    --deselect 'test/mitmproxy/proxy/layers/http/test_http3.py::test_http3_client_aborts[RST-request-stream]'
    --deselect 'test/mitmproxy/proxy/layers/http/test_http3.py::test_http3_client_aborts[RST-request-]'
    --deselect 'test/mitmproxy/proxy/layers/http/test_http3.py::test_http3_client_aborts[RST-response-stream]'
    --deselect 'test/mitmproxy/proxy/layers/http/test_http3.py::test_http3_client_aborts[RST-response-]'
    --deselect 'test/mitmproxy/proxy/layers/http/test_http3.py::test_http3_client_aborts[disconnect-request-stream]'
    --deselect 'test/mitmproxy/proxy/layers/http/test_http3.py::test_http3_client_aborts[disconnect-request-]'
    --deselect 'test/mitmproxy/proxy/layers/http/test_http3.py::test_http3_client_aborts[disconnect-response-stream]'
    --deselect 'test/mitmproxy/proxy/layers/http/test_http3.py::test_http3_client_aborts[disconnect-response-]'
    --deselect 'test/mitmproxy/proxy/layers/http/test_http3.py::test_http3_client_aborts[RST+disconnect-request-stream]'
    --deselect 'test/mitmproxy/proxy/layers/http/test_http3.py::test_http3_client_aborts[RST+disconnect-request-]'
    --deselect 'test/mitmproxy/proxy/layers/http/test_http3.py::test_http3_client_aborts[RST+disconnect-response-stream]'
    --deselect 'test/mitmproxy/proxy/layers/http/test_http3.py::test_http3_client_aborts[RST+disconnect-response-]'
    --deselect 'test/mitmproxy/proxy/layers/http/test_http3.py::test_rst_then_close'
    --deselect 'test/mitmproxy/proxy/layers/http/test_http3.py::test_cancel_then_server_disconnect'
    --deselect 'test/mitmproxy/proxy/layers/http/test_http3.py::test_cancel_during_response_hook'
    --deselect 'test/mitmproxy/proxy/layers/http/test_http3.py::test_stream_concurrency'
    --deselect 'test/mitmproxy/proxy/layers/http/test_http3.py::test_stream_concurrent_get_connection'
    --deselect 'test/mitmproxy/proxy/layers/http/test_http3.py::test_kill_stream'
    --deselect 'test/mitmproxy/proxy/layers/http/test_http3.py::test_receive_stop_sending[RESET_STREAM]'
    --deselect 'test/mitmproxy/proxy/layers/http/test_http3.py::test_receive_stop_sending[STOP_SENDING]'
    --deselect 'test/mitmproxy/proxy/layers/http/test_http3.py::TestClient::test_no_data_on_closed_stream'
    --deselect 'test/mitmproxy/proxy/layers/http/test_http3.py::TestClient::test_ignore_wrong_order'
    --deselect 'test/mitmproxy/proxy/layers/http/test_http3.py::test_early_server_data'
    --deselect 'test/mitmproxy/addons/test_dns_resolver.py'
    --deselect 'test/mitmproxy/proxy/test_mode_servers.py'
  )

  pytest -vv "${deselected_tests[@]}"
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim:set ts=2 sw=2 et:
