# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgbase=serd
pkgname=(serd serd-docs)
pkgver=0.32.4
pkgrel=1
pkgdesc="Lightweight C library for RDF syntax supporting reading/ writing Turtle and NTriples"
arch=(x86_64)
url="https://drobilla.net/software/serd.html"
license=(
  '0BSD OR ISC'
  BSD-3-Clause
  ISC
)
makedepends=(
  doxygen
  mandoc
  meson
  python-sphinx
  python-sphinxygen
)
source=(https://download.drobilla.net/$pkgname-$pkgver.tar.xz{,.sig})
sha512sums=('5dd0c2547929d3a52e8d8cc6fb1546c3c82f9f023e93d761569bafcabeee4c0660368b3e03774e5a8625ad29b561b75931944119ed907571de012cbcb9d82159'
            'SKIP')
b2sums=('62b8f9f066f7193eec95474d3b8ffcdba5c639d0090e33ab3bc977856b00410fb1f47e5213bde5c980e22f9f7660b223eb79c8a0489cf2f8b912ddf9ee9ed7a5'
        'SKIP')
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3') # David Robillard <d@drobilla.net>

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

build() {
  artix-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build
}

package_serd() {
  depends=(glibc)
  optdepends=('serd-docs: for developer documentation')
  provides=(libserd-0.so)

  meson install -C build --destdir "$pkgdir"
  install -vDm 644 $pkgbase-$pkgver/LICENSES/* -t "$pkgdir/usr/share/licenses/$pkgname/"

  (
    cd "$pkgdir"
    _pick $pkgname-docs usr/share/doc
  )

  install -vDm 644 $pkgname-$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,NEWS,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}

package_serd-docs() {
  pkgdesc+=" - documentation"

  mv -v $pkgname/* "$pkgdir"
  mv -v "$pkgdir/usr/share/doc/"serd{-0,}
  install -vDm 644 $pkgbase-$pkgver/LICENSES/* -t "$pkgdir/usr/share/licenses/$pkgname/"
}
