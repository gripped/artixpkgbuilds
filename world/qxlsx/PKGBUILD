# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Carlos Aznarán <caznaranl@uni.pe>

pkgname=qxlsx
pkgver=1.5.1
pkgrel=1
pkgdesc='Excel file reader/writer library using Qt'
arch=(x86_64)
url='https://github.com/QtExcel/QXlsx'
license=(MIT)
depends=(glibc
         libgcc
         libstdc++
         qt6-base)
makedepends=(cmake
             git)
conflicts=(qxlsx-qt6)
replaces=(qxlsx-qt6)
source=(git+https://github.com/QtExcel/QXlsx#tag=v$pkgver
        fix-install-dir.patch)
sha256sums=('ba3ccabf06cf6bbd83938a21a4232b9ce0da9e6b5d0b2bd7a83248b86ce970d1'
            '684d6d913a23e86116150edca3f3772e41cf95d20d5e5c734705d5ecf2afc760')

prepare() {
  patch -d QXlsx -p1 < fix-install-dir.patch
}

build() {
  cmake -B build -S QXlsx/QXlsx \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 QXlsx/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname 
}
