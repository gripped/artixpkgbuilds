# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Adrian Perez de Castro <aperez@igalia.com>

pkgdesc='FUSE implementation of overlayfs'
pkgname=fuse-overlayfs
pkgver=1.16
pkgrel=1
arch=(x86_64)
url=https://github.com/containers/fuse-overlayfs
license=(GPL-2.0-only)
depends=(fuse3)
makedepends=(git)
source=("git+https://github.com/containers/fuse-overlayfs.git#tag=v$pkgver?signed")
validpgpkeys=('AC404C1C0BF735C63FF4D562263D6DF2E163E1EA')
sha512sums=('7637ca89ec5742aa883d25eb1335a750712dadedb40e4b594ff38e0fd70b123680bd29143a071511225572c17376d4055f0743eebc99b8c64973ef32d3881554')

build () {
	cd "${pkgname}"
	autoreconf -fis
	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin
	make
}

check(){
	cd "${pkgname}"
	make check
}

package (){
	cd "${pkgname}"
	make DESTDIR="${pkgdir}" install
}
