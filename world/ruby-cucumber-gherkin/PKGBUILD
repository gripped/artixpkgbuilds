# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bert Peters <bertptrs@archlinux.org>

pkgname=ruby-cucumber-gherkin
pkgver=37.0.1
pkgrel=1
pkgdesc="Gherkin for Ruby"
arch=(any)
url='https://github.com/cucumber/gherkin/blob/main/ruby'
license=(MIT)
depends=(
  ruby
  ruby-cucumber-messages
)
makedepends=(
  ruby-rake
  ruby-rspec
)
checkdepends=(
  ruby-bundler
)
options=(!emptydirs)
source=(
  "https://github.com/cucumber/gherkin/archive/v$pkgver/cucumber-gherkin-$pkgver.tar.gz"
)
sha512sums=('49204fdc22767ed1de1bc155be550b6b9e1636658cbbc014af958da7f800f21d4c83341b21bbbf88a83618f7dce03e4a5ca967a74121389f5dde62f190ddc7dc')
b2sums=('0ae0594cc73c135417f95f1f0896427f46428ce5b0bb13b5524c9d32470c7226def0b72b949d221f6dc6fe97da8364de38d51cb0d4a3bea78fbadec7240d380c')

prepare() {
  cd gherkin-$pkgver

  cd ruby

  sed -r -e 's|~>|>=|g' -e "s/, '< 24'//" -i cucumber-gherkin.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd gherkin-$pkgver/ruby
  gem build cucumber-gherkin.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    cucumber-gherkin-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd gherkin-$pkgver/ruby
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd gherkin-$pkgver/ruby
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 ../LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
