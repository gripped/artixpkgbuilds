# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bert Peters <bertptrs@archlinux.org>

pkgname=ruby-cucumber-gherkin
pkgver=42.0.0
pkgrel=1
pkgdesc="Gherkin for Ruby"
arch=(any)
url='https://github.com/cucumber/gherkin/blob/main/ruby'
license=(MIT)
depends=(
  ruby
  ruby-cucumber-messages
)
makedepends=(
  git
  ruby-rake
  ruby-rspec
)
checkdepends=(
  ruby-bundler
)
options=(!emptydirs)
source=(
  "git+https://github.com/cucumber/gherkin.git#tag=v$pkgver"
)
sha512sums=('5b55d36313be814ec7d58ae0fd451261a80242f753f2439ec52e999513baf232c8b3d6761b1b9c2a0b1602f75544e2da81e97d1608b47d408e8ccfc0ed6ba854')
b2sums=('4ce5f15e5df4bd54d959ba82606e63bbb619953e8528b958f30473fc918fbdf2e7c8ff96b5ace8f0066240a303e7b80252b6b869d7e4f9d025a03c2f6952c2b2')

prepare() {
  cd gherkin

  cd ruby

  sed -r -e 's|~>|>=|g' -e "s/, '< 35'//" -i cucumber-gherkin.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd gherkin/ruby
  gem build cucumber-gherkin.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    cucumber-gherkin-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd gherkin/ruby
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd gherkin/ruby
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 ../LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
