# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bert Peters <bertptrs@archlinux.org>

pkgname=ruby-cucumber-gherkin
pkgver=33.1.0
pkgrel=1
pkgdesc="Gherkin for Ruby"
arch=(any)
url='https://github.com/cucumber/gherkin/blob/main/ruby'
license=(MIT)
depends=(
  ruby
  ruby-cucumber-messages
)
makedepends=(
  ruby-rake
  ruby-rspec
)
checkdepends=(
  ruby-bundler
)
options=(!emptydirs)
source=(
  "https://github.com/cucumber/gherkin/archive/v$pkgver/cucumber-gherkin-$pkgver.tar.gz"
)
sha512sums=('176a9e8bf448f07da54a0e35095e40d82ff4a842132ef58052e4451d3ccd3a551d493319c6f44c0148ae2e6dec950e4a0baaffea5aea77c161bafc0970c18f48')
b2sums=('026dc11c4451e5243dc4ede0108c19162cb29784507095642d4a2599c52143f8a594b6dba21552a4c061f62fccfe71f3ed4bb574453d68e35231f99340876e90')

prepare() {
  cd gherkin-$pkgver

  cd ruby

  sed -r -e 's|~>|>=|g' -e "s/, '< 24'//" -i cucumber-gherkin.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd gherkin-$pkgver/ruby
  gem build cucumber-gherkin.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    cucumber-gherkin-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd gherkin-$pkgver/ruby
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd gherkin-$pkgver/ruby
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 ../LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
