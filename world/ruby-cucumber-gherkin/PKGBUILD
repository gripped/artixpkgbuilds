# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bert Peters <bertptrs@archlinux.org>

pkgname=ruby-cucumber-gherkin
pkgver=40.0.0
pkgrel=1
pkgdesc="Gherkin for Ruby"
arch=(any)
url='https://github.com/cucumber/gherkin/blob/main/ruby'
license=(MIT)
depends=(
  ruby
  ruby-cucumber-messages
)
makedepends=(
  git
  ruby-rake
  ruby-rspec
)
checkdepends=(
  ruby-bundler
)
options=(!emptydirs)
source=(
  "git+https://github.com/cucumber/gherkin.git#tag=v$pkgver"
)
sha512sums=('0660c6a54ad14a3f5abdfc925a1ab36e3a7c8871b1aacf8834df020709c9390546bcbd9efce93a0173b8d304bb5c832312ba852e12fe0db7262d4a72d9a329a5')
b2sums=('300d297108e61e4f6cb214a3b67bdd99bc52bdc3c1ac35f53874f44370d13bb86d36637f350d3b64452f52b0e429695a0c9d7788c7e6c6f97d1f96578aa8b288')

prepare() {
  cd gherkin

  cd ruby

  sed -r -e 's|~>|>=|g' -e "s/, '< 33'//" -i cucumber-gherkin.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd gherkin/ruby
  gem build cucumber-gherkin.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    cucumber-gherkin-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd gherkin/ruby
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd gherkin/ruby
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 ../LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
