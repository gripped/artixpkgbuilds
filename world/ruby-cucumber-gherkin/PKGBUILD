# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bert Peters <bertptrs@archlinux.org>

pkgname=ruby-cucumber-gherkin
pkgver=39.1.0
pkgrel=1
pkgdesc="Gherkin for Ruby"
arch=(any)
url='https://github.com/cucumber/gherkin/blob/main/ruby'
license=(MIT)
depends=(
  ruby
  ruby-cucumber-messages
)
makedepends=(
  git
  ruby-rake
  ruby-rspec
)
checkdepends=(
  ruby-bundler
)
options=(!emptydirs)
source=(
  "git+https://github.com/cucumber/gherkin.git#tag=v$pkgver"
)
sha512sums=('81f852c5c426c5381526db0d2f4e13221986f773ff72f698eb1e375ea79d451859b1085e41d4667605702610e57c633f8a6f2e95db774df48e1f1a1bc03f35f0')
b2sums=('43b9fabf88d0edb4e9ec382e7d54be7a1a17040fa74489341baf3fd29eeeeeb19a2e2c846635016b0d05631afe01f601e593b23ae1273b4c7db531e96fd2f423')

prepare() {
  cd gherkin

  cd ruby

  sed -r -e 's|~>|>=|g' -e "s/, '< 33'//" -i cucumber-gherkin.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd gherkin/ruby
  gem build cucumber-gherkin.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    cucumber-gherkin-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd gherkin/ruby
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd gherkin/ruby
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 ../LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
