# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bert Peters <bertptrs@archlinux.org>

pkgname=ruby-cucumber-gherkin
pkgver=39.0.0
pkgrel=1
pkgdesc="Gherkin for Ruby"
arch=(any)
url='https://github.com/cucumber/gherkin/blob/main/ruby'
license=(MIT)
depends=(
  ruby
  ruby-cucumber-messages
)
makedepends=(
  git
  ruby-rake
  ruby-rspec
)
checkdepends=(
  ruby-bundler
)
options=(!emptydirs)
source=(
  "git+https://github.com/cucumber/gherkin.git#tag=v$pkgver"
)
sha512sums=('d6b8d8389507abea229e47de0bae70b0132fd5d59235c670ec0c4d676ef3e3d938b2d2d7f33ffd9a503166a9384570aaca5b491f958fd9b095aa9d9d6d5cebd2')
b2sums=('2870466cf84bbf8a35bb2d0e13b7f7ac932c035034a8d05df1ed2efa519902d6b05cbad9d0b0d8d327be60b31aec37feafa9ca40f3c7507aa3fec51f7c7b5a51')

prepare() {
  cd gherkin

  cd ruby

  sed -r -e 's|~>|>=|g' -e "s/, '< 33'//" -i cucumber-gherkin.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd gherkin/ruby
  gem build cucumber-gherkin.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    cucumber-gherkin-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd gherkin/ruby
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd gherkin/ruby
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 ../LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
