# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bert Peters <bertptrs@archlinux.org>

pkgname=ruby-cucumber-gherkin
pkgver=41.0.0
pkgrel=1
pkgdesc="Gherkin for Ruby"
arch=(any)
url='https://github.com/cucumber/gherkin/blob/main/ruby'
license=(MIT)
depends=(
  ruby
  ruby-cucumber-messages
)
makedepends=(
  git
  ruby-rake
  ruby-rspec
)
checkdepends=(
  ruby-bundler
)
options=(!emptydirs)
source=(
  "git+https://github.com/cucumber/gherkin.git#tag=v$pkgver"
)
sha512sums=('8a1dc0fed031bc50bd7bb221151f268f8a95af7fa2b7985850b39317e5c78e7549eb75cfd1ef5ea4017952582ec34213d76bc5671be02e7821b11dbb5adb18de')
b2sums=('69891ff5236af794e09d664c6ecfdbc6eb2975baa70bdd1486c80fd93271efb2549f43881b1bed615c1ad613385becd50541c363c03e73800b89167dbea4523a')

prepare() {
  cd gherkin

  cd ruby

  sed -r -e 's|~>|>=|g' -e "s/, '< 33'//" -i cucumber-gherkin.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd gherkin/ruby
  gem build cucumber-gherkin.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    cucumber-gherkin-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd gherkin/ruby
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd gherkin/ruby
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 ../LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
