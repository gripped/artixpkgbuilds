# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Julien MISCHKOWITZ <wain@archlinux.fr>
# Contributor: Panagiotis Papadopoulos pano_90 (AT) gmx (DOT) net

pkgname=languagetool
pkgver=6.6
pkgrel=2
pkgdesc='An open source language checker'
arch=(any)
url='https://www.languagetool.org'
license=(LGPL-2.1-or-later)
depends=(java-runtime-headless)
makedepends=(unzip)
optdepends=('java-runtime: needed for the GUI version'
            'libxtst: needed for the GUI version'
            'gtk3: GTK style for the GUI')
source=(https://www.languagetool.org/download/LanguageTool-$pkgver.zip
        languagetool.sh)
noextract=($pkgname-$pkgver.zip)
sha256sums=('53600506b399bb5ffe1e4c8dec794fd378212f14aaf38ccef9b6f89314d11631'
            '1230bed5e596ba161a08cddc70764afb46f018e8f483e9934017b3c7b2aaea70')

package() {
  install -d "$pkgdir"/usr/{bin,share/java/$pkgname/libs}
  unzip -q LanguageTool-$pkgver.zip -d "$pkgdir"/usr/share
  cd "$pkgdir"/usr/share/
  mv LanguageTool-${pkgver/.a/} $pkgname
  cd $pkgname
  mv *.jar "$pkgdir"/usr/share/java/$pkgname
  mv libs/*.jar "$pkgdir"/usr/share/java/$pkgname/libs
  rm -rf libs
  install -m755 "$srcdir"/$pkgname.sh "$pkgdir"/usr/bin/$pkgname

}
