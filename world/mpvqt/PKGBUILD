# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=mpvqt
pkgver=1.1.0
pkgrel=1
pkgdesc='A libmpv wrapper for QtQuick2 and QML'
license=(LGPL-2.1-only
         LGPL-3.0-only)
arch=(x86_64)
url='https://invent.kde.org/libraries/mpvqt'
depends=(gcc-libs
         glibc
         libglvnd
         mpv
         qt6-base
         qt6-declarative)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('cd7a7c58b816e42bcbc0980bfe06062d6cf87f5254962ac6940f6d5f86fd0f71'
            'SKIP')
validpgpkeys=(4E421C6554B89766DF9B7A37E12AB207C8755905) # George Florea Bănuș <georgefb899@gmail.com>

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
