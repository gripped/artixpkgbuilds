# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Bruno Pagani <archange@archlinux.org>
# Contributor: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

pkgname=cli11
pkgver=2.6.0
pkgrel=1
pkgdesc="Command line parser for C++11"
arch=(any)
url="https://github.com/CLIUtils/CLI11"
license=(BSD-3-Clause)
makedepends=(cmake)
source=($url/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('8c11bc049090a66cb71c3e90350cddaa792b2a45e0a7841799900b95ca38b101')

build() {
  local cmake_options=(
    -B build
    -S ${pkgname^^}-$pkgver
    -W no-dev
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCLI11_BUILD_TESTS=OFF
    -DCLI11_BUILD_EXAMPLES=OFF
  )
  cmake "${cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 ${pkgname^^}-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
