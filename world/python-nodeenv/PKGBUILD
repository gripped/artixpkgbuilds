# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-nodeenv
pkgver=1.11.0
pkgrel=1
pkgdesc="Node.js virtual environment builder"
url="https://github.com/ekalinin/nodeenv"
license=('BSD-3-Clause')
arch=('any')
depends=('python-setuptools' 'make')
makedepends=('git' 'python-build' 'python-installer' 'python-wheel' 'python-setuptools-scm')
optdepends=('nodejs: for --node=system and --prefer-system'
            'python-certifi: certificate bundle for --with-certifi')
checkdepends=('dash' 'fish' 'nodejs' 'python-pytest' 'python-coverage' 'zsh')
source=("git+https://github.com/ekalinin/nodeenv.git#tag=$pkgver")
sha512sums=('fc7d2232cb50fda6b5fd6bf3a1631dcf02bb8d9275b1fba348a1b8958a9671753bde170cc93cfcb1206ee090dc3ef3f0dcba110c4e35bf732c0ef12d8e5337de')

prepare() {
  cd nodeenv
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd nodeenv
  python -m build --wheel --no-isolation
}

check() {
  cd nodeenv
  pytest
}

package() {
  cd nodeenv
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
