# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libgnomekbd
pkgver=3.28.1
pkgrel=2
epoch=1
pkgdesc="Keyboard management library"
url="https://gitlab.gnome.org/GNOME/libgnomekbd"
arch=(x86_64)
license=(LGPL-2.0-or-later)
depends=(
  cairo
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  libx11
  libxklavier
  pango
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  meson
)
provides=(libgnomekbd{,ui}.so)
source=(
  "git+https://gitlab.gnome.org/GNOME/libgnomekbd.git#tag=$pkgver"
  0001-Fixes-for-meson-build.patch
)
b2sums=('418a510979b0230b05a753d95518709fd8174017cdab2dad634c2d9587094361246f2d3aef554f24abffb9f0593b441af0d8df42ca851fab6fc6338e6c4925aa'
        'f880ce5a1f801ff931b315e8b5bcc0d13123ebf2f4e8e9fae727d09de346b5ef12278dc5410680d6cb828a072f23afecdae15ece92cff9b06e8c14bc108bde20')

prepare() {
  cd $pkgname
  git apply -3 ../0001-Fixes-for-meson-build.patch
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
