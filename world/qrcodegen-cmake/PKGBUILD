# Maintainer: tytan652 <tytan652 at tytanium dot xyz>

pkgbase=qrcodegen-cmake
pkgname=(qrcodegen-cmake qrcodegencpp-cmake)
pkgver=1.8.0
_cmakepkgver=$pkgver-cmake3
pkgrel=4
pkgdesc="High-quality C and C++ QR Code generator library with CMake and pkgconfig"
arch=("x86_64")
url="https://github.com/nayuki/QR-Code-generator"
license=("MIT")
depends=(glibc)
makedepends=("cmake")
options=('debug')
source=(
  "QR-Code-generator-$pkgver.tar.gz::https://github.com/nayuki/QR-Code-generator/archive/refs/tags/v$pkgver.tar.gz"
  "qrcodegen-cmake-$pkgver.tar.gz::https://github.com/EasyCoding/qrcodegen-cmake/archive/refs/tags/v$_cmakepkgver.tar.gz"
)
sha256sums=('2ec0a4d33d6f521c942eeaf473d42d5fe139abcfa57d2beffe10c5cf7d34ae60'
            '2037b0596e5651ce020f4d36e958d24672dbee75631bdb09ae8a45659121f4d0')

prepare() {
  cp -R qrcodegen-cmake-$_cmakepkgver/{cmake,CMakeLists.txt} QR-Code-generator-$pkgver
}

build() {
  cmake -B build -S QR-Code-generator-$pkgver \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_SHARED_LIBS=ON

  cmake --build build
}

package_qrcodegen-cmake() {
  pkgdesc="High-quality C QR Code generator library with CMake and pkgconfig"
  provides=(qrcodegen)
  conflicts=(qrcodegen)

  DESTDIR="$pkgdir" cmake --install build

  rm -rf "$pkgdir"/usr/include/qrcodegencpp
  rm -rf "$pkgdir"/usr/lib/cmake/qrcodegencpp
  rm -rf "$pkgdir"/usr/lib/pkgconfig/qrcodegencpp.pc
  rm -rf "$pkgdir"/usr/lib/libqrcodegencpp.*

  install -Dm644 QR-Code-generator-$pkgver/Readme.markdown "$pkgdir/usr/share/licenses/qrcodegen-cmake/Readme.markdown"
  install -Dm644 qrcodegen-cmake-$_cmakepkgver/LICENSE "$pkgdir/usr/share/licenses/qrcodegen-cmake/LICENSE"
}

package_qrcodegencpp-cmake() {
  pkgdesc="High-quality C++ QR Code generator library with CMake and pkgconfig"
  depends+=(gcc-libs)
  provides=(qrcodegencpp)
  conflicts=(qrcodegencpp)

  DESTDIR="$pkgdir" cmake --install build

  rm -rf "$pkgdir"/usr/include/qrcodegen
  rm -rf "$pkgdir"/usr/lib/cmake/qrcodegen
  rm -rf "$pkgdir"/usr/lib/pkgconfig/qrcodegen.pc
  rm -rf "$pkgdir"/usr/lib/libqrcodegen.*

  install -Dm644 QR-Code-generator-$pkgver/Readme.markdown "$pkgdir/usr/share/licenses/qrcodegencpp-cmake/Readme.markdown"
  install -Dm644 qrcodegen-cmake-$_cmakepkgver/LICENSE "$pkgdir/usr/share/licenses/qrcodegencpp-cmake/LICENSE"
}
