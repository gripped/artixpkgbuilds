# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=orca
pkgver=49.6
pkgrel=1
pkgdesc="Screen reader for individuals who are blind or visually impaired"
url="https://orca.gnome.org/"
arch=(any)
license=(LGPL-2.1-or-later)
depends=(
  at-spi2-core
  glib2
  gobject-introspection-runtime
  gsettings-desktop-schemas
  gst-plugins-base # playbin, audiotestsrc, basic decoders
  gst-plugins-good # pulsesink, more decoders
  gstreamer
  gtk3
  hicolor-icon-theme
  liblouis
  libwnck3
  pango
  python
  python-brltty
  python-cairo
  python-dasbus
  python-dbus
  python-gobject
  python-psutil
  python-setproctitle
  speech-dispatcher
  xorg-xkbcomp
  xorg-xmodmap
)
checkdepends=(
  python-pytest
  python-pytest-mock
)
makedepends=(
  git
  itstool
  meson
  yelp-tools
)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/orca.git?signed#tag=${pkgver/[a-z]/.&}")
b2sums=('9e2e466a8272fdd0c94a7c720ba0af915cb98e7809604ae0b8c9deed6c6ccf0d90a2fcb21aff0fffe24c3fa37500e984a49879de65d5e6a36ec69d121edd0288')
validpgpkeys=(
  DBDB67681333AA61BBCB97140A042BFD3DA3816C # Joanmarie Diggs <jdiggs@igalia.com>, older
  85D0D0B3FB02946101A46295E7A697B5609D4701 # Joanmarie Diggs <jdiggs@igalia.com>, newer
)

prepare() {
  cd orca
}

build() {
  local meson_options=(
    -D spiel=false
  )

  artix-meson orca build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
