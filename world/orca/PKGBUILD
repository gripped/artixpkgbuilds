# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=orca
pkgver=50.1.2
pkgrel=1
pkgdesc="Screen reader for individuals who are blind or visually impaired"
url="https://orca.gnome.org/"
arch=(any)
license=(LGPL-2.1-or-later)
depends=(
  at-spi2-core
  dconf
  glib2
  gobject-introspection-runtime
  gsettings-desktop-schemas
  gst-plugins-base # playbin, audiotestsrc, basic decoders
  gst-plugins-good # pulsesink, more decoders
  gstreamer
  gtk3
  hicolor-icon-theme
  liblouis
  libwnck3
  pango
  python
  python-brltty
  python-cairo
  python-dasbus
  python-dbus
  python-gobject
  python-psutil
  python-setproctitle
  speech-dispatcher
  xorg-xkbcomp
  xorg-xmodmap
)
checkdepends=(
  python-pytest
  python-pytest-mock
  xorg-server-xvfb
)
makedepends=(
  git
  itstool
  meson
  yelp-tools
)
groups=(gnome)
source=(
  "git+https://gitlab.gnome.org/GNOME/orca.git?signed#tag=${pkgver/[a-z]/.&}"
  0001-Fix-tests.patch
)
b2sums=('b3a976524307dbd529df30320006ecf5fb3e7393f05f81423e4b890112e825e763e1445192ca5a18c49092a6b9342dfbc101ee0003bb151b694a4ceabbbc12a9'
        'dfad5061040f9caa202e35647632697a7876a013c9803db057b9b28807d3f7b800e09b492d5baf7555c565bdad1b8e1a3386360c25c2c9bca31354da0cfdb519')
validpgpkeys=(
  DBDB67681333AA61BBCB97140A042BFD3DA3816C # Joanmarie Diggs <jdiggs@igalia.com>, older
  85D0D0B3FB02946101A46295E7A697B5609D4701 # Joanmarie Diggs <jdiggs@igalia.com>, newer
)

prepare() {
  cd orca
  git apply -3 ../0001-Fix-tests.patch
}

build() {
  local meson_options=(
    -D spiel=false
  )

  artix-meson orca build "${meson_options[@]}"
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
