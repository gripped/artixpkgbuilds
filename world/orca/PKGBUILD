# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=orca
pkgver=47.2
pkgrel=2
pkgdesc="Screen reader for individuals who are blind or visually impaired"
url="https://orca.gnome.org/"
arch=(any)
license=(LGPL-2.1-or-later)
depends=(
  at-spi2-core
  brltty
  glib2
  gobject-introspection-runtime
  gsettings-desktop-schemas
  gstreamer
  gst-plugins-base # playbin, audiotestsrc, basic decoders
  gst-plugins-good # pulsesink, more decoders
  gtk3
  hicolor-icon-theme
  liblouis
  libwnck3
  pango
  python
  python-cairo
  python-gobject
  python-psutil
  python-setproctitle
  speech-dispatcher
  xorg-xkbcomp
  xorg-xmodmap
)
makedepends=(
  git
  itstool
  meson
  yelp-tools
)
groups=(gnome)
_pkgver=${pkgver/[a-z]/.&}
_pkgver=${_pkgver^^}
source=("git+https://gitlab.gnome.org/GNOME/orca.git#tag=ORCA_${_pkgver//./_}")
b2sums=('e3465d5a485cdfecbba1034666ade88d628653933532382ecd675e3e4b21aafb456a5a15763d597273126aad6df3b70bb12ad04de0c0cbe01a4b5ead95bb0bfb')

prepare() {
  cd orca
}

build() {
  local meson_options=(
    -D spiel=false
  )

  artix-meson orca build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
