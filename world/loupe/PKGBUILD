# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=loupe
pkgver=46.0
pkgrel=1
pkgdesc="A simple image viewer for GNOME"
arch=(x86_64)
url="https://gitlab.gnome.org/GNOME/loupe"
license=('GPL-3.0-or-later')
groups=('gnome')
depends=(
  cairo
  dconf
  gcc-libs
  glib2
  glibc
  glycin
  graphene
  gtk4
  hicolor-icon-theme
  lcms2
  libadwaita
  libgweather-4
  libseccomp
)
makedepends=(
  git
  itstool
  meson
  rust
)
_commit=b3238e28bd451120cdb516ec8dd76197218e3cdb  # tags/46.0^0
source=("git+https://gitlab.gnome.org/GNOME/loupe.git#commit=$_commit")
b2sums=('SKIP')

# Use LTO
export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

# Use debug
export CARGO_PROFILE_RELEASE_DEBUG=2

pkgver() {
  cd $pkgname
  git describe --tags | sed -r 's/\.([a-z])/\1/;s/([a-z])\./\1/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
