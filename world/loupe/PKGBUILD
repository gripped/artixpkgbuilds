# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=loupe
pkgver=47.4
pkgrel=1
pkgdesc="A simple image viewer for GNOME"
url="https://apps.gnome.org/Loupe/"
arch=(x86_64)
license=(GPL-3.0-or-later)
groups=(gnome)
depends=(
  bubblewrap
  cairo
  dconf
  fontconfig
  gcc-libs
  glib2
  glibc
  glycin
  graphene
  gtk4
  hicolor-icon-theme
  lcms2
  libadwaita
  libgweather-4
  libseccomp
)
makedepends=(
  git
  itstool
  meson
  rust
)
source=("git+https://gitlab.gnome.org/GNOME/loupe.git?signed#tag=${pkgver/[a-z]/.&}")
b2sums=('591fb61c4cdbcef49581ccc0a90f511c218242f70fc2fc6d5254668199c5acccca621a99e3d6cbe9357d76e237a0a9df06e95d42a0cfecf5f18b67c093dd7368')
validpgpkeys=(
  4587A0EE1EE5478AAB82C0A93BA28A5559F08EBD # Sophie Herold <sophieherold@gnome.org>
)

# Use LTO
export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

# Use debug
export CARGO_PROFILE_RELEASE_DEBUG=2

prepare() {
  cd $pkgname

  # Match CARGO_HOME in src/meson.build
  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
