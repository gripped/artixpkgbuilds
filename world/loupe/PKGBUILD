# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=loupe
pkgver=48.0
pkgrel=1
pkgdesc="A simple image viewer for GNOME"
url="https://apps.gnome.org/Loupe/"
arch=(x86_64)
license=(GPL-3.0-or-later)
groups=(gnome)
depends=(
  bubblewrap
  cairo
  dconf
  fontconfig
  gcc-libs
  glib2
  glibc
  glycin
  graphene
  gtk4
  hicolor-icon-theme
  lcms2
  libadwaita
  libgweather-4
  libseccomp
)
makedepends=(
  git
  itstool
  meson
  rust
)
source=("git+https://gitlab.gnome.org/GNOME/loupe.git?signed#tag=${pkgver/[a-z]/.&}")
b2sums=('a30f853c4a17eb9a724ebf94ae9897fa4538091ad4520b3613b938b06508d8ec8d81bd5264ec780016539f02edf00deb6f79a35581c98d1fda464d7bf264ede8')
validpgpkeys=(
  4587A0EE1EE5478AAB82C0A93BA28A5559F08EBD # Sophie Herold <sophieherold@gnome.org>
)

# Use LTO
export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

# Use debug
export CARGO_PROFILE_RELEASE_DEBUG=2

prepare() {
  cd $pkgname

  # Match CARGO_HOME in src/meson.build
  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-rebuild
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
