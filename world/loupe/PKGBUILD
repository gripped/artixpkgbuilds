# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=loupe
pkgver=46.1
pkgrel=1
pkgdesc="A simple image viewer for GNOME"
arch=(x86_64)
url="https://gitlab.gnome.org/GNOME/loupe"
license=('GPL-3.0-or-later')
groups=('gnome')
depends=(
  cairo
  dconf
  gcc-libs
  glib2
  glibc
  glycin
  graphene
  gtk4
  hicolor-icon-theme
  lcms2
  libadwaita
  libgweather-4
  libseccomp
)
makedepends=(
  git
  itstool
  meson
  rust
)
source=("git+$url.git?signed#tag=${pkgver/[a-z]/.&}")
b2sums=('99de72ebf5c0b98feb2ec2c83cb331bc5b7ecb440d7a7a3e8ac63f1d19de84c7e3c25b35a23da6222541771ea12eb78284d874b33bba1ef6107ff7b206782539')
validpgpkeys=(
  4587A0EE1EE5478AAB82C0A93BA28A5559F08EBD # Sophie Herold <sophieherold@gnome.org>
)

# Use LTO
export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

# Use debug
export CARGO_PROFILE_RELEASE_DEBUG=2

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
