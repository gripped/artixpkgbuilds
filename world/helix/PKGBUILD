# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Orhun Parmaksız <orhun@archlinux.org>
# Contributor: Maxim Baz <archlinux at maximbaz dot com>
# Contributor: Wojciech Kępka (wojciech@wkepka.dev)
# Contributor: Théo Daron (theo@daron.be)

pkgname=helix
pkgver=25.07.1
pkgrel=2
pkgdesc="A post-modern modal text editor"
arch=('x86_64')
url="https://helix-editor.com"
license=('MPL-2.0')
depends=('glibc' 'hicolor-icon-theme' 'libgcc' 'libstdc++')
makedepends=('cargo' 'git')
optdepends=(
  'ansible-language-server: for Ansible language support'
  'bash-language-server: for Bash language support'
  'clang: for C/C++ language support'
  'dart: for Dart language support'
  'elvish: for elvish language support'
  'gopls: for Go language support'
  'haskell-language-server: for Haskell language support'
  'julia: for Julia language support'
  'lua-language-server: for Lua language support'
  'marksman: for Markdown language support'
  'python-lsp-server: for Python language support'
  'r: for R and rmarkdown language support'
  'racket: for racket language support'
  'rust-analyzer: for Rust language support'
  'taplo: for TOML language support'
  'texlab: for LaTeX language support'
  'typescript-language-server: for jsx, tsx, typescript language support'
  'typst-lsp: for Typst language support'
  'vue-language-server: for Vue language support'
  'vscode-css-languageserver: for CSS and SCSS support'
  'vscode-html-languageserver: for HTML language support'
  'vscode-json-languageserver: for JSON language support'
  'yaml-language-server: for YAML language support'
  'zls: for Zig language support'
  'xclip: for the x11 clipboard'
  'xsel: for the x11 clipboard'
  'wl-clipboard: for the wayland clipboard'
)
install="$pkgname.install"
source=("$pkgname-$pkgver.tar.gz::https://github.com/helix-editor/helix/archive/$pkgver.tar.gz"
        'fix-gotmpl-grammar-source.patch')
b2sums=('aadcec0be8d13e3957ac2e032431bafe7a0b743f48532c9a2d888fae17ec85dcb3b38d24b9905b7a6fa0a1cf73b761992a79987b17a762ad42cf8370109d7a4d'
        '897d4ffe09fe4c6a04221d19ee967e393a8c4122a680e5d8f21213c3cf1c07591a774cd754f5ce7b34adbb4f04a7eea5ca2708afd36d85c665a30df15ebf2103')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"

  # update gotmpl grammar source
  patch -Np1 -i ../fix-gotmpl-grammar-source.patch

  # NOTE: we are renaming hx to helix so there is no conflict with hex (providing hx)
  sed -i "s|hx|helix|g" contrib/completion/hx.*
  sed -i 's|hx|helix|g' contrib/Helix.desktop
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --frozen --release
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/hx" "$pkgdir/usr/lib/$pkgname/hx"
  install -vdm 755 "$pkgdir/usr/bin"
  ln -sv /usr/lib/$pkgname/hx "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"

  local runtime_dir="$pkgdir/usr/lib/$pkgname/runtime"
  mkdir -p "$runtime_dir/grammars"
  cp -r "runtime/queries" "$runtime_dir"
  cp -r "runtime/themes" "$runtime_dir"
  find "runtime/grammars" -type f -name '*.so' -exec \
    install -Dm 755 {} -t "$runtime_dir/grammars" \;
  install -Dm 644 runtime/tutor -t "$runtime_dir"

  install -Dm 644 "contrib/completion/hx.bash" "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -Dm 644 "contrib/completion/hx.fish" "$pkgdir/usr/share/fish/vendor_completions.d/$pkgname.fish"
  install -Dm 644 "contrib/completion/hx.zsh" "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
  install -Dm 644 "contrib/Helix.desktop" -t "$pkgdir/usr/share/applications"
  install -Dm 644 "contrib/Helix.appdata.xml" -t "$pkgdir/usr/share/metainfo"
  install -Dm 644 "contrib/$pkgname.png" -t "$pkgdir/usr/share/icons/hicolor/256x256/apps"
}

# vim: ts=2 sw=2 et:
