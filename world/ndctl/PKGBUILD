# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ndctl
pkgver=82
pkgrel=2
pkgdesc='Utility library for managing the libnvdimm (non-volatile memory device) sub-system in the Linux kernel'
arch=('x86_64')
url="https://github.com/pmem/ndctl"
license=('LGPL-2.1-only' 'GPL-2.0-only' 'CC0-1.0' 'MIT')
depends=('iniparser' 'json-c' 'keyutils' 'kmod' 'libtraceevent' 'libtracefs' 'libudev'
         'util-linux-libs')
makedepends=('git' 'asciidoctor' 'bash-completion' 'cmake' 'meson' 'udev' 'xmlto')
source=("git+https://github.com/pmem/ndctl.git#tag=v$pkgver")
sha512sums=('60e7a7babafce63ee7a273b071f511848f9954d508f7ae8df7a8e1faf27aef7a1d74fc50b308dd70c595f785d498807ef18c018aba839cd273ffe91dd2396a8d')

prepare() {
  cd ndctl
  # fix location of modprobe.d conf
  sed -i "s|^modprobedatadir =.*|modprobedatadir = '/usr/lib/modprobe.d/'|" contrib/meson.build
}

build() {
  artix-meson ndctl build -Dsystemd=disabled
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
