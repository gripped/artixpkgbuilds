# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Tim Meusel <tim@bastelfreak.de>
# Contributor: loqs

_gemname=memoist
pkgname=ruby-${_gemname}
pkgver=0.16.2
pkgrel=4
pkgdesc='ActiveSupport::Memoizable with a few enhancements'
arch=(any)
url="https://github.com/matthewrudy/${_gemname}"
license=(MIT)
depends=(ruby)
makedepends=(ruby-rdoc)
checkdepends=(
  ruby-bundler
  ruby-minitest
  ruby-rake
)
options=(!emptydirs)
source=($url/archive/v$pkgver/$_gemname-$pkgver.tar.gz)
sha256sums=('6fd211fccd2cc4a37f519d36e4da708ab23c9bc3140d11ff5ce37bbc1e74b76f')

prepare() {
  cd ${_gemname}-${pkgver}

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' $_gemname.gemspec

  # we don't build from a git checkout
  sed --in-place --regexp-extended 's|git ls-files -z|find . -type f -not -path "*/\.git/*" -print0|' $_gemname.gemspec

  sed --in-place --regexp-extended '/add_development_dependency/d' $_gemname.gemspec
}

build() {
  cd ${_gemname}-${pkgver}

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd ${_gemname}-${pkgver}

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rake
}

package() {
  cd ${_gemname}-${pkgver}

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE.md --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 README.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}
