# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=pim-sieve-editor
pkgver=25.04.3
pkgrel=1
pkgdesc='Mail sieve editor'
arch=(x86_64)
url='https://kontact.kde.org'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kbookmarks
         kcolorscheme
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         kdbusaddons
         ki18n
         kiconthemes
         kmailtransport
         kuserfeedback
         kwidgetsaddons
         kwindowsystem
         kxmlgui
         libksieve
         pimcommon
         qt6-base
         qtkeychain-qt6)
makedepends=(extra-cmake-modules
             kdoctools)
groups=(kde-applications
        kde-pim)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('b22482d7c74e2578bc347d08cc7958f214661482826f14c8f5e957c92f6ec006'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
