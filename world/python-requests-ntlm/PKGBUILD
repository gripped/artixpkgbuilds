# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Christoph W <c w e g e n e r at gmail dot com>
# Contributor: Gary van der Merwe <g a r y v d m at gmail dot com>

pkgname=python-requests-ntlm
_module='requests_ntlm'
pkgver=1.3.0
pkgrel=1
pkgdesc="Allows for HTTP NTLM authentication using the requests library"
url="https://github.com/requests/requests-ntlm"
depends=('python' 'python-requests' 'python-ntlm-auth' 'python-pyspnego')
makedepends=('python-setuptools' 'python-build' 'python-wheel' 'python-installer')
license=('custom:ISC License (ISCL)')
arch=(any)
source=("https://files.pythonhosted.org/packages/source/r/requests_ntlm/requests_ntlm-${pkgver}.tar.gz")
sha512sums=('f7649a708effb4f9bfaad3e76b80eb2363edc1ab209b9e5e9ce3f7355e58cc596980af0587b73e8d64d578fc892ac26a9967ece3000ec13306de6e6ff99847f7')

build() {
  cd "${srcdir}/${_module}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${srcdir}/${_module}-${pkgver}"
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/python-requests-ntlm/LICENSE"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
