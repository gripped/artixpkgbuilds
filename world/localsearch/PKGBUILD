# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgbase=localsearch
pkgname=(
  localsearch
  localsearch-testutils
)
pkgver=3.10.2
pkgrel=1
pkgdesc="Filesystem indexer and metadata extractor"
url="https://gnome.pages.gitlab.gnome.org/localsearch/"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  dconf
  exempi
  ffmpeg
  gcc-libs
  gexiv2
  giflib
  glib2
  glibc
  gst-plugins-base-libs
  gstreamer
  gupnp-dlna
  icu
  libcue
  libexif
  libgsf
  libgudev
  libgxps
  libiptcdata
  libjpeg-turbo
  libosinfo
  libpng
  libseccomp
  libtiff
  libwebp
  libxml2
  poppler-glib
  tinysparql
  totem-plparser
  upower
)
makedepends=(
  asciidoc
  bash
  git
  glib2-devel
  gobject-introspection
  meson
  python
  python-dbusmock
  python-gobject
)
source=("git+https://gitlab.gnome.org/GNOME/localsearch.git#tag=${pkgver/[a-z]/.&}")
b2sums=('8a06eb3ee9be2146c04558ebe6ae27089cca15029e9754100bf27c058c84e091b3ae3f77c1ade526c38f08b1c6f6a95edb2bd6fb3770def56b81a39b6a62187e')

prepare() {
  cd localsearch
}

build() {
  local meson_options=(
    -D systemd_user_services=false
    -D landlock=enabled
    -D tests_tap_protocol=true
  )

  artix-meson localsearch build "${meson_options[@]}"
  meson compile -C build
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_localsearch() {
  depends+=(libtinysparql-3.0.so)
  provides=("tracker3-miners=$pkgver")
  replaces=('tracker3-miners<=3.7.3-2')
  conflicts=('tracker3-miners<=3.7.3-2')

  meson install -C build --destdir "$pkgdir"

  _pick testutils "$pkgdir"/usr/lib/localsearch-3.0/trackertestutils
}

package_localsearch-testutils() {
  pkgdesc+=" (test utilities)"
  depends=(
    bash
    glib2
    python
    python-gobject
    tinysparql
  )
  mv testutils/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
