# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgbase=localsearch
pkgname=(
  localsearch
  localsearch-testutils
)
pkgver=3.12.0
pkgrel=1
pkgdesc="Filesystem indexer and metadata extractor"
url="https://gnome.pages.gitlab.gnome.org/localsearch/"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  dconf
  exempi
  ffmpeg
  gexiv2
  giflib
  glib2
  glibc
  gst-plugins-base-libs
  gstreamer
  gupnp-dlna
  icu
  libcue
  libexif
  libgcc
  libgsf
  libgudev
  libgxps
  libiptcdata
  libjpeg-turbo
  libmediainfo
  libosinfo
  libpng
  libseccomp
  libstdc++
  libtiff
  libwebp
  libxml2
  libzip
  poppler-glib
  tinysparql
  totem-plparser
  upower
)
makedepends=(
  asciidoc
  bash
  git
  glib2-devel
  gobject-introspection
  meson
  python
  python-dbusmock
  python-gobject
)
source=("git+https://gitlab.gnome.org/GNOME/localsearch.git#tag=${pkgver/[a-z]/.&}")
b2sums=('f72e010830ec9a6362a5c265d9322c886ad67f842fd19df48f24e001da57292076c32141e6487b338d461226b7a503fd9940626f8ad56540b1942fd8b4cc6529')

prepare() {
  cd localsearch
}

build() {
  local meson_options=(
    -D systemd_user_services=false
    -D landlock=enabled
    -D tests_tap_protocol=true
  )

  artix-meson localsearch build "${meson_options[@]}"
  meson compile -C build
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_localsearch() {
  depends+=(libtinysparql-3.0.so)
  provides=("tracker3-miners=$pkgver")
  replaces=('tracker3-miners<=3.7.3-2')
  conflicts=('tracker3-miners<=3.7.3-2')

  meson install -C build --no-rebuild --destdir "$pkgdir"

  _pick testutils "$pkgdir"/usr/lib/localsearch-3.0/trackertestutils
}

package_localsearch-testutils() {
  pkgdesc+=" (test utilities)"
  depends=(
    bash
    glib2
    python
    python-gobject
    tinysparql
  )
  mv testutils/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
