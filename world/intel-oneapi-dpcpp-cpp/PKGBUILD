# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Torsten Ke√üler <tpkessler at archlinux dot org>

pkgname="intel-oneapi-dpcpp-cpp"
_pkgver=2025.0
pkgver=2025.0.4
# Figure out new _debpkgrel value using
# curl -L https://apt.repos.intel.com/oneapi/dists/all/main/binary-amd64/Packages | grep dpcpp-cpp
_debpkgrel=1519
pkgrel=1.1
pkgdesc="Intel oneAPI DPC++/C++ Compiler"
arch=('x86_64')
url='https://software.intel.com/content/www/us/en/develop/tools/oneapi.html'
license=("LicenseRef-Intel-EULA-Developer-Tools")
source=("https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb")
b2sums=('27b7032aadce6daa17a03e163836915784ae1c55e34c1ef1470ede7a91ebe3cd5615bf8c5dcab9e22b45186fa53cc2a3a45f167b963439938b83fb4055d6d2a6')
noextract=(
    "${pkgname}-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
)
depends=('intel-oneapi-compiler-dpcpp-cpp-common' 'intel-oneapi-compiler-dpcpp-cpp-runtime'
         'intel-oneapi-compiler-shared-runtime' 'intel-oneapi-compiler-shared'
         'intel-oneapi-tbb' 'intel-oneapi-dev-utilities' 'intel-oneapi-common'
         'gcc-libs' 'glibc' 'bash' 'zlib')
conflicts=('intel-oneapi-basekit')

package() {
    cd "${srcdir}"
    ar x "${srcdir}/${pkgname}-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
    tar xvf data.tar.xz -C "${pkgdir}"
    rm data.tar.xz

    # allow libs to be found
    local _lib_path='/opt/intel/oneapi/compiler'
    local _ldso_conf="${pkgdir}"/etc/ld.so.conf.d
    install -d "${_ldso_conf}"
    echo "${_lib_path}/latest/linux/lib" > "${_ldso_conf}/${pkgname}.conf"
    echo "${_lib_path}/latest/linux/lib/x64" >> "${_ldso_conf}/${pkgname}.conf"

    install -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
    ln -s /usr/share/licenses/intel-oneapi "${pkgdir}"/usr/share/licenses/"${pkgname}"/oneapi
}
