# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Arvid Ephraim Picciani <aep@exys.org>
# Contributor: Michael Krauss <hippodriver@gmx.net>

pkgname=python-pyparsing
pkgver=3.3.3
pkgrel=1
pkgdesc='General parsing module for Python'
arch=(any)
url='https://github.com/pyparsing/pyparsing/'
license=(MIT)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-flit-core
)
checkdepends=(
  python-jinja
  python-railroad-diagrams
  python-pytest
  python-matplotlib
)
optdepends=('python-railroad-diagrams: for generating Railroad Diagrams'
            'python-jinja: for generating Railroad Diagrams')
source=("$pkgname::git+https://github.com/pyparsing/pyparsing.git#tag=$pkgver")
sha512sums=('610b424be434bf784431116840a3b24db0a3a92721cdd4517304f702f472dd62252bb0852a2abc58141e7c46f8d8d722aa909fdc149d40b8d575d09402504402')
b2sums=('847fe91e37d8fcf7debfe8b3088b2c43f1395262fd3d9df682ff02f6c442260055509cc41e2a23e582febe8bf8053b6af2ec007b38c0378410f89130c1c215d0')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  python -m unittest
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim:set ts=2 sw=2 et:
