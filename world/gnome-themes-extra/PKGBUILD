# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-themes-extra
pkgver=3.28
pkgrel=1
epoch=1
pkgdesc='Extra GNOME themes (legacy HighContrast icon theme and index files for Adwaita)'
arch=(any)
url='https://gitlab.gnome.org/Archive/gnome-themes-extra'
license=(LGPL-2.1-or-later)
makedepends=(
  git
  gtk3
  intltool
)
options=(!emptydirs)
source=("git+https://gitlab.gnome.org/Archive/gnome-themes-extra.git?signed#tag=$pkgver")
b2sums=(e317e8b56cfabde93e051e415575f1a4e1accda64f900b5d0ca157acd2851b78acd8c2720a73d19c97ba725e32e78cf48ad5fd893657ddb660b7b3fb7b9cc83c)
validpgpkeys=(28B668E8681305CEC60C6FCA19DE6E133A0F5A11) # Juraj Fiala <jurajfiala@protonmail.com>

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-gtk2-engine
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  make -C themes/HighContrast/gtk-2.0 DESTDIR="$pkgdir" uninstall
}
