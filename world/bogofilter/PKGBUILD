# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Low Kian Seong <fastmail_low@speedymail.org>

pkgbase=bogofilter
pkgname=('bogofilter-db' 'bogofilter-kyotocabinet' 'bogofilter-lmdb' 'bogofilter-sqlite')
pkgver=1.2.5
pkgrel=12
pkgdesc="A fast Bayesian spam filtering tool"
arch=('x86_64')
license=('GPL-2.0-or-later')
url="https://bogofilter.sourceforge.io/"
depends=('perl' 'gsl' 'glibc' 'sh')
makedepends=('valgrind' 'xmlto' 'db5.3' 'kyotocabinet' 'lmdb' 'sqlite')
backup=('etc/bogofilter/bogofilter.cf')
source=(https://downloads.sourceforge.net/project/${pkgbase}/${pkgbase}-stable/${pkgbase}-${pkgver}.tar.xz{,.asc}
        bdb_5.3.patch) # asc file doesn't  work with pacman
sha1sums=('c779c3afb3e57ae0208ee503a854aff716b0cafd'
          'SKIP'
          'd42a6517a4d90835f1d54517056e704f7c01ea19')
sha256sums=('3248a1373bff552c500834adbea4b6caee04224516ae581fb25a4c6a6dee89ea'
            'SKIP'
            '5adb184d81a302ec13cf6320ce30ef177432ba346373b56febf5ab9b1f449999')
validpgpkeys=('DC4A655BD993CD4871FA8210E412B156EFF3855A') # "Matthias Andree <mandree@FreeBSD.org>"

# note: build system doesn't allow easy overlinking prevention - no libtool hack possible

prepare() {
#  pwd && sleep 5
  mkdir ${srcdir}/build-{db,kyotocabinet,lmdb,sqlite}
  cp -av "$pkgbase-$pkgver"/* ${srcdir}/build-db/
  cp -av "$pkgbase-$pkgver"/* ${srcdir}/build-kyotocabinet/
  cp -av "$pkgbase-$pkgver"/* ${srcdir}/build-lmdb/
  cp -av "$pkgbase-$pkgver"/* ${srcdir}/build-sqlite/

  cd build-db
  patch -Np1 -i ../bdb_5.3.patch
  autoreconf -vfi
}

# keep Berkeley DB as backend for now
# sqlite3 based backend doesn't allow
# to use db-transaction option that is
# used to bf_compact the wordlist.db
# other backend aren't available 
# currently in Arch repos
# as of 1.2.5 new backend can be KyotoCabinet and lmdb

build() {
  # Berkeley DB v5.3 backend
  cd ${srcdir}/build-db
  ./configure CFLAGS="$CFLAGS -I/usr/include/db5.3" \
    --prefix=/usr \
    --sysconfdir=/etc/bogofilter  \
    --localstatedir=/var \
    --enable-transactions \
    --with-database=db
  make

  # kyotocabinet backend
  cd ${srcdir}/build-kyotocabinet
  ./configure --prefix=/usr \
    --sysconfdir=/etc/bogofilter  \
    --localstatedir=/var \
    --enable-transactions \
    --with-database=kyotocabinet
  make

  # lmdb backend
  cd ${srcdir}/build-lmdb
  ./configure --prefix=/usr \
    --sysconfdir=/etc/bogofilter  \
    --localstatedir=/var \
    --enable-transactions \
    --with-database=lmdb
  make

  # sqlite backend
  cd ${srcdir}/build-sqlite
  ./configure --prefix=/usr \
    --sysconfdir=/etc/bogofilter  \
    --localstatedir=/var \
    --enable-transactions \
    --with-database=sqlite
  make
}

check() {
  cd ${srcdir}/build-db
  BF_RUN_VALGRIND=1 make -k -C src check
  cd ${srcdir}/build-kyotocabinet
  BF_RUN_VALGRIND=1 make -k -C src check
  cd ${srcdir}/build-lmdb
  BF_RUN_VALGRIND=1 make -k -C src check
  cd ${srcdir}/build-sqlite
  BF_RUN_VALGRIND=1 make -k -C src check
}

package_bogofilter-db() {
  pkgdesc+=" - Berkeley DB v5.3 backend"
  depends+=('db5.3')
  provides=('bogofilter')
  replaces=('bogofilter')
  conflicts=('bogofilter-kyotocabinet' 'bogofilter-lmdb' 'bogofilter-sqlite')
  install=$pkgbase.install

  cd ${srcdir}/build-db
  make DESTDIR="${pkgdir}" install

  mv "${pkgdir}/etc/bogofilter/bogofilter.cf.example" "${pkgdir}/etc/bogofilter/bogofilter.cf"

  install -dm755 "${pkgdir}/usr/share/${pkgname}/contrib"
  install -m644 contrib/* "${pkgdir}/usr/share/${pkgname}/contrib/"
  rm "${pkgdir}"/usr/share/${pkgname}/contrib/bogogrep.o

  # for db migration
  cd "${pkgdir}"/usr/bin
  ln -vsf  /usr/bin/bogoutil "${pkgdir}"/usr/bin/bogoutil-berkeleydb5.3
}

package_bogofilter-kyotocabinet() {
  pkgdesc+=" - Kyotocabinet DB backend"
  depends+=('kyotocabinet')
  provides=('bogofilter')
  conflicts=('bogofilter' 'bogofilter-db' 'bogofilter-lmdb' 'bogofilter-sqlite')
  optdepends=('db5.3: for migration using bogoutil-berkeleydb5.3 tool')
  install=$pkgbase.install

  cd ${srcdir}/build-kyotocabinet
  make DESTDIR="${pkgdir}" install

  mv "${pkgdir}/etc/bogofilter/bogofilter.cf.example" "${pkgdir}/etc/bogofilter/bogofilter.cf"

  install -dm755 "${pkgdir}/usr/share/${pkgname}/contrib"
  install -m644 contrib/* "${pkgdir}/usr/share/${pkgname}/contrib/"
  rm "${pkgdir}"/usr/share/${pkgname}/contrib/bogogrep.o

  # for db migration
  cp  "${srcdir}"/build-db/src/bogoutil "${pkgdir}"/usr/bin/bogoutil-berkeleydb5.3
}

package_bogofilter-lmdb() {
  pkgdesc+=" - lmdb DB backend"
  depends+=('lmdb')
  provides=('bogofilter')
  conflicts=('bogofilter' 'bogofilter-db' 'bogofilter-kyotocabinet' 'bogofilter-sqlite')
  optdepends=('db5.3: for migration using bogoutil-berkeleydb5.3 tool')
  install=$pkgbase.install

  cd ${srcdir}/build-lmdb
  make DESTDIR="${pkgdir}" install

  mv "${pkgdir}/etc/bogofilter/bogofilter.cf.example" "${pkgdir}/etc/bogofilter/bogofilter.cf"

  install -dm755 "${pkgdir}/usr/share/${pkgname}/contrib"
  install -m644 contrib/* "${pkgdir}/usr/share/${pkgname}/contrib/"
  rm "${pkgdir}"/usr/share/${pkgname}/contrib/bogogrep.o

  # for db migration
  cp "${srcdir}"/build-db/src/bogoutil  "${pkgdir}"/usr/bin/bogoutil-berkeleydb5.3
}

package_bogofilter-sqlite() {
  pkgdesc+=" - sqlite DB backend"
  depends+=('sqlite')
  provides=('bogofilter')
  conflicts=('bogofilter' 'bogofilter-db' 'bogofilter-kyotocabinet' 'bogofilter-lmdb')
  optdepends=('db5.3: for migration using bogoutil-berkeleydb5.3 tool')
  install=$pkgbase.install

  cd ${srcdir}/build-sqlite
  make DESTDIR="${pkgdir}" install

  mv "${pkgdir}/etc/bogofilter/bogofilter.cf.example" "${pkgdir}/etc/bogofilter/bogofilter.cf"

  install -dm755 "${pkgdir}/usr/share/${pkgname}/contrib"
  install -m644 contrib/* "${pkgdir}/usr/share/${pkgname}/contrib/"
  rm "${pkgdir}"/usr/share/${pkgname}/contrib/bogogrep.o

  # for db migration
  cp  "${srcdir}"/build-db/src/bogoutil "${pkgdir}"/usr/bin/bogoutil-berkeleydb5.3
}
