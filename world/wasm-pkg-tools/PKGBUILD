# Maintainer: Frederik Schwan <freswa at archlinux.org>

pkgname=wasm-pkg-tools
pkgver=0.16.1
pkgrel=1
pkgdesc="Tools to package up Wasm Components"
url="https://github.com/bytecodealliance/wasm-pkg-tools"
arch=('x86_64')
license=('MIT')
depends=(
  cargo
  glibc
  libgcc
)
makedepends=(
  git
)
options=(!lto)
source=("git+https://github.com/bytecodealliance/wasm-pkg-tools.git#tag=v${pkgver}")
sha256sums=('7ced202ddda15b86aacb789bfb3bd389e599e1e180cac86100c24ee44b6e11d4')
b2sums=('fa52f05e901b27db86930a9c4b7b45737bb944866af166ee7f22dab9311b11c92e5863d7150b15d24eccfa2b61651b8516cc6dacda0011764c23100a01df426f')

prepare() {
  cd ${pkgname}
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd ${pkgname}
  cargo build --release --locked
}

package() {
  cd ${pkgname}
  install -Dm755 target/release/wkg -t "${pkgdir}"/usr/bin
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
