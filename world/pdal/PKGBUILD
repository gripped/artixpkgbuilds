# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: SaultDon <sault.don gmail>
pkgname=pdal
pkgver=2.6.1
pkgrel=2
pkgdesc="A C++ library for translating and manipulating point cloud data"
arch=('x86_64')
url="http://www.pdal.io"
license=('BSD')
depends=('gdal' 'libgeotiff' 'jsoncpp')
makedepends=('cmake' 'python' 'python-numpy' 'ninja' 'postgresql-libs')
optdepends=('python-numpy: for the Python plugin'
            'sqlite: for the sqlite plugin'
            'postgresql-libs: for the postgresql plugin')
provides=('pdal')
source=("https://github.com/PDAL/PDAL/releases/download/${pkgver}/PDAL-${pkgver}-src.tar.bz2"
        $pkgname-libxml2.patch::https://github.com/PDAL/PDAL/pull/4257.patch)
sha512sums=('1fdf7c0051996cc39c6e08492b33faa226f153bcf7c57a568fbcb4dc6b72d6732e005ebeeb3c717c9f31df4079f3968634c09da0b416bf51f614c1c174444ccc'
            'f883d1576b2b9ecb55ce5863b8afd6ed38984b86d9593df2efad41aee47c9d71d9edc855501464a58b4618ff5489d746a87a2b7c328f735bfa1be9c7c0cfa486')

prepare() {
  cd "PDAL-$pkgver-src/"
  patch -Np1 -i "$srcdir"/$pkgname-libxml2.patch
}

build() {
  cd "PDAL-$pkgver-src/"

  artix-cmake \
      -Bbuild \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DBUILD_PLUGIN_PGPOINTCLOUD=ON \
      -DWITH_COMPLETION=ON

  ninja -C build
}

package() {
  cd "PDAL-$pkgver-src"

  DESTDIR="$pkgdir/" ninja -C build install

  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
