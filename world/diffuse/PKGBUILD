# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: TDY <tdy@gmx.com>
# Contributor: zhuqin <zhuqin83@gmail.com>

pkgname=diffuse
pkgver=0.10.0
pkgrel=3
pkgdesc='Graphical tool for merging and comparing text files'
arch=(any)
url='https://github.com/MightyCreak/diffuse'
license=(GPL)
depends=(python-cairo python-gobject)
makedepends=(git meson ninja)
source=("git+$url#tag=v$pkgver")
b2sums=('1c1d64438c82fedc70f0b07f2f76d8113e3a5c445779ae80b160667b38dc87c5308506f90974ccc52b421fcf8317a240fbb5fe75abcf02b08ce1798b554565d6')

build() {
  sed -i "s/'appdata'/'metainfo'/g" $pkgname/data/meson.build
  mkdir -p build
  artix-meson build $pkgname -D b_ndebug=true
  ninja -C build
}

check() {
  ninja -C build test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
