# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=ruby-chef-utils
_pkgname="${pkgname#ruby-}"
pkgver=19.4.13
pkgrel=1
pkgdesc='Basic utility functions for Core Chef Infra development'
arch=(any)
url='https://github.com/chef/chef'
license=(Apache-2.0)
depends=(ruby ruby-concurrent)
makedepends=(git ruby-rdoc ruby-rake ruby-bundler)
options=(!emptydirs)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('a38b4880c5453cbf73cafcfcb848d1e093b14e466df1b6bcc2cfb6e48996e22dfe65beab99214d4f0214faddd6e6beee3c7110a58c55d0f9f20deda4f5a1d25e')
b2sums=('f4013eb86f6e334e2743534e55c3b617d8ace307d1af3c566bd6ce0fb10d8c9c9b70fef4bf983114143529f2d26d4fec8e2d0d1f0df5deae828dd3617e196b35')

build() {
  cd "$pkgname/$_pkgname"

  rake build
}

package() {
  cd "$pkgname/$_pkgname"

  local _gemdir="$(gem env gemdir)"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "pkg/$_pkgname-$pkgver.gem"

  # license
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  ln -sf "$_gemdir/gems/$_pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
