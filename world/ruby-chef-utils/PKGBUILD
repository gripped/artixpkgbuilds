# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=ruby-chef-utils
_pkgname="${pkgname#ruby-}"
pkgver=19.4.36
pkgrel=1
pkgdesc='Basic utility functions for Core Chef Infra development'
arch=(any)
url='https://github.com/chef/chef'
license=(Apache-2.0)
depends=(ruby ruby-concurrent)
makedepends=(git ruby-rdoc ruby-rake ruby-bundler)
options=(!emptydirs)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('9322c5aa6ef305ddf06ed6c0fe48d7d768ba5fb2c063f14d8d42d77ca2616831f74db206c23354d804e951305145c526712431b5b73d77b9ac1bcfed8d8124ea')
b2sums=('0d4340d32eef1c137be948ac40571f1c7fda76b43e66e65e5b249905636dd3a653fdc92309bbacda110e4478d3a26c18c60d04cba9eef73a01fd2062d7801ba5')

build() {
  cd "$pkgname/$_pkgname"

  rake build
}

package() {
  cd "$pkgname/$_pkgname"

  local _gemdir="$(gem env gemdir)"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "pkg/$_pkgname-$pkgver.gem"

  # license
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  ln -sf "$_gemdir/gems/$_pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
