# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Bert Peters <bert@bertptrs.nl>
# Contributor: Varakh <varakh@varakh.de>
# Contributor: Florian Klink <flokli@flokli.de>

pkgname=spotifyd
pkgver=0.4.2
pkgrel=1
pkgdesc='Lightweight spotify streaming daemon with spotify connect support'
arch=('x86_64')
url='https://github.com/Spotifyd/spotifyd'
license=('GPL3')
depends=(
  alsa-lib
  dbus
  libogg
  libpulse
)
makedepends=(
  clang
  cmake
  rust
)
options=(!lto)
source=("https://github.com/Spotifyd/spotifyd/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
b2sums=('bf61c133252cddc79ef492836a99da8ab099527f0dc508f8ea29d6057f4dd4ac5603bd4e38e69f748bb3ac7bb14e03341377cfd4889f0d8c3667e85562518303')

prepare() {
  cd spotifyd-${pkgver}
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd spotifyd-${pkgver}
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --release --locked --features pulseaudio_backend,dbus_mpris,rodio_backend
}

check() {
  cd spotifyd-${pkgver}
  export RUSTUP_TOOLCHAIN=stable
  cargo test --release --locked --target-dir=target
 }

package() {
  cd spotifyd-${pkgver}
  cargo install --locked --root "${pkgdir}"/usr --path "${srcdir}"/${pkgbase}-${pkgver} --features pulseaudio_backend,dbus_mpris,rodio_backend
  rm "${pkgdir}"/usr/{.crates.toml,.crates2.json}
}
