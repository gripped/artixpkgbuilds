# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: travisghansen <travisghansen@yahoo.com>
# Contributor: aleiphoenix <aleiphoenix@gmail.com>

pkgname=librabbitmq-c
pkgver=0.15.0
pkgrel=1
pkgdesc='RabbitMQ(amqp) library written in C-language'
url='https://github.com/alanxz/rabbitmq-c'
arch=('x86_64')
license=('MIT')
depends=('popt' 'openssl')
makedepends=('cmake' 'docbook-xsl' 'doxygen' 'xmlto' 'ninja')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('62b4e92fc270c5bdc5343cfaef5245e29a9b6d8983071a47391a93ae1b766ed7b98a6a546e8528befbc284f5ed17da4647595e94341380bfa76598569191e6c0')
b2sums=('2eac338e200796211984c3106976149e47c3d3da90b7dbe6bc4181d1eccf924e853854743718a42f4e928c37b0453181235263fe60a7c2f7eeccea952ddaeef4')

build() {
  cd ${pkgname:3}-$pkgver
  cmake \
    -B build \
    -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_EXAMPLES=OFF \
    -DBUILD_TOOLS=ON \
    -DBUILD_TOOLS_DOCS=ON \
    -DBUILD_TESTS=OFF \
    -DENABLE_SSL_SUPPORT=ON \
    -DBUILD_API_DOCS=ON \
    -Wno-dev
  ninja -C build -v
}

package() {
  cd ${pkgname:3}-$pkgver
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: ts=2 sw=2 et:
