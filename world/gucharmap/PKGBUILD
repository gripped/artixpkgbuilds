# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan De Groot <jgc@archlinux.org>

pkgname=gucharmap
pkgver=18.0.0
pkgrel=1
pkgdesc="Unicode Character Map"
url="https://wiki.gnome.org/Apps/Gucharmap"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  at-spi2-core
  cairo
  dconf
  glib2
  glibc
  gtk3
  pango
  pcre2
)
makedepends=(
  "unicode-character-database>=${pkgver%%.*}.0.0"
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  meson
  unzip
  vala
  yelp-tools
)
provides=(libgucharmap_2_90.so)
options=(!lto)
source=("git+https://gitlab.gnome.org/GNOME/gucharmap.git#tag=$pkgver")
b2sums=('f056b3a01637659c05e6705b617d8871b2276a3e5fcec04e41174e47ad0793abbad4ccb97207789b7be61f28a32bc564811dc2fe9020c32c4dd3974d1823c2c1')

prepare() {
  cd gucharmap
}

build() {
  local meson_options=(
    -D b_lto=false
    -D ucd_path=/usr/share/unicode
  )

  artix-meson gucharmap build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
