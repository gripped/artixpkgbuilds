# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan De Groot <jgc@archlinux.org>

pkgname=gucharmap
pkgver=17.0.1
pkgrel=1
pkgdesc="Unicode Character Map"
url="https://wiki.gnome.org/Apps/Gucharmap"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  at-spi2-core
  cairo
  dconf
  glib2
  glibc
  gtk3
  pango
  pcre2
)
makedepends=(
  "unicode-character-database>=${pkgver%%.*}.0.0"
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  meson
  unzip
  vala
  yelp-tools
)
provides=(libgucharmap_2_90.so)
options=(!lto)
source=("git+https://gitlab.gnome.org/GNOME/gucharmap.git#tag=$pkgver")
b2sums=('e756ac6724f6ac14999d0c1b756689a746d66e93cbdf937464c245e29c0e8b1c1514319a6fb2d2c0ab9750550f6314d5f2dc8dab2be7e91d9fb8184324321bc0')

prepare() {
  cd gucharmap
}

build() {
  local meson_options=(
    -D b_lto=false
    -D ucd_path=/usr/share/unicode
  )

  artix-meson gucharmap build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
