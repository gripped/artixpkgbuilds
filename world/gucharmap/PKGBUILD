# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan De Groot <jgc@archlinux.org>

pkgname=gucharmap
pkgver=17.0.2
pkgrel=1
pkgdesc="Unicode Character Map"
url="https://wiki.gnome.org/Apps/Gucharmap"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  at-spi2-core
  cairo
  dconf
  glib2
  glibc
  gtk3
  pango
  pcre2
)
makedepends=(
  "unicode-character-database>=${pkgver%%.*}.0.0"
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  meson
  unzip
  vala
  yelp-tools
)
provides=(libgucharmap_2_90.so)
options=(!lto)
source=("git+https://gitlab.gnome.org/GNOME/gucharmap.git#tag=$pkgver")
b2sums=('4d7b1768f981064d2953983d5775e5f39a5b64fed4b1a347e0867294e15da797c4f61dd753634fea12a708866aabead525d1e873e6837ce167965064d3055092')

prepare() {
  cd gucharmap
}

build() {
  local meson_options=(
    -D b_lto=false
    -D ucd_path=/usr/share/unicode
  )

  artix-meson gucharmap build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
