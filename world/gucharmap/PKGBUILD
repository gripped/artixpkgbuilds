# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan De Groot <jgc@archlinux.org>

pkgname=gucharmap
pkgver=15.1.5
pkgrel=1
pkgdesc="Gnome Unicode Charmap"
url="https://wiki.gnome.org/Apps/Gucharmap"
arch=(x86_64)
license=(GPL3)
depends=(gtk3)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  meson
  unicode-character-database
  vala
  yelp-tools
  unzip
)
provides=(libgucharmap_2_90.so)
options=(!lto)
source=("git+https://gitlab.gnome.org/GNOME/gucharmap.git#tag=$pkgver")
b2sums=('eca34fc506935c480a3cc1cf03c214b38839cb510536852333256b03adc0503e165edc18f409be278f16fbb080ccc8f2a55045bd15f5247b6e766465218bcb08')

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D b_lto=false
    -D ucd_path=/usr/share/unicode
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
