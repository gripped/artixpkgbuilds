# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=glade
pkgver=3.40.0+r14+g1fbca367
pkgrel=7
pkgdesc="User Interface Builder for GTK applications"
url="https://glade.gnome.org/"
arch=(x86_64)
license=(GPL LGPL)
depends=(gtk3 libxml2)
makedepends=(gtk-doc glib2-devel gobject-introspection itstool docbook-xsl git meson gjs
             python-gobject webkit2gtk-4.1 python-setuptools)
checkdepends=(xorg-server-xvfb)
optdepends=('devhelp: development help'
            'gjs: gjs module'
            'python-gobject: python module'
            'webkit2gtk-4.1: webkit2gtk module')
provides=(libgladeui-2.so)
groups=(gnome-extra)
_commit=1fbca367cf33991f1d8d20a256ca704eb778fca9  # master
source=("git+https://gitlab.gnome.org/GNOME/glade.git#commit=$_commit"
        fix-build.diff
        0001-Raise-existing-window-instead-of-opening-new-one-on-.patch)
sha256sums=('f5ed9cf6de49449307b6099271caf6357f0a3c26374764667efd415f78764921'
            '0dd99583c4b7b6327e84a909614f46ec0062dbc9f67be733ea97dc294e40317b'
            '8551b481c57ace84df08ead3e72f7f69a8db392802a5a0657a3ff8409007ff87')

pkgver() {
  cd glade
  git describe --tags | sed 's/^GLADE_//;s/_/./g;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd glade

  # Fix missing dependency on vcs_tag
  git apply -3 ../fix-build.diff

  # build with webkit2gtk-4.1
  sed -i 's/webkit2gtk-4.0/webkit2gtk-4.1/' meson.build

  # Raise existing window instead of opening new one on activation
  patch -Np1 -i ../0001-Raise-existing-window-instead-of-opening-new-one-on-.patch

  # Fix window icon name
  sed -i '/gtk_window_set_default_icon_name/ s/glade/org.gnome.Glade/' src/main.c
}

build() {
  artix-meson glade build -D gtk_doc=true
  meson compile -C build
}

check() {
  # FAIL: cannot register existing type 'GIRepository'
  dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs || :
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
