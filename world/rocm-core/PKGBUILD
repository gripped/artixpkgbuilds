# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Torsten Keßler <tpkessler at archlinux dot org>
# Contributor: Christian Heusel <gromit@archlinux.org>
# Contributor: Greg Land <landjgregory at gmail dot com>

pkgname=rocm-core
pkgver=7.2.1
pkgrel=1
pkgdesc='AMD ROCm core package (version files)'
license=('MIT')
arch=('x86_64')
depends=(
    'gcc-libs'
    'glibc'
    'python-pyelftools'
    'python-pyaml'
    'python-prettytable'
    'python'
)
makedepends=('cmake')
source=("rocm-$pkgver.tar.gz::https://github.com/ROCm/rocm-systems/archive/refs/tags/rocm-$pkgver.tar.gz"
        'rocm-ld.conf'
        'rocm-profile.sh'
        'rocm.fish')
b2sums=('7f91e445e66297d11491314f61a0d99e62b391b1699cfd043564acdc8d3c29d4db86659e74b830d4989cff315dfb4101151647d31f8e648574cbe7a95a659088'
        'd045c357d8e7e8a4840ab137404f12cd08419444ffc478046c13ed3bd13a5d33358c1443bf76ee571a7a062454e2bdda1a5507a70edbd001bce004f18775e4b2'
        '4372bcbe97d7c95d4918ad4beacc4fe9bfc8bfb8cafcf08d9ebbcba7df3e3bf535ff51f90c2d0f653858b0ae03b108ac3cb32b61b4ecac3abb609acc06be3ee3'
        '16bae84aeb429a298d548945860102be13b80b06f823abae13fd37a9d3b571de732f2e304fe67625520b414595fd070944dd29855b0405883d713b5a966c66b9')
url='https://rocm.docs.amd.com/en/latest/'
_projectDir="rocm-systems-rocm-$pkgver/projects/$pkgname"

build() {
  local cmake_args=(
    -Wno-dev
    -B build
    -S $_projectDir
    -D CMAKE_BUILD_TYPE=None
    -D BUILD_SHARED_LIBS=ON
    -D CMAKE_INSTALL_PREFIX=/opt/rocm
    -D ROCM_VERSION="$pkgver"
  )
  cmake "${cmake_args[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 rocm-ld.conf "$pkgdir/etc/ld.so.conf.d/rocm.conf"
  install -Dm644 rocm-profile.sh "$pkgdir/etc/profile.d/rocm.sh"
  install -Dm644 rocm.fish -t "$pkgdir/usr/share/fish/vendor_conf.d"
  install -Dm644 "$srcdir/$_projectDir/LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
