# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdevelop-python
pkgver=24.05.0
pkgrel=1
pkgdesc='Python language and documentation plugin for KDevelop'
arch=(x86_64)
url='https://www.kdevelop.org/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kconfig5
         kcoreaddons5
         kdevelop
         ki18n5
         kparts5
         ktexteditor5
         kwidgetsaddons5
         kxmlgui5
         python
         qt5-base
         threadweaver5)
makedepends=(extra-cmake-modules)
optdepends=('python-pycodestyle: for Python style checking')
groups=(kde-applications
        kdevelop)
source=(https://download.kde.org/stable/release-service/$pkgver/src/kdev-python-$pkgver.tar.xz{,.sig})
sha256sums=('18383c301a3f8ffae6642104f6b40f24ce45b23236ea22fb3f194cd4e558bf94'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  sed -e 's|<3.12|<3.13|' -i kdev-python-$pkgver/CMakeLists.txt # Accept Python 3.12
}

build() {
  cmake -B build -S kdev-python-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
