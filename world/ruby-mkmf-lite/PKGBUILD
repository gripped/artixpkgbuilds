# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Tim Meusel <tim@bastelfreak.de>
# Contributor: loqs

_gemname=mkmf-lite
pkgname=ruby-${_gemname}
pkgver=0.6.0
pkgrel=4
pkgdesc='A lighter version of mkmf designed for use as a library'
arch=(any)
url="https://github.com/djberg96/${_gemname}"
license=(Apache-2.0)
depends=(
  ruby-memoist
  ruby-ptools
)
makedepends=(ruby-rdoc)
checkdepends=(
  ruby-rake
  ruby-rspec
)
options=(!emptydirs)
source=($url/archive/$_gemname-$pkgver.tar.gz)
sha256sums=('3921f596705acd6b0ef50adad20c2fc713d3b021a26a0dc6b0d01b3fbf3f8a80')

prepare() {
  cd ${_gemname}-${_gemname}-${pkgver}

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' $_gemname.gemspec

  sed --in-place --regexp-extended '/add_development_dependency/d' $_gemname.gemspec
  sed --in-place --regexp-extended '/rubocop/Id' Rakefile
}

build() {
  cd ${_gemname}-${_gemname}-${pkgver}

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd ${_gemname}-${_gemname}-${pkgver}

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rake
}

package() {
  cd ${_gemname}-${_gemname}-${pkgver}

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}
