# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-mines
pkgver=48.1
pkgrel=1
pkgdesc="Clear hidden mines from a minefield"
url="https://wiki.gnome.org/Apps/Mines"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  dconf
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  libgnome-games-support-2
  librsvg
)
makedepends=(
  appstream
  git
  libgee
  meson
  vala
  yelp-tools
)
groups=(gnome-extra)
source=("git+https://gitlab.gnome.org/GNOME/gnome-mines.git#tag=$pkgver")
b2sums=('58a08aca3e0bc88ead587763f2fe7d0f4e534b95510b3255b6ca1cc3a372d623694dfb104c589c93a4c5ba4e09e01fb83d03a91b2045ef3f6e86eabaefdf55e6')

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
