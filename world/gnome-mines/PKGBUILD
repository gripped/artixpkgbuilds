# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-mines
pkgver=49.0.1
pkgrel=1
pkgdesc="Clear hidden mines from a minefield"
url="https://wiki.gnome.org/Apps/Mines"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  dconf
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  libgee
  librsvg
)
makedepends=(
  appstream
  git
  meson
  vala
  yelp-tools
)
groups=(gnome-extra)
options=(!emptydirs)
source=("git+https://gitlab.gnome.org/GNOME/gnome-mines.git#tag=$pkgver")
b2sums=('a81a20d625f79f882b558559593471c3c8844d63992e0a1ad59bbdb2acaf3b85ace3d9f40c0d9914817a831d2cf4b6f6eddd3db3b18d4aee55458a606327b214')

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
