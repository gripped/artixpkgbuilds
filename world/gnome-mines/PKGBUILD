# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-mines
pkgver=40.1
pkgrel=3
pkgdesc="Clear hidden mines from a minefield"
url="https://wiki.gnome.org/Apps/Mines"
arch=(x86_64)
license=(GPL)
depends=(gtk3 librsvg libgnome-games-support)
makedepends=(yelp-tools appstream-glib vala git meson)
groups=(gnome-extra)
source=("git+https://gitlab.gnome.org/GNOME/gnome-mines.git#tag=$pkgver")
b2sums=('f3980321785e4f734f00f64aa64722ddb5420a7b6843c8dce9196c3288f364d26bd9b23bf1fb8440eecdc6bbc0bd335bd9f4b35dbf05b2b0e88642f827c39ed6')

prepare() {
  cd $pkgname

  # Fix window icon name
  # https://gitlab.gnome.org/GNOME/gnome-mines/-/merge_requests/40
  sed -i '/icon_name/ s/gnome-mines/org.gnome.Mines/' src/interface.ui
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
