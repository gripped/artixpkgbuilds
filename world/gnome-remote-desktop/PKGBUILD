# Maintainer: Fabian Bornschein <fabiscafe-at-mailbox-dot-org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-remote-desktop
pkgver=47.0
pkgrel=2
pkgdesc="GNOME Remote Desktop server"
url="https://gitlab.gnome.org/GNOME/gnome-remote-desktop"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  freerdp
  fuse3
  gcc-libs
  glib2
  glibc
  libdrm
  libei
  libepoxy
  libfdk-aac
  libnotify
  libpipewire
  libsecret
  libvncserver
  libxkbcommon
  opus
  pipewire
  polkit
  tpm2-tss
)
makedepends=(
  asciidoc
  ffnvcodec-headers
  git
  glib2-devel
  meson
)
groups=(gnome)
source=("git+$url.git?signed#tag=${pkgver/[a-z]/.&}")
b2sums=('53df46816fcbe198138eb4938525ce06e37dcf92ea9fd67607488cb08de8c69ce332cef69dc8c3da9f17c644025e20b59aed83c87cfce79b19946a94db6ac4e9')
validpgpkeys=(
  8307C0A224BABDA1BABD0EB9A6EEEC9E0136164A # Jonas Ådahl <jadahl@gmail.com>
  E60DADB5546D3F01AF35B87289F540EAB24513E2 # Ray Strode <rstrode@redhat.com>
)

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D systemd=false
    -D tests=false
    -D vnc=true
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
