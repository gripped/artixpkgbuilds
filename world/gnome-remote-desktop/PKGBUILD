# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-remote-desktop
pkgver=47.1
pkgrel=1
pkgdesc="GNOME Remote Desktop server"
url="https://gitlab.gnome.org/GNOME/gnome-remote-desktop"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  freerdp
  fuse3
  gcc-libs
  glib2
  glibc
  libdrm
  libei
  libepoxy
  libfdk-aac
  libnotify
  libpipewire
  libsecret
  libvncserver
  libxkbcommon
  opus
  pipewire
  polkit
  tpm2-tss
)
makedepends=(
  asciidoc
  ffnvcodec-headers
  git
  glib2-devel
  meson
)
groups=(gnome)
source=("git+$url.git?signed#tag=${pkgver/[a-z]/.&}")
b2sums=('24796ff19165dd317046276c680d1860eba1f8e0604552610aabef0b51da9fb3b596063137c43a84fc30b8fb6bb521e26ca36fffcb1e367509319c9fa6e7a22c')
validpgpkeys=(
  8307C0A224BABDA1BABD0EB9A6EEEC9E0136164A # Jonas Ã…dahl <jadahl@gmail.com>
  E60DADB5546D3F01AF35B87289F540EAB24513E2 # Ray Strode <rstrode@redhat.com>
)

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D systemd=false
    -D tests=false
    -D vnc=true
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
