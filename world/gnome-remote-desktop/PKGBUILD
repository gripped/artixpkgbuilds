# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-remote-desktop
pkgver=49.2
pkgrel=1
pkgdesc="GNOME Remote Desktop server"
url="https://gitlab.gnome.org/GNOME/gnome-remote-desktop"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  freerdp
  fuse3
  gcc-libs
  glib2
  glibc
  libdrm
  libei
  libepoxy
  libfdk-aac
  libnotify
  libpipewire
  libsecret
  libva
  libvncserver
  libxkbcommon
  opus
  pipewire
  polkit
  tpm2-tss
  vulkan-icd-loader
)
makedepends=(
  asciidoc
  ffnvcodec-headers
  git
  glib2-devel
  meson
  vulkan-headers
)
groups=(gnome)
source=("git+$url.git?signed#tag=${pkgver/[a-z]/.&}")
b2sums=('7423534a972aa9bf98f2087bbc102615d7f15527fa0ce95c1169abde17eff460c59e657ea937914136af12a7262bbabc635c0b16df993d157efb74995c732468')
validpgpkeys=(
  8307C0A224BABDA1BABD0EB9A6EEEC9E0136164A # Jonas Ã…dahl <jadahl@gmail.com>
  E60DADB5546D3F01AF35B87289F540EAB24513E2 # Ray Strode <rstrode@redhat.com>
)

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D systemd=false
    -D tests=false
    -D vnc=true
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
