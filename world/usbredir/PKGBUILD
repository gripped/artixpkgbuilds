# Maintainer: Anatol Pomozov
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>

pkgname=usbredir
pkgver=0.14.0
pkgrel=1
pkgdesc="USB traffic redirection protocol"
arch=(x86_64)
url="https://www.spice-space.org/usbredir.html"
license=(
  GPL-2.0-or-later
  LGPL-2.1-or-later
)
depends=(
  glib2
  glibc
  libusb
)
makedepends=(
  git
  meson
)
provides=(
  libusbredirhost.so
  libusbredirparser.so
)
source=("git+https://gitlab.freedesktop.org/spice/usbredir.git#tag=$pkgname-$pkgver")
sha256sums=('f881a5430d8d350adcd0bd71035f143d101516204c324d9e6857e3023b629796')

prepare() {
  cd $pkgname
  sed -i '/-D_FORTIFY_SOURCE=2/d' meson.build
}

build() {
  cd $pkgname
  artix-meson . build
  meson compile -C build
}

check() {
  cd $pkgname
  meson test -C build --print-errorlogs
}

package() {
  cd $pkgname
  meson install -C build --destdir="$pkgdir"
}
