# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgbase=percona-server
pkgname=('percona-server-clients' 'percona-server')
pkgver=9.7.1_1
_pkgver=${pkgver/_/-}
_myver=${pkgver/_rel*}
pkgrel=5
arch=('x86_64')
makedepends=('cmake' 'zlib' 'lz4' 'zstd' 'libaio' 'pam' 'numactl' 'jemalloc' 'openssl'
             'rpcsvc-proto' 'doxygen' 'graphviz' 'libevent' 'libfido2' 'protobuf' 'editline')
license=('GPL-2.0-only')
url="https://www.percona.com/software/mysql-database/percona-server"
source=("https://www.percona.com/downloads/Percona-Server-${pkgver%.*_*}/Percona-Server-$_pkgver/source/tarball/percona-server-$_pkgver.tar.gz"
        'gcc-14.patch'
        'my.cnf'
        'mysql-user.conf'
        'mysqlrouter-user.conf')
sha256sums=('cfa835f66b415a46e64420d515096281f42a7bcf189bda0f6c434ea5a55d63ee'
            'eeb9c6c8f70dd5eb05f735df0cdfc294365b393e5819f26029320e38714069a6'
            'b467b04d6d06152b2abc33f2a6de63fef0fc922dd5119d2ee1d07d3c1a489731'
            'b4e357a0e2e3a7dc01f9459efcbed77ce3229f8d94273e3ff464bcd67c9413fc'
            '4c8bdd0bc71b4ab9d69485b7e51937bf45f1c87323e380290db357483c154505')

prepare() {
	cd $pkgbase-$_pkgver
	rm -v sql/sql_yacc.{cc,h}

	sed -r -e s@/var/run/mysqlrouter@/run/mysqlrouter@ \
	       -e s@lib64/mysql@lib/mysql@ \
	       -i cmake/install_layout.cmake



  # Remove flags that are already set
	sed 's/-DNDEBUG -D_FORTIFY_SOURCE=2//' -i CMakeLists.txt

	patch -p1 -i ../gcc-14.patch # Fix build with GCC 14
}

build() {
	local cmake_options=(
		-B build
		-S "$pkgbase-$_pkgver"
		-D CMAKE_C_FLAGS_RELWITHDEBINFO="${CFLAGS} -DNDEBUG -ffat-lto-objects"
		-D CMAKE_CXX_FLAGS_RELWITHDEBINFO="${CXXFLAGS} -DNDEBUG -ffat-lto-objects"
		-W no-dev
		-D CMAKE_BUILD_TYPE=RelWithDebInfo
		-D CMAKE_POSITION_INDEPENDENT_CODE=ON
		-D CMAKE_POLICY_VERSION_MINIMUM=3.5
		-D BUILD_CONFIG=mysql_release
		-D REPRODUCIBLE_BUILD=ON
		-D CMAKE_INSTALL_PREFIX=/usr
		-D SYSCONFDIR=/etc/mysql
		-D MYSQL_DATADIR=/var/lib/mysql
		-D MYSQL_UNIX_ADDR=/run/mysqld/mysqld.sock
		-D DEFAULT_CHARSET=utf8mb4
		-D DEFAULT_COLLATION=utf8mb4_0900_ai_ci
		-D WITH_EXTRA_CHARSETS=all
		-D ENABLED_LOCAL_INFILE=ON
		-D INSTALL_LAYOUT=RPM
		-D INSTALL_SBINDIR=bin
		-D INSTALL_LIBDIR=lib
		-D INSTALL_PLUGINDIR=lib/perconaserver/plugin
		-D INSTALL_INCLUDEDIR=include/perconaserver
		-D INSTALL_INFODIR=share/doc/percona-server
		-D INSTALL_DOCREADMEDIR=share/doc/percona-server
		-D INSTALL_DOCDIR=share/doc/percona-server
		-D INSTALL_MANDIR=share/man
		-D INSTALL_SUPPORTFILESDIR=share/mysql
		-D INSTALL_MYSQLSHAREDIR=share/mysql
		-D INSTALL_STATIC_LIBRARIES=OFF
		-D WITH_ZLIB=system
		-D WITH_LZ4=system
		-D WITH_ZSTD=system
		-D WITH_SSL=system
		-D WITH_ICU=system
		-D WITH_FIDO=system
		-D WITH_PROTOBUF=system
		-D WITH_EDITLINE=system
		-D WITH_ENCRYPTION_UDF=ON
		-D WITH_LIBWRAP=OFF
		-D WITH_MECAB=OFF
		-D WITH_PAM=ON
		-D WITH_ARCHIVE_STORAGE_ENGINE=ON
		-D WITH_BLACKHOLE_STORAGE_ENGINE=ON
		-D WITH_FEDERATED_STORAGE_ENGINE=ON
		-D WITH_ROCKSDB=ON
		-D ROCKSDB_DISABLE_AVX2=1
		-D ALLOW_NO_SSE42=ON
		-D WITH_AUTHENTICATION_CLIENT_PLUGINS=1
		-D WITH_VALGRIND=OFF
		-D DEBUG_EXTNAME=OFF
		-D BUILD_TESTING=OFF
		-D WITH_UNIT_TESTS=OFF
		-D WITH_SYSTEMD=0
		-D CMAKE_EXE_LINKER_FLAGS="${LDFLAGS} -ljemalloc"
		-D WITH_LTO=ON
		-D WITH_NUMA=ON
	)
	if [[ "${CARCH}" == "x86_64" ]]; then
		cmake_options+=(-D ROCKSDB_BUILD_ARCH=x86-64)
	fi
	cmake "${cmake_options[@]}"
	cmake --build build
}

package_percona-server-clients() {
	pkgdesc='Percona Server client tools'
	depends=('glibc' 'libgcc' 'libstdc++' 'zlib' 'zstd' 'openssl' 'jemalloc' 'libfido2' 'libedit')
	replaces=('libperconaserverclient')
	conflicts=('mysql-clients' 'libperconaserverclient')
	provides=("mysql-clients=$_myver" "mariadb-clients=$_myver")
	optdepends=(
		'libsasl: authentication_ldap_sasl_client plugin'
		'krb5: authentication_kerberos_client plugin'
	)

	cd build
	for dir in client include libmysql libservices; do
		make -C $dir DESTDIR="$pkgdir" install
	done

	rm -vrf "$pkgdir"/usr/lib/perconaserver/plugin/debug

	install -Dm755 scripts/mysql_config "$pkgdir"/usr/bin/perconaserver_config
	install -Dm644 "$srcdir/$pkgbase-$_pkgver/man/mysql_config.1" "$pkgdir"/usr/share/man/man1/perconaserver_config.1
	sed s@/lib64@/lib@ -i "$pkgdir"/usr/bin/perconaserver_config

	install -D -m0644 scripts/perconaserverclient.pc "$pkgdir"/usr/lib/pkgconfig/perconaserverclient.pc
	install -D -m0644 "$srcdir/$pkgbase-$_pkgver/support-files/mysql.m4" "$pkgdir"/usr/share/aclocal/perconaserverclient.m4

	# install man pages
	install -d "$pkgdir"/usr/share/man/man1
	for man in mysql{,admin,check,dump,import,show,slap,binlog}; do
		install -m644 "$srcdir/$pkgbase-$_pkgver/man/$man.1" "$pkgdir"/usr/share/man/man1/$man.1
	done

	# provided by percona-server
	rm "$pkgdir"/usr/bin/mysql{_config_editor,_secure_installation}
}

package_percona-server() {
	pkgdesc='Drop-in replacement for MySQL that provides improved performance, diagnostics, instrumentation and MyRocks storage engine'
	backup=('etc/mysql/my.cnf' 'etc/mysqlrouter/mysqlrouter.conf' 'etc/conf.d/mysql')
	depends=('glibc' 'libgcc' 'libstdc++' 'libaio' 'pam' 'jemalloc' 'numactl' 'lz4' 'zstd' 'openssl'
	         'libtirpc' 'curl' 'libevent' 'icu' 'protobuf' 'zlib' 'libldap')
	optdepends=('perl-dbd-mysql')
	conflicts=('mysql')
	provides=("mysql=$_myver" "mariadb=$_myver")
	options=('emptydirs')

	cd build
	make DESTDIR="$pkgdir" install

	cd "$pkgdir"
	install -Dm644 "$srcdir/my.cnf" etc/mysql/my.cnf
	install -Dm644 "$srcdir/mysql-user.conf" usr/lib/sysusers.d/mysql.conf

	install -dm755 etc/mysqlrouter etc/conf.d
	sed -e 's:@ROUTER_RUNTIMEDIR@:/run/mysqlrouter:' \
		-e 's:^logging_folder.*:logging_folder =:' \
		"$srcdir/$pkgbase-$_pkgver/packaging/rpm-common/mysqlrouter.conf.in" \
		> etc/mysqlrouter/mysqlrouter.conf
	install -Dm644 "$srcdir/mysqlrouter-user.conf" usr/lib/sysusers.d/mysqlrouter.conf

	echo MYSQLD_OPTS= > etc/conf.d/mysql

	chmod 755 usr
	rm -vrf usr/{cmake,lib/perconaserver/plugin/debug,lib/tmpfiles.d}

	# Move documentation
	if [ -f usr/PATENTS ]; then
		mv usr/{PATENTS,README.md} usr/share/doc/$pkgname/
		rm usr/COPYING.*
	fi

	# provided by percona-server-clients
	rm usr/bin/mysql_config
	rm usr/lib/libperconaserverclient*
	rm -r usr/include/
	rm usr/share/man/man1/mysql_config.1
	rm -r usr/share/aclocal usr/lib/pkgconfig
	rm usr/bin/mysql{,admin,check,dump,import,show,slap,binlog,test,_migrate_keyring}
	rm usr/lib/perconaserver/plugin/{authentication_*_client,mysql_native_password,dialog}.so
	rm usr/share/man/man1/mysql{,admin,check,dump,import,show,slap,binlog}.1

	# not needed
	rm -r usr/share/mysql-test
}
