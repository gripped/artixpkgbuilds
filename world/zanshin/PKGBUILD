# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=zanshin
pkgver=25.12.1
pkgrel=1
pkgdesc='To-do management application based on Akonadi'
arch=(x86_64)
url='https://apps.kde.org/zanshin/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kcalendarcore
         kconfig
         kcoreaddons
         kcrash
         kdepim-runtime
         ki18n
         kitemmodels
         kontactinterface
         kparts
         krunner
         kwidgetsaddons
         kwindowsystem
         kxmlgui
         libakonadi
         qt6-base)
makedepends=(boost
             extra-cmake-modules
             kdoctools)
groups=(kde-applications
        kde-pim)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('cab673e121caacbaa9db5203dc307fb137696d4ff615968b7eecd2146cef9ecd'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
