# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=libva-intel-driver
pkgver=2.4.5
pkgrel=1
pkgdesc='VA-API implementation for Intel G45 and HD Graphics family'
arch=(x86_64)
url=https://github.com/irql-notlessorequal/intel-vaapi-driver
license=(MIT)
depends=(
  glibc
  libva
  libdrm
)
makedepends=(
  git
  meson
)
source=(intel-vaapi-driver-irql::git+https://github.com/irql-notlessorequal/intel-vaapi-driver.git#tag=${pkgver})
b2sums=('f63b87aa4bd661213dd45d39cfb2bf46920d5a7ec6b877196d5bc341f30ac7bedc0b587ca3cfd05a1437b826eb96976595239f9e385643aa711df4c721d7ed3a')

build() {
  artix-meson intel-vaapi-driver-irql build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
  install -Dm 644 intel-vaapi-driver-irql/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}

# vim: ts=2 sw=2 et:
