# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=spglib
pkgver=2.7.0
pkgrel=2
pkgdesc='C library for finding and handling crystal symmetries'
arch=(x86_64)
url='https://spglib.readthedocs.io/en/latest/'
license=(BSD-3-Clause)
depends=(gcc-libs
         glibc)
makedepends=(cmake
             gcc-fortran
             git
             gtest
             pybind11
             python-build
             python-installer
             python-numpy
             python-scikit-build-core
             python-setuptools-scm)
optdepends=('gcc-libs: Fortran interface'
            'python-numpy: Python interface')
source=(git+https://github.com/spglib/spglib#tag=v$pkgver)
sha256sums=('06462a670f6501a2fb6d4e0f0fd0b848a87c628a3ae48ede3c93f444d1ef0739')

prepare() {
  sed -e 's|TARGET Spglib_symspg|0|' -i $pkgname/python/CMakeLists.txt # Don't duplicate shared libs in python tree
}

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSPGLIB_WITH_Fortran=ON
  cmake --build build
  cd $pkgname
  python -m build --wheel --no-isolation
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
