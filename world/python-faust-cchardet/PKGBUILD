# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=python-faust-cchardet
pkgver=3.2.0
pkgrel=1
pkgdesc="Fork of the original cChardet"
url="https://github.com/faust-streaming/cChardet"
arch=(x86_64)
license=('GPL-2.0-only OR LGPL-2.1-only OR MPL-1.1')
depends=(
  glibc
  libgcc
  libstdc++
  python
)
makedepends=(
  cython
  git
  meson-python
  python-build
  python-installer
  python-wheel
)
provides=("python-cchardet=$pkgver")
conflicts=(python-cchardet)
source=(
  "faust-cchardet::git+$url#tag=v$pkgver"
  "git+https://gitlab.freedesktop.org/uchardet/uchardet.git"
)
b2sums=('560ab9d1cd5ac7f785e335c9cd12b08b39c9d7de47f9165fa37cdea30f1dbebac737620ed56f21c04baf1d30d618827e3ee611dd987c4c8bf005cf69b24fc5aa'
        'SKIP')

prepare() {
  cd faust-cchardet

  git submodule init
  git submodule set-url src/ext/uchardet "$srcdir/uchardet"
  git -c protocol.file.allow=always -c protocol.allow=never submodule update
}

build() {
  python -m build --wheel --no-isolation faust-cchardet
}

package() {
  python -m installer --destdir="$pkgdir" faust-cchardet/dist/*.whl
}

# vim:set sw=2 sts=-1 et:
