# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-sus-fixtures-async-http
pkgver=0.9.0
pkgrel=2
pkgdesc='Test fixtures for running in Async::HTTP'
arch=(any)
url='https://github.com/socketry/sus-fixtures-async-http'
license=(MIT)
depends=(
  ruby
  ruby-async-http
  ruby-async-io
  ruby-sus
  ruby-sus-fixtures-async
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-bake-modernize
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-decode
  ruby-io-stream
  ruby-io-endpoint
)
options=(!emptydirs)
source=("git+${url}.git#tag=v${pkgver}")
sha256sums=('cd52b54b5ab93b470d7a768f36c782bf7da1d7dc79b5958bb1ad90e68a727b61')

prepare() {
  cd sus-fixtures-async-http

  sed -r \
    -e 's|~>|>=|g' \
    -e '/signing_key/d' \
    -i sus-fixtures-async-http.gemspec

  sed -i \
    -e '/group :maintenance/,/end/d' \
    -e '/rubocop/d' \
    gems.rb
  #sed -i 's/be < 60/be <= 60/' test/sus/fixtures/async/http/server_context.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd sus-fixtures-async-http
  gem build sus-fixtures-async-http.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    sus-fixtures-async-http-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd sus-fixtures-async-http
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd sus-fixtures-async-http
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 license.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
