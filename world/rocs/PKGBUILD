# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Roja <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=rocs
pkgver=26.08.1
pkgrel=1
pkgdesc='Graph Theory IDE'
url='https://apps.kde.org/rocs/'
arch=(x86_64)
license=(GPL-2.0-or-later
         LGPL-2.0-or-later)
depends=(glibc
         karchive
         kcompletion
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         ki18n
         kitemviews
         ktexteditor
         ktexttemplate
         ktextwidgets
         kparts
         kwidgetsaddons
         kxmlgui
         libgcc
         libstdc++
         qt6-base
         qt6-declarative)
makedepends=(boost
             extra-cmake-modules
             kdoctools)
groups=(kde-applications
        kde-education)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('a20fb3a100523181e23ff7de3402f9f74339cbeec8367dd2b24dc125f5555834'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
