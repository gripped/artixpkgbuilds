# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-anyio
# https://github.com/agronholm/anyio/blob/master/docs/versionhistory.rst
pkgver=4.6.2.post1
pkgrel=2
pkgdesc='High level compatibility layer for multiple asynchronous event loop implementations'
arch=(any)
url='https://github.com/agronholm/anyio'
license=(MIT)
depends=(python python-idna python-sniffio)
makedepends=(python-build python-installer python-setuptools python-setuptools-scm python-wheel
             python-uvloop python-trio)
checkdepends=(python-pytest python-trustme python-hypothesis python-pytest-mock python-psutil
              python-truststore)
optdepends=(
  'python-trio: trio backend'
  'python-outcome: trio backend'
  'python-uvloop: use uvloop for asyncio backend'
  'python-pytest: pytest plugin'
)
source=(https://github.com/agronholm/anyio/archive/$pkgver/anyio-$pkgver.tar.gz
        no-exceptiongroup.diff
        python-3.12.diff)
sha256sums=('aae56b725ba03ba3005f47b27c1d8c70c9d89d30f706ce9624a9b89edcf25308'
            '72658edd045a382790a3218736c641d2b6e16e4b464137b4fb01e488112ca609'
            '9cd05959ecc9408653e6a96be2bcd27104a430f5426b86edf22ef257b68139d4')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

prepare() {
  cd anyio-$pkgver
  # Remove "error" from pytest filterwarnings
  sed -i '/"error"/d' pyproject.toml

  # Avoid checkdepends on python-exceptiongroup, which was removed from Arch Linux [1].
  # Specifically, I rewrite a test to use the new except* syntax, which is inspired by [2].
  # [1] https://gitlab.archlinux.org/archlinux/packaging/state/-/commit/de33ab36729b786b80bf314f4e0296fc0cefec5b
  # [2] https://github.com/agronholm/exceptiongroup?tab=readme-ov-file#catching-exceptions
  patch -Np1 -i ../no-exceptiongroup.diff

  # Partial revert of https://github.com/agronholm/anyio/commit/4e9f18dfaa6e7db55231a35fcd93e18c343519a4
  # Don't fail tests with features that require uvloop 0.21
  patch -Np1 -i ../python-3.12.diff
}

build() {
  cd anyio-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd anyio-$pkgver
  # Install to a temporary root as the test suite requires the entry point for
  # its pytest plugin
  pyver=$(python -c "import sys; print('{}.{}'.format(*sys.version_info[:2]))")
  python -m installer --destdir="$PWD/tmp_install" dist/*.whl
  PYTHONPATH="$PWD/tmp_install/usr/lib/python$pyver/site-packages" pytest
}

package() {
  cd anyio-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
