# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux,org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=kfind
pkgver=25.12.1
pkgrel=1
pkgdesc='Find Files/Folders'
arch=(x86_64)
url='https://apps.kde.org/kfind/'
license=(LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         karchive
         kcompletion
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         kfilemetadata
         ki18n
         kio
         kwidgetsaddons
         kxmlgui
         qt6-5compat
         qt6-base)
makedepends=(extra-cmake-modules
             kdoctools)
optdepends=('mlocate: search using mlocate index')
groups=(kde-applications
        kde-utilities)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('960d257825b2ff99329823be5a5964836c2e83e43280ddf796c8f9f1681f8cf4'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
