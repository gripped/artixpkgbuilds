# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-music
pkgver=47.1
pkgrel=2.1
epoch=1
pkgdesc="Music player and management application"
url="https://apps.gnome.org/Music/"
arch=(any)
license=(GPL-2.0-or-later)
depends=(
  dconf
  gdk-pixbuf2
  glib2
  graphene
  grilo
  grilo-plugins
  gst-plugins-base
  gst-plugins-base-libs
  gst-plugins-good
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
  libgirepository
  libmediaart
  libsoup3
  localsearch
  python
  python-cairo
  python-gobject
  tinysparql
)
makedepends=(
  appstream-glib
  git
  gobject-introspection
  meson
  yelp-tools
)
optdepends=(
  'gst-libav: Extra media codecs'
  'gst-plugins-bad: Extra media codecs'
  'gst-plugins-ugly: Extra media codecs'
)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/gnome-music.git#tag=${pkgver/[a-z]/.&}")
b2sums=('aea299a5d817e2f82b8810269914a1cf0b92624a8c9325206f0170569c89915dcc1be9710a081077d3b3a2dcd9962a4c1e32697aaf269d55ecfb9e7e26602bd6')

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
