# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-music
pkgver=48beta
pkgrel=2
epoch=1
pkgdesc="Music player and management application"
url="https://apps.gnome.org/Music/"
arch=(any)
license=(GPL-2.0-or-later)
depends=(
  dconf
  gdk-pixbuf2
  glib2
  graphene
  grilo
  grilo-plugins
  gst-plugins-base
  gst-plugins-base-libs
  gst-plugins-good
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
  libgirepository
  libmediaart
  libsoup3
  localsearch
  python
  python-cairo
  python-gobject
  tinysparql
)
makedepends=(
  appstream
  git
  gobject-introspection
  meson
  yelp-tools
)
optdepends=(
  'gst-libav: Extra media codecs'
  'gst-plugins-bad: Extra media codecs'
  'gst-plugins-ugly: Extra media codecs'
)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/gnome-music.git#tag=${pkgver/[a-z]/.&}")
b2sums=('929ca750f1aa8409e5e10c5dc22a140d32460da7c732c354a4d6041988fc911a92653422e733c391283835b7993c653368b81b8dcfcb820bb8118493358ff74e')

prepare() {
  cd $pkgname

  # build: Detect GIRepository version to use based on glib version
  # https://gitlab.archlinux.org/archlinux/packaging/packages/gnome-music/-/issues/2
  git cherry-pick -n 802adb184acc178db145e91a52a6097dc067b271
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
