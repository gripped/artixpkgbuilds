# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Tom Kuther <gimpel@sonnenkinder.org>

pkgname=snapper
pkgver=0.13.0
pkgrel=1
pkgdesc="A tool for managing BTRFS and LVM snapshots"
arch=('x86_64')
url="http://snapper.io"
license=('GPL-2.0-only')
depends=('btrfs-progs' 'libxml2' 'dbus' 'boost-libs' 'acl' 'json-c')
makedepends=('boost' 'lvm2' 'libxslt' 'docbook-xsl' 'pam' 'git')
optdepends=('pam: pam_snapper')
backup=(
  'etc/conf.d/snapper'
  'etc/cron.hourly/snapper'
  'etc/logrotate.d/snapper'
)
source=("git+https://github.com/openSUSE/snapper.git#tag=v${pkgver}"
        "conf-d.patch"
        "cron-rename.patch"
        "drift-file-path.patch"
        "usr-paths.patch")
sha256sums=('0bb7bd70c8811c848d176a99e197c0bede118d530017dc82c5465614e9afacd8'
            '527fc127eb0569eea9dcb888833e0b1f54302bfcb3a88f0101ae32837771bc61'
            '60884bfe5d0f2f53afb37fd2b2687059bb3346ca6c2a146b22d9bb9a14eada59'
            '093c7993e466a0cf9c0794a971825f5f1b40047512857bc124ed0d63dbb306d6'
            'd7ace894d7a59fb70c45c9314b8be1f89c730e1ddd2d6e28d24c5ea08ad2e7bf')

prepare() {
  cd $pkgname
  patch -p1 -i "$srcdir/conf-d.patch"
  patch -p1 -i "$srcdir/cron-rename.patch"
  patch -p1 -i "$srcdir/drift-file-path.patch"
  patch -p1 -i "$srcdir/usr-paths.patch"
  autoreconf -fiv
}

build() {
  cd $pkgname
  ./configure --prefix=/usr \
              --sbindir=/usr/bin \
              --with-conf=/etc/conf.d \
              --with-pam-security=/usr/lib/security \
              --disable-zypp \
              --disable-silent-rules \
              --disable-systemd
  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
  install -Dm644 data/sysconfig.snapper \
    "$pkgdir"/etc/conf.d/snapper

  rm -rf "$pkgdir/usr/lib/snapper/testsuite/"

  rm -rf "$pkgdir"/usr/lib/snapper/systemd-helper
}
