# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Torsten Keßler <tpkessler at archlinux dot org>
# Contributor: Konstantin Gizdov <arch at kge dot pw>

pkgname=intel-oneapi-compiler-shared
_pkgver=2025.0
pkgver=2025.0.4
_debpkgrel=1519
pkgrel=1
pkgdesc='Intel oneAPI compiler runtime libraries'
arch=('x86_64')
url='https://software.intel.com/content/www/us/en/develop/tools/oneapi.html'
license=("LicenseRef-Intel-EULA-Developer-Tools")
source=("https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb")
sha256sums=('8c33df8f47f9e7f31b9ff76cd290e9ca3cf7942468235e361f92227a459b1595')
depends=('intel-oneapi-compiler-shared-common' 'intel-oneapi-compiler-shared-runtime'
         'intel-oneapi-dpcpp-debugger' 'intel-oneapi-common' 'intel-oneapi-compiler-shared-runtime-libs'
         'level-zero-loader' 'glibc' 'gcc-libs' 'zlib')
provides=("${pkgname}-common")
conflicts=('intel-oneapi-basekit')

noextract=(
    "${pkgname}-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
)
_lib_path='/opt/intel/oneapi/compiler'

package() {
    cd "${srcdir}"
    ar x "${srcdir}/${pkgname}-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
    tar xvf data.tar.xz -C "${pkgdir}"
    rm data.tar.xz

    find "${pkgdir}" -type f -iname "*.a" -exec chmod 644 '{}' \;

    install -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
    ln -s /usr/share/licenses/intel-oneapi "${pkgdir}"/usr/share/licenses/"${pkgname}"/oneapi
}
