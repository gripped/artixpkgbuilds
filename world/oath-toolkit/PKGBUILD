# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: SÃ©bastien Luttringer
# Contributor: L42y <423300@gmail.com>

pkgname=oath-toolkit
pkgver=2.6.14
pkgrel=1
pkgdesc='OATH one-time password toolkit'
arch=('x86_64')
url='https://oath-toolkit.codeberg.page/'
license=('GPL-3.0-or-later')
depends=('glibc' 'pam' 'xmlsec')
provides=('liboath.so' 'libpskc.so')
validpgpkeys=('B1D2BD1375BECB784CF4F8C4D73CF638C53C06BE') # Simon Josefsson <simon@josefsson.org>
source=("https://codeberg.org/${pkgname}/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha256sums=('8b1da365759f1249be57a82aec6e107f7b57dc77d813f96dc0aaf81624f28971'
            'SKIP')

build() {
  cd "${pkgname}-${pkgver}"

  ./configure \
    --prefix=/usr
  make
}

check() {
  cd "${pkgname}-${pkgver}"

  make check
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # Install basic docs too
  install -D -m0644 pam_oath/README "${pkgdir}/usr/share/doc/${pkgname}/PAM_README"
  install -D -m0644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
