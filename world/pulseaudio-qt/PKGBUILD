# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor:Antonio Rojas <arojas@archlinux.org>

pkgname=pulseaudio-qt
pkgver=1.9.0
pkgrel=1
pkgdesc='Qt bindings for libpulse'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL-2.1-only
         LGPL-3.0-only)
depends=(glibc
         libpulse
         libstdc++
         qt6-base)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('c2aace3ac1a9c8c9b9c57ba0ffc99a3d310737cc004db0491802e03536e27c69'
            'SKIP')
validpgpkeys=(2D1D5B0588357787DE9EE225EC94D18F7F05997E  # Jonathan Riddell <jr@jriddell.org>
              90A968ACA84537CC27B99EAF2C8DF587A6D4AAC1) # Nicolas Fella <nicolas.fella@kdab.com>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
