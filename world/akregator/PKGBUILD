# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=akregator
pkgver=26.04.3
pkgrel=1
pkgdesc='A Feed Reader by KDE'
arch=(x86_64)
url='https://apps.kde.org/akregator/'
license=(GPL-2.0-or-later)
depends=(glibc
         grantleetheme
         kcmutils
         kcodecs
         kcolorscheme
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         ki18n
         kiconthemes
         kio
         knotifications
         knotifyconfig
         kontactinterface
         kparts
         kstatusnotifieritem
         ktextaddons
         ktexttemplate
         kuserfeedback
         kwidgetsaddons
         kxmlgui
         libgcc
         libkdepim
         libstdc++
         messagelib
         pimcommon
         qt6-base
         qt6-webengine
         syndication)
makedepends=(extra-cmake-modules
             kdoctools)
optdepends=('kdepim-addons: additional plugins')
groups=(kde-applications
        kde-pim)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('c59d7d6899e34b1a9c58089cd139838a7be22e358ff036532c8700871c182910'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
