# Maintainer: Jiachen YANG <farseerfc@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Gavin Lloyd <gavinhungry@gmail.com>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Hexchain Tong <i@hexchain.org>

pkgname=ttf-opensans
pkgver=3.003
_commit=bd7e37632246368c60fdcbd374dbf9bad11969b6
pkgrel=1
pkgdesc='Sans-serif typeface commissioned by Google'
url='https://github.com/googlefonts/opensans'
arch=('any')
license=('OFL-1.1')
source=("$pkgname-$_commit.tar.gz::$url/archive/$_commit.tar.gz")
sha256sums=('a1b16d859522daa826fb093d791ee252a1627274ef1b90f2773d670eb73a2a92')

pkgver() {
  cd opensans-$_commit
  sed -n 's/^[[:space:]]*<string>Version \([0-9.]*\)<\/string>/\1/p' \
    sources/OpenSans-BoldItalic.ufo/fontinfo.plist
}

package() {
  cd opensans-$_commit
  install -vDm644 -t "$pkgdir/usr/share/fonts/TTF" fonts/ttf/*.ttf
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" OFL.txt
}
