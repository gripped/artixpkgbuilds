# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Joaquin <joaquingc123 at gmail dot com>
# Contributor: Dies <JerryCasiano(at)gmail(dot)com>
# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>
# Contributor: Guan 'kuno' Qing <neokuno(at)gmail(dot)com>
# Contributor: Guten Ye <ywzhaifei(at)gmail(dot)com>
# Contributor: i_magnific0 <i_magnific0(at)yahoo(dot)com>
# Contributor: Pavlo <pavlofreemen(at)gmail(dot)com>

pkgname=font-manager
pkgver=0.9.1
pkgrel=2.1
pkgdesc='A simple font management application for GTK+ Desktop Environments'
url=https://fontmanager.github.io
arch=(x86_64 i686)
license=(GPL-3.0-only)
depends=(gtk4
         json-glib
         libsoup3
         libxml2
         sqlite
         webkitgtk-6.0)
optdepends=(file-roller
            libnautilus-extension)
makedepends=(gobject-introspection
             intltool
             meson
             nautilus
             ninja
             vala
             yelp-tools)
_archive="$pkgname-$pkgver"
_url="https://github.com/FontManager/font-manager"
source=("$_url/releases/download/$pkgver/$_archive.tar.xz")
sha256sums=('a83c294322cabaa424b35efa8a20a28f04d379dcf7701ee860a29725e50e5e72')

# fix for Wayfire WM
prepare() { 
    cd "${pkgname}-${pkgver}"/data
    sed -i 's/DBusActivatable=true/DBusActivatable=false/' com.github.FontManager.FontManager.desktop.in.in
    sed -i 's/DBusActivatable=true/DBusActivatable=false/' com.github.FontManager.FontViewer.desktop.in.in
}

build() {
	artix-meson "$_archive" build \
		-Dnautilus=true \
		-Dreproducible=true 
		ninja -C build
}

package() {
	DESTDIR="$pkgdir" ninja -C build install
}
