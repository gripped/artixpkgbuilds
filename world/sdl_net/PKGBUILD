# Maintainer: Bert Peters <bertptrs@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=sdl_net
pkgver=1.2.8
pkgrel=7
pkgdesc="A small sample cross-platform networking library"
url="https://github.com/libsdl-org/SDL_net"
arch=(x86_64)
license=(LicenseRef-SDL)
depends=(sdl)
source=(https://www.libsdl.org/projects/SDL_net/release/SDL_net-$pkgver.tar.gz)
sha256sums=('5f4a7a8bb884f793c278ac3f3713be41980c5eedccecff0260411347714facb4')

prepare() {
  cd SDL_net-$pkgver
  # --foreign is needed, otherwise automake fails due to the files AUTHORS,
  # ChangeLog and NEWS not being present.
  export AUTOMAKE='automake --foreign'
  autoreconf --force --install
}

build() {
  cd SDL_net-$pkgver
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd SDL_net-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
