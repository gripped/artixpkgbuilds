# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-engineio
pkgver=4.13.0
pkgrel=1
pkgdesc='Python implementation of the Engine.IO realtime server'
url='https://github.com/miguelgrinberg/python-engineio'
arch=(any)
license=(MIT)
depends=(
  python
  python-requests
  python-simple-websocket
  python-websocket-client
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-sphinx
  python-wheel
)
checkdepends=(
  python-aiohttp
  python-eventlet
  python-gevent
  python-gevent-websocket
  python-pytest
  python-pytest-asyncio
  python-tornado
)
optdepends=(
  'python-aiohttp: aiohttp driver'
  'python-eventlet: eventlet driver'
  'python-gevent-websocket: gevent driver'
  'python-gevent: gevent driver'
  'python-tornado: tornado driver'
)
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('1d0eb283b2c395a20daa78b576891a05a926f2c14fa43a276917dd0c858b5a02b4852adcb8a82f4c540b80645697757e1381d4118088d17a158dfe13912dea73')
b2sums=('9b8056aa638aea0c4c65d4f796e3c354f44215c62c6d367fe18697acf726eef5e08a50e71417e645bf8938b27a19304c057a1bf72a714442a2c2f543af92016e')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
  PYTHONPATH="$PWD/src" make -j1 -C docs man text SPHINXBUILD=sphinx-build
}

check() {
  cd $pkgname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  # Deselect test failing with the following error, not sure why:
  #   tests/common/test_server.py:128: AssertionError
  test-env/bin/python -m pytest \
    --deselect tests/common/test_server.py::TestServer::test_async_mode_eventlet
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md docs/_build/text/*.txt
  install -vDm 644 -t "$pkgdir/usr/share/man/man1" docs/_build/man/python-engineio.1
  install -vDm 644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim: ts=2 sw=2 et:
