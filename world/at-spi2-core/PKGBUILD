# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=at-spi2-core
pkgname=(
  at-spi2-core
  at-spi2-core-docs
)
pkgver=2.56.3
pkgrel=1
pkgdesc="Protocol definitions and daemon for D-Bus at-spi"
url="https://gitlab.gnome.org/GNOME/at-spi2-core"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  bash
  dbus
  gcc-libs
  glib2
  glibc
  gsettings-desktop-schemas
  libx11
  libxi
  libxtst
  xorg-xprop
)
makedepends=(
  dbus-broker
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  libxml2
  meson
  python-sphinx
)
checkdepends=(
  at-spi2-core
)
source=("git+https://gitlab.gnome.org/GNOME/at-spi2-core.git?signed#tag=$pkgver")
b2sums=('002cbbc609993e3c1e364baed3b3e56137d4871e59411bdf24f7eb18d12f44ff762dbe9045b0d5ca91b0cfadc154e4b2df921a28caabebc28e312fe552c04006')
validpgpkeys=(
  276BFDAFB36212F8DB095E2140823A056FD6D4B3 # Mike Gorse <mgorse@suse.com>
)

prepare() {
  cd at-spi2-core
}

build() {
  local meson_options=(
    -D use_systemd=false
    -D default_bus=dbus-daemon
    -D docs=true
  )

  artix-meson at-spi2-core build "${meson_options[@]}"
  meson compile -C build
}

check() {
  dbus-run-session meson test -C build --print-errorlogs
}

package_at-spi2-core() {
  provides=(
    "at-spi2-atk=$pkgver-$pkgrel"
    "atk=$pkgver-$pkgrel"
    libatk-1.0.so
    libatk-bridge-2.0.so
    libatspi.so
  )
  conflicts=(
    'at-spi2-atk<=2.38.0-2'
    'atk<=2.38.0-2'
  )
  replaces=(
    'at-spi2-atk<=2.38.0-2'
    'atk<=2.38.0-2'
  )

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc

  rm -r $pkgdir/usr/lib/systemd
}

package_at-spi2-core-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
