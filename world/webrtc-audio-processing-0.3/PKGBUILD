# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=webrtc-audio-processing-0.3
pkgver=0.3.1
pkgrel=6
pkgdesc="AudioProcessing library based on Google's implementation of WebRTC (legacy)"
url="https://freedesktop.org/software/pulseaudio/webrtc-audio-processing/"
arch=(x86_64)
license=(BSD-3-Clause)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
)
provides=(
  libwebrtc_audio_processing.so
)
replaces=(
  'webrtc-audio-processing<=0.3.1-5'
)
source=("git+https://gitlab.freedesktop.org/pulseaudio/webrtc-audio-processing.git?signed#tag=v$pkgver")
b2sums=('5ebffa25cd06b1ebefb84d05e7698b0f1cd1f25355480953742bd95b13bb6348e6f0fa2aa019ce5017783eec6919debf7a98a5425bfa6e403dc8c0a4c5773b98')
validpgpkeys=(
  76EFEDBD2EEF4A59DAF0EC53B7A1A30FB2FFCBF3 # Arun Raghavan <arun@arunraghavan.net>
)

prepare() {
  cd webrtc-audio-processing
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  local configure_options=(
    --prefix /usr
    --disable-static
  )

  cd webrtc-audio-processing
  ./configure "${configure_options[@]}"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  make -C webrtc-audio-processing check
}

package() {
  make -C webrtc-audio-processing install DESTDIR="$pkgdir"
  install -Dm644 webrtc-audio-processing/COPYING \
    -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 sts=-1 et:
