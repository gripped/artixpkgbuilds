# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: kastor <kastor@fobos.org.ar>

_name=pupnp
pkgname=libupnp
pkgver=1.14.22
pkgrel=1
pkgdesc='Portable Open Source UPnP Development Kit'
arch=(x86_64)
url="https://pupnp.sourceforge.io/"
license=(BSD-3-Clause)
depends=(glibc)
makedepends=(cmake)
provides=(
  libixml.so
  libupnp.so
)
source=(https://github.com/$_name/$_name/archive/release-$pkgver/$_name-release-$pkgver.tar.gz)
sha512sums=('bcde34311a4fd788d5e232d5852a53927ba2f447acf4f219253e425891c0c266b5b7bde8e6982687169a6932715fd88098329e084cd16fe700fac1679561f834')
b2sums=('11d75ac9b6c7c4b25152c29e4b994ffb3fe021d686c48db538df4237d047c632a04dfad35eb019551d8a4245437c0b4d08f7a8512bbe15bafbf4841a7d8d1f9a')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D UPNP_BUILD_STATIC=OFF
    -D blocking_tcp_connections=OFF
    -D reuseaddr=ON
    -S $_name-release-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $_name-release-$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 $_name-release-$pkgver/{ChangeLog,README.md} -t "$pkgdir/usr/share/doc/$pkgname"
# Fix broken cmake files
  sed -e 's|/COMPONENT||g' -e 's|/UPNP_Development||g' -i "$pkgdir"/usr/lib/cmake/*/*.cmake
}

# vim: ts=2 sw=2 et:
