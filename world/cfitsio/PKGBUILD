# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=cfitsio
pkgver=4.6.0
pkgrel=4
epoch=1
pkgdesc='A library of C and Fortran subroutines for reading and writing data files in FITS (Flexible Image Transport System) data format'
arch=(x86_64)
url='https://heasarc.gsfc.nasa.gov/fitsio/'
license=(LicenseRef-cfitsio)
depends=(curl
         glibc
         zlib)
makedepends=(cmake
             git)
source=(git+https://github.com/HEASARC/cfitsio#tag=cfitsio-$pkgver)
sha256sums=('41d79d1cabc3ae860ad3ecf25fc22ec1d6595f3618d1af6934fad7996cdddf84')

prepare() {
# Fix dirs in pc file https://github.com/HEASARC/cfitsio/issues/38
  sed -e 's|@LIB_DESTINATION@|/usr/lib|' -e 's|@INCLUDE_INSTALL_DIR@|/usr/include|' -i $pkgname/cfitsio.pc.cmake
}

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_PTHREADS=ON \
    -DTESTS=OFF \
    -DUTILS=ON
  cmake --build build
}

check() {
  cd build
  ctest
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -D -m644 $pkgname/licenses/* "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

# Rename generic binaries
  for _bin in imcopy smem speed; do
    mv "$pkgdir"/usr/bin/{,cfitsio-}$_bin
  done
}
