# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Steve Holmes <steve.holmes88@gmail.com>
# Contributor: Giulio Bai <giulio@hewle.com>

pkgname=accerciser
pkgver=3.46.2
pkgrel=1
pkgdesc="Interactive Python accessibility explorer for the GNOME desktop"
url="https://wiki.gnome.org/Apps/Accerciser"
arch=(any)
license=(BSD-3-Clause)
depends=(
  at-spi2-core
  dconf
  gdk-pixbuf2
  glib2
  gobject-introspection-runtime
  gtk3
  hicolor-icon-theme
  ipython
  librsvg
  libwnck3
  pango
  python
  python-atspi
  python-cairo
  python-dbus
  python-gobject
  python-setuptools
  python-traitlets
  python-xdg
)
makedepends=(
  appstream
  git
  meson
  yelp-tools
)
groups=(gnome-extra)
source=("git+https://gitlab.gnome.org/GNOME/accerciser.git#tag=$pkgver")
b2sums=('87cbc2086e9cb37f786d79abd7d95f80b5f8e27c2ccae93a2e58ab950fdcf41e9f936f9269825717cf78302dc1facab0a952017aa9c4067ef9c0e18456b0411d')

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname/COPYING
}

# vim:set sw=2 sts=-1 et:
