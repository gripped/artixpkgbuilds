# Maintainer:
pkgname=python-tpm2-pytss
_name=${pkgname#python-}
pkgver=2.3.0
pkgrel=4
pkgdesc='Python bindings for tpm2-tss'
arch=(x86_64)
url='https://github.com/tpm2-software/tpm2-pytss'
license=(BSD-2-Clause)
depends=(
  glibc
  python
  python-asn1crypto
  python-cffi
  python-cryptography
  python-packaging
  python-yaml
)
makedepends=(
  git
  python-build
  python-installer
  python-pkgconfig
  python-setuptools
  python-setuptools-scm
  python-wheel
  tpm2-tss
)
checkdepends=(
  python-pytest
  swtpm
  tpm2-tools
)
source=(
  "git+$url.git#tag=$pkgver"
  "adjust-tpm-pt-revision-to-fix-test.patch"
)
sha512sums=('ca6013685b5f0e251a691086a48476f87fde6a74166bd499a12105d1bce8648b2b35e4a3c0f49f894b5b5a09b60ecc771b709c75a5bb806d20f4676911f13281'
            '5be9f4944b080910ef082feb73f408ab3b3443a5aa7a84544016e20cbc5b3ee07276e770005d75d589a4177c34289996ef4d10757e5299af180d004846b49fec')

prepare() {
  cd $_name
  git cherry-pick -n 55d28b259f1a68f60c937ea8be7815685d32757f 61d00b4dcca131b3f03f674ceabf4260bdbd6a61 # Fix build with GCC 15
  git cherry-pick -n 6ab4c74e6fb3da7cd38e97c1f8e92532312f8439 # Fix tests with cyptography 45
  patch -Np1 -i ../adjust-tpm-pt-revision-to-fix-test.patch
}

build() {
  cd $_name
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
  )

  cd $_name
  local _python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-$_python_version" pytest "${pytest_options[@]}"
}

package() {
  depends+=(
    tpm2-tss libtss2-esys.so libtss2-fapi.so libtss2-mu.so libtss2-rc.so libtss2-tctildr.so
  )

  cd $_name
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
 
