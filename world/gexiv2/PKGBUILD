# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=gexiv2
pkgname=(
  gexiv2
  gexiv2-docs
)
pkgver=50.0
pkgver=0.16.1
pkgrel=1
pkgdesc='GObject-based wrapper around the Exiv2 library'
url='https://gitlab.gnome.org/GNOME/gexiv2'
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  exiv2
  glib2
  glibc
  libgcc
  libstdc++
)
makedepends=(
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  meson
  vala
)
source=("git+$url.git?signed#tag=$pkgver")
b2sums=('7e04030f4e122be52154c09fafa8c251e5bbab80a4c877273304bd253f3288537738b9a91d69412888e250b6ba12ad0eb42c269048e891755e0896b69c06a6cb')
validpgpkeys=(AC9CD4E32D7C7F6357BA8ADD10F6E970175D29E1) # Jens Georg <mail@jensge.org>

build() {
  local meson_options=(
    -D gtk_doc=true
    -D tests=true
    # Keep python binding disabled for now, it conflicts with 'libexiv2'
    ## https://gitlab.gnome.org/GNOME/gexiv2/-/issues/88
    -D python3=false
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_gexiv2() {
  depends+=(
    libg{lib,object,io}-2.0.so
    libexiv2.so
  )
  provides+=(libgexiv2-0.16.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_gexiv2-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}
