# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=alligator
pkgver=25.12.1
pkgrel=1
pkgdesc='Kirigami-based RSS reader'
url='https://apps.kde.org/alligator/'
arch=(x86_64)
license=(GPL-2.0-or-later
         LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kcolorscheme
         kconfig
         kcoreaddons
         ki18n
         kirigami
         kirigami-addons
         qqc2-desktop-style
         qt6-base
         qt6-declarative
         syndication)
makedepends=(extra-cmake-modules
             python)
groups=(kde-applications
        kde-network)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('2158ac2625b13efc43f06aa46aad2a75e9c4a92e416d825018fc9b8059dda35f'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
