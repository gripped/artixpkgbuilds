# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux,org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=krfb
pkgver=25.12.1
pkgrel=1
pkgdesc='Desktop Sharing'
url='https://apps.kde.org/krfb/'
arch=(x86_64)
license=(LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         kdbusaddons
         kdnssd
         ki18n
         knotifications
         kpipewire
         kstatusnotifieritem
         kwallet
         kwayland
         kwidgetsaddons
         kwindowsystem
         kxmlgui
         libvncserver
         libx11
         libxcb
         libxtst
         qt6-base
         wayland
         xcb-util-image
         xdg-desktop-portal)
makedepends=(extra-cmake-modules
             kdoctools
             libxdamage
             plasma-wayland-protocols)
groups=(kde-applications
        kde-network)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('e6a43161fda9f0a5cf1a750fbb90866049aa198a9e0c3987added54009f28969'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
