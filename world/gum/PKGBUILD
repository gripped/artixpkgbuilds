# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Christian Heusel <gromit@archlinux.org>

pkgname=gum
pkgver=2.0.2
pkgrel=1
pkgdesc='A tool for glamorous shell scripts'
arch=(x86_64)
url='https://github.com/charmbracelet/gum'
license=(MIT)
depends=(glibc)
makedepends=(git go)
options=(!lto)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('e996e12d2474a1a092ac7a8af98436c1c39f2fc3775eb7ba41af080c6622d7d811d5f9aba214c89f4e19227c883f3969d4c1e058f4855d6b20a50a4d5b59d3f1')
b2sums=('c5ca5ca5e98fc3ca5fd8c52fe346639fd740765c251691e2b184fc165b0fe0f9e8aa1f998eefc8e2d7bbd378f94ba887739fd5e9c01c74268de1787ae299d86c')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  # commit date for binary & man page
  local _commit_date=$(git show --no-patch --format=%cd --date=format:%Y-%m-%d)

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags \"${LDFLAGS}\" \
    -X main.Version=$pkgver \
    -X main.CommitSHA=$(git rev-parse HEAD) \
    -X main.CommitDate=$_commit_date" \
    -o build \
    .

  # man page
  ./build/gum man > build/gum.1

  # i'm not 100% sure where the man sub-command gets the date from, i assume
  # it uses $TODAYS_DATE, so this should make the man page reproducible
  sed \
    -i build/gum.1 \
    -e "s/\"[0-9]\{4\}-[0-9]\{2\}-[0-9]\{2\}\"/\"$_commit_date\"/"

  # completions
  for shell in bash fish zsh; do
    ./build/gum completion "$shell" > "build/$shell-completion"
  done
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/gum

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # man pages
  install -vDm644 -t "$pkgdir/usr/share/man/man1" build/gum.1

  # completions
  install -vDm644 build/bash-completion "$pkgdir/usr/share/bash-completion/completions/gum"
  install -vDm644 build/fish-completion "$pkgdir/usr/share/fish/vendor_completions.d/gum.fish"
  install -vDm644 build/zsh-completion "$pkgdir/usr/share/zsh/site-functions/_gum"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
