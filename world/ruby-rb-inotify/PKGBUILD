# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=rb-inotify
pkgname=ruby-rb-inotify
pkgver=0.11.1
pkgrel=1
pkgdesc='Thorough inotify wrapper for Ruby using FFI'
url='https://github.com/guard/rb-inotify'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-ffi')
makedepends=('ruby-rdoc')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/guard/rb-inotify/archive/v${pkgver}.tar.gz)
sha256sums=('e3c2430715bd7966bf6c70b3cb31c77db059151a3218f382e08ac4371432cbd6')
sha512sums=('46966609686878d99c5a6623f5a3f55ef203ae32e48d0efbef295f97cc728c6938759360d445e052b82206322c7bf6f995db3007c7f3c0b2818549b8088be9bf')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
  sed 's|git ls-files|find -type f|' -i ${_gemname}.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 README.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
