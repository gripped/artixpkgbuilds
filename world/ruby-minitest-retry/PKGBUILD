# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Tim Meusel <tim@bastelfreak.de>

_gemname='minitest-retry'
pkgname="ruby-${_gemname}"
pkgver=0.2.3
pkgrel=1
pkgdesc='re-run the test when the test fails'
arch=('any')
url='https://github.com/y-yagi/minitest-retry'
license=('MIT')
depends=(
  ruby
  ruby-minitest
)
makedepends=(
  ruby-rake
  ruby-bundler
)
options=('!emptydirs')
source=(
  "${url}/archive/v${pkgver}/${_gemname}-v${pkgver}.tar.gz"
)
sha512sums=('830dfbfc920542e8f3e1dab6f50ed74c177b30de12b6a004e826d0da9f209547ec75a51414d4ee3d810260572ed8b75cc44cd7e9f7238a1b5dc105c435884567')
b2sums=('b006e6b0bb18432d67afae24d691349928cd985b21d8ff9fe8d2a40273fe1acdbcf93024ccb66acfe71b06692f91f4b834e5ebc39953d243608be8a2d6ad7dbf')

prepare() {
  cd "${srcdir}/${_gemname}-${pkgver}"

  # allow latest dependencies
  sed --in-place 's|`git ls-files -z`|`find . -print0`|' "${_gemname}.gemspec"

  # drop useless dependency
  sed --in-place '/bundler/d' Rakefile "${_gemname}.gemspec"
  sed --in-place '/pry/d' Gemfile test/test_helper.rb
}

build() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  gem build "${_gemname}.gemspec"
}

check() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  rake test
}

package() {
cd "${srcdir}/${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"

  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm -rf "${pkgdir}/${_gemdir}/cache"
 }
