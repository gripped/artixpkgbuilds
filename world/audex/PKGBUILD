# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: mightyjaym <jmambrosino@gmail.com>
# Contributor: moostik <mooostik_at_gmail.com>

pkgname=audex
pkgver=25.12.0
pkgrel=1
pkgdesc='Tool for ripping compact discs'
arch=(x86_64)
url='https://apps.kde.org/audex/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kcmutils
         kcolorscheme
         kcompletion
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         ki18n
         kiconthemes
         kio
         ktextwidgets
         kwidgetsaddons
         kxmlgui
         libcdio
         libcdio-paranoia
         libkcddb
         qt6-base
         solid)
makedepends=(extra-cmake-modules)
optdepends=('faac: MP4/M4A/AAC encoder'
	    'flac: FLAC encoder'
	    'lame: MP3 encoder'
            'vorbis-tools: OGG encoder')
groups=(kde-applications
        kde-multimedia)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('8a6215b85a315e1d5e48e7d87ad61aa5969d6c2b543a891426de534959ba7cb8'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
