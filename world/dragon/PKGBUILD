# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org> 
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=dragon
pkgver=25.12.1
pkgrel=1
pkgdesc='A multimedia player where the focus is on simplicity, instead of features'
arch=(x86_64)
url='https://apps.kde.org/dragonplayer/'
license=(GPL-2.0-or-later)
depends=(ffmpeg
         gcc-libs
         glibc
         kconfig
         kcoreaddons
         kcrash
         ki18n
         kio
         kirigami
         kservice
         kwindowsystem
         qt6-base
         qt6-declarative
         qt6-multimedia
         qt6-multimedia-ffmpeg)
makedepends=(extra-cmake-modules)
groups=(kde-applications
        kde-multimedia)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('f7f751a4b6cbf3f1b02afc215ecfea31d0cbeac4c7e8add6e50a0cbeafff0948'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() { 
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
