# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bernhard Landauer <oberon@manjaro.org>
# Contributor: Alexsandr Pavlov <kidoz at mail dot ru>

_gemname=hoe
pkgname=ruby-${_gemname}
pkgver=4.2.3
pkgrel=1
pkgdesc='Hoe is a rake/rubygems helper for project Rakefiles'
url='https://www.zenspider.com/projects/hoe.html'
arch=('any')
license=('MIT')
depends=(
  ruby
  ruby-rake
)
makedepends=(
  git
  ruby-minitest
  ruby-rdoc
)
options=('!emptydirs')
source=("git+https://github.com/seattlerb/hoe#tag=v${pkgver}")
sha512sums=('98d075464aee9c96d677880c71066460bf6e6a6c52501481301e1f1c6a290cff5fb57839882dfde8b984af791a712f1aa122fee780e405873a0e13d9d01aabe2')
b2sums=('ae2d828ca2dd554ad40ccaf68d2cd5b3f71c4e9e0e1cdca8f52c3d674ac6b426079bb23775662cb42e62a772f3627f99a59104a72bb7054f520c7de112d1cc58')

pkgver() {
  cd ${_gemname}

  grep 'VERSION = "' lib/hoe.rb | sed --regexp-extended 's|.*"(.+)"|\1|'
}

build() {
  cd ${_gemname}

  local _gemdir="$(gem env gemdir)"

  rake gem

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "pkg/${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd ${_gemname}

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rake test
}

package() {
  cd ${_gemname}

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 README.rdoc --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.rdoc --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
