# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-clocks
pkgver=49.0
pkgrel=1
pkgdesc="Clocks applications for GNOME"
url="https://apps.gnome.org/Clocks"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  dconf
  geoclue
  geocode-glib-2
  glib2
  glibc
  gnome-desktop-4
  gsettings-desktop-schemas
  gtk4
  hicolor-icon-theme
  libadwaita
  libgweather-4
)
makedepends=(
  git
  gobject-introspection
  meson
  vala
  yelp-tools
)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/gnome-clocks.git?signed#tag=${pkgver/[a-z]/.&}")
b2sums=('50f7488d5a16a26710f59e79664e05e3eab7a68e98fb1c2212f223453ecc5f9d7ea55afb34e25a95025b1cd66b2ec9cc97b632852bc9e030ba2ecbc4c6c09b2c')
validpgpkeys=(
  3475CBA8D3483594C889B470D64A8D747F6FE706 # Maximiliano Sandoval <msandova@gnome.org>
)

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
