# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-clocks
pkgver=51.0
pkgrel=2
pkgdesc="Clocks applications for GNOME"
url="https://apps.gnome.org/Clocks"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  dconf
  geoclue
  geocode-glib-2
  glib2
  glibc
  gnome-desktop-4
  gsettings-desktop-schemas
  gtk4
  hicolor-icon-theme
  icu
  libadwaita
  libgcc
  libgweather-4
)
makedepends=(
  git
  gobject-introspection
  meson
  vala
  vorbis-tools
  yelp-tools
)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/gnome-clocks.git?signed#tag=${pkgver/[a-z]/.&}")
b2sums=('7acee23d02fc4eb0c7a0d8a5be66facba03a1f1f2fc7730c1a6c021224e4a90f5e0c3df167d91356a711b705a150c5638be9c0dbbb1133e277b19534c07b1448')
validpgpkeys=(
  3475CBA8D3483594C889B470D64A8D747F6FE706 # Maximiliano Sandoval <msandova@gnome.org>
)

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
