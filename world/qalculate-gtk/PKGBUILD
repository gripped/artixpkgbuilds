# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Eric Bélanger <eric@archlinux.org>

pkgname=qalculate-gtk
pkgver=5.11.0
pkgrel=1
pkgdesc='GTK frontend for libqalculate'
arch=(x86_64)
url='https://qalculate.github.io/'
license=(GPL-2.0-only)
depends=(cairo
         gdk-pixbuf2
         glib2
         glibc
         gtk3
         libgcc
         libqalculate
         libstdc++
         pango)
makedepends=(intltool
             git
             glib2-devel)
source=(git+https://github.com/Qalculate/qalculate-gtk#tag=v$pkgver)
sha256sums=('7d2c9329ccff66d0f48d5a81dc963a05a27e1d8b9fb40bb96bf3acc50283256d')

prepare() {
  cd $pkgname
  autoreconf -vif -I /usr/share/gettext/m4
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
