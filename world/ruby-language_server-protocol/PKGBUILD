# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='language_server-protocol'
pkgname="ruby-${_gemname}"
pkgver=3.17.0.5
pkgrel=1
pkgdesc='A Language Server Protocol SDK'
arch=('any')
url="https://github.com/mtsmfm/${_gemname}-ruby"
license=('MIT')
depends=(
  ruby
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-activesupport
  ruby-benchmark-ips
  ruby-bundler
  ruby-m
  ruby-minitest
  ruby-minitest-power_assert
  ruby-pry-byebug
  ruby-rake
)
options=('!emptydirs')
source=("git+${url}#tag=v${pkgver}")
sha512sums=('a3ff433000358e0a901154ee05522b20cb0bd0d256b305a2fe195b13a9826f389bf8eb45d12a402626d31a2df7917689e62fe8b0393dd7b46047adc71e0ef021')
b2sums=('636554d9cbace543efc49b56b6d68be2e69e560b7c1e3c97d94d53b02b2279376f29e3d66999f113a73a491bb2061785ad53c99bce783af030249fafc6fefd5e')

prepare() {
  cd "${_gemname}-ruby"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"

  sed --in-place --regexp-extended \
    --expression '/steep/d' \
    Gemfile

  rm --verbose Steepfile
}

build() {
  cd "${_gemname}-ruby"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unreproducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${_gemname}-ruby"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rake test
}

package() {
  cd "${_gemname}-ruby"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
