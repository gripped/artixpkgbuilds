# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=onboard
pkgver=1.4.1
pkgrel=11
pkgdesc='On-screen keyboard useful on tablet PCs or for mobility impaired users'
arch=('x86_64')
url='https://launchpad.net/onboard'
license=('GPL')
depends=('gtk3' 'hunspell' 'iso-codes' 'libcanberra' 'libxkbfile' 'python-cairo' 'python-dbus' 'python-gobject' 'udev')
makedepends=('python-distutils-extra')
optdepends=('mousetweaks: hover click with mouse')
source=("https://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz"{,.asc}
        '0001-python-3-12.patch'
        '0002-thread-state.patch')
validpgpkeys=('BCFE9B5A71A0B0F860521CF963087078B67DCFED') # Francesco Fumanti
sha256sums=('01cae1ac5b1ef1ab985bd2d2d79ded6fc99ee04b1535cc1bb191e43a231a3865'
            'SKIP'
            '8d664731749a4dfb66fc49513c4a9ae2133ce2a8f9a5ad4c0eb03152be060e2f'
            'c074e7209b447e28f2344fa1f24246a5e9935f2875886cd0219562e637914a19')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 < ../0001-python-3-12.patch
  patch -Np1 < ../0002-thread-state.patch
}

build() {
  cd $pkgname-$pkgver
  python3 setup.py build
}

package() {
  cd $pkgname-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
}
