# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-sudoku
pkgver=46.2
pkgrel=1
pkgdesc="Test your logic skills in this number grid puzzle"
url="https://wiki.gnome.org/Apps/Sudoku"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  gcc-libs
  glib2
  glibc
  graphene
  gtk4
  hicolor-icon-theme
  json-glib
  libadwaita
  libgee
  pango
  qqwing
)
makedepends=(
  git
  meson
  vala
  yelp-tools
)
groups=(gnome-extra)
source=("git+https://gitlab.gnome.org/GNOME/gnome-sudoku.git#tag=${pkgver/[a-z]/.&}")
b2sums=('18000f03e020bb8af643969ef6a5ebb16c8d0b4f16d7c365add2762becc9e8711e846fa91a4ebf23207a9c31cceb29d4f7067ce1ee376b7d98c3068a9a732219')

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
