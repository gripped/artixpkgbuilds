# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-sudoku
pkgver=50.3
pkgrel=1
pkgdesc="Test your logic skills in this number grid puzzle"
url="https://gitlab.gnome.org/GNOME/gnome-sudoku/-/wikis/home"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  json-glib
  libadwaita
  libgcc
  libgee
  libstdc++
  pango
  qqwing
)
makedepends=(
  blueprint-compiler
  git
  meson
  vala
  yelp-tools
)
groups=(gnome-extra)
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-sudoku.git#tag=${pkgver/[a-z]/.&}"
)
b2sums=('c57b2336c418237095be365a47d90681d6c7d6564d3b89f8ac082bfeb846c0e11059677b394545a731f99712bf3b1e2a62bc37bcdb6458f1b04d2b25feb20459')

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
