# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-sudoku
pkgver=47.0
pkgrel=1
pkgdesc="Test your logic skills in this number grid puzzle"
url="https://wiki.gnome.org/Apps/Sudoku"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  gcc-libs
  glib2
  glibc
  graphene
  gtk4
  hicolor-icon-theme
  json-glib
  libadwaita
  libgee
  pango
  qqwing
)
makedepends=(
  git
  meson
  vala
  yelp-tools
)
groups=(gnome-extra)
source=("git+https://gitlab.gnome.org/GNOME/gnome-sudoku.git#tag=${pkgver/[a-z]/.&}")
b2sums=('0b35a13270eab1f2c8eba3a08d6836ec6a6f5f2f5be23f9babc03c6afe643a8da466d9f7a2f17eebb919013824e95534e1faa607c3fd0cfe7abfded7f8a5644b')

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
