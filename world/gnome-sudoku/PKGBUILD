# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-sudoku
pkgver=49.3
pkgrel=1
pkgdesc="Test your logic skills in this number grid puzzle"
url="https://gitlab.gnome.org/GNOME/gnome-sudoku/-/wikis/home"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  json-glib
  libadwaita
  libgee
  pango
  qqwing
)
makedepends=(
  blueprint-compiler
  git
  meson
  vala
  yelp-tools
)
groups=(gnome-extra)
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-sudoku.git#tag=${pkgver/[a-z]/.&}"
)
b2sums=('6b3ea1507aba4b046a31966d0ef5f8103d1ffa2670f2db000ad1f398e98838875af69c8889410407871deae0fecf848595230d33891c53f6104b831300584399')

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
