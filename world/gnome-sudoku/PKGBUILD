# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-sudoku
pkgver=45.5
pkgrel=1
pkgdesc="Test your logic skills in this number grid puzzle"
url="https://wiki.gnome.org/Apps/Sudoku"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  gtk4
  json-glib
  libadwaita
  libgee
  qqwing
)
makedepends=(
  git
  meson
  vala
  yelp-tools
)
groups=(gnome-extra)
_commit=76f4c95139d0fa8d92ecb4a8cd27416eb7140355  # tags/45.5^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-sudoku.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
