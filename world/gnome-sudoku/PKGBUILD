# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-sudoku
pkgver=50.0
pkgrel=1
pkgdesc="Test your logic skills in this number grid puzzle"
url="https://gitlab.gnome.org/GNOME/gnome-sudoku/-/wikis/home"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  json-glib
  libadwaita
  libgcc
  libgee
  libstdc++
  pango
  qqwing
)
makedepends=(
  blueprint-compiler
  git
  meson
  vala
  yelp-tools
)
groups=(gnome-extra)
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-sudoku.git#tag=${pkgver/[a-z]/.&}"
)
b2sums=('984f2660330e2d29c9ef5e91db73940d790bf0867d56bc4cb497c0a310c1c0b3b5bfccd05b6d5853b39d22ebe5bbed98589829d8ca5a27e35a2c10b3ce3bf0e2')

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
