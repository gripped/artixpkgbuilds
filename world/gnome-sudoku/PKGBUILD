# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-sudoku
pkgver=49.1
pkgrel=1
pkgdesc="Test your logic skills in this number grid puzzle"
url="https://gitlab.gnome.org/GNOME/gnome-sudoku/-/wikis/home"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  json-glib
  libadwaita
  libgee
  pango
  qqwing
)
makedepends=(
  blueprint-compiler
  git
  meson
  vala
  yelp-tools
)
groups=(gnome-extra)
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-sudoku.git#tag=${pkgver/[a-z]/.&}"
)
b2sums=('c53b7414beee4ce95f7cff05d32311de0c7aa1f7224e2cb10e77ff5662a53ba224acc065140c2baa782d4ea3fcbcbccb2c8c6847288478b4e3595f5ef1595848')

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
