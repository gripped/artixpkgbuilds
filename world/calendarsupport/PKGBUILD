# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=calendarsupport
pkgver=26.08.0
pkgrel=1
pkgdesc='Calendar support library'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL-2.0-or-later)
depends=(akonadi
         akonadi-calendar
         glibc
         kcalendarcore
         kcalutils
         kcodecs
         kconfig
         kcoreaddons
         kguiaddons
         kholidays
         ki18n
         kidentitymanagement
         kio
         kmime
         kservice
         kwidgetsaddons
         libgcc
         libstdc++
         qt6-base)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('8e6843bcc9965cc0658db46780d2f1486c84aa64308a1e0cefe830d9d852f57d'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
