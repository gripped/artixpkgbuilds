# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=hyprlock
pkgver=0.9.6
pkgrel=1
pkgdesc='hyprland’s GPU-accelerated screen locking utility'
arch=(x86_64 aarch64)
url="https://github.com/hyprwm/$pkgname"
license=(BSD-3-Clause)
depends=(cairo # libcairo.so
         libgcc # libgcc_s.so
         'libstdc++' # libstdc++.so
         glib2 libgobject-2.0.so libglib-2.0.so
         glibc # libc.so libm.so
         hyprgraphics libhyprgraphics.so
         hyprlang libhyprlang.so
         hyprutils libhyprutils.so
         hyprwayland-scanner
         libdrm # libdrm.so
         libglvnd libOpenGL.so libEGL.so
         libxkbcommon libxkbcommon.so
         mesa # libgbm.so
         pam libpam.so
         pango libpango-1.0.so libpangocairo-1.0.so
         sdbus-cpp libsdbus-c++.so
         wayland libwayland-client.so libwayland-egl.so
         wayland-protocols)
makedepends=(cmake)
backup=("etc/pam.d/$pkgname")
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('7f653b87765bcfff7c1f8e04af326a7e73bbcacd1fb7d4436f6a16624a2247bb')

build() {
	cd "$_archive"
	cmake -B build \
		-D CMAKE_INSTALL_PREFIX=/usr \
		-D CMAKE_BUILD_TYPE=Release
	cmake --build build
}

package() {
	cd "$_archive"
	DESTDIR="$pkgdir" cmake --install build
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
