# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=rouge
pkgname=ruby-rouge
pkgver=4.7.0
pkgrel=1
pkgdesc='Pure-ruby code highlighter that is compatible with pygments'
url='https://rouge.jneen.net/'
arch=('any')
license=('MIT')
depends=(
  ruby
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bundler
  ruby-memory_profiler
  ruby-minitest
  ruby-minitest-power_assert
  ruby-mutex_m
  ruby-pry
  ruby-puma
  ruby-rake
  ruby-redcarpet
  ruby-sinatra
  ruby-yard
)
options=('!emptydirs')
source=("https://github.com/jneen/rouge/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('22d6ee0843c3b07b419d1b2f0c2ce3f83320f0849a8c520dfd78152d3bd8721d5055ad0f39e92db673dbd494d22ac5bc74a472ff510f0fdefce7e62de5c9aa8a')
b2sums=('2882674c50a04b9b39fe10e7598d38220961933b2cd9349d89c2114642c319ab55b1bca893498219eebfe3d02bfe62a799101e7c3c0ac0fdc822d0085d372d6e')

prepare() {
  cd "${_gemname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"

  sed --in-place --regexp-extended \
    --expression '/changelog/d' \
    --expression '/git/d' \
    --expression '/rubocop/d' \
    --expression '/shotgun/d' \
    Gemfile \
    Rakefile

  rm --verbose \
    tasks/check/style.rake \
    tasks/update/changelog.rake
}

build() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rake check:specs check:warnings
}

package() {
  cd "${_gemname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
