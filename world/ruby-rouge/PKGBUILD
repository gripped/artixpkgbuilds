# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=rouge
pkgname=ruby-rouge
pkgver=5.0.0
pkgrel=1
pkgdesc='Pure-ruby code highlighter that is compatible with pygments'
url='https://rouge.jneen.net/'
arch=('any')
license=('MIT')
depends=(
  ruby
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bundler
  ruby-memory_profiler
  ruby-minitest
  ruby-minitest-power_assert
  ruby-mutex_m
  ruby-nokogiri
  ruby-pry
  ruby-puma
  ruby-rake
  ruby-redcarpet
  ruby-sinatra
  ruby-yard
)
options=('!emptydirs')
source=("https://github.com/jneen/rouge/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('082a656739394f8eb3e1a7047601dc8f5db6f2c686633d9a29a277deacdd6165a292739272b1241e0ec3133fa99c32f46334c20fe0866ccd76263116d73797d7')
b2sums=('048051d11391517583f01afb9c21b8b5cc5a33201e50d6df9c79df2b3566e5624b19ba1cebfed074a2e35ece43bc9d1fd29f6ae4f3e99477ce5120a8817c2c95')

prepare() {
  cd "${_gemname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"

  sed --in-place --regexp-extended \
    --expression '/changelog/d' \
    --expression '/git/d' \
    --expression '/rubocop/d' \
    --expression '/shotgun/d' \
    --expression '/svn-downloader/d' \
    Gemfile \
    Rakefile

  sed --in-place --regexp-extended \
    --expression '#net/dav#d' \
    tasks/builtins/apache.rake

  rm --verbose \
    tasks/check/style.rake \
    tasks/update/changelog.rake \
    spec/cops/no_building_alternation_pattern_in_regexp_spec.rb
}

build() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rake check:specs check:warnings
}

package() {
  cd "${_gemname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
