# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=squashfuse
pkgver=0.3.0
pkgrel=2
pkgdesc="FUSE filesystem to mount squashfs archives"
arch=(x86_64)
url="https://github.com/vasi/squashfuse"
license=(custom)
depends=(fuse zstd)
makedepends=()
source=(https://github.com/vasi/squashfuse/archive/$pkgver/$pkgname-$pkgver.tar.gz
        https://github.com/vasi/squashfuse/commit/662963d0.patch)
sha256sums=('cc59722e1c7fafb142146f394eacec20f020330ada848113c2e1fc83ab679c05'
            'c5f2e1e5f28f15ac1117b85234d8559ff9d240cfd9bc0ae155e5f3832e4b035f')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../662963d0.patch # Fix ABI break
  ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  sed -e 's/ -shared / -Wl,-O1,--as-needed\0/g' -i libtool # Fix overlinking
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname

# install missing headers
  install -Dm644 *.h -t "$pkgdir"/usr/include/squashfuse
}
