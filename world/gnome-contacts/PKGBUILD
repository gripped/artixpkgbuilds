# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-contacts
pkgver=47.1.1
pkgrel=1
pkgdesc="Contacts Manager for GNOME"
url="https://apps.gnome.org/Contacts"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  evolution-data-server
  folks
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gnome-online-accounts
  gst-plugin-gtk4
  gst-plugin-pipewire
  gst-plugins-base
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
  libgee
  libgoa
  libportal
  libportal-gtk4
  pango
  qrencode
)
makedepends=(
  appstream-glib
  git
  gobject-introspection
  meson
  vala
)
groups=(gnome)
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-contacts.git?signed#tag=${pkgver/[a-z]/.&}"
)
b2sums=('a470ad97ce7049adb077bd40a67ca15a169cfb6390a3f58fe717ea9d574fb513382b0597db8475bf84163781072ca0c1b90130a30042432d3d6e6e4e61c6f49c')
validpgpkeys=(
  A7C626E13F9AD776776BD9CA1D8A57CF2E8D36A3 # Niels De Graef (nielsdg) <nielsdegraef@gmail.com>
)

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
