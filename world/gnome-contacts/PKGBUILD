# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-contacts
pkgver=51.0
pkgrel=1
pkgdesc="Contacts Manager for GNOME"
url="https://apps.gnome.org/Contacts"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  dconf
  evolution-data-server
  folks
  gdk-pixbuf2
  glib2
  glibc
  glycin
  glycin-gtk4
  gnome-online-accounts
  gst-plugin-gtk4
  gst-plugin-pipewire
  gst-plugins-base
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
  libgcc
  libgee
  libgoa
  libportal
  libportal-gtk4
  pango
  qrencode
)
makedepends=(
  appstream
  blueprint-compiler
  git
  gobject-introspection
  meson
  vala
)
groups=(gnome)
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-contacts.git?signed#tag=${pkgver/[a-z]/.&}"
)
b2sums=('8ef1ffc0fdd7cbe21be36668876ce2eeb42a834e94374979d7369a1c75b402cfe2c87d9b6b0af9dc59e5f53450b6947ee458d2963be4d89010ab8a53d01c6270')
validpgpkeys=(
  A7C626E13F9AD776776BD9CA1D8A57CF2E8D36A3 # Niels De Graef (nielsdg) <nielsdegraef@gmail.com>
  4D0BE12F0E4776D8AACE9696E66C775AEBFE6C7D # Jeremy Bícha <jbicha@ubuntu.com>
)

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
