# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-contacts
pkgver=47.0
pkgrel=1
pkgdesc="Contacts Manager for GNOME"
url="https://apps.gnome.org/Contacts"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  evolution-data-server
  folks
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gnome-online-accounts
  gtk4
  hicolor-icon-theme
  libadwaita
  libgee
  libgoa
  libportal
  libportal-gtk4
  pango
  qrencode
)
makedepends=(
  appstream-glib
  git
  gobject-introspection
  meson
  vala
)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/gnome-contacts.git#tag=${pkgver/[a-z]/.&}")
b2sums=('621ee692e7c80024fad4e311fc67304c552291163bbba083b90eb743e5807cb738ac9406fad6bbc434458e3704d77f5f8513af64f36119dc00b04e0521b9fe0a')

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
