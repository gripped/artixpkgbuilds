# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-contacts
pkgver=49.0
pkgrel=1
pkgdesc="Contacts Manager for GNOME"
url="https://apps.gnome.org/Contacts"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  evolution-data-server
  folks
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gnome-online-accounts
  gst-plugin-gtk4
  gst-plugin-pipewire
  gst-plugins-base
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
  libgee
  libgoa
  libportal
  libportal-gtk4
  pango
  qrencode
)
makedepends=(
  appstream
  blueprint-compiler
  git
  gobject-introspection
  meson
  vala
)
groups=(gnome)
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-contacts.git?signed#tag=${pkgver/[a-z]/.&}"
)
b2sums=('ed78ef124a0b7a55d834b978e525870f5c2f493883e5fd806fb2a29d1064cb83eec2f5b36e2609065d621b56e7037735ed3b8be05c4727f2dfded1ef723b974e')
validpgpkeys=(
  A7C626E13F9AD776776BD9CA1D8A57CF2E8D36A3 # Niels De Graef (nielsdg) <nielsdegraef@gmail.com>
  4D0BE12F0E4776D8AACE9696E66C775AEBFE6C7D # Jeremy BÃ­cha <jbicha@ubuntu.com>
)

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
