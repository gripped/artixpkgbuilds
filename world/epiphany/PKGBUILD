# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=epiphany
pkgver=50.5
pkgrel=1
pkgdesc="A GNOME web browser based on the WebKit rendering engine"
url="https://apps.gnome.org/Epiphany"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  gcr-4
  gdk-pixbuf2
  glib2
  glibc
  gmp
  gstreamer
  gtk4
  hicolor-icon-theme
  json-glib
  libadwaita
  libarchive
  libgcc
  libportal
  libportal-gtk4
  libsecret
  libsoup3
  libxml2
  nettle
  pango
  sqlite
  webkitgtk-6.0
)
makedepends=(
  blueprint-compiler
  git
  glib2-devel
  gobject-introspection
  lsb-release
  meson
  python-docutils
  startup-notification
  yelp-tools
)
checkdepends=(
  aspell
  glibc-locales
  hspell
  hunspell
  libvoikko
  nuspell
  xdg-desktop-portal-gtk
  xorg-server-xvfb
)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/epiphany.git#tag=${pkgver/[a-z]/.&}")
b2sums=('a32c41935cc93329bbf1deef1de7cbe15fd2ec3fc6ee10cb444c0635c6c914793f06e1127daeb222e0be0b1c0b65696de0168a596222830d1e4c0e5f1460edfe')

prepare() {
  cd epiphany
}

build() {
  artix-meson epiphany build
  meson compile -C build
}

check() (
  export XDG_RUNTIME_DIR="$PWD/runtime-dir"
  mkdir -p -m 700 "$XDG_RUNTIME_DIR"

  export LC_COLLATE=en_US.UTF-8
  export LIBGL_ALWAYS_SOFTWARE=1
  export WEBKIT_DISABLE_SANDBOX_THIS_IS_DANGEROUS=1

  xvfb-run -s '-nolisten local' dbus-run-session \
    meson test -C build --print-errorlogs
)

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
