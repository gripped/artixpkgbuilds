# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=libnotify
pkgname=(
  libnotify
  libnotify-docs
)
pkgver=0.8.7
pkgrel=2
pkgdesc="Library for sending desktop notifications"
url="https://gitlab.gnome.org/GNOME/libnotify"
arch=(x86_64)
license=(LGPL-2.1-or-later)
depends=(
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
)
makedepends=(
  docbook-xsl
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  meson
  xmlto
)
checkdepends=(
  dbus
  gtk4
  python-dbusmock
  python-gobject
  xorg-server-xvfb
)
source=("git+$url.git?signed#tag=$pkgver")
b2sums=('681ca138787cfe7f9b0619b98c770c436cf2f6d314d3d9257a19c3b261c034bca6628d7fc27ae7f9d075feecb8be556d1b3d93d8025127fcbb736630c8fae10c')
validpgpkeys=(
  98FF7532A4639119B8FB4F15E520923A38618D6C # Logan Rathbone (GNOME) <larathbone@gnome.org>
  D4C501DA48EB797A081750939449C2F50996635F # Marco Trevisan (Trevi√±o) <mail@3v1n0.net>
)

prepare() {
  cd libnotify

  # notify: Do not make spec versions assumptions without a proxy
  # notification-hints: Add an header with the standard notification hints
  # notification: Simplify the logic to support the legacy hints names
  git cherry-pick -n \
    '92c3d2c315deeac94ff5e75333b58a629f0455a9' \
    '7dbd6baed011e495d8617bd9b569b90e8817527c' \
    '00a7b773e520161bdbd9c8139a9fddef976b2136'

  # Use external Xvfb
  sed -i "/xvfb_run, '-a'/d" tests/meson.build
}

build() {
  local meson_options=(
  )

  artix-meson libnotify build "${meson_options[@]}"
  meson compile -C build
}

check() {
  xvfb-run -s '-nolisten local' meson test -C build --print-errorlogs
}

package_libnotify() {
  provides=(libnotify.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_libnotify-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
