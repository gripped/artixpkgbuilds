# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Morten Linderud <foxboron@archlinux.org>

pkgname=aardvark-dns
pkgver=2.1.0
pkgrel=3
pkgdesc="Authoritative dns server for A/AAAA container records"
arch=(x86_64)
url="https://github.com/containers/aardvark-dns"
license=(Apache-2.0)
depends=(
  glibc
  libgcc
)
makedepends=(
  cargo
  git
)
source=(git+$url#tag=v$pkgver)
sha512sums=('565e54ada2c360331461d3d6ea4d862fcb115bfe1b8bd5bda21bd3733a0e591bd511ef323054c875ae2d17efd824942d0f46b11ffbd40ed3225cf07696de1e22')
b2sums=('53374fc70cc4715e3ee265e5926115e53b039d3c905188ee3704ff2ef4741b2ced1a40e6b064365b5b218686c9b2ce66d7b968eb9fd4b36a4f4c0c74b103ed14')

prepare() {
  cd $pkgname
  cargo fetch --locked --target host-tuple
}

build() {
  cd $pkgname
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname
  cargo test --frozen --all-features
}

package() {
  cd $pkgname
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/lib/podman/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
