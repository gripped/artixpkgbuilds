# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: andreas_baumann <abaumann@yahoo.com>
# Contributor: zhuqin <zhuqin83@gmail.com>

pkgname=gengetopt
pkgver=2.23.1
_gnulib='642fd1c4536d081c10dea795e1554727fe5e918c' # latest from recent stable branch 'stable-202607'
pkgrel=1
pkgdesc='A tool to write command line option parsing code for C programs'
arch=('x86_64')
url='https://www.gnu.org/software/gengetopt/gengetopt.html'
license=('GPL-3.0-or-later')
options=('!docs' '!makeflags')
depends=('glibc'
         'libgcc' 'libgcc_s.so'
         'libstdc++' 'libstdc++.so')
makedepends=('git' 'gengen' 'gengetopt' 'help2man')
validpgpkeys=('92C05C30F5E5A772B04D459150248D1A100E6AE6'  # Gray Wolf <wolf@wolfsden.cz>
              'CD7AB2B200F374043F92E5D42485C3A5CF0FF62F') # Gray Wolf <wolf@wolfsden.cz>
source=("git+https://git.savannah.gnu.org/git/gengetopt.git?signed#tag=rel_${pkgver//./_}"
        "git+https://git.savannah.gnu.org/git/gnulib#commit=${_gnulib}")
sha256sums=('ed16afbd66b12154b55e0505eb6fde33865f662682c1f27f46dc2806b5cdf5fa'
            '10d72c294e8d78f176cc6273d42531a06c0f58fd50e93ddea61fecc8cf03d435')

prepare() {
  cd "${pkgname}"

  ../gnulib/gnulib-tool --update
  autoreconf -fi
}

build() {
  cd "${pkgname}"

  ./configure \
    --prefix=/usr
  make
}

package() {
  cd "${pkgname}"

  make DESTDIR="${pkgdir}" install
}

