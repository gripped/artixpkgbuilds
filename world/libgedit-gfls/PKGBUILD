# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=libgedit-gfls
pkgver=0.3.0
pkgrel=1
epoch=1
pkgdesc='File loading and saving library'
arch=(x86_64)
url='https://gitlab.gnome.org/World/gedit/libgedit-gfls'
license=(LGPL-3.0-or-later)
depends=(
  glib2
  glibc
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  gtk3
  meson
)
provides=(libgedit-gfls-1.so)
source=("git+https://gitlab.gnome.org/World/gedit/libgedit-gfls.git?signed#tag=$pkgver")
b2sums=(ff3b9e5da6caed42caace11879d66bf9eaf55dfb0bb35436c692c5fef4dad82b76b5b4e2f923da242a2728d48f572481d547578fe46fee0edd0cb73fc2c15020)
validpgpkeys=(08C0A6B1A08310A45363B328426F014A261FD87F) # Sébastien Wilmet <swilmet@mailfence.com>

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
