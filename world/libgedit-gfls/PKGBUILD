# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=libgedit-gfls
pkgver=0.4.0
pkgrel=1
epoch=1
pkgdesc='File loading and saving library'
arch=(x86_64)
url='https://gitlab.gnome.org/World/gedit/libgedit-gfls'
license=(LGPL-3.0-or-later)
depends=(
  glib2
  glibc
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  gtk3
  meson
)
provides=(libgedit-gfls-1.so)
source=("git+https://gitlab.gnome.org/World/gedit/libgedit-gfls.git#tag=$pkgver")
b2sums=(31ee623c59f5bf0eb737d04cb016c2a1859ee39665a697de2a8735513f77dc2e8ef4e9239f03b173a47194ca125055485c81ee9e9d6ba9a519fd079c58be4679)
validpgpkeys=(08C0A6B1A08310A45363B328426F014A261FD87F) # Sébastien Wilmet <swilmet@mailfence.com>

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
