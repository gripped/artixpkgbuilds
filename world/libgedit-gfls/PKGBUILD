# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=libgedit-gfls
pkgver=0.2.1
pkgrel=1
epoch=1
pkgdesc='A module dedicated to file loading and saving.'
arch=('x86_64')
url='https://gitlab.gnome.org/World/gedit/libgedit-gfls'
license=('LGPL-3.0-or-later')
depends=(
  glib2
  glibc
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  gtk3
  gtk-doc
  meson
)
checkdepends=(xorg-server-xvfb)
provides=(libgedit-gfls-1.so)
source=("git+https://gitlab.gnome.org/World/gedit/libgedit-gfls.git#tag=${pkgver}")
b2sums=('8a589042bd0ef7a8c98652551f795ddad0feab62473340f62d47dcf3b623b9685d00b0b41227fc765ffcbf2fc9e206df61d038c8f220b428b247edf2b821b948')

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
