# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=libgedit-gfls
pkgver=0.4.2
pkgrel=1
epoch=1
pkgdesc='File loading and saving library'
arch=(x86_64)
url='https://gitlab.gnome.org/World/gedit/libgedit-gfls'
license=(LGPL-3.0-or-later)
depends=(
  glib2
  glibc
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  gtk3
  meson
)
provides=(libgedit-gfls-1.so)
source=("git+https://gitlab.gnome.org/World/gedit/libgedit-gfls.git#tag=$pkgver")
b2sums=(e308bcd057fd5b399b15ccab05b762cea5c0b0f4c5e6af331a8c4abc29c13b3e7453404f804e19b4844633a9a064f1a10ba1664dda364249ec762922848fd108)
validpgpkeys=(08C0A6B1A08310A45363B328426F014A261FD87F) # Sébastien Wilmet <swilmet@mailfence.com>

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
