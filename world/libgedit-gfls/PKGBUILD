# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=libgedit-gfls
pkgver=0.3.1
pkgrel=1
epoch=1
pkgdesc='File loading and saving library'
arch=(x86_64)
url='https://gitlab.gnome.org/World/gedit/libgedit-gfls'
license=(LGPL-3.0-or-later)
depends=(
  glib2
  glibc
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  gtk3
  meson
)
provides=(libgedit-gfls-1.so)
source=("git+https://gitlab.gnome.org/World/gedit/libgedit-gfls.git#tag=$pkgver")
b2sums=('d4213915b751432abff0cd3a64b214e0bce5e4dcd1c2de72b8011058f84380a3d065d427d9637cbc708fd5c2f88452c9369dc2ba345899976de2653d897e0c53')
validpgpkeys=(08C0A6B1A08310A45363B328426F014A261FD87F) # Sébastien Wilmet <swilmet@mailfence.com>

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
