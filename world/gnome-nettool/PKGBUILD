# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Kritoke <kritoke@nospam.gmail.com>

pkgname=gnome-nettool
pkgver=42.0
pkgrel=4
pkgdesc="Graphical interface for various networking tools"
url="https://gitlab.gnome.org/GNOME/gnome-nettool"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  iputils
  libgtop
  pango
  whois
)
makedepends=(
  bind
  git
  meson
  net-tools
  nmap
  yelp-tools
)
optdepends=(
  'bind: DNS lookup'
  'net-tools: Network statistics'
  'nmap: Port scanning'
)
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-nettool.git#tag=$pkgname-${pkgver//./-}"
  0001-Fix-build-with-recent-Meson.patch
)
b2sums=('7ebe7ea4164df45e1d3316a1b43e27644d34b108f8b6f8395cdbc9a8c30a61c7e78d6264bd5d0f972f81c0116540637462692d3fbf2111d868689e7c16bc71b9'
        'ca105075b2f8ffd805d2dc5df515856319b6c3da3796c67ec03e1303923c660c909c75e413e26952a7e116bbc1872a3bf3ae76b117a5c9b5d15f28dd5f30f40d')

prepare() {
  cd $pkgname
  git apply -3 ../0001-Fix-build-with-recent-Meson.patch
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim set:sw=2 sts=-1 et:
