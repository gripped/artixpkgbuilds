# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=kongress
pkgver=25.12.0
pkgrel=1
pkgdesc='Companion application for conferences'
url='https://apps.kde.org/kongress/'
arch=(x86_64)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kcalendarcore
         kconfig
         kcontacts # for kosmindoormap
         kcoreaddons
         kcrash
         kdbusaddons
         ki18n
         kirigami
         kirigami-addons
         knotifications
         kosmindoormap
         qqc2-desktop-style
         qt6-base
         qt6-declarative
         qt6-location
         qt6-positioning)
makedepends=(extra-cmake-modules
             python)
groups=(kde-applications
        kde-utilities)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('9c141cc43c131e75ca92f33a59ed1f5d7aba3dbc9b85e22e2888444d47203c13'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
