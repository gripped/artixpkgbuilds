# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Adrián Pérez de Castro <aperez@igalia.com>

pkgname=lowdown
pkgver=3.2.1
pkgrel=1
pkgdesc='A simple Markdown translator'
arch=(x86_64)
url='https://kristaps.bsd.lv/lowdown'
license=(ISC)
depends=(glibc libmd)
makedepends=(git bmake)
provides=(liblowdown.so)
source=("$pkgname::git+https://github.com/kristapsdz/lowdown#tag=$(echo $pkgver | sed -e 's/^/VERSION_/' -e 's/\./_/g')")
sha512sums=('8fb1e085faccda743849f72792968885e9a3912281afe5ee586730645a8d088613ed6a1d3b01cb85614b94769e2c900bfeac9ee5fa0dd68d7f7d0d8410f3cc20')
b2sums=('c7f3678ed91b1b6341e31bdeacad5ca2606a52f604f09ac961a92005c4de04a93175d84f85815d9eb04ab4eec4e2b0e8ba211fd83d64fced9a0084cbb9290cf2')

build () {
  cd "$pkgname"

	./configure \
    PREFIX=/usr \
    MANDIR=/usr/share/man

  # ensure LDFLAGS is passed correctly
  sed -i "s/^LDFLAGS.*/LDFLAGS = $LDFLAGS/" Makefile.configure

	bmake
}

check () {
	bmake -C "$pkgname" regress
}

package () {
  cd "$pkgname"

  # package
	bmake DESTDIR="$pkgdir" \
    install \
    install_lib_common \
    install_shared

  # symlink unversioned to versioned shared library
  local LIBVER=$(grep "^LIBVER" Makefile | sed "s/.*= //")
  ln -sf "/usr/lib/liblowdown.so.$LIBVER" "$pkgdir/usr/lib/liblowdown.so"

  # license
	install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
