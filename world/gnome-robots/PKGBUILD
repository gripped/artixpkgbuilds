# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-robots
pkgver=41.2
pkgrel=3
pkgdesc="Avoid the robots and make them crash into each other"
url="https://wiki.gnome.org/Apps/Robots"
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
  libxml2
  pango
)
makedepends=(
  appstream
  git
  librsvg
  meson
  rust
  yelp-tools
)
groups=(gnome-extra)
source=("git+https://gitlab.gnome.org/GNOME/gnome-robots.git#tag=${pkgver/[a-z]/.&}")
b2sums=('a014e7ed291da79c3266532c4231e8ca8d03227a2ab802e0b299f40b5cf2b82173928a5aa3603c41266e967475bbc30b52d5201557e536ea5ff4dfa83584a3f5')

prepare() {
  cd $pkgname

  # Match CARGO_HOME in src/meson.build
  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  local meson_options=(
    -D profile=release
  )

  # Use debug
  export CARGO_PROFILE_RELEASE_DEBUG=2

  # Use LTO
  export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
