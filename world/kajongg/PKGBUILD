# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kajongg
pkgver=25.12.1
pkgrel=1
pkgdesc='The ancient Chinese board game for 4 players'
url='https://apps.kde.org/kajongg/'
arch=(any)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(libkmahjongg
         python
         python-qtpy
         python-twisted
         python-zope-interface
         qt6-python-bindings)
makedepends=(extra-cmake-modules
             kdoctools)
optdepends=('vorbis-tools: for playing sounds')
groups=(kde-applications
        kde-games)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('410210575af712d8cdf52e5d8d5fd976b69e2fb9796b0dd6ce9e2d76202ffaa8'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
